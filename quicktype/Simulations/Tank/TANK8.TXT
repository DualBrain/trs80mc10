0 CLS:DIMD(8,4),P(2),B(2),T(2),L(2),V(2),R(2),K(255),P,D,A,X,Y,T,N,C,Q,L,M,P1,P2,S,V,B,NT(2):GOTO8000
1 Q=Q-1:IFQ<1THENQ=8
2 D=Q:P=1:GOSUB50:RETURN
3 Q=Q+1:IFQ>8THENQ=1
4 D=Q:P=1:GOTO50
5 SOUND1,1:D=Q:P=1:V=175:GOSUB50:V=96:RETURN
10 R=R-1:IFR<1THENR=8
11 D=R:P=2:GOSUB50:RETURN
12 R=R+1:IFR>8THENR=1
13 D=R:P=2:GOTO50
14 SOUND1,1:D=R:P=2:V=175:GOSUB50:V=96:RETURN
17 IFS(1)=0THENL(1)=B(1):S(1)=1:V(1)=R(1)
18 RETURN
19 IFS(2)=0THENL(2)=B(2):S(2)=1:V(2)=R(2)
20 RETURN
21 N=PEEK(L(P)+V(P)):IFN<>VTHENGOSUB25:RETURN
22 L(P)=L(P)+V(P):POKEL(P),S:S(P)=2:RETURN
23 N=PEEK(L(P)+V(P)):IFN<>VTHENPOKEL(P),V:GOSUB25:RETURN
24 POKEL(P),V:L(P)=L(P)+V(P):POKEL(P),S:RETURN
25 IFN=BTHENL(P)=L(P)+V(P):POKEL(P),V:S(P)=0:RETURN
26 IFN=T(1)ORN=T(2)ORN=D(1,2)ORN=D(2,2)ORN=D(3,2)ORN=D(4,2)THENS(P)=0:ONPGOTO40,41
28 IFN=STHEN30
29 S(P)=0
30 RETURN
40 NT(2)=NT(2)-1:M$=STR$(NT(2)):PRINT@18,;:GOSUB199:POKEP(2),191:POKEB(2),126:SOUND1,1:RETURN
41 NT(1)=NT(1)-1:M$=STR$(NT(1)):PRINT@1,;:GOSUB199:POKEP(1),191:POKEB(1),126:SOUND1,1:RETURN
42 RETURN
50 IFPEEK(P(P)+D(D,1))=D(D,2)THEN60
52 IFPEEK(P(P)+D(D,1))=96THENPOKEB(P),V:B(P)=P(P)+D(D,1):POKEB(P),D(D,2):R(P)=D(D,1):RETURN
54 RETURN
60 X=PEEK(P(P)+(D(D,1)*2)):IFX=96THENPOKEP(P),V:P(P)=P(P)+D(D,1):POKEP(P),T(P):B(P)=P(P)+D(D,1):POKEB(P),D(D,2)
70 IFX=175THENSTOP
80 RETURN
90 FORL=-1TO0:A=0:I$=INKEY$:IFI$<>""THENA=ASC(I$)
91 ONK(A)GOSUB1,3,4,17,19,10,12,13,5,14:P=1:ONS(P)GOSUB21,23:P=2:ONS(P)GOSUB21,23:L=(NT(1)>1ANDNT(2)>1):NEXT
92 IFS(2)>0THENP=2:FORL=1TO33:ONS(P)GOSUB21,23:NEXT
93 IFS(1)>0THENP=1:FORL=1TO33:ONS(P)GOSUB21,23:NEXT
94 IFNT(1)>NT(2)THENM$="TANK * WON!":P1=P1+1
95 IFNT(2)>NT(1)THENM$="TANK # WON!":P2=P2+1
96 IFNT(1)=NT(2)THENM$="THE BATTLE WAS A DRAW!"
97 PRINT@480,;:GOSUB199
99 I$=INKEY$:C=RND(0):IFI$=""THEN99
100 IFI$="Y"THENCLS:GOTO8020
101 IFI$="N"THENCLS:PRINT"# VICTORIES =";P1:PRINT"* VICTORIES =";P2:END
102 GOTO99
199 PRINTM$;:Y=((PEEK(17024)AND1)*256+PEEK(17025))-1+M:FORX=Y-LEN(M$)+1TOY:POKEX,PEEK(X)-64:NEXT:RETURN
8000 GOSUB8500:P1=0:P2=0:S=103:V=96:B=134:FORY=1TO8:FORX=1TO2:READD(Y,X):NEXT:NEXT
8010 M=16384:X=1
8015 K(65)=1:K(68)=2:K(87)=3:K(32)=4:K(50)=4:K(83)=9
8016 K(75)=6:K(59)=7:K(79)=8:K(13)=5:K(57)=5:K(76)=10
8020 FORX=1TO25:Y=RND(416):PRINT@Y," †";:PRINT@Y+32,"†††";:PRINT@Y+64," †";:NEXT
8030 P=0:D=0:A=0:X=0:Y=0:T=0:N=0:C=0:L=0:S(1)=0:S(2)=0
8100 P(1)=M+68:B(1)=P(1)+1:R(1)=D(5,1):T(1)=42
8110 P(2)=M+443:B(2)=P(2)-1:R(2)=D(1,1):T(2)=35
8150 POKEP(1),T(1):POKEP(1)+D(5,1),D(5,2):POKEP(2),T(2):POKEP(2)+D(1,1),D(1,2)
8160 PRINT@0,;:M$="T    S    M      T    S    M    ":GOSUB199  
8170 FORX=0TO31:POKEM+480+X,32:NEXT
8230 Q=5:R=1:GOTO90
8500 PRINT" TANK! CONCEPT BY SHAWN MEEHAN "
8505 PRINT"               & MIKE ROWLEY   "
8506 PRINT"       MC-10 VERSION J. GERRIE "      
8507 PRINT" ŒŒŒŒŒŒŒŒŒŒŒŒŒŒŒŒŒŒŒŒŒŒŒŒŒŒŒŒŒ "
8508 PRINT" MONTY VS. THE DESERT FOX      "
8509 PRINT
8510 PRINT" USE THE FOLLOW KEYS TO MOVE:  "
8511 PRINT
8520 PRINT"       left tank    right tank "
8525 PRINT
8525 PRINT" STEER =    W              O   "
8530 PRINT"          A   D          K   ; "
8540 PRINT" LAY MINE = S              L   "
8542 PRINT" FIRE = SPACE OR 2   ENTER OR 9"
8550 PRINT
8555 PRINT" PRESS enter TO BEGIN ";
8560 I$=INKEY$:C=RND(0):IFI$<>CHR$(13)THEN8560
8570 CLS:NT(1)=3:NT(2)=3:RETURN
9000 DATA -1,109
9010 DATA -33,92
9020 DATA -32,97
9030 DATA -31,111
9040 DATA 1,109
9050 DATA 33,92
9060 DATA 32,97
9070 DATA 31,111
