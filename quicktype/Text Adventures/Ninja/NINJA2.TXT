0 CLS:DIMC1,CC,P,M$:GOTO10
1 C1=1:CC=32
2 IFMID$(M$,CC,1)<>""ANDMID$(M$,CC,1)<>" "THENCC=CC-1:GOTO2
3 PRINTMID$(M$,C1,CC-C1):C1=CC+1:CC=C1+31:IFC1<=LEN(M$)THEN2
4 RETURN
10 LO(1)=2:LO(2)=4:I=0:LC=1:CLS:PRINT"NINJA V1.30":PRINT"BY: PAUL PANKS (DUNRIC@YAHOO.COM)"
20 PRINT:IF LC=1 THEN PRINT"IT IS DARK, YOU SEE A SHINTO SHRINE."
30 IFLC=1THENPRINT"THE MOUNTAINS OF TOKYO HOVER BEHIND YOU."
35 IFLC=1THENPRINT"THE CITY RISES TO THE EAST."
36 GOSUB201
40 INPUT A$:IF CT>24 THEN GOTO100
41 CT=CT+1:K=I:IF K>20 THEN PRINT">20"
50 IFLC=1THENIFA$="NORTH"ORA$="GO NORTH"ORA$="N"THENPRINT"THE WAY IS BLOCKED. FIND ANOTHER, MASTER...":I=I-5:GOTO40
51 IFLC=1THENIFA$="SOUTH"ORA$="GO SOUTH"ORA$="S"THENPRINT"THE ROAD IS TOO STEEP.":I=I-2:GOTO40
52 IFA$="INVENTORY"THENPRINT"YOU ARE CARRYING:":GOSUB500:I=I+3:GOTO40
53 IFA$="SCORE"THENPRINT"ONLY THE KAMI KNOW!":I=I+1:GOTO40
54 IFA$="HELP"ORA$="HINT"THEN1000
55 IFLC=1THENIFA$="EAST"ORA$="GO EAST" OR A$="E" THEN PRINT"YOU ARE BY THE SHINTO SHRINE TO THE EAST.":I=I+3:GOTO40
56 IFLC=1THENIFA$="WEST"ORA$="GO WEST" OR A$="W" THEN PRINT"THAT WAY IS BLOCKED BY TREES.":I=I-4:GOTO40
57 IFLC=1THENIFA$="UP"ORA$="GO UP" OR A$="U" THEN PRINT"THE SKY IS UP, NOT THE ASCENDED MASTER...":I=I-5:GOTO40
58 IFLC=1THENIFA$="DOWN"ORA$="GO DOWN" OR A$="D" THEN PRINT"THE LORDS OF THE FOREST FORBID IT.":I=I-3:GOTO40
59 IFA$="EXAMINE SHRINE"THEN1020
60 IFA$="EXAMINE MOUNTAINS" THEN PRINT"THE MOUNTAINS ARE TOO FAR AWAY, MASTER...":I=I-8:GOTO40
61 IFA$="EXAMINE TOKYO"ORA$="EXAMINE TOKYO"THEN1040
63 IFA$="QUIT" OR A$="BYE" OR A$="EXIT" THEN PRINT"YOU HAVE NOT YET COMPLETED THIS ADVENTURE, MASTER...":I=I-1:GOTO40
64 IFA$="SAVE" OR A$="SAVE GAME" THEN PRINT"THE GODS ARE NOT WITH YOU ON THIS ONE, O' MASTER RYOKO...":I=I-2:GOTO40
65 IFA$="LOOK" THEN I=I-6:GOTO20
66 IFA$="I" OR A$="L" THEN PRINT"YOU CANNOT DO THAT. YOU ARE NOT A MASTER YET...":I=I-5:GOTO40
67 IFLEFT$(A$,4)="KILL"ORLEFT$(A$,5)="FIGHT"THEN1060
68 IFA$="EXAMINE STARS" THEN PRINT"THE STARS TWINKLE LIKE DIAMONDS.":I=I+4:GOTO40
69 IFA$="EXAMINE SKY"ORA$="EXAMINE HEAVEN"ORA$="EXAMINE HEAVENS"THEN1070
70 IFA$="EXAMINE BIRDS"THEN1080
71 IFA$="EXAMINE CITY"THEN1100
72 IFA$="EXAMINE SUN" THEN PRINT"THE SUN IS FADING DOWNWARD, BELOW THE HORIZON. THE SKY IS GETTING DARKER.":I=I+2:GOTO40
73 IFA$="EXAMINE HORIZON" THEN PRINT"THE HORIZON TWINKLES LIKE THE STARS ABOVE, AHEAD OF THE SUN.":I=I+4:GOTO40
74 IFA$="EXAMINE MOON" THEN PRINT"THE MOON IS NOT OUT YET, O' MASTER RYOKO...":I=I-2:GOTO40
75 IFLEFT$(A$,14)="EXAMINE SHINTO" THEN A$="EXAMINE SHRINE":GOTO59
76 IFA$="EXAMINE TREES" THEN PRINT"THE TREES PARALLEL YOUR PATH IN ALL DIRECTIONS HERE.":I=I-1:GOTO40
77 IFA$="EXAMINE ROAD"THEN1120
78 IFA$="EXAMINE ORCHARD" THEN PRINT"THE ORCHARD IS TOO FAR AWAY. YOU DO NOTICE CHERRIES ON THE TREES.":I=I+3:GOTO40
79 IFA$="EXAMINE CHERRIES"THEN1150
80 IFA$="GET SWORD"ORA$="TAKE SWORD"THEN1130
81 IFA$="GET SWORD" OR A$="TAKE SWORD" THEN LO(1)=101:PRINT"TAKEN, O' MASTER RYOKO...":I=I+5:GOTO40
82 IFA$="ENTER SHRINE" AND LC=1 THEN LC=2:PRINT"YOU ENTER THE SHRINE...":I=I+10:GOTO20
83 IFA$="DROP SWORD" AND LO(1)<>101 THEN PRINT"YOU DO NOT CARRY IT, O' MASTER RYOKO...":I=I-4:GOTO40
84 IFA$="DROP SWORD" THEN LO(1)=LC:PRINT"YOU DROP THE SWORD. IT CLATTERS AS IT HITS THE GROUND.":I=I-2:GOTO40
85 IFA$="EXIT SHRINE" AND LC=2 THEN LC=1:PRINT"YOU EXIT THE SHRINE...":I=I+5:GOTO20
86 IFLC=2THEN IF A$="GO NORTH" OR A$="NORTH" OR A$="N" THEN PRINT"THERE'S A WALL THERE.":I=I-3:GOTO40
87 IFLC=2THEN1160
88 IFLC=2THEN IF A$="GO EAST" OR A$="EAST" OR A$="E" THEN PRINT"YOU BUMPED YOUR HEAD ON A WALL.":I=I-3:GOTO40
89 IFLC=2THEN1180
90 IFLC=2THENIFA$="GO WEST"ORA$="WEST"ORA$="W"THENLC=3:I=I+6:PRINT"YOU PASS THROUGH THE WINDOW TO THE WEST...":GOTO20
91 IFLC=3THEN1200
92 IFLC=3THENIF A$="GO EAST" OR A$="EAST" OR A$="E" THEN LC=2:I=I+2:PRINT"YOU ENTER THE WINDOW TO THE EAST...":GOTO20
93 IFLC=3THEN1230
94 IFLC=3THEN1270
95 IFA$="CUT TREE"ORA$="CHOP TREE"ORA$="CHOP DOWN TREE"ORA$="CUT DOWN TREE"THEN1300
96 IFA$="CUT TREE"ORA$="CHOP TREE"ORA$="CHOP DOWN TREE"ORA$="CUT DOWN TREE"THEN1340
97 IFA$="CUT TREE"ORA$="CHOP TREE"ORA$="CHOP DOWN TREE"ORA$="CUT DOWN TREE"THEN1370
98 GOTO600
99 PRINT"YOU CANNOT DO THAT.":I=I-5:GOTO40
100 PRINT"A NINJA LEAPS FROM THE SHADOWS!":I=I+10
101 A=INT(RND(0)*I)+1
102 IFA>49THEN1390
103 IFA>20 THEN PRINT"THE NINJA ADVANCES TOWARDS YOU...":PRINT:U=INT(RND(0)*200)+1:HP=ABS(A+I):R=ABS(K):M=R*2:GOTO155
104 PRINT"THE NINJA STRUCK YOU, SLICING YOU IN HALF...":PRINT"IT IS THE END...":PRINT:PRINT"@ THE END @":GOTO150
150 GOSUB200:PRINT"PLAY AGAIN (Y/N)PRINT"
151 B$=INKEY$:IF B$="" THEN151
152 IFB$="Y"ORB$="Y"THENRUN
153 IFB$="N"ORB$="N"THENPRINT"THANKS AGAIN... :)":END
154 GOTO151
155 I=INT(RND(0)*25)+1:PRINT">":IFHP<1THENHP=0
156 PRINT"@ YOU ATTACK @":IF I<5 THEN PRINT"ALAS, YOU MISSED THE NINJA..."
157 IFI>4 AND I<10 THEN PRINT"YOU HIT THE NINJA VERY HARD.":U=U-R
158 IFI>9 AND I<=15 THEN PRINT"AMAZING BLOW! THE NINJA IS SLICED BADLY!":U=U-(R*4)
159 IFI>15 THEN PRINT"THE NINJA DODGED YOUR ATTACK..."
160 PRINT">":I=INT(RND(0)*25)+1:IF U<1 THEN U=0
161 PRINT"@ THE NINJA ATTACKS @":IF I<5 THEN PRINT"HE MISSED YOU..."
162 IFI>4 AND I<10 THEN PRINT"THE NINJA STRUCK YOU HARD!":HP=HP-M
163 IFI>9 AND I<=15 THEN PRINT"WOW! THE NINJA SLICED YOU BADLY!!":HP=HP-(M*2)
164 IFI>15 THEN PRINT"YOU DODGED THE ATTACK..."
165 PRINT">":PRINT"YOUR HIT POINTS:";HP:PRINT"THE NINJA:";U:PRINT">":IF U<1 OR HP<1 THEN172
166 PRINT"@ PRESS ANY KEY... @"
167 B$=INKEY$:IF B$="" THEN167
168 GOTO155
172 IFU<1 THEN PRINT">":A=101:GOTO102
173 PRINT">":GOTO104
200 PRINT:PRINT"SCORE:";A;"/";I:PRINT:RETURN
201 IFLC<>2THEN203
202 M$="YOU ARE WITHIN THE SHINTO SHRINE. THE ROOM IS LIT ONLY BY THE LIGHT OF A SINGLE WINDOW. ALL ELSE IS DARKNESS.":GOSUB1
203 IFLC=2THEN PRINT"YOU MAY 'EXIT SHRINE' TO THE SOUTH, OR HEAD WEST":PRINT"OUT THE WINDOW."
204 IFLC=3THEN PRINT"YOU ARE BY AN OLD OAK TREE OUTSIDE THE SHRINE."
205 IFLC=3THEN PRINT"THERE IS A RIVER TO THE WEST, BUT THERE IS"
206 IFLC=3THEN1420
207 IFLC=4THEN1450
208 IFLC=4 THEN PRINT"THE ONLY EXIT IS EAST."
209 IFLO(1)=LC THEN PRINT"THERE IS A SWORD HERE. IT GLEAMS IN THE MOONLIGHT."
210 IFLO(2)=LC THEN PRINT"THERE IS A GOLDEN IDOL HERE."
211 IFLC=5THEN1480
299 RETURN
400 PRINT:PRINT"VERBS: GO, GET, DROP, USE, EXAMINE, LOOK, INVENTORY, CUT, CHOP, KILL, SCORE, HELP, QUIT"
410 PRINT"SAVE, CLIMB, ENTER AND EXIT.":RETURN
500 IFLO(1)=101 THEN PRINT" SWORD, GLEAMING BRIGHTLY IN THE MOONLIGHT.":RETURN
501 PRINT" NOTHING.":RETURN
600 IFA$="GO EAST" OR A$="EAST" OR A$="E" THEN IF LC=4 THEN LC=3:PRINT"YOU ESCAPE TO THE EAST...":I=I+4:GOTO20
601 IFA$="CLIMB TREE" OR A$="GO UP" OR A$="U" THEN IF LC=3 AND TR=0 THEN LC=5:PRINT"YOU CLIMB UP THE TREE...":I=I+8:GOTO20
602 IFA$="CLIMB TREE" OR A$="GO DOWN" OR A$="D" THEN IF LC=5 THEN LC=3:PRINT"YOU CLIMB DOWN THE TREE...":I=I+4:GOTO20
603 IFA$="GET IDOL"ORA$="GET GOLDEN IDOL"ORA$="TAKE IDOL"ORA$="TAKE GOLDEN IDOL"THEN1500
604 IFA$="GET IDOL"ORA$="GET GOLDEN IDOL"ORA$="TAKE IDOL"ORA$="TAKE GOLDEN IDOL"THEN1520
605 IFA$="DROP IDOL"ORA$="DROP GOLDEN IDOL"THEN1530
606 IF A$="DROP IDOL" OR A$="DROP GOLDEN IDOL" THEN IF LO(2)=101 AND LC=2 THEN700
607 IF A$="CLIMB TREE" THEN PRINT"EVEN YOU CANNOT DO THAT, O' MASTER RYOKO...":I=I-4:GOTO40
608 IF LEFT$(A$,3)="GET" OR LEFT$(A$,4)="TAKE" THEN PRINT"YOU CANNOT DO THAT. YOU ARE NOT A MASTER YET...":I=I-4:GOTO40
699 GOTO99
700 PRINT"YOU DROP THE GOLDEN IDOL, PLACING IT ATOP THE ALTAR OF"
701 PRINT"THE SHINTO SHRINE. YOUR GRANDFATHER, WHO TAUGHT YOU WELL, WOULD"
702 PRINT"HAVE BEEN MOST PROUD OF THIS DAY, O' MASTER RYOKO, FOR YOU"
703 PRINT"ARE A TRUE WARRIOR OF JAPAN, SERVING "
704 PRINT"THE WORLD WITH YOUR PEACEFUL"
705 PRINT"WAYS OF KAMI. MAY THE GODS BLESS YOU ALWAYS, O' MASTER...":PRINT
706 PRINT"@ THE END @":A=A+ABS(K):IFA>ITHENA=I
708 GOTO150
1000 PRINT"IT'S LIKE A FINGER POINTING AWAY TO THE MOON. DON'T CONCENTRATE ON THE"
1010 PRINT"FINGER, OR YOU WILL MISS ALL THE HEAVENLY GLORY.":GOSUB400:I=I-5:GOTO40
1020 PRINT"THE SHRINE IS A HEAVENLY STRUCTURE POINTED UPWARDS TO THE HEAVENS ABOVE YOU."
1030 PRINT"BIRDS FLY ATOP IT, SCATTERING THE SKY WITH A DISTANT BREEZE.":I=I+25:GOTO40
1040 PRINT"THE CITY RISES IN THE EAST, NEAREST THE FADING SUN. YOU NOTICE"
1050 PRINT"STARS ABOUT YOU, TWINKLING LIKE DIAMONDS IN THE SKY.":I=I+8:GOTO40
1060 PRINT"THE GODS OF PEACE ARE IN YOUR VEINS. BE STRONG IN PEACE, BE STRONG IN SELF...":I=I-15:GOTO40
1070 PRINT"THE SKY IS A MIXTURE OF LIGHT AND DARK, LIKE THE YIN AND THE YANG.":I=I+12:GOTO40
1080 PRINT"THE BIRDS ARE MOVING TOO FAST TO EXAMINE CLOSELY. THEY APPEAR TO BE HEADING"
1090 PRINT"TOWARDS THE EASTERN HORIZON.":I=I+6:GOTO40
1100 PRINT"THE CITY IS ALIVE WITH LIGHT. IT IS LIKE A FOREST, ONLY"
1110 PRINT"LARGER. YOU WONDER IF ANYONE WHO KNOWS YOU CAN SEE YOU.":I=I+3:GOTO40
1120 PRINT"THE ROAD DESCENDS SOME ONE-HUNDRED FEET AHEAD, NEAR":PRINT"A SMALL CHERRY ORCHARD.":I=I+6:GOTO40
1130 IFLO(1)<>LC THEN PRINT"EVEN THAT YOU CANNOT DO, O' MASTER RYOKO...":GOTO40
1140 GOTO81
1150 PRINT"THE CHERRIES ARE RED AND LOOK DELICIOUS!":PRINT"(TOO BAD YOU CANNOT REACH THEM...)":I=I+12:GOTO40
1160 IFA$="GO SOUTH"ORA$="SOUTH"ORA$="S"THENPRINT"YOUR PATH IS BLOCKED. TRY 'EXIT SHRINE' INSTEAD.":I=I-2:GOTO40
1170 GOTO88
1180 IFA$="GO UP"ORA$="UP"ORA$="U"ORA$="GO DOWN"ORA$="DOWN"ORA$="D"THENPRINT"THE GODS FORBID IT, MASTER....":I=I-8:GOTO40
1190 GOTO90
1200 IFA$="EXAMINE TREE"ORA$="EXAMINE OAK"ORA$="EXAMINE OAK TREE"THEN1220
1210 GOTO92
1220 PRINT"IT IS AN OLD OAK, WHITHERED BY AGE AND TIME.":PRINT"IF FALLEN, IT WOULD SPAN THE RIVER.":I=I+5:GOTO40
1230 IFA$="GO WEST"ORA$="WEST"ORA$="W"THEN1250
1240 GOTO94
1250 IFTR=0THENPRINT"THE RIVER IS TOO DANGEROUS TO CROSS NOW.":PRINT"(SOMEHOW, YOU NEED TO CROSS IT...)":I=I-2:GOTO40
1260 GOTO94
1270 IFA$="GO WEST"ORA$="WEST"ORA$="W"THEN1290
1280 GOTO95
1290 PRINT"YOU WALK ACROSS THE TREE, REACHING THE OTHER SIDE OF THE RIVER...":LC=4:I=I+12:GOTO40
1300 IFLC=3ANDLO(1)=101ANDTR=0THEN1320
1310 GOTO96
1320 TR=1:PRINT"WWWWHHHOOOSSSHHH!!!":PRINT"< THUD!!! >"
1330 PRINT"THE TREE FALLS DOWN, SPANNING THE RIVER TO THE WEST.":I=I+10:GOTO40
1340 IFLC=3ANDLO(1)=101ANDTR=1THEN1360
1350 GOTO97
1360 PRINT"THE TREE HAS ALREADY FALLEN DOWN, O' MASTER RYOKO...":PRINT"YOU BOW TO THE FALLEN FOE.":I=I+3:GOTO40
1370 IFLC=3THENPRINT"ALAS, YOU'LL NEED SOMETHING ELSE.":PRINT"YOU'LL NEED A SWORD, MASTER...":I=I-3:GOTO40
1380 GOTO98
1390 PRINT"YOU DEFTLY AVOIDED IT, SLICING IT WITH A MORTAL BLOW."
1400 PRINT"YOU HAVE DEFEATED THE NINJA, O' MASTER...":PRINT
1410 PRINT"MAY THE GODS BLESS YOU...":PRINT:PRINT"@ THE END @":GOTO150
1420 PRINT"NARY A BRIDGE IN SIGHT.":PRINT"ACROSS THE RIVER STANDS THE SHRINE OF THE EVIL ONE."
1430 PRINT"(YOU MUST REACH IT SOMEHOW...)":IFTR=1THENPRINT"A TREE HAS FALLEN, SPANNING THE RIVER TO THE WEST."
1440 GOTO207
1450 PRINT"YOU ARE ACROSS THE RIVER, IN FRONT OF THE EVIL ONE'S"
1460 PRINT"SHRINE. IT LOOKS DARK AND FORBODING. YOU FEEL A PRESENCE IN"
1470 PRINT"THE SHADOWS NEARBY. YOU MUST LEAVE, NOW!":CT=CT+5:GOTO208
1480 PRINT"YOU ARE ATOP AN OLD TREE. LOOKING DOWN, YOU SEE THE RIVER"
1490 PRINT"RAGING BELOW, AND A SHRINE TO THE WEST, JUST BEYOND":PRINT"THE RIVER'S EDGE.":GOTO299
1500 IFLO(2)=LCANDLC=4THENLO(2)=101:PRINT"TAKEN, O' MASTER RYOKO...":PRINT"QUICK, YOU MUST LEAVE, NOW!":I=I+15:CT=CT-5:GOTO40
1510 GOTO604
1520 PRINT"YOU CANNOT GET THAT, MASTER...":I=I-4:GOTO40
1530 IFLO(2)=101ANDLC<>2THEN1550
1540 GOTO606
1550 PRINT"YOU CANNOT DROP IT HERE, MASTER...":PRINT"THE IDOL BELONGS INSIDE THE SAFETY OF THE SHRINE!":I=I-2:GOTO40
