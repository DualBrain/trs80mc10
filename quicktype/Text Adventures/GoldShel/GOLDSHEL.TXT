10 CLEAR5000
20 CLS:PRINT"EXAMPLE ADVENTURE":PRINT
30 PRINT"COPYRIGHT MCGRAW-HILL BOOK      COMPANY (UK) LIMITED"
40 PRINT:PRINT"PLEASE WAIT"
50 GOSUB1500
60 CLS:PRINT"WOULD YOU LIKE INSTRUCTIONS?"
70 A$=INKEY$:IFA$<>"Y"ANDA$<>"N"THEN70
80 IFA$="N"THEN200
90 CLS:PRINT"EXAMPLE ADVENTURE":PRINT:PRINT"YOUR OBJECTIVE IN THIS GAME IS"
100 PRINT"TO FIND THE GOLDEN SHELL, WHICH (IT IS RUMOURED) HAS BEEN HIDDEN";
110 PRINT"SOMEWHERE IN THE VICINITY"
120 PRINT:PRINT"TO COMMUNICATE YOUR WISHES, YOU MUST ENTER FAIRLY SIMPLE COMM-
130 PRINT"ANDS, SUCH AS: TAKE THE SHELL   OR GO NORTH."
140 PRINT"THE GAME ENDS WHEN YOU RETURN   THE SHELL TO THE HOUSE."
150 PRINT:PRINT"PRESS ANY KEY TO START."
160 IFINKEY$=""THEN160
170 CLS
200 REM DISPLAY ROOM
210 IFR=RR THEN250
220 PRINTR$(R):RR=R
230 FORI=1TONI:IFI(I)=R THENPRINTI1$(I)
240 NEXT
250 IFI(7)=4THENPRINT"WELL DONE!!":PRINT"YOU TOOK "NT" TURNS":GOTO31200
400 NT=NT+1:PRINT"WHAT ARE YOU GOING TO DO?":INPUTI$
410 IFI$=""THENPRINT"PLEASE SAY SOMETHING TO ME":GOTO400
420 GOSUB9000:PRINT:PRINT:PRINT
430 GOSUB31500:GOSUB31400
440 IFV=2ANDN$="INVE"THENGOSUB30100:GOTO400
450 IFV=1THENRR=-1
460 IFV$="QUIT"THEN31200
470 IFV$="SAVE"THENGOSUB30700:GOTO400
480 IFV$="LOAD"THENGOSUB30800:GOTO400
490 IFV$="LOOK"ORV$="L"THENRR=-1:GOTO200
500 GOSUB5000
510 IFF THEN200
520 IFV=1THENGOSUB30600:GOTO200
530 IFV=2ORV=3THENGOSUB30200:GOTO200
540 PRINT"THAT IS NOT POSSIBLE.":GOTO200
1500 REM INITIALISATION
1510 R=1:NR=12:NN=21:NV=19:NM=14:NI=10:NW=9:NT=0
1520 DIMG(NI),N$(NN),N(NN),V$(NV),V(NV),GT(NR,12),I1$(NI),I2$(NI)
1530 DIMM$(NM),R$(NR),W(NI),IM$(12),I(15),F$(NW),T$(NW)
1540 FORI=1TONI:READG(I),I1$(I),I2$(I),I(I),W(I):NEXT
1550 DATA 0,THE WARDROBE IS NOW OPEN,,0,0
1560 DATA 0,THE WARDROBE IS CLOSED,,4,0
1570 DATA 14,THERE IS A SMALL BOAT HERE,BOAT,0,60
1580 DATA 0,THE CUPBOARD IS CLOSED,,6,0
1590 DATA 0,THE CUPBOARD HAS BEEN OPENED,,0,0
1600 DATA 10,THERE IS A KEY HERE,KEY,0,10
1610 DATA 16,THERE IS A GOLDEN SHELL HERE!,GOLDEN SHELL,0,30
1620 DATA 17,A SPADE LIES HERE,SPADE,1,35
1630 DATA 0,THE DOOR IS CLOSED,,9,0
1640 DATA 0,THE DOOR IS OPEN - REVEALING A  LARGE CUPBOARD,,0,0
1700 FORI=1TONV:READV$(I),V(I):NEXT
1710 DATA GO,1,WALK,1,RUN,1,ENTE,1,DIVE,1,SWIM,1,CLIM,1
1720 DATA GET,2,TAKE,2,STEA,2
1730 DATA DROP,3,LEAV,3,THRO,3
1740 DATA OPEN,4,UNLO,4,CLOS,5,LOCK,5
1750 DATA DIG,6,EXAM,7
1800 FORI=1TONN:READN$(I),N(I):NEXT
1810 DATA NORT,1,SOUT,2,EAST,3,WEST,4,UP,5,DOWN,6
1820 DATA LAKE,7,WARD,8,CUPB,9,KEY,10,KITC,11,LIVI,12,OUT,13
1830 DATA BOAT,14,DOOR,15,SHEL,16,GOLD,16,SPAD,17,SHOV,17
1840 DATA HOUS,18,STAI,19
1880 FORY=1TONR:FORZ=0TO12:READV:IFV=0THENNEXTY:GOTO1900
1890 GT(Y,Z)=V:NEXTZ,Y
1900 DATA 1,4,2,2,3,10,4,3,18,4,15,4,0
1910 DATA 1,1,2,2,3,2,4,3,13,1,0
1920 DATA 1,3,2,3,3,1,4,3,13,1,0
1930 DATA 2,1,3,7,4,5,13,1,8,7,0,3,4,5,6,19,6,0
1940 DATA6,5,19,5,0,3,8,4,4,13,4,0
1950 DATA 3,9,4,7,13,7,0,4,8,0,4,1,2,11,7,11,0
1960 DATA 2,12,1,10,7,10,0,1,11,2,12,3,12,4,12,13,11,0
2000 FORI=1TONM:READM$(I):NEXT
2010 DATA OKAY. YOU HAVE CLOSED IT,SOMETHING FALLS OUT
2020 DATA YOU CANNOT SWIM THE LAKE,YOU DIDN'T FIND ANYTHING
2030 DATA YOU FOUND SOMETHING!,IT'S LOCKED - YOU NEED A KEY
2040 DATA YOU CROSS THE LAKE...,IT IS NOT OPEN!
2050 DATA IT is OPEN!,IT'S NOT BIG ENOUGH,THE FLOORBOARDS ARE TOO STRONG!
2060 DATA OK. YOU'RE IN THE BOAT,OK,THE GROUND IS TOO HARD
2100 FORI=1TONR:READR$(I):NEXT
2110 DATA YOU ARE SOUTH OF A DESERTED     HOUSE. THERE IS A SMALL LAKE TO THE EAST AND FOREST ALL ROUND.  THE FRONT DOOR HANGS OFF ITS    HINGES.
2120 DATA YOU ARE IN A DARK CONFUSING     FOREST. THERE IS LIGHT TO THE   NORTH.
2125 DATA YOU ARE IN A DARK CONFUSING     FOREST. A PATH LEADS TO THE EAST
2130 DATA YOU ARE INSIDE THE LIVING ROOM. A DOOR ON THE WEST LEADS TO THE KITCHEN.  THERE IS A LARGE WARD-ROBE ON THE EAST SIDE.
2140 DATA YOU ARE IN THE KITCHEN.  STAIRS LEAD UP AND THERE IS A DOOR TO  THE EAST.
2150 DATA YOU ARE UPSTAIRS IN THE BEDROOM.THERE IS A WOODEN CUPBOARD ON   THE WALL.  STAIRS LEAD DOWN.
2160 DATA YOU ARE INSIDE THE WARDROBE. A  SMALL TUNNEL LEADS EAST SLOPING DOWNWARDS. THE EXIT FROM THIS   WARDROBE IS ON THE WEST SIDE.
2170 DATA YOU ARE IN AN EAST/WEST PASSAGE.THE CEILING GETS VERY LOW       TOWARDS THE EAST.
2180 DATA YOU ARE AT THE EASTERN END OF A LONG PASSAGE. THERE IS A SMALL  DOOR HERE.
2190 DATA YOU ARE EAST OF THE HOUSE ON THENORTH BANK OF A SMALL LAKE.
2200 DATA YOU ARE ON THE SOUTH BANK OF A  SMALL LAKE. THE SAND IS QUITE   SOFT HERE.
2210 DATA YOU ARE IN A DARK FOREST.  THEREIS LIGHT TO THE NORTH.
2300 FORI=1TO12:READIM$(I):NEXT
2310 DATA FROM,TO,IN,THRO,UNDE,OVER,AROU,AT,ON,THE,AN,A
2400 FORI=1TONW:READF$(I),T$(I):NEXT
2410 DATAN,GO NORT,S,GO SOUT,E,GO EAST,W,GO WEST,U,GO UP,D,GO DOWN
2420 DATAI,TAKE INVE,INVE,TAKE INVE,Q,QUIT
2430 CLS:RETURN
5000 REM COMMANDS
5010 F=0:ONV GOTO 5200,6000,6500,7000,7500,8000,8500
5020 PRINT"I'M SORRY I DON'T UNDERSTAND.":F=-1:RETURN
5200 IF(N=8ORN=3)ANDR=4ANDI(2)=R THENPRINTM$(8):F=-1:RETURN
5210 IFN=9ANDR=9ANDI(10)=R THENPRINTM$(10):F=-1:RETURN
5220 IFN=9ANDR=6THENPRINTM$(10):F=-1:RETURN
5230 IF(N=2ORN=7)ANDR=10ANDI(3)<>-1THENPRINTM$(3):F=-1:RETURN
5240 IF(N=2ORN=7)ANDR=10THENPRINTM$(7):RETURN
5250 IF(N=1ORN=7)ANDR=11ANDI(3)<>-1THENPRINTM$(3):F=-1:RETURN
5260 IF(N=1ORN=7)ANDR=11THENPRINTM$(7):RETURN
5270 IFN=14AND(I(3)=R ORI(3)=-1)THENI(3)=-1:PRINTM$(12):F=-1:RETURN
5280 RETURN
6000 RETURN
6500 RETURN
7000 IFN=15ANDR=9ANDI(9)=R ANDI(6)<>-1THENPRINTM$(6):F=-1:RETURN
7010 IFN=15ANDR=9ANDI(9)<>R THENPRINTM$(9):F=-1:RETURN
7020 IFN=15ANDR=9ANDI(9)=R ANDI(3)=0ANDI(6)=-1THENI(9)=0:I(10)=R:I(3)=R:F=-1:PRINTM$(13):RETURN
7030 IFN=15ANDR=9ANDI(9)=R ANDI(6)=-1THENI(9)=0:I(10)=R:F=-1:PRINTM$(13):RETURN
7040 IFN=9ANDR=6ANDI(5)=R THENPRINTM$(9):F=-1:RETURN
7050 IFN=9ANDR=6ANDI(6)=0THENI(4)=0:I(5)=R:I(6)=R:PRINTM$(2):F=-1:RETURN
7060 IFN=9ANDR=6THENI(4)=0:I(5)=R:F=-1:PRINTM$(13):RETURN
7070 IFN=8ANDI(1)=R THENPRINTM$(9):F=-1:RETURN
7080 IFN=8ANDI(2)=R THENPRINTM$(13):I(1)=R:I(2)=0:F=-1:RETURN
7090 PRINT"YOU CANNOT OPEN THAT!":F=-1:RETURN
7500 IFN=9ANDR=6ANDI(5)=R THENI(4)=R:I(5)=0:PRINTM$(1):F=-1:RETURN
7510 IFN=9ANDR=6THENPRINTM$(8):F=-1:RETURN
7520 IFN=15ANDR=9ANDI(9)=R THENPRINTM$(8):F1=-1:RETURN
7530 IFN=15THENI(10)=0:I(9)=R:PRINTM$(1):F=-1:RETURN
7540 IFN=8ANDI(2)=R THENPRINTM$(8):F=-1:RETURN
7550 IFN=8ANDI(2)<>R THENPRINTM$(1):I(1)=0:I(2)=R:F=-1:RETURN
7560 PRINT"YOU CANNOT CLOSE THAT!":F=-1:RETURN
8000 IFI(8)<>-1ANDI(8)<>R THENPRINT"DIGGING WITHOUT A SPADE IS A BITDIFFICULT!":F=-1:RETURN
8005 IFR>3ANDR<7THENPRINTM$(11):F=-1:RETURN
8010 IFR=11AND(I(8)=-1ORI(8)=R)ANDI(7)=0THENPRINTM$(5):I(7)=R:F=-1:RETURN
8020 IFR=11THENPRINTM$(4):F=-1:RETURN
8030 PRINTM$(14):F=-1:RETURN
8500 IFN=18ANDR=1THENPRINT"THE HOUSE IS SMALL AND DERELICT":F=-1:RETURN
8510 IFN=8ANDR=4THENPRINT"THE WARDROBE IS TALL, AND IS    CLOSE TO ONE WALL":F=-1:RETURN
8520 IFN=19ANDR=5THENPRINT"THE STAIRS GO UP":F=-1:RETURN
8530 IFN=19ANDR=6THENPRINT"THE STAIRS GO DOWN":F=-1:RETURN
8540 IFN=10AND(I(6)=-1OR I(6)=R)THENPRINT"IT IS A SMALL BRASS KEY":F=-1:RETURN
8550 IFN=14AND(I(3)=R ORI(3)=-1)THENPRINT"IT IS A SMALL WOODEN BOAT  - IN GOOD CONDITION":F=-1:RETURN
8560 IFN=15ANDR=9THENPRINT"THE DOOR IS OLD BUT STURDY":F=-1:RETURN
8570 IFN=7AND(R=10OR R=11)THENPRINT"THE LAKE IS TOO DEEP FOR SWIM-  MING.":F=-1:RETURN
8580 IFN=16AND(I(7)=R ORI(7)=-1)THENPRINT"THE GOLDEN SHELL LOOKS VALUABLE!":F=-1:RETURN
8590 IFN=17AND(I(8)=R ORI(8)=-1)THENPRINT"IT IS A NORMAL SPADE":F=-1:RETURN
8600 RR=-1:RETURN
9000 REM TRANSLATE I$
9010 FORI=1TONW:IFI$=F$(I)THENI$=T$(I):RETURN
9020 NEXT:RETURN
30100 I8=-1:FORZ=1TONI:IFI(Z)<>-1THEN30150
30120 IFI8 THENI8=0:PRINT"YOU HAVE:-"
30130 I9=0:PRINTI2$(Z)
30150 NEXT
30160 IFI8 THENPRINT"YOU HAVE NOTHING AT ALL!"
30170 RETURN
30200 REM PICKUP/DROP ITEM
30205 F=0:FORZ=1TONI
30210 IFG(Z)<>N THEN30280
30220 IFV<>2THEN30260
30230 IFI(Z)<>R THEN30280
30240 GOSUB30300:IFI THENPRINT"YOU CANNOT CARRY ANY MORE!":RETURN
30250 I(Z)=-1:PRINT"OK, YOU'VE GOT THE ";:IFPOS(0)+LEN(I2$(Z))>32THENPRINT
30255 PRINTI2$(Z):F=1:GOTO30280
30260 IFI(Z)<>-1THEN30280
30270 I(Z)=R:PRINT"OK. YOU'VE DROPPED THE ";:IFPOS(0)+LEN(I2$(Z))>32THENPRINT
30275 PRINTI2$(Z):F=1
30280 NEXT
30285 IFF=1THENRETURN
30290 IFV=2THENPRINT"YOU AREN'T CAPABLE OF THAT.":RETURN
30295 PRINT"YOU DON'T HAVE IT!":RETURN
30300 REM WEIGHT CHECK
30305 I=0:T=0:FORY=1TONI:IFI(Y)=-1THENT=T+W(Y)
30310 NEXT:T=T+W(Z):IFT>100THENI=-1
30330 RETURN
30600 REM GO
30605 FORZ=0TO12STEP2:IFGT(R,Z)=0THEN30690
30610 IFGT(R,Z)=N THEN30660
30620 NEXT:GOTO30690
30660 R=GT(R,Z+1):RETURN
30690 PRINT"YOU CANNOT TRAVEL IN THAT DIR-  ECTION.":RETURN
30700 REM SAVE GAME
30710 PRINT"PLEASE PRESS ANY KEY WHEN YOUR  CASSETTE RECORDER IS READY"
30715 IFINKEY$=""THEN30715
30720 I(13)=R:I(14)=RR:I(15)=NT
30740 CSAVE*I,"SHELDATA"
30750 PRINT"FINISHED!!"
30760 RETURN
30800 PRINT"PLEASE PLAYBACK THE CASSETTE    CONTAINING YOUR SAVED GAME"
30810 CLOAD*I,"SHELDATA"
30820 R=I(13):RR=I(14):NT=I(15)
30840 RETURN
31200 PRINT:PRINT"THAT'S THE END OF THE GAME      WOULD YOU LIKE TO PLAY AGAIN?"
31210 Z$=INKEY$:IFA$=""THEN31210
31220 IFZ$="N"THENEND
31230 IFZ$<>"Y"THEN31210
31240 RUN
31400 REM FIND VERB/NOUN
31410 FORZ=1TONV:IFV$(Z)<>V$THENNEXT:V=0:GOTO31450
31420 V=V(Z)
31450 FORZ=1TONN:IFN$(Z)<>N$THENNEXT:N=0:RETURN
31460 N=N(Z):RETURN
31500 REM TURN I$ INTO V$/N$
31505 Y=1:V$="":N$=""
31510 GOSUB31600:IFZ$=""THEN31580
31520 GOSUB31700:IFZ$=""THEN31510
31530 V$=Z$
31540 GOSUB31600:IFZ$=""THEN31580
31550 GOSUB31700:IFZ$=""THEN31540
31560 N$=Z$
31580 RETURN
31600 REM EXTRACT WORD
31610 IFMID$(I$,Y,1)<>" "THEN31630
31620 Y=Y+1:IFY<=LEN(I$)THEN31610
31625 Z$="":RETURN
31630 K=Y
31640 Y=Y+1:IFY>LEN(I$) THEN31660
31650 IFMID$(I$,Y,1)<>" "THEN31640
31660 Z$=MID$(I$,K,Y-K):RETURN
31700 Z$=LEFT$(Z$,4)
31705 FORZ=1TO12:IFZ$=IM$(Z)THENZ$=""
31710 NEXT:RETURN
