100 DIM A(17,10),P(5),O1$(11),O2$(11),M$(27),T(5),ROOM$(17),IN$(11),TR(11)
190 GOSUB 4230:REM INITIALIZE
200 LOCATE 21,1
210 PRINT "ESCAPE FROM S.S.A.D.B, VERSION 1.0"
220 PRINT "MENCO, INC."
230 PRINT:GOSUB 3240:REM ROOM DESCRIPTION
240 GOSUB 1900:REM OBJECTS IN ROOM?
250 REM ** INPUT HANDLING **
260 KW=0
270 IF SIREN=1 AND SET=0 THEN MV=1:SET=1
280 IF SET=1 THEN MV=MV+1:IF MV=3 AND RO=6 AND BOX=0 THEN PRINT:PRINT "A WORKER COMES RUNNING IN AND GLANCES AROUND. HE SEES YOU AND CALLS THE GUARDS":PRINT "WHO TAKE YOU AWAY AND TORTURE YOU.":GOTO 3140
290 IF MV=3 AND RO=6 AND BOX=1 THEN PRINT:PRINT "A WORKER COMES IN AND GLANCES AROUND. HE GLANCES AROUND AND NOT NOTICING":PRINT "ANYTHING, HE HEADS TO THE BALCONY.":TR(11)=7
300 IF MV=3 THEN TR(11)=7
310 PRINT:INPUT ">",A$
320 IF A$="" THEN PRINT:PRINT "WHAT?":GOTO 270
330 AS=ASC(A$)
340 IF AS<65 OR AS>90 THEN PRINT:PRINT "CAPITAL LETTERS PLEASE.":GOTO 270
350 M=LEN(A$):IF M<7 THEN A$=A$+" ":GOTO 350
360 B$=LEFT$(A$,3)
370 IF B$="INV" OR B$="I  " THEN GOSUB 730:GOTO 270
380 IF B$="SCO" THEN GOSUB 820:GOTO 270
390 IF B$="QUI" OR B$="Q  " THEN GOSUB 850:GOTO 270
400 IF B$="HEL" OR B$="CLU" THEN PRINT:PRINT "SORRY, THERE IS NO HELP FOR LOST AGENTS IN THIS GAME.":GOTO 270
410 IF B$="HID" THEN GOSUB 2360:GOTO 270
420 IF B$="STA" THEN GOSUB 2410:GOTO 270
430 IF B$="LOO" OR B$="L  " THEN GOTO 230
440 IF B$="N  " THEN C$="N  ":GOSUB 920:GOTO 270
450 IF B$="S  " THEN C$="S  ":GOSUB 920:GOTO 270
460 IF B$="E  " THEN C$="E  ":GOSUB 920:GOTO 270
470 IF B$="W  " THEN C$="W  ":GOSUB 920:GOTO 270
480 IF B$="U  " THEN C$="U  ":GOSUB 920:GOTO 270
490 IF B$="D  " THEN C$="D  ":GOSUB 920:GOTO 270
500 N=1
510 IF MID$(A$,N,1)=" " THEN C$=MID$(A$,N+1,3):IF LEFT$(C$,1)<>" " THEN 540 ELSE 530
520 IF N<M THEN N=N+1:GOTO 510
530 PRINT:PRINT "IN THAT FORM, I DON'T KNOW THE WORD ";A$:GOTO 270
540 IF B$="GO " OR B$="MOV" OR B$="WAL" THEN KW=1:GOSUB 920:GOTO 270
550 IF B$="EXA" OR B$="SEA" THEN GOSUB 2590:GOTO 270
560 IF B$="TAK" OR B$="GET" THEN KW=1:GOSUB 1600:GOTO 270
570 IF B$="DRO" OR B$="PUT" THEN KW=1:GOSUB 1140:GOTO 270
580 IF B$="UNL" THEN KW=1:GOSUB 2110:GOTO 270
590 IF B$="LOC" THEN KW=1:GOSUB 2440:GOTO 270
600 IF B$="OPE" THEN KW=1:GOSUB 1280:GOTO 270
610 IF B$="CLO" THEN KW=1:GOSUB 2510:GOTO 270
620 IF B$="REA" THEN KW=1:GOSUB 1440:GOTO 270
630 IF B$="PUS" OR B$="PRE" THEN KW=1:GOSUB 2260:GOTO 270
640 IF B$="PUL" OR B$="UNP" THEN KW=1:GOSUB 2980:GOTO 270
650 IF B$="TYP" THEN KW=1:GOSUB 3030:GOTO 270
660 IF B$="POU" OR B$="SPR" THEN KW=1:GOSUB 2910:GOTO 270
670 IF B$="INS" THEN KW=1:GOSUB 1830:GOTO 270
680 IF B$="KIC" OR B$="HIT" THEN KW=1:GOSUB 1970:GOTO 270
690 IF B$="MIX" OR B$="COM" THEN KW=1:GOSUB 2800:GOTO 270
700 IF B$="THR" THEN KW=1:GOSUB 2190:GOTO 270
710 PRINT:PRINT "I DON'T KNOW THAT WORD.":GOTO 270
720 REM **** BEGIN SUBROUTINES ****
730 REM ** INVENTORY **
740 PRINT:PRINT "  YOU ARE CARRYING:"
750 PRINT
760 FOR I=1 TO 8
770 IF TR(I)=200 THEN PRINT "    ";IN$(I):AADS=1
780 NEXT I
790 IF AADS<>1 THEN PRINT "    NOTHING"
800 AADS=0
810 RETURN
820 REM ** SCORE **
830 PRINT:PRINT "OUT OF 200 POINTS, YOU GOT ";SCORE" POINTS."
840 RETURN
850 REM ** QUITING **
860 PRINT:PRINT "OUT OF 200 POINTS, YOU GOT "SCORE" POINTS."
870 PRINT
880 INPUT "ARE YOU SURE YOU WANT TO QUIT";F$
890 IF F$="" THEN 870
900 IF F$="Y" OR F$="F" OR F$="YES" THEN END
910 PRINT:PRINT "OK.":GOTO 270
920 REM ** MOVEMENT COMMAND **
930 C$=LEFT$(C$,1)
940 IF RO=10 AND GUARD=1 THEN PRINT:PRINT "AS YOU TRY TO LEAVE THE GUARD SPIES YOU AND CAPTURES YOU. HE ALERTS OTHERS":PRINT "AND SOON YOU ARE TORTURED AND KILLED.":GOTO 3140
950 IF RO=9 AND C$="S" AND PLUG=0 THEN PRINT:PRINT "AS YOU STEP INTO THE RECORD ROOM, THE CAMERA SPIES YOU AND ALERTS THE GUARDS.":PRINT "THEY TAKE YOU AWAY AND TORTURE YOU WHICH EVENTUALLY KILLS YOU.":GOTO 3140
960 IF RO=13 AND C$="S" AND TR(11)=16 THEN PRINT:PRINT "AS YOU STEP INTO THE MISSLE SITE, A WORKER WORKING ON THE MX-13 MISSLE":PRINT "SEES YOU AND CALLS FOR GUARDS. THEY TAKE YOU AWAY AND TORTURE YOU. YOU":PRINT "DIE IN THEIR HANDS.":GOTO 3140
970 IF RO=6 AND BOX=1 THEN PRINT:PRINT "YOU CAN'T GO THAT WAY WHILE YOU ARE CROUCHING IN THE BOX.":RETURN
980 IF RO=7 AND TR(11)=7 AND C$="W" THEN PRINT:PRINT "AS YOU TRY TO LEAVE, THE WORKER GRABS YOU AND CALLS FOR THE GUARDS. WHEN THE":PRINT "GUARDS COME, THEY TAKE YOU AWAY TO BE TORTURED. THE END!":GOTO 3140
990 IF C$="N" AND A(RO,1)=0 THEN PRINT:PRINT "YOU CAN'T GO THAT WAY.":RETURN
1000 IF C$="S" AND A(RO,2)=0 THEN PRINT:PRINT "YOU CAN'T GO THAT WAY.":RETURN
1010 IF C$="E" AND A(RO,3)=0 THEN PRINT:PRINT "YOU CAN'T GO THAT WAY.":RETURN
1020 IF C$="W" AND A(RO,4)=0 THEN PRINT:PRINT "YOU CAN'T GO THAT WAY.":RETURN
1030 IF C$="U" AND A(RO,5)=0 THEN PRINT:PRINT "YOU CAN'T GO THAT WAY.":RETURN
1040 IF C$="D" AND A(RO,6)=0 THEN PRINT:PRINT "YOU CAN'T GO THAT WAY.":RETURN
1050 IF C$="N" THEN RO=A(RO,1)
1060 IF C$="S" THEN RO=A(RO,2)
1070 IF C$="E" THEN RO=A(RO,3)
1080 IF C$="W" THEN RO=A(RO,4)
1090 IF C$="U" THEN RO=A(RO,5)
1100 IF C$="D" THEN RO=A(RO,6)
1110 IF RO=3 AND SOP1=0 THEN SCORE=SCORE+10:SOP1=1:GOSUB 3100
1120 IF RO=10 AND DSF=0 THEN SCORE=SCORE+30:DSF=1:GOSUB 3100
1130 RETURN 230
1140 REM ** DROP ROUTINE **
1150 FLAG=0:OBJECT=0
1160 PRINT
1170 FOR I=1 TO 8
1180 IF TR(I)=200 THEN FLAG=1
1190 NEXT I
1200 IF FLAG=0 THEN PRINT "YOU AREN'T CARRYING ANYTHING.":RETURN
1210 FOR I=1 TO 8
1220 IF C$=O1$(I) AND TR(I)=200 THEN OBJECT=I
1230 NEXT I
1240 IF OBJECT=0 THEN PRINT "YOU'RE NOT HOLDING THAT ITEM.":RETURN
1250 TR(OBJECT)=RO
1260 PRINT "OK."
1270 RETURN
1280 REM ** OPEN ROUTINE **
1290 IF C$<>"DOO" AND C$<>"CAB" AND C$<>"VAU" THEN PRINT:PRINT "YOU CAN'T OPEN THAT.":RETURN
1300 IF C$<>"DOO" THEN 1360
1310 IF C$="DOO" AND DOOR=0 AND RO=15 THEN PRINT:PRINT "THE DOOR IS LOCKED.":RETURN
1320 IF C$="DOO" AND DOOR=1 AND RO=15 THEN PRINT:PRINT "THE DOOR SWINGS OPEN, REVEALING A PASSAGEWAY TO THE NORTH.":DOOR=2:A(10,2)=15:A(15,1)=10:RETURN
1330 IF C$="DOO" AND DOOR=1 AND RO=10 THEN PRINT:PRINT "THE DOOR SWINGS OPEN, REVEALING A PASSAGEWAY TO THE SOUTH.":DOOR=2:A(10,2)=15:A(15,1)=10:RETURN
1340 IF C$="DOO" AND DOOR=2 AND RO=15 OR RO=10 THEN PRINT:PRINT "THE DOOR IS ALREADY OPEN.":RETURN
1350 PRINT:PRINT "YOU CAN'T SEE A DOOR HERE.":RETURN
1360 IF C$<>"VAU" THEN 1400
1370 IF C$="VAUL" AND VAULT=0 AND RO=1 THEN PRINT "YOU HAVE TO TYPE IN THE CORRECT CODE FOR THE VAULT TO OPEN.":RETURN
1380 IF C$="VAUL" AND VAULT=1 AND RO=1 THEN PRINT "IT'S ALREADY OPEN.":RETURN
1390 PRINT:PRINT "YOU CAN'T SEE THE VAULT HERE.":RETURN
1400 IF C$="CAB" AND CAB=0 AND RO=14 THEN PRINT "THE CABINET IS NOW OPEN.":CAB=1:TR(7)=14:RETURN
1410 IF C$="CAB" AND CAB=1 AND RO=14 THEN PRINT "THE CABINET IS ALREADY OPEN.":RETURN
1420 PRINT:PRINT "YOU CAN'T SEE A CABINET HERE.":RETURN
1430 RETURN
1440 REM ** READ ROUTINE **
1450 FLAG=0
1460 PRINT
1470 IF C$<>"PLA" THEN 1500
1480 IF RO<>6 THEN PRINT "YOU DON'T SEE THE PLAQUE HERE.":RETURN
1490 PRINT "THE PLAQUE READS: 'PRESS IN CASE OF EMERGENCY.'":RETURN
1500 FOR I=1 TO 8
1510 IF C$=O1$(I) AND TR(I)=200 THEN FLAG=1
1520 NEXT
1530 IF FLAG=0 THEN PRINT "YOU DON'T HAVE THAT ITEM.":RETURN
1540 HH$=LEFT$(C$,3)
1550 IF HH$<>"NOT" AND HH$<>"NEW" AND HH$<>"SLI" AND HH$<>"FIL" THEN PRINT "YOU CAN'T READ THAT!":RETURN
1560 IF HH$="NEW" AND TR(2)=200 THEN PRINT "THE NEWSPAPER READS: 'SIGNS OF MX-13 MISSLES BEING SHIPPED TO RUSSIA...' IT":PRINT "GOES ON BUT YOU CAN'T MAKE ANYMORE OUT.":RETURN
1570 IF HH$="SLI" AND TR(6)=200 THEN PRINT "THE SLIP READS: 'PORCH NEEDS FIXING AND NEW CODE IS '971'.'":RETURN
1580 IF HH$="FIL" AND TR(7)=200 THEN PRINT "THE FILE READS: 'TOP-SECRET INFORMATION ON MX-13 AND DEPLOYMENT.'":RETURN
1590 IF HH$="NOT" AND TR(3)=200 THEN PRINT "THE NOTE READS: 'NEW SCIENTIFIC BREAKTHROUGH: ANALGESIC AND LIQUID IN BEAKER":PRINT "MAKES GREAT ACID!'":RETURN
1600 REM ** GET ROUTINE **
1610 PRINT
1620 IF C$="GUA" THEN PRINT "YOU CAN'T GET THAT.":RETURN
1630 IF C$="WOR" THEN PRINT "YOU CAN'T GET THAT.":RETURN
1640 FLAG=0:HHG=0:OBJECT=0
1650 FOR I=1 TO 8
1660 IF TR(I)=200 THEN HHG=HHG+1
1670 NEXT
1680 IF HHG>=6 THEN PRINT "YOU CAN'T CARRY ANYMORE.":RETURN
1690 FOR I=1 TO 8
1700 IF TR(I)=RO OR TR(2)=220 THEN FLAG=1
1710 NEXT I
1720 IF FLAG=0 THEN PRINT "THERE IS NOTHING HERE TO PICK UP.":RETURN
1730 IF TR(2)=220 AND C$="NEW" AND TR(4)=220 THEN OBJECT=2:INSERT=0:TR(4)=15:GOTO 1790
1740 IF TR(2)=220 AND C$="NEW" THEN OBJECT=2:INSERT=0:GOTO 1790
1750 FOR I=1 TO 8
1760 IF C$=O1$(I) AND TR(I)=RO THEN OBJECT=I
1770 NEXT
1780 IF OBJECT=0 THEN PRINT "I DON'T SEE THAT OBJECT HERE.":RETURN
1790 TR(OBJECT)=200
1800 PRINT "OK.":IF TR(7)=200 AND SOP=0 THEN SCORE=SCORE+50:SOP=1:GOSUB 3100
1810 IF OBJECT=4 AND DGF=0 THEN SCORE=SCORE+20:DGF=1:GOSUB 3100
1820 RETURN
1830 REM ** INSERT ROUTINE **
1840 IF C$<>"NEW" THEN PRINT:PRINT "YOU CAN'T INSERT THAT.":RETURN
1850 INPUT "  UNDER WHAT";F$:F$=LEFT$(F$,3)
1860 IF F$<>"DOO" THEN PRINT:PRINT "YOU CAN'T INSERT THE NEWSPAPER UNDER THAT.":RETURN
1870 IF RO<>15 THEN PRINT:PRINT "YOU CAN'T SEE A DOOR HERE.":RETURN
1880 PRINT:PRINT "OK.":INSERT=1:TR(2)=220
1890 RETURN
1900 REM ** OBJECTS IN ROOM? **
1910 PRINT
1920 FOR I=1 TO 11
1930 IF TR(I)=RO THEN PRINT O2$(I)
1940 NEXT I
1950 RETURN
1960 END
1970 REM ** HIT ROUTINE **
1980 PRINT
1990 IF RO<>15 AND RO<>7 THEN PRINT "I CAN'T USE THAT WORD HERE.":RETURN
2000 IF C$<>"DOO" AND C$<>"WOR" THEN PRINT "YOU CAN'T HIT THAT.":RETURN
2010 IF C$="WOR" THEN GOTO 2080
2020 IF RO=15 AND C$="DOO" AND KEE=0 THEN PRINT "YOU HEAR A SOUND FROM THE OTHER SOUND OF THE DOOR.":KEE=1:GOTO 2040
2030 IF RO=15 AND C$="DOO" AND KEE=1 THEN PRINT "IT DOESN'T WORK THIS TIME.":RETURN
2040 IF INSERT=0 THEN PRINT:PRINT "SORRY, BECAUSE OF THAT LAST ACTION, THERE IS NO WAY FOR YOU TO PROCEED.":GOTO 3140
2050 IF INSERT=1 THEN PRINT:PRINT "THERE IS NOW SOMETHING ON THE NEWSPAPER."
2060 TR(4)=220
2070 RETURN
2080 IF RO=7 AND C$="WOR" AND WORKER=0 THEN PRINT "THE WORKER FALLS OVER THE PORCH, YELLING. AS HE FALLS, SOMETHING FALLS OUT":PRINT "OF THE WORKER'S POCKET":TR(8)=7:TR(11)=255:WORKER=1:RETURN
2090 IF RO=7 AND C$="WOR" AND WORKER=1 THEN PRINT "THE WORKER IS DEAD, BECAUSE OF YOU!":RETURN
2100 RETURN
2110 REM ** UNLOCK ROUTINE **
2120 PRINT
2130 IF RO<>15 AND RO<>10 THEN PRINT "I CAN'T USE THAT WORD HERE.":RETURN
2140 IF C$<>"DOO" THEN PRINT "I CAN'T UNLOCK THAT.":RETURN
2150 IF TR(4)<>200 THEN PRINT "YOU HAVE NOTHING TO UNLOCK THE DOOR WITH.":RETURN
2160 IF DOOR=0 THEN PRINT "THE DOOR IS NOW UNLOCKED.":DOOR=1:GOTO 2180
2170 IF DOOR=1 OR DOOR=2 THEN PRINT "THE DOOR IS ALREADY UNLOCKED."
2180 RETURN
2190 REM ** THROW ROUTINE **
2200 IF RO<>10 THEN PRINT:PRINT "I CAN'T USE THAT WORD HERE.":RETURN
2210 IF C$<>"BRI" THEN PRINT:PRINT "YOU CAN'T THROW THAT.":RETURN
2220 INPUT "  AT WHAT";F$:F$=LEFT$(F$,3)
2230 IF F$="GUA" AND GUARD=1 THEN PRINT:PRINT "YOU HIT THE GUARD IN THE BACK OF THE HEAD WITH THE BRICK AND HE BECOMES":PRINT "UNCONSICOUS.":TR(9)=255:TR(10)=10:GUARD=0:TR(1)=10:SCORE=SCORE+20:GOSUB 3100:RETURN
2240 IF F$="GUA" AND GUARD=0 THEN PRINT:PRINT "THE GUARD IS ALREADY UNCONSICOUS.":RETURN
2250 PRINT "YOU CAN'T THROW THE BRICK AT THAT.":RETURN
2260 REM ** PUSH ROUTINE **
2270 IF RO<>6 AND RO<>11 AND RO<>12 AND RO<>13 THEN PRINT:PRINT "I CAN'T USE THAT WORD HERE.":RETURN
2280 PRINT
2290 IF C$="ONE" THEN PRINT "THE ELEVATOR DOORS CLOSE...":FOR I=1 TO 100:NEXT:PRINT "THEN THEY OPEN....":RO=11:RETURN
2300 IF C$="TWO" THEN PRINT "THE ELEVATOR DOORS CLOSE...":FOR I=1 TO 100:NEXT:PRINT "THEN THEY OPEN....":RO=12:RETURN
2310 IF C$="THR" THEN PRINT "THE ELEVATOR DOORS CLOSE...":FOR I=1 TO 100:NEXT:PRINT "THEN THEY OPEN....":RO=13:RETURN
2320 IF C$="BUT" AND RO=6 AND GHT=0 THEN PRINT "A SIREN SOUNDS AND A WORKER WILL BE IN HERE ANY MINUTE. THE BUTTON GOES IN SO":PRINT "YOU CAN'T PRESS IT AGAIN.":SIREN=1::GHT=1:RETURN
2330 IF C$="BUT" AND RO=6 AND GHT=1 THEN PRINT "YOU CAN'T PUSH THE BUTTON AGAIN.":RETURN
2340 PRINT "YOU CAN'T PUSH THAT.":RETURN
2350 RETURN
2360 REM ** HIDE ROUTINE **
2370 IF RO<>6 THEN PRINT:PRINT "I CAN'T USE THAT WORD HERE.":RETURN
2380 INPUT "  IN WHAT";F$:F$=LEFT$(F$,3)
2390 IF F$="BOX" THEN PRINT:PRINT "OK, YOU ARE NOW CROUCHING IN THE BOX.":BOX=1:RETURN
2400 PRINT:PRINT "YOU CAN'T HIDE IN THERE.":RETURN
2410 REM ** STAND ROUTINE **
2420 IF BOX=0 THEN PRINT:PRINT "YOU ARE ALREADY STANDING.":RETURN
2430 IF BOX=1 THEN PRINT:PRINT "OK, YOU ARE NOW STANDING OUTSIDE THE BOX.":BOX=0:RETURN
2440 REM ** LOCK ROUTINE **
2450 IF RO<>10 AND RO<>15 THEN PRINT:PRINT "I CAN'T USE THAT WORD HERE.":RETURN
2460 IF C$<>"DOO" THEN PRINT:PRINT "YOU CAN'T LOCK THAT.":RETURN
2470 IF TR(4)<>200 THEN PRINT:PRINT "YOU HAVE NOTHING TO LOCK THE DOOR WITH.":RETURN
2480 IF DOOR=0 THEN PRINT:PRINT "THE DOOR IS ALREADY LOCKED.":RETURN
2490 IF DOOR=1 OR DOOR=2 THEN PRINT:PRINT "OK, THE DOOR IS NOW CLOSED AND LOCKED.":DOOR=0:A(15,1)=0:A(10,2)=0:RETURN
2500 RETURN
2510 REM ** CLOSE ROUTINE **
2520 IF C$<>"DOO" AND C$<>"CAB" THEN PRINT:PRINT "YOU CAN'T CLOSE THAT.":RETURN
2530 IF C$="DOO" AND DOOR=2 AND RO=15 THEN PRINT:PRINT "THE DOOR IS NOW CLOSED.":DOOR=1:A(10,2)=0:A(15,1)=0:RETURN
2540 IF C$="DOO" AND DOOR=2 AND RO=10 THEN PRINT:PRINT "THE DOOR IS NOW CLOSED.":DOOR=1:A(15,1)=0:A(10,2)=0:RETURN
2550 IF C$="DOO" AND DOOR=0 OR DOOR=1 AND RO=10 OR RO=15 THEN PRINT:PRINT "THE DOOR IS ALREADY CLOSED.":RETURN
2560 IF C$="CAB" AND CAB=1 AND RO=14 THEN PRINT:PRINT "THE CABINET IS NOW CLOSED.":RETURN
2570 IF C$="CAB" AND CAB=0 AND RO=14 THEN PRINT:PRINT "THE CABINET IS ALREADY CLOSED.":RETURN
2580 PRINT "YOU CAN'T CLOSE THAT HERE.":RETURN
2590 REM ** EXAMINE ROUTINE **
2600 PRINT
2610 IF C$="DOO" AND RO=15 OR RO=10 THEN PRINT "THE LARGE, WOODEN DOOR HAS A KEYHOLE AND A SMALL SPACE BELOW IT. THROUGH":PRINT "THE KEYHOLE YOU CAN SEE A KEY, UNREACHABLE FROM THIS SIDE.":RETURN
2620 IF C$="BRI" AND TR(1)=200 THEN PRINT "THE BRICK IS JUST A NORMAL, HEAVY BRICK.":RETURN
2630 IF C$="NEW" AND TR(2)=200 THEN PRINT "THE NEWSPAPER JUST HAS SOME READABLE WRITING ON IT.":RETURN
2640 IF C$="NOT" AND TR(3)=200 THEN PRINT "THE NOTE JUST HAS SOME WRITING ON IT.":RETURN
2650 IF C$="KEY" AND TR(4)=200 THEN PRINT "I SEE NOTHING SPECIAL ABOUT THAT.":RETURN
2660 IF C$="BEA" AND TR(5)=200 THEN PRINT "I SEE NOTHING SPECIAL ABOUT THAT.":RETURN
2670 IF C$="SLI" AND TR(6)=200 THEN PRINT "THE SLIP OF PAPER JUST HAS SOME WRITING ON IT.":RETURN
2680 IF C$="FIL" AND TR(7)=200 THEN PRINT "THE FILE CONTAINS TOP-SECRET INFORMATION.":RETURN
2690 IF C$="ASP" AND TR(8)=200 THEN PRINT "THE ASPIRIN HAS 'BUFFERIN' ON IT.":RETURN
2700 IF C$="GUA" AND TR(9)=RO THEN PRINT "THE GUARD IS A HARD WORKING EMPLOYEE OF THE RUSSIANS.":RETURN
2710 IF C$="GUA" AND TR(10)=RO THEN PRINT "THE GUARD IS UNCONSICOUS.":RETURN
2720 IF C$="WOR" AND TR(11)=RO THEN PRINT "THE WORKER HAS SOMETHING IN HIS BACK POCKET.":RETURN
2730 IF C$="PLA" AND RO=6 THEN PRINT "THE PLAQUE HAS WRITING ON IT.":RETURN
2740 IF C$="BOX" AND RO=6 THEN PRINT "THE BOX IS BIG ENOUGH FOR YOU TO HIDE IN IT.":RETURN
2750 IF C$="CAB" AND RO=14 AND CAB=1 THEN PRINT "THE CABINET IS OPEN.":RETURN
2760 IF C$="CAB" AND RO=14 AND CAB=0 THEN PRINT "THE CABINET IS CLOSED.":RETURN
2770 IF C$="PLU" AND RO=2 AND PLUG=0 THEN PRINT "THE PLUG IS CONNECTED TO AN OUTLET.":RETURN
2780 IF C$="PLU" AND RO=2 AND PLUG=1 THEN PRINT "THE PLUG IS NOT CONNECTED TO AN OUTLET.":RETURN
2790 PRINT "I CAN'T EXAMINE THAT NOW.":RETURN
2800 REM ** MIX ROUTINE **
2810 IF C$<>"ASP" AND C$<>"BEA" THEN PRINT:PRINT "I CAN'T MIX THAT.":RETURN
2820 IF C$="BEA" THEN GOTO 2870
2830 IF C$="ASP" AND TR(8)=200 THEN INPUT "  IN WHAT";F$:F$=LEFT$(F$,3)
2840 IF F$="BEA" AND TR(5)=200 THEN PRINT:PRINT "THE BEAKER BEGINS TO BUBBLE AND INSIDE THERE IS AN ACIDY SOLUTION.":TR(8)=255:BEAKER=1:RETURN
2850 IF F$="BEA" THEN PRINT:PRINT "YOU DON'T HAVE THE BEAKER.":RETURN
2860 PRINT:PRINT "YOU CAN'T MIX THE ASPIRIN WITH THAT.":RETURN
2870 INPUT "  WITH WHAT";F$:F$=LEFT$(F$,3)
2880 IF F$="ASP" AND TR(8)=200 THEN PRINT:PRINT "THE BEAKER BEGINS TO BUBBLE AND INSIDE THERE IS AN ACIDY SOLUTION.":BEAKER=1:TR(8)=255:RETURN
2890 IF F$="ASP" THEN PRINT:PRINT "YOU DON'T HAVE THE ASPIRIN.":RETURN
2900 PRINT:PRINT "YOU CAN'T MIX THAT WITH THE BEAKER.":RETURN
2910 REM ** POUR ROUTINE **
2920 PRINT
2930 IF RO<>8 THEN PRINT:PRINT "I CAN'T USE THAT WORD HERE.":RETURN
2940 IF C$="BEA" OR C$="LIQ" AND BEAKER=0 THEN PRINT:PRINT "THE LIQUID IS STUCK INSIDE (FOR NOW).":RETURN
2950 IF C$="BEA" OR C$="LIQ" AND BEAKER=1 THEN INPUT "  ON WHAT";F$:F$=LEFT$(F$,3)
2960 IF F$="WAL" OR F$="NOR" THEN PRINT:PRINT "THE WALL TO THE NORTH, BECOMES A GIANT HOLE, ALLOWING YOU TO GO THAT WAY.":A(8,1)=3:TR(5)=255:RETURN
2970 PRINT:PRINT "YOU CAN'T POUR THE LIQUID ON THAT.":RETURN
2980 REM ** PULL ROUTINE **
2990 IF RO<>2 THEN PRINT:PRINT "I CAN'T USE THAT WORD HERE.":RETURN
3000 IF C$<>"PLU" THEN PRINT:PRINT "YOU CAN'T PULL THAT.":RETURN
3010 IF PLUG=1 THEN PRINT:PRINT "THE PLUG IS ALREADY UNPLUGGED.":RETURN
3020 PRINT "OK, THE PLUG IS NOW UNPLUGGED.":PLUG=1:RETURN
3030 REM ** TYPE ROUTINE **
3040 IF RO<>1 THEN PRINT:PRINT "I CAN'T USE THAT WORD HERE.":RETURN
3050 IF C$<>"971" THEN PRINT:PRINT "YOU TYPE IN THE WRONG CODE AND A LOUD SIREN  SOUNDS. GUARDS RUSH IN AND TAKE":PRINT "YOU AWAY. YOU DON'T RETURN.":GOTO 3100
3060 PRINT:PRINT "CONGRADULATIONS! YOU GOT THE RIGHT CODE. YOU RUSH OUTSIDE TO FIND THAT YOU ARE":PRINT "INSIDE A LARGE SEWER SYSTEM. BUT, ABOVE YOU SEE A MAN HOLE, SO YOU CLIMB OUT.":PRINT "YOU RUSH TO TELL THE POLICE YOUR STORY."
3070 IF TR(7)=200 THEN PRINT "YOU SHOW THE POLICE THE FILE AND YOU ARE REWARDED WITH A HERO'S REWARD!":SCORE=SCORE+70:GOSUB 3100:GOTO 3140
3080 PRINT "YOU HAVE NO PROOF TO SHOW THE POLICE, SO THEY LOCK YOU UP. IN EXACTLY FIVE":PRINT "DAYS, THE SOVIETS LAUNCH THEIR SUPPLY OF MX-13 MISSLES ON THE U.S. ALL":PRINT "LIFE AS YOU KNOW IT, STOPS TO EXIST. THE HUMAN RACE IS NEVER HEARD OF AGAIN."
3090 GOTO 3140
3100 REM ** SCORE ROUTINE **
3110 COLOR 0,7
3120 LOCATE 25,40:PRINT "SCORE:";SCORE
3130 COLOR 7,0:RETURN
3140 REM ** SCORING **
3150 PRINT:PRINT "OUT OF 200 POINTS, YOU GOT ";SCORE;" POINTS."
3160 PRINT:INPUT "WOULD YOU LIKE TO PLAY AGAIN";F$:F$=LEFT$(F$,1)
3170 IF F$="Y" OR F$="Y" THEN GOTO 3210
3180 IF F$="N" OR F$="N" THEN PRINT:PRINT "OK.":PRINT:PRINT:END
3190 PRINT "PLEASE ANSWER THE QUSETION."
3200 GOTO 3160
3210 RO=17:SOP=0:SOP1=0:DOOR=0:KEE=0:SCORE=0:GUARD=0:BOX=0:BEAKER=0:DSF=0:DGF=0
3220 TR(1)=17:TR(2)=17:TR(3)=8:TR(4)=255:TR(5)=8:TR(6)=5:TR(7)=255:TR(8)=16
3230 CLS:GOTO 200
3240 REM ** ROOM DESCRIPTIONS **
3250 COLOR 0,7
3260 LOCATE 25,1:PRINT "                                                                               "
3270 LOCATE 25,1:PRINT ROOM$(RO)
3280 LOCATE 25,40:PRINT "SCORE:";SCORE
3290 COLOR 7,0
3300 ON RO GOSUB 3320,3380,3450,3500,3550,3600,3660,3700,3750,3790,3840,3890,3940,3990,4040,4100,4160
3310 RETURN
3320 REM ROOM ONE
3330 PRINT "VAULT ROOM"
3340 PRINT "YOU ARE INSIDE A VERY LARGE ROOM WHICH IS THE EXIT TO THE OUTSIDE WORLD. BUT"
3350 PRINT "BEFORE EVERY PERSON GETS OUTSIDE, THEY MUST PASS THROUGH A GIANT, METAL VAULT"
3360 PRINT "WHICH HAS A LARGE KEYBOARD ATTACHED TO IT."
3370 RETURN
3380 REM ROOM TWO
3390 PRINT "MAINTENCE ROOM"
3400 PRINT "THIS IS A ROOM WHERE MAINTENCE EQUIPMENT IS STORED. THERE ARE MANY  BROOMS,"
3410 PRINT "DUSTPANS AND MOPS AROUND HERE. A DARK PASSAGE WAY LEADS TO THE SOUTH, WHILE"
3420 PRINT "A LIGHTED WAY LEADS TO THE EAST. THERE IS A PLUG ON THE EAST WALL CONNECT"
3430 PRINT "TO AN OUTLET."
3440 RETURN
3450 REM ROOM THREE
3460 PRINT "SECRET ROOM"
3470 PRINT "YOU ARE IN A SECRET ROOM BEHIND THE BASE LABROTORY. THE HOLE IN THE SOUTH"
3480 PRINT "WALL IS SMALLER ON THIS SIDE, PREVENTING YOU FROM GOING SOUTH."
3490 RETURN
3500 REM ROOM FOUR
3510 PRINT "HALLWAY"
3520 PRINT "THIS IS A NON DESCRIPT HALLWAY RUNNING NORTH AND SOUTH."
3530 RETURN
3540 RETURN
3550 REM ROOM FIVE
3560 PRINT "REPAIR CLOSET"
3570 PRINT "THIS IS A REPAIR CLOSET WHERE HAMMERS, NAILS AND SCREWDRIVERS ARE KEPT. THE"
3580 PRINT "ONLY EXITS IS THE WAY YOU CAME."
3590 RETURN
3600 REM ROOM SIX
3610 PRINT "STORAGE ROOM";:IF BOX=1 THEN PRINT " (CROUCHING IN THE BOX)"
3620 PRINT "YOU ARE IN A STORAGE ROOM WHERE MISCELLANOUS EQUIPMENT IS STORED. THERE IS A"
3630 PRINT "LARGE, HEAVY BOX IN THE SOUTH CORNER. TO THE EAST IS A BALCONY. ALSO ON THE"
3640 PRINT "NORTH WALL, THERE IS A BUTTON WITH A SMALL PLAQUE BENEATH IT."
3650 RETURN
3660 REM ROOM SEVEN
3670 PRINT "BALCONY"
3680 PRINT "THIS IS AN OLD, CRUMBLING BALCONY."
3690 RETURN
3700 REM ROOM EIGHT
3710 PRINT "LABROTORY"
3720 PRINT "YOU ARE IN A SCIENTIFIC LABROTORY WHERE GREAT EXPERIMENTS ARE DONE. THE USUAL"
3730 PRINT "ARRAY OF SUPPLIES AND EQUIPMENT IS HERE. THE NORTH WALL IS A BIT LESS SOLID":PRINT "THEN THE OTHERS."
3740 RETURN
3750 REM ROOM NINE
3760 PRINT "HALLWAY"
3770 PRINT "THIS IS A VERY NON-DESCRIPT HALLWAY RUNNING EAST TO WEST."
3780 RETURN
3790 REM ROOM TEN
3800 PRINT "GUARD ROOM"
3810 PRINT "YOU ARE IN A GUARD ROOM, WHERE A GUARD IS SUPPOSED TO GUARD THE CELL TO THE"
3820 PRINT "SOUTH. PASSAGEWAYS RUN OFF IN THREE DIRECTIONS."
3830 RETURN
3840 REM ROOM ELEVEN
3850 PRINT "ELEVATOR"
3860 PRINT "THIS IS AN ELEVATOR CONNECTING ALL THREE LEVELS OF THE BASE. THERE IS A PANEL"
3870 PRINT "HERE WITH THREE BUTTONS LABELED 1,2 AND 3."
3880 RETURN
3890 REM ROOM TWELVE
3900 PRINT "ELEVATOR"
3910 PRINT "THIS IS AN ELEVATOR CONNECTING ALL THREE LEVELS OF THE BASE. THERE IS A PANEL"
3920 PRINT "HERE WITH THREE BUTTONS LABELED 1,2 AND 3."
3930 RETURN
3940 REM ROOM THIRTEEN
3950 PRINT "ELEVATOR"
3960 PRINT "THIS IS AN ELEVATOR CONNECTING ALL THREE LEVELS OF THE BASE. THERE IS A PANEL"
3970 PRINT "HERE WITH THREE BUTTONS LABELED 1,2 AND 3."
3980 RETURN
3990 REM ROOM FOURTEEN
4000 PRINT "RECORD ROOM"
4010 PRINT "THIS IS A RECORD ROOM WHERE TOP SECRET INFORMATION IS KEPT. THERE IS A FILE"
4020 PRINT "CABINET ON THE WEST WALL AND A CAMERA ON THE WEST WALL NEAR THE CEILING."
4030 RETURN
4040 REM ROOM FIFTEEN
4050 PRINT "NORTH CELL"
4060 PRINT "THIS IS A NORTH CELL WHERE TOP SECURITY PRISONERS ARE KEPT. THERE IS A"
4070 PRINT "LARGE, WOODEN DOOR ON THE NORTH WALL. TO THE SOUTH A LIGHT CAN BE SEEN."
4080 IF INSERT=1 THEN PRINT "UNDER THE DOOR, YOU CAN SEE A NEWSPAPER."
4090 RETURN
4100 REM ROOM SIXTEEN
4110 PRINT "MISSLE SITE"
4120 PRINT "THIS IS A LARGE ROOM IN THE SHAPE OF A MISSLE SILO, WHERE A VERY IMPORTANT"
4130 PRINT "MISSLE IS KEPT. THE ROUNDED CEILING IS PARTLY OPEN, AWAITING THE PREPARATION"
4140 PRINT "OF THE MX-13, BEING KEPT IN THE CENTER OF THE ROOM."
4150 RETURN
4160 REM ROOM SEVENTEEN
4170 PRINT "SOUTH CELL"
4180 PRINT "YOU ARE IN A CELL WHERE PRISONERS ARE KEPT. THE SECOND PART OF THIS TWO ROOM"
4190 PRINT "CELL IS TO THE NORTH WHILE A CRACK IN THE CEILING IS LETTING IN A LARGE AMOUNT"
4200 PRINT "OF LIGHT."
4210 RETURN
4220 END
4230 REM ** INITIALIZE **
4240 CLS:KEY OFF:PRINT "PLEASE WAIT WHILE I SET THE ADVENTURE..."
4250 SCORE=0:RO=17:GUARD=1:FLAG=0:KEE=0:DOOR=0
4260 A(15,7)=99:REM PROBLEM DOOR 1
4270 A(8,7)=100:REM PROBLEM DOOR 2
4280 FOR X=1 TO 17
4290 FOR Y=1 TO 6
4300 READ A(X,Y)
4310 NEXT Y
4320 NEXT X
4330 FOR X=1 TO 11
4340 READ O1$(X),O2$(X),TR(X),IN$(X)
4350 NEXT X
4360 FOR I=1 TO 17
4370 READ ROOM$(I)
4380 NEXT I
4390 RETURN
4400 REM ** ROOM DATA **
4410 DATA 0,4,0,0,0,0
4420 DATA 0,8,3,0,0,0
4430 DATA 0,0,0,2,0,0
4440 DATA 1,10,5,0,0,0
4450 DATA 0,0,0,4,0,0
4460 DATA 0,12,7,0,0,0
4470 DATA 0,0,0,6,0,0
4480 DATA 0,0,9,0,0,0
4490 DATA 0,14,10,8,0,0
4500 DATA 4,0,11,9,0,0
4510 DATA 0,0,0,10,0,0
4520 DATA 6,0,0,0,0,0
4530 DATA 0,16,0,0,0,0
4540 DATA 9,0,0,0,0,0
4550 DATA 0,17,0,0,0,0
4560 DATA 13,0,0,0,0,0
4570 DATA 15,0,0,0,0,0
4580 DATA "BRI","THERE IS A BRICK ON THE FLOOR NEAR YOU.",17,"A BRICK"
4590 DATA "NEW","ON THE FLOOR, THERE IS AN OLD NEWSPAPER.",17,"A NEWSPAPER"
4600 DATA "NOT","THERE IS A NOTE LAYING NEARBY.",8,"A NOTE"
4610 DATA "KEY","THERE IS A SHINING KEY HERE.",255,"A KEY"
4620 DATA "BEA","THERE IS A SCIENTIFIC BEAKER FULL OF LIQUID NEAR YOU.",8,"A BEAKER FULL OF LIQUID"
4630 DATA "SLI","THERE IS A SLIP OF PAPER LYING HERE.",5,"A SLIP"
4640 DATA "FIL","A FILE CONTAING IMPORTANT INFORMATION IS HERE.",255,"A FILE"
4650 DATA "ASP","THERE IS A ASPIRIN HERE.",255,"AN ASPIRIN"
4660 DATA "GUA","THERE IS AN ALERT GUARD HERE.",10,""
4670 DATA "GUA","THERE IS AN UNCONSICOUS GUARD HERE.",255,""
4680 DATA "WOR","THERE IS A MISSLE WORKER HERE.",16,""
4690 DATA VAULT ROOM,MAINTENCE ROOM,SECRET ROOM,HALLWAY,REPAIR CLOSET
4700 DATA STORAGE ROOM,BALCONY,LABROTORY,HALLWAY,GUARD ROOM,ELEVATOR
4710 DATA ELEVATOR,ELEVATOR,RECORD ROOM,NORTH CELL,MISSLE SITE
4720 DATA SOUTH CELL,LABORATORY,HALLWAY,GUARD ROOM,ELEVATOR
4710 DATA ELEVATOR,ELEVATOR,RECORD ROOM,NORT

