10 REM GOSTSHIP
20 REM COPYRIGHT (C) 7/84
30 REM BY PHILIP COURIE
40 REM P.O. BOX 862
50 REM FLORENCE, KY 41042
60 CLS
70 DATA 71,79,83,84,32,83,72,73,80
80 FOR X=1 TO 9
90 READ D
100 PRINT@ 106+X,CHR$(D)
110 FOR Y=1 TO 35:NEXT Y
120 NEXT X
130 DATA 66,89
140 FOR X=1 TO 2
150 READ D
160 PRINT@ 173+X,CHR$(D)
170 FOR Y=1 TO 35:NEXT Y
180 NEXT X
190 DATA 80,72,73,76,73,80,32,67,79,85,82,73,69
200 FOR X=1 TO 13
210 READ D
220 PRINT@ 232+X,CHR$(D)
230 FOR Y=1 TO 35:NEXT Y
240 NEXT X
250 DATA 67,79,80,89,82,73,71,72,84,32,40,67,41,32,49,57,56,52
260 FOR X=1 TO 18
270 READ D
280 PRINT@ 293+X,CHR$(D)
290 FOR Y=1 TO 32:NEXT Y
300 NEXT X
310 FOR X=1 TO 400:NEXT X
320 CLS
330 PRINT@4,"**NOTES ON ADVENTURING**"
340 FOR X=32 TO 63
350 PRINT@X,CHR$(45);:NEXT X
360 PRINT@65,"IN THIS ADVENTURE YOU HAVE":PRINT@97,"A GOAL TO COMPLETE."
361 PRINT@129,"TO FIND THE TREASURE OF THE     GOST SHIP."
362 PRINT@171," AT TIMES YOU WILL    NEED AN":PRINT@201,"OBJECT TO COMPLETE      A TASK."
370 FOR X=256 TO 287:PRINT@X,CHR$(45);:NEXT X
380 PRINT@289,"IN THIS ADVENTURE YOU WILL":PRINT@321,"USE A VERB AND A NOUN."
390 PRINT@353,"example 1) GET COIN":PRINT@393,"2) N<enter>"
400 FOR X=416 TO 447:PRINT@X,CHR$(45);:NEXT X
410 PRINT@448," PRESS ANY KEY TO CONTINUE."
420 K$=INKEY$:IF K$="" THEN 420
430 CLS
440 PRINT@33,"DO YOU WISH TO PLAY A SAVED":INPUT" GAME [Y/N]";A$
450 IF A$="" THEN 440
455 IF A$="N" THEN 480
460 IF A$="Y" THEN 560
470 GOTO 440
480 CLS:PRINT@65,"PLEASE ENTER":PRINT " YOU NAME IN EIGHT":INPUT " LETTERS OR LESS";PN$
490 IF LEN(PN$)>8 THEN 480
500 PRINT:PRINT" YOU ENTERED ";PN$
510 INPUT " IS THIS CORRECT [Y/N]";Q$
520 IF Q$="" THEN 510
530 IF Q$="N" THEN 480
535 IF Q$<> "Y" THEN 480
540 CLS
550 PRINT@196,"PLEASE WAIT READING DATA."
560 DIM OP(2)
570 DIM L$(29),D$(6),MM(29,6)
580 DIM VI$(34),II$(34),VI(53)
590 GOTO 4000
600 IFX=13ORX=14ORX=15ORX=16OR X=17OR X=18OR X=19ORX=20ORX=21ORX=22ORX=23ORX=24ORX=25ORX=27ORX=28ORX=29THENGOSUB3490
610 IF X=26 THEN VI(1)=X:GOTO 620
615 VI(1)=1
620 KK=1:CLS:PRINT PN$;" YOU ARE"
630 PRINT L$(X)
640 PRINT:PRINT"YOU SEE A WAY TO GO:"
650 FOR Y=1 TO 6:IF MM(X,Y)<>0 THENPRINT D$(Y);:NEXT Y:GOTO 660
655 NEXT Y
660 C=0:PRINT:PRINT:PRINT"VISABLE ITEMS ARE:"
670 FOR I=1 TO 34
680 KK$=STR$(KK):IF VI(I)=X THEN PRINT KK$;"> ";VI$(I):C=1:KK=KK+1
690 NEXT
700 IF C=0 THEN PRINT"NOTHING!"
710 PRINT:INPUT "WHAT NEXT";A$
720 IF A$="N" THEN 3540
730 IF A$="S" THEN 3580
740 IF A$="E" THEN 3630
750 IF A$="W" THEN 3650
760 IF A$="U" THEN 3670
770 IF A$="D" THEN 3690
780 M$=LEFT$(A$,2):IF M$="GO" THEN 920
790 M$=LEFT$(A$,3):IF M$="RUB" THEN 1380
791 IF M$="GET" THEN 1790
792 IF M$="USE" THEN 2050
793 IF M$="BUY" THEN 2190
794 IF M$="DIG" THEN 2270
800 M$=LEFT$(A$,4):IF M$="LOOK" THEN 1100
801 IF M$="RING" THEN 1120
802 IF M$="DROP" THEN 1200
803 IF M$="TAKE" THEN 1460
804 IF M$="OPEN" THEN 1500
805 IF M$="BAIL" THEN 1630
810 IF M$="READ" THEN 1690
820 IF M$="SAVE" THEN 4540
830 M$=LEFT$(A$,5):IF M$="SCORE" THEN 2330
831 IF M$="DRINK" THEN 2350
832 IF M$="CLIMB" THEN 2440
833 IF M$="ENTER" THEN 2520
840 M$=LEFT$(A$,6):IF M$="POLISH" THEN 3060
841 IF M$="UNLOCK" THEN 3140
850 M$=LEFT$(A$,7):IF M$="EXAMINE" THEN 2640
860 M$=LEFT$(A$,8):IF M$="PURCHASE" THEN 3010
870 M$=LEFT$(A$,9):IF M$="INVENTORY" THEN 3230
880 PRINT "WHAT WAS THAT ";PN$;"?"
890 IF X=9 AND VI(23)=X THEN 3300
900 IF X=9 AND VI(23)=-2 THEN 3400
910 GOTO 710
920 REM GO
930 GOSUB 3710
940 IF M$="NORTH" THEN 3540
950 IF M$="SOUTH" THEN 3580
960 IF M$="EAST" THEN 3630
970 IF M$="WEST" THEN 3650
980 IF M$="UP" THEN 3670
990 IF M$="DOWN" THEN 3690
1000 SC=SC+1
1010 IF M$="NEST" AND X=1 THENPRINT "IT'S TO HIGH.":GOTO 710
1020 IF M$=II$(9) AND X=4 THEN 2550
1030 IF M$=II$(10) AND X=5 THEN 2580
1040 IF M$=II$(31) AND X=13 THEN 2590
1050 IF M$=II$(17) AND X=4 THEN X=8:GOTO 600
1060 IF M$=II$(17) AND X=11 THENX=10:GOTO 600
1070 IF M$=II$(7) AND X=8 THEN X=4:GOTO 600
1080 IF M$=II$(32) AND X=10 THEN X=11:GOTO 600
1090 GOTO 3520
1100 REM LOOK
1110 GOTO 600
1120 REM RING
1130 SC=SC+1
1140 GOSUB 3800
1150 IF M$=II$(2) AND X=1 THENPRINT "DING DING   DING DING":GOTO 710
1160 PRINT "YOU CAN'T RING THE ";M$;"."
1170 IF X=9 AND VI(23)=X THEN 3300
1180 IF X=9 AND VI(23)=-2 THEN 3400
1190 GOTO 710
1200 REM DROP
1210 GOSUB 3800
1220 SC=SC+1
1230 FOR I=1 TO 34
1240 IF VI(I)=-1 THEN 1300
1250 NEXT I
1260 PRINT "YOU DON'T HAVE THE ";M$;".":PRINT "CHECK YOUR inventory ";PN$;"."
1270 IF X=9 AND VI(23)=X THEN 3300
1280 IF X=9 AND VI(23)=-2 THEN 3400
1290 GOTO 710
1300 FOR I=1 TO 34
1310 IF M$=II$(I) THEN L=L-1:VI(I)=X:GOTO 1340
1320 NEXT I
1330 GOTO 1260
1340 PRINT "OK ";PN$
1350 IF X=9 AND VI(23)=X THEN 3300
1360 IF X=9 AND VI(23)=-2 THEN 3400
1370 GOTO 710
1380 REM RUB
1390 SC=SC+1
1400 GOSUB 3760
1410 IF M$=II$(2) AND X=1 THENPRINT "A GOST OF AN OLD SEAMAN APEARS.":VI(12)=1:GOTO 710
1420 PRINT "IT HAS NO EFFECT."
1430 IF X=9 AND VI(23)=X THEN 3300
1440 IF X=9 AND VI(23)=-2 THEN 3400
1450 GOTO 710
1460 REM TAKE
1470 GOSUB 3800
1480 SC=SC+1
1490 GOTO 1820
1500 REM OPEN
1510 SC=SC+1
1520 GOSUB 3800
1530 IF M$=II$(9) AND UN=0 THENPRINT"IT'S LOCKED.":GOTO 710
1540 IF M$=II$(9) AND UN=1 THEN OP=1:PRINT "OK IT'S OPEN.":GOTO 710
1550 IF M$=II$(16) AND VI(24)=-1 THENPRINT"IT ALRADY OPEN.":IF X=9 AND VI(23)=X THEN 3300
1551 IF X=9 AND VI(23)=-2 THEN 3400
1552 GOTO 710
1560 IF M$=II$(16) AND VI(16)<>-1 THENPRINT"YOU DON'T HAVE IT.":IF X=9 AND VI(23)=X THEN 3300
1561 IF X=9 AND VI(23)=-2 THEN 3400
1562 GOTO 710
1570 IFM$=II$(16)ANDX=9THENPRINT"A BLUE LIQUID POURS OUT IN TO   THE WATER AND THE SHARK LEAVES.":VI(23)=-2:GOTO3400
1580 IFM$=II$(16)THENPRINT"A BLUE LIQUID SPILLS TO THE     GROUND.":VI(16)=0:VI(24)=-1:GOTO 710
1590 PRINT"YOU CAN'T OPEN THE ";M$;"."
1600 IF X=9 AND VI(23)=X THEN 3300
1610 IF X=9 AND VI(23)=-2 THEN 3400
1620 GOTO 710
1630 REM BAIL
1640 SC=SC+1
1650 GOSUB 3800
1660 IF M$="WATER" AND X=9 AND VI(19)=-1 THEN VI(23)=-3:PRINT"THAT'S A LOT BETTER.":GOTO 710
1670 PRINT"YOU CAN'T BAIL THE ";M$;"."
1680 GOTO 710
1690 REM READ
1700 SC=SC+1
1710 GOSUB 3800
1720 IF M$=II$(21) AND VI(21)<>-1 THENPRINT"YOU DON'T HAVE IT.":GOTO 710
1730 IF M$=II$(21) AND VI(21)=-1 THEN PRINT"PAM A DEEN LLIW UOY                   NIATPAC EHT":GOTO710
1740 IF M$=II$(18) AND VI(18)=-1 THENPRINT"GOTO TO ISLAND FIND POND GO     W,W,N,E AND DIG.":GOTO 710
1750 PRINT"YOU CAN'T READ THE ";M$;"."
1760 IF X=9 AND VI(23)=X THEN 3300
1770 IF X=9 AND VI(23)=-2 THEN 3400
1780 GOTO 710
1790 REM GET
1800 SC=SC+1
1810 GOSUB 3760
1820 IF M$=II$(1) AND X=1 OR M$=II$(1) AND X=26 THEN 2040
1830 IF M$=II$(3) OR M$=II$(4) OR M$=II$(5) THEN 2040
1840 IF M$=II$(10) OR M$=II$(11) OR M$=II$(12) THEN 2040
1850 IF M$=II$(17) OR M$=II$(20) OR M$=II$(9) OR M$=II$(22) OR M$=II$(23) THEN 2040
1860 IF M$=II$(25) OR M$=II$(26) OR M$=II$(27) OR M$=II$(28) OR M$=II$(29) OR M$=II$(31) THEN 2040
1870 IF M$=II$(32) OR M$=II$(33) THEN 2040
1880 IF M$=II$(34) AND TS=0 THENPRINT"HAY THAT WILL COST YOU ONE      GOLD COIN.":GOTO 710
1890 IF L=5 THEN 2030
1900 IF M$=II$(13) AND X=1 AND VI(11)=1 THEN VI(13)=-1:L=L+1:GOTO 2000
1910 IF M$=II$(14) AND X=1 AND VI(12)=1 THEN VI(14)=-1:L=L+1:GOTO 2000
1920 IF M$=II$(6) AND X=2 THENPRINT "A LARGE KEY DROPS TO THE GROUND.":VI(15)=X:VI(6)=-1:L=L+1:GOTO 710
1930 FOR I=1 TO 34
1940 IF L=5 THEN 2030
1941 IF M$=II$(I) AND VI(I)=X THEN L=L+1:VI(I)=-1:GOTO 2000
1950 NEXT I
1960 PRINT "I DON'T SEE IT HERE."
1970 IF X=9 AND VI(23)=X THEN 3300
1980 IF X=9 AND VI(23)=-2 THEN 3400
1990 GOTO 710
2000 PRINT "OK ";PN$
2010 IF X=9 THEN 3300
2020 GOTO 710
2030 PRINT "YOU CAN'T CARRY ANY MORE!":GOTO 710
2040 PRINT "YOU CAN'T TAKE THE ";M$;".":GOTO 710
2050 REM USE
2060 SC=SC+1
2070 GOSUB 3760
2080 IF M$=II$(8) AND X=3 AND VI(8)=-1 THEN PRINT "I SEE A SMALL ISLAND IN THE     DISTANCE ";PN$;".":GOTO 710
2090 IF M$=II$(16) AND VI(24)=-1 THENPRINT"IT'S EMPTY.":IF X=9 AND VI(23)=X THEN 3300
2091 GOTO 710
2100 IF M$=II$(16) AND VI(23)=-2 AND X=9 THENPRINT"THE BOTTLE IS TO SMALL!":GOTO 3400
2110 IF M$="REPELLENT" AND VI(24)=-1 THENPRINT"THE BOTTLE IS EMPTY.":IF X=9 AND VI(23)=X THEN 3300
2111 IF X=9 AND VI(23)=-2 THEN 3400
2112 GOTO 710
2120 IF M$=II$(16) AND VI(16)<>-1 THENPRINT"YOU DON'T HAVE IT.":IF X=9 AND VI(23)=X THEN 3300
2121 IF X=9 AND VI(23)=-2 THEN 3400
2122 GOTO 710
2130 IF M$="REPELLENT" AND VI(16)<>-1 THENPRINT"YOU DON'T HAVE IT.":IF X=9 THEN 3300
2132 GOTO 710
2140 IF M$=II$(16) AND X=9 AND OP(2)=0 THENPRINT"IT'S NOT OPEN.":GOTO 710
2150 IF M$="REPELLENT" AND X=9 AND OP(2)=0 THENPRINT"IT'S NOT OPEN.":GOTO 710
2160 PRINT"YOU CAN'T USE THE ";M$;"."
2170 IF X=9 THEN 3300
2180 GOTO 710
2190 REM BUY
2200 SC=SC+1
2210 GOSUB 3760
2220 IF M$=II$(34) AND X=12 AND VI(14)<>-1 THENPRINT"I NEED PAYMENT BY A GOLD COIN.":GOTO 710
2230 IF M$=II$(34) AND X=12 AND VI(14)=-1 THENPRINT"OK YOU MAY HAVE THE SHOVEL.":TS=1:VI(14)=0:GOTO 710
2240 IF X=9 AND VI(23)=X THEN 3300
2250 IF X=9 AND VI(23)=-2 THEN 3400
2260 PRINT"YOU CAN'T BUY THE ";M$;".":GOTO 710
2270 REM DIG
2280 SC=SC+1
2290 GOSUB 3760
2300 IF M$=II$(27) AND VI(34)=-1 AND X=29 THEN 4170
2310 IF M$=II$(27) AND VI(34)=-1 AND VI(27) OR VI(30)=X THENPRINT"OK":GOTO 710
2320 PRINT"YOU CAN'T DIG THE ";M$;".":GOTO 710
2330 REM SCORE
2340 PRINT PN$;" YOU HAVE MADE";SC;"MOVES.":GOTO 710
2350 REM DRINK
2360 SC=SC+1
2370 GOSUB 3840
2380 IF M$=II$(1) AND X=1 THEN PRINT "IT'S SALT WATER AND NO GOOD TO  DRINK ";PN$;".":GOTO 710
2390 IF M$=II$(1) AND X=26 THEN PRINT "COOL WATER.":GOTO 710
2400 PRINT "YOU CAN'T DRINK THE ";M$;"."
2410 IF X=9 AND VI(23)=X THEN 3300
2420 IF X=9 AND VI(23)=-2 THEN 3400
2430 GOTO 710
2440 REM CLIMB
2450 SC=SC+1
2460 GOSUB 3840
2470 IF M$="MAST" AND X=1 THEN X=3:GOTO 600
2480 PRINT "YOU CAN'T CLIMB THE ";M$;"."
2490 IF X=9 AND VI(23)=X THEN 3300
2500 IF X=9 AND VI(23)=-2 THEN 3400
2510 GOTO 710
2520 REM ENTER
2530 SC=SC+1
2540 GOSUB 3840
2550 IF M$=II$(9) AND X=4 AND UN=0 THENPRINT"IT'S LOCKED.":GOTO 710
2560 IF M$=II$(9) AND X=4 AND OP=0 THENPRINT"IT'S NOT OPEN.":GOTO 710
2570 IF M$=II$(9) AND X=4 AND OP=1 AND UN=1 THEN X=5:GOTO 600
2580 IF M$=II$(10) AND X=5 THEN X=6:GOTO 600
2590 IF M$=II$(31) AND X=13 THEN X=12:GOTO 600
2600 PRINT"YOU CAN'T ENTER THE ";M$;"."
2610 IF X=9 AND VI(23)=X THEN 3300
2620 IF X=9 AND VI(23)=-2 THEN 3400
2630 GOTO 710
2640 REM EXAMINE
2650 SC=SC+1
2660 GOSUB 3920
2670 IF M$=II$(1) AND X=1 THEN 2850
2680 IF M$=II$(2) AND X=1 THEN 2860
2690 IF M$=II$(3) AND X=1 THEN 2870
2700 IF M$=II$(4) AND X=1 THEN 2880
2710 IF M$=II$(5) AND X=2 THEN 2890
2720 IF M$=II$(9) AND X=4 THEN 2900
2730 IF M$=II$(10) AND X=5 THEN 2910
2740 IF M$=II$(11) AND X=1 THEN 2920
2750 IF M$=II$(12) AND VI(12)=1 AND RB=0 THEN GOTO 2930
2760 IF M$="SEAMAN" AND VI(12)=1 AND RB=0 THEN GOTO 2930
2770 IF M$="NEST" AND X=3 THEN 2940
2780 IF M$=II$(20) AND X=7 AND OT=0 THEN 2950
2790 IF M$=II$(20) AND X=7 AND OT=1 THEN 2960
2800 IF M$=II$(16) AND VI(24)=-1 THEN 2980
2810 IF M$=II$(16) AND VI(16)<>0 THEN 2970
2820 IF M$=II$(21) AND X=7 THEN 2990
2830 IF M$=II$(33) AND X=12 AND TS=0 THEN 3000
2840 PRINT"I SEE NOTHING SPECIAL.":GOTO 710
2850 PRINT "YOU DISCOVER SOMETHING!":VI(11)=1:GOTO 710
2860 PRINT "IT'S VERY DULL.":GOTO710
2870 PRINT "THE FLAG HAS A SKULL AND CROSS  BONES ON IT.":GOTO 710
2880 PRINT "IT HAS A CROW'S NEST IN IT.":GOTO 710
2890 PRINT "IT'S VERY OLD.":GOTO 710
2900 PRINT "THE HATCHWAY IS LOCKED.":GOTO 710
2910 PRINT "IT'S DARK IN HERE. SOMETHING    JUST RAN ACCOSS YOUR FOOT. I    THINK IT WAS A RAT!!":GOTO 710
2920 PRINT "SHE HAS A WOODEN PADDLE.":GOTO 710
2930 PRINT "HE HAS A GOLD COIN.":RB=1:GOTO 710
2940 PRINT"YOU FOUND SOMETHING.":VI(18)=X:GOTO 710
2950 PRINT"YOU FOUND SOMETHING.":VI(21)=X:OT=1:GOTO 710
2960 PRINT"YOU FOUND SOMETHING.":VI(16)=X:GOTO 710
2970 PRINT"IT'S FILLED WITH A BLUE LIQUID. IT LOOKS LIKE SHARK REPELLENT.":GOTO 710
2980 PRINT"IT'S EMPTY.":IF X=9 THEN 3300
2982 GOTO 710
2990 PRINT"IT HAS SOMETHING WRITTEN ON IT.":GOTO 710
3000 PRINT"HI I'VE GOT A SHOVEL FOR SALE.":GOTO 710
3010 REM PURCHASE
3020 SC=SC+1
3030 GOSUB 3960
3040 IF M$=II$(34) THEN 2220
3050 GOTO 2260
3060 REM POLISH
3070 SC=SC+1
3080 GOSUB 3880
3090 IF M$=II$(2) AND X=1 THENPRINT "A GOST OF AN OLD SEAMAN APPEARS.":VI(12)=1:GOTO 710
3100 PRINT"IT HAS NO EFFECT."
3110 IF X=9 AND VI(23)=X THEN 3300
3120 IF X=9 AND VI(23)=-2 THEN 3400
3130 GOTO 710
3140 REM UNLOCK
3150 SC=SC+1
3160 GOSUB 3880
3170 IF M$=II$(9) AND VI(15)<>-1 THENPRINT"YOU HAVE NO WAY TO DO THAT.":GOTO 710
3180 IF M$=II$(9) AND VI(15)=-1 THEN UN=1:PRINT"OK IT'S UNLOCKED.":GOTO 710
3190 PRINT"YOU CAN'T UNLOCK THE ";M$;"."
3200 IF X=9 AND VI(23)=X THEN 3300
3210 IF X=9 AND VI(23)=-2 THEN 3400
3220 GOTO 710
3230 KK=1:CLS:PRINT PN$;" YOU ARE CARRYING:"
3240 C=0:FOR I=1 TO 34
3250 IF VI(I)=-1 THEN C=1
3260 KK$=STR$(KK):IF VI(I)=-1 THEN PRINT KK$;"> ";VI$(I):KK=KK+1:C=1
3270 NEXT
3280 IF C=0 THEN PRINT"NOTHING!"
3290 GOTO 710
3300 REM SHARK ATTACK
3310 IF SH=0 THEN PRINT"THE SHARK ATTACKS YOU. HE MUST  THINK THAT HE'S JAWS.":SH=SH+1:GOTO 710
3320 IF SH=1 THEN PRINT"OH NO HERE HE COMES AGAIN.":SH=SH+1:GOTO710
3330 IF SH=2 THEN PRINT"HE LOOK'S A LOT LIKE JAW'S.":SH=SH+1:GOTO710
3340 IF SH=3 THEN PRINT"I DON'T THINK WE CAN TAKE       ANOTHER ATTACK!":SH=SH+1:GOTO 710
3350 IF SH=4 THEN PRINT"THE BOAT IS sinking!!":SH=SH+1:GOTO 710
3360 IF SH=5 THEN PRINT"YOU HAVE DIED BY SHARK ATTACK!"
3370 PRINT
3380 END
3390 IF VI(23)=X THENPRINT"THE SHARK WILL NOT LET YOU!":GOTO 710
3400 REM BOAT SINKING
3410 IF SB=0 THENPRINT:PRINT"THE BOAT BEGINS TO LEAK FROM    THE SHARK ATTACK!":SB=SB+1:GOTO710
3420 IF SB=1 THENPRINT"THE BOTTOM OF THE BOAT IS       FILLED WITH WATER NOW.":SB=SB+1:GOTO 710
3430 IF SB=2 THENPRINT"IT NOT LOOKING GOOD ";PN$;".":SB=SB+1:GOTO 710
3440 IF SB=3 THENPRINT"CAN YOU SWIM ";PN$;".":SB=SB+1:GOTO 710
3450 IF SB=4 THENPRINT"IT'S ALL OVER YOU HAVE DROWNED!"
3460 PRINT
3470 END
3480 PRINT"YOU CAN'T THE BOAT IS SINKING!":GOTO 710
3490 REM MAZE ITEMS
3500 VI(25)=X:VI(26)=X:VI(27)=X
3510 RETURN
3520 PRINT"YOU CAN'T GO THAT WAY!":GOTO 710
3530 PRINT"THE BOAT WILL NOT MOVE!":GOTO 710
3540 IF X=9 AND VI(23)=-2 THEN GOTO 3480
3550 IF X=9 AND VI(23)=X THEN GOTO 3390
3560 IF MM(X,1)=0 THEN GOTO 3520
3570 IF MM(X,1)>0 THEN X=MM(X,1):SC=SC+1:GOTO 600
3580 IF X=8 AND VI(13)<>-1 THEN 3530
3590 IF X=9 AND VI(23)=-2 THEN GOTO 3480
3600 IF X=9 AND VI(23)=X THEN GOTO 3390
3610 IF MM(X,2)=0 THEN GOTO 3520
3620 IF MM(X,2)>0 THEN X=MM(X,2):SC=SC+1:GOTO 600
3630 IF MM(X,3)=0 THEN GOTO 3520
3640 IF MM(X,3)>0 THEN X=MM(X,3):SC=SC+1:GOTO 600
3650 IF MM(X,4)=0 THEN GOTO 3520
3660 IF MM(X,4)>0 THEN X=MM(X,4):SC=SC+1:GOTO 600
3670 IF MM(X,5)=0 THEN GOTO 3520
3680 IF MM(X,5)>0 THEN X=MM(X,5):SC=SC+1:GOTO 600
3690 IF MM(X,6)=0 THEN GOTO 3520
3700 IF MM(X,6)>0 THEN X=MM(X,6):SC=SC+1:GOTO 600
3710 M$=RIGHT$(A$,LEN(A$)-2)
3720 IF M$="" AND A$="GO" THENPRINT LEFT$(A$,2);" WHERE?":GOTO 710
3730 IF M$="" THENPRINT LEFT$(A$,2);" WHAT?":GOTO 710
3740 M$=RIGHT$(A$,LEN(A$)-3)
3750 RETURN
3760 M$=RIGHT$(A$,LEN(A$)-3)
3770 IF M$="" THENPRINT LEFT$(A$,3);" WHAT ";PN$"?":GOTO 710
3780 M$=RIGHT$(A$,LEN(A$)-4)
3790 RETURN
3800 M$=RIGHT$(A$,LEN(A$)-4)
3810 IF M$="" THENPRINT LEFT$(A$,4);" WHAT ";PN$"?":GOTO 710
3820 M$=RIGHT$(A$,LEN(A$)-5)
3830 RETURN
3840 M$=RIGHT$(A$,LEN(A$)-5)
3850 IF M$="" THENPRINT LEFT$(A$,5);" WHAT ";PN$"?":GOTO 710
3860 M$=RIGHT$(A$,LEN(A$)-6)
3870 RETURN
3880 M$=RIGHT$(A$,LEN(A$)-6)
3890 IF M$="" THENPRINT LEFT$(A$,6);" WHAT ";PN$"?":GOTO 710
3900 M$=RIGHT$(A$,LEN(A$)-7)
3910 RETURN
3920 M$=RIGHT$(A$,LEN(A$)-7)
3930 IF M$="" THENPRINT LEFT$(A$,7);" WHAT ";PN$"?":GOTO 710
3940 M$=RIGHT$(A$,LEN(A$)-8)
3950 RETURN
3960 M$=RIGHT$(A$,LEN(A$)-8)
3970 IF M$="" THENPRINT LEFT$(A$,8);" WHAT ";PN$"?":GOTO 710
3980 M$=RIGHT$(A$,LEN(A$)-9)
3990 RETURN
4000 DATA STANDING ON THE DECK OF AN OLD  SHIP.,AT THE BOW OF THE SHIP.,IN A CROW'S NEST.
4001 DATA STANDING AFT ON THE DECK.,STANDING INSIDE A HATCHWAY.,STANDING IN A PASSAGEWAY.
4002 DATA IN THE CAPTAIN'S CABIN.,IN A BOAT.,IN A BOAT.,IN A BOAT.,ON A SMALL BEACH.
4010 DATA INSIDE A HUT.,ON AN ISLAND.,ON AN ISLAND.,ON AN ISLAND.,ON AN ISLAND.,ON AN ISLAND.
4011 DATA ON AN ISLAND.,ON AN ISLAND.,ON AN ISLAND.,ON AN ISLAND.,ON AN ISLAND.,ON AN ISLAND.
4012 DATA ON AN ISLAND.,ON AN ISLAND.
4020 DATA AT A SMALL POND.,ON AN ISLAND.,ON AN ISLAND.,ON AN ISLAND.
4030 FOR X=1TO29:READ L$(X):NEXT X
4040 DATA <north>,<south>,<east>,<west>,<up>,<down>
4050 FOR Y=1TO6:READ D$(Y):NEXT Y
4060 DATA 2,4,0,0,0,0,0,1,0,0,0,0,0,0,0,0,0,1,1,0,0,0,0,0,0,0,0,4,0,0,0,0,7,5,0,0,0,0,0,6,0
4061 DATA 0,0,9,0,0,0,0,8,10,0,0,0,0,9,0,0,0,0,0,13,0,0,0,0,0
4070 DATA 0,0,13,0,0,0,16,11,14,0,0,0,17,0,15,13,0,0,0,0,0,14,0,0,26,13,0,0,0,0,22,14,0,0,0
4071 DATA 0,23,0,0,0,0,0,20,0,0,0,0,0,24,19,21,0,0,0,0,0,0,21,0,0
4080 DATA 0,17,23,0,0,0,0,18,0,22,0,0,28,20,0,25,0,0,27,0,26,24,0,0,0,16,0,25,0,0,0,25,0,0
4081 DATA 0,0,0,24,29,0,0,0,0,0,0,28,0,0
4090 FOR X=1TO29:FOR Y=1TO6:READ MM(X,Y):NEXT Y,X
4100 DATA WATER,WATER,1,A BELL,BELL,1,A FLAG,FLAG,1,A TALL MAST,MAST,1,AN ANCHOR,ANCHOR,2
4101 DATA A COIL OF ROPE,ROPE,2,AN OLD SHIP BELOW,SHIP,3,A TELESCOPE,TELESCOPE,3
4102 DATA A HATCHWAY,HATCHWAY,4,A PASSAGEWAY,PASSAGEWAY,5
4110 DATA A MERMAID,MERMAID,0,A GOST OF AN OLD SEAMAN,GOST,0,A WOODEN PADDLE,PADDLE,0
4111 DATA A GOLD COIN,COIN,0,A LARGE KEY,KEY,0,A SMALL BOTTLE,BOTTLE,0,A SMALL BOAT,BOAT,4,A MAP,MAP,0
4120 DATA A WOODEN BUCKET,BUCKET,6,AN OLD TABLE,TABLE,7,A PIECE OF PAPER,PAPER,0,AN OLD SHIP,SHIP,8
4121 DATA A VERY LARGE SHARK,SKARK,9,AN EMPTY BOTTLE,BOTTLE,0
4130 DATA TREES,TREES,0,FLOWERS,FLOWERS,0,SAND,SAND,0,A SMALL BOAT,BOAT,11,THE OCEAN,OCEAN,11
4131 DATA SAND,SAND,11,A GRASS HUT,HUT,13,A SMALL BEACH,BEACH,10
4140 DATA AN OLD MAN,MAN,12,A SHOVEL,SHOVEL,12
4150 FOR I=1 TO 34:READ VI$(I),II$(I),VI(I):NEXT I
4160 X=1:IF A$="Y" THEN 4250
4162 GOTO 600
4170 REM WIN
4180 CLS
4190 PRINT@39,"CONGRATULATIONS"
4200 PRINT@70,"YOU HAVE FOUND THE"
4210 PRINT@99,"TREASURE OF THE GOST SHIP."
4220 PRINT:PRINT PN$;" YOU WIN 25 BONUS POINTS"
4230 PRINT"YOUR FINAL SCORE IS";SC-25
4240 GOTO 4240
4250 REM READ DATA
4260 CLS
4340 PRINT@33,"READY CASSETTE":PRINT" PRESS play only":PRINT:PRINT
4350 INPUT" ENTER NAME";N$
4355 IF LEN(N$)>8 THEN 4350
4360 CLOAD*VI,N$
4370 X=VI(35):OP=VI(36):UN=VI(37):OT=VI(38)
4380 SH=VI(39):BO=VI(40):SB=VI(41):TS=VI(42):RB=VI(43):L=VI(44):SC=VI(45)
4390 PN$="":FORZZ=46TO53:IFVI(ZZ)<>32THENPN$=PN$+CHR$(VI(ZZ))
4400 NEXTZZ
4410 IF LEN(PN$)=0 THEN PN$="PLAYER"
4420 GOTO 600
4540 REM SAVE
4550 CLS
4560 VI(35)=X:VI(36)=OP:VI(37)=UN:VI(38)=OT
4570 VI(39)=SH:VI(40)=BO:VI(41)=SB:VI(42)=TS:VI(43)=RB:VI(44)=L:VI(45)=SC
4580 FORZZ=46TO53:VI(ZZ)=ASC(MID$(PN$+"        ",ZZ-45,1))
4590 NEXTZZ
4630 PRINT@33,"READY CASSETTE":PRINT " PRESS PLAY AND RECORD":PRINT:PRINT
4640 INPUT " ENTER NAME";N$
4650 IF LEN(N$) >8 THEN 4640
4660 IF N$="" THEN 4540
4670 CSAVE*VI,N$
4730 CLS
4740 PRINT@34,"THIS GAME IS SAVED.":END
