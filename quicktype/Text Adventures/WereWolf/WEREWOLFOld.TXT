10 DIM A$(13),M$(30),N(13),S(13),E(13),W(13)
20 FOR A=1 TO 12
30 READ A$(A)
40 NEXT A: REM THIS READS IN ROOM DESCRIPTION DATA
50 FOR A=1 TO 12
60 READ N(A),S(A),E(A),W(A)
70 NEXT A
80 O(1)=12:O(2)=6:O(3)=5:O(4)=4
90 FOR A=1 TO 6: READ M$(A):NEXT A
100 M=24: REM YOU HAVE 24 MOVES
110 R=1: REM YOU START OUT IN ROOM #1
120 HR=12:MI=0: REM IT'S 12 PM, WITH 0 MINUTES
130 CLS
140 GOSUB 1600: REM THIS SUBROUTINE DISPLAYS THE GAME INSTRUCTIONS
150 GOSUB 1800: REM THIS SUBROUTINE DESCRIBES THE GAME SITUATION
200 REM THE PROGRAM PROPER STARTS HERE
210 PRINT A$(R):PRINT:REM THIS DISPLAYS THE ROOM MESSAGE OF THE ROOM YOU'RE IN.
220 REM LINES 230 THROUGH 260 CHECK THE OBJECT LIST TO SEE IF YOU'RE IN A ROOM CONTAINING AN OBJECT.
230 IF O(1)=R THEN PRINT"THERE IS CATNIP HERE":PRINT
240 IF O(2)=R THEN PRINT"THERE ARE PLANT CLIPPERS HERE":PRINT
250 IF O(3)=R THEN PRINT"THERE IS WOLVESBANE HERE":PRINT
260 IF O(4)=R THEN PRINT"THERE IS A PANTHER HERE THAT GUARDS A PASSAGE TO THE WEST":PRINT
300 REM FOLLOWING IS THE TIME KEEPING SECTION. 
310 REM THE VARIABLES "HR" AND "MI" ARE FOR THE HOURS AND MINUTES.
320 MI=MI+15:IF MI=60 THEN MI=0:HR=HR+1:IF HR>12 THEN HR=1
330 IF MI=0THENPRINTM$(HR):PRINT
340 IF HR=6 THEN PRINT:GOTO1470
350 REM FOLLOWING ARE THE DISPLAYS FOR THE TIME AND THE ROUTINE ASKING WHAT YOU WANT TO DO
360 PRINT HR;":";MI;:IF MI=0 THEN PRINT MI;
370 INPUT " AM. COMMAND: ";C$:CLS
380 IF C$="GO" THEN PRINT:INPUT "GO WHERE? ";C$:PRINT:GOTO430
390 IF C$="GET" THEN 600
400 IF C$="CUT" THEN 800
410 IF C$="THROW" THEN 700
420 IF C$="EAT" THEN 900
430 REM LINES 440 THROUGH 550 ARE PART OF THAT MOVING AROUND ROUTINE FROM THE MAY ISSUE. NOT VERY COMPLICATED, HUH?
440 IF C$="N" AND N(R)>0 THEN R=N(R):GOTO200
450 IF C$="NORTH" AND N(R)>0 THEN R=N(R):GOTO200
460 IF C$="S" AND S(R)>0 THEN R=S(R):GOTO 200
470 IF C$="SOUTH" AND S(R)>0 THEN R=S(R):GOTO200
480 IF C$="E" AND E(R)>0 THEN R=E(R):GOTO200
490 IF C$="EAST" AND E(R)>0 THEN R=E(R):GOTO200
500 IF O(4)=R AND C$="WEST" THEN PRINT "SORRY, THE PANTHER WON'T LET YOU PASS...ALIVE!!!":PRINT:GOTO 200
510 IF O(4)=R AND C$="W" THEN PRINT "SORRY, THE PANTHER WON'T LET YOU PASS...ALIVE!!!":PRINT:GOTO 200
520 IF C$="W" AND W(R)>0 THEN R=W(R):GOTO200
530 IF C$="WEST" AND W(R)>0 THEN R=W(R):GOTO200
540 REM *** THIS IS IF THE COMMAND IS ILLEGAL ***
550 PRINT "I CANNOT DO THAT":PRINT:GOTO200
600 REM THIS ROUTINE GETS THINGS FOR YOU. MORE TOO ON THIS NEXT ISSUE.
610 INPUT "WHAT DO YOU WANT TO GET? ";C$:PRINT
620 IF C$="CATNIP" AND O(1)=R THEN O(1)=0:I(1)=1:PRINT "YOU GOT IT":PRINT:GOTO200
630 IF C$="CLIPPERS" AND O(2)=R THEN O(2)=0:I(2)=1:PRINT "YOU GOT IT":PRINT:GOTO200
640 IF C$="WOLVESBANE" AND O(3)=R AND WC=1 THEN O(3)=0:I(3)=1:PRINT "YOU GOT IT":PRINT:GOTO200
650 IF C$="WEREWOLF" AND R=9 THEN PRINT "OH NO YOU WOKE HIM UP AND HERE HE COMES.":PRINT:PRINT "YOU LOSE": END
660 IF C$="PANTHER" AND R=4 THEN PRINT "THE PANTHER GETS YOU":PRINT:PRINT "YOU LOSE.": END
670 PRINT "I CANNOT":PRINT:GOTO200
700 REM YOU'LL NEED TO THROW THE CATNIP AND THIS TELLS THE PROGRAM WHERE AND HOW
710 INPUT "WHAT DO YOU WANT TO THROW? ";C$:PRINT
720 IFC$="CATNIP"AND I(1)=1 AND O(4)=R THEN4000
730 PRINT:PRINT "YOU CAN'T DO THAT HERE":GOTO200
800 REM SUBROUTINE FOR CUTTING
810 IF I(2)=0 THEN GOTO 850
820 INPUT "WHAT AM I TO CUT? ";C$:PRINT
830 IF C$="WOLVESBANE" AND R=5 THEN WC=1:PRINT"THE WOLVESBANE IS CUT!!!":PRINT:GOTO200
840 PRINT "I CANNOT CUT THAT":PRINT:GOTO200
850 PRINT "I CANNOT CUT WITHOUT THE CLIPPERS.":PRINT:GOTO 200
900 REM THIS IS THE EATING ROUTINE
910 IF I(3)=1 THEN 930
920 PRINT "I DON'T HAVE ANY WOLVESBANE.":PRINT:GOTO200
930 REM THIS IS THE WINNING ROUTINE
940 CLS:PRINT "A SURGE OF ENERGY RUSHES THROUGH YOUR BODY AND YOU FEEL REJUVENATED."
950 PRINT:PRINT "YOU'RE CURED AND YOU'VE WON THIS GAME."
960 PRINT:PRINT "UNFORTUNATELY, BECAUSE OF ALL THE COMMOTION A WEREPIG RUSHES FROM THE BUSHES, NIPS YOU ON THE LEG AND FLEES...BUT THAT'S ANOTHER GAME!!"
970 END
1000 REM ROOM DESCRIPTIONS
1010 DATA "YOU ARE AT A MOSS PATCH. TO THE EAST, THERE IS FOREST. TO THE WEST,A SMALL PATH. AND TO THE NORTH, A CAVE ENTRANCE
1020 DATA "YOU ARE IN A FOREST. TALL TREES SURROUND YOU. TO THE NORTH AND EAST ARE IMPASSIBLE MOUNTAINS; BUT TO THE SOUTH THERE LOOKS LIKE THERE IS A PATH THROUGH."
1030 DATA "YOU ARE ON A PASS THROUGH THE MOUNTAINS. GIANT MOUNTAINS THOUSANDS OF FEET HIGH ARE ON EITHER SIDE OF YOU, BUT TO THE SOUTH,THE PATH CONTINUES ON."
1040 DATA "YOU HAVE REACHED A PLATEAU. GRASS IS RICHLY DISTRIBUTED HERE, A FEW SHEEP CRINGE IN FEAR, AND RUN AWAY AS YOU APPROACH."
1050 DATA "YOU ARE IN A SMALL DARK ALCOVE"
1060 DATA "YOU HAVE COME UPON A DENSE SWAMP.PASSAGE MOVING ANYWHERE BUT BACK TO THE WEST IS IMPOSSIBLE."
1070 DATA "YOU ARE AT THE SHORE OF A SMALL, BUT VERY DEEP LAKE. THE WATERS ARE IN TURMOIL, MAKING SWIMMING IMPOSSIBLE. THERE IS A SMALL PATH LEADING EAST."
1080 DATA "YOU ARE AT A 'T' INTERSECTION. YOU CAN GO EAST, WEST OR SOUTH. THE PATH YOU'RE ON SEEMS TO HAVE BEEN USED BY A LARGE WILD ANIMAL."
1090 DATA "YOU ARE IN THE DEN OF A WEREWOLF. THE WEREWOLF IS HERE EITHER UNCONSCIOUS OR SLEEPING. IT HAS A LARGE BUMP ON ITS HEAD."
1100 DATA "YOU ARE AT A CAVE ENTRANCE. THE INTERIOR OF THE CAVE STRETCHES NORTH. TO THE SOUTH IS A MOSS PATCH."
1110 DATA "YOU ARE IN A VERY SMALL CAVE. IT TURNS (TIGHTLY) TO THE EAST AND TO THE SOUTH."
1120 DATA "YOU HAVE REACHED THE END OF THE CAVE. THERE IS BARELY ENOUGH ROOM TO TURN AROUND AND GET OUT.
1200 REM DATA FOR DIRECTIONS
1210 DATA 10,0,2,8
1220 DATA 0,3,0,1
1230 DATA 2,4,0,0
1240 DATA 3,0,0,5
1250 DATA 0,0,4,0
1260 DATA 0,0,0,7
1270 DATA 8,0,6,0
1280 DATA 0,7,1,9
1290 DATA 0,0,8,0
1300 DATA 11,1,0,0
1310 DATA 0,10,12,0
1320 DATA 0,0,0,11
1400 REM YOU'RE TURNING INTO A WEREWOLF DESCRIPTIONS
1410 DATA "YOU SUDDENLY FEEL YOU NEED A SHAVE... ALL OVER!!!!"
1420 DATA "YOU SCRATCH AT A FLEA ON YOUR NECK... WITH YOUR LEFT FOOT."
1430 DATA "YOU HAVE AN INTENSE CRAVING FOR A MILK BONE."
1440 DATA "YOU'RE GETTING ANGRY...YOU WANT TO KILL AND MAIM."
1450 DATA "THE SKY IS BEGINNING TO GET LIGHT. YOU HAVE AN INTENSE DESIRE TO HOWL."
1460 DATA "YOU DROP TO ALL FOURS AND TUCK YOUR TAIL BETWEEN YOUR LEGS."
1470 REM YOU'VE LOST
1480 PRINT "YOU TURN INTO A WEREWOLF AND BEGIN HOWLING AT THE RISING SUN."
1490 PRINT:PRINT"YOU ARE NETTED BY THE LOCAL DOGCATCHER AND AFTER BEING KEPT FOR THREE DAYS SENT TO THE LOCAL ZOO"
1491 PRINT"WHERE YOU ARE KEPT IN A CAGE NEXT TO NASTASSIA KINSKI."
1500 END
1600 REM INSTRUCTIONS
1610 PRINT "THIS GAME USES ONE WORD"
1620 PRINT "COMMANDS. YOU MAY USE "
1630 PRINT "THE WORDS go, get, cut, eat &"
1640 PRINT "throw AND THE DIRECTIONS n, s,"
1650 PRINT "e AND w."
1680 PRINT "THE GAME BEGINSAT MIDNIGHT."
1690 PRINT "YOU HAVE UNTIL 6 AM. TO "
1700 PRINT "SAVE YOURSELF. GOOD LUCK!"
1710 PRINT:PRINT"  PRESS ANY KEY TO CONTINUE"
1711 A$=INKEY$:IFA$=""THEN1711
1720 RETURN
1800 CLS:PRINT "THE LAST THING YOU REMEMBER"
1810 PRINT"IS HITTING A LARGE DOG-LIKE"
1820 PRINT"CREATURE WITH YOUR SILVER"
1830 PRINT"CANE. OVERHEAD IS A FULL MOON."
1840 PRINT"IN THE DISTANCE YOU HEAR THE "
1850 PRINT"CHURCH CLOCK CHIME MIDNIGHT."
1870 PRINT"LOOKING DOWN YOU SEE A WOLF-"
1880 PRINT"TAG. ON IT ARE THE WORDS:"
1900 PRINT"CAUTION...THIS ANIMAL HAS BEEN"
1910 PRINT"INOCULATED WITH WEREWOLF "
1920 PRINT"SIMPLEX II.  IF BITTEN TAKE"
1930 PRINT"WOLVESBANE IMMEDIATELY"
1931 PRINT"(IF NOT SOONER)."
1950 PRINT"HIS NAME IS LARRY."
1960 PRINT"IT IS MIDNIGHT."
1970 PRINT"PRESS ANY KEY TO CONTINUE";
1971 A$=INKEY$:IFA$=""THEN1971
1980 CLS
1990 RETURN
2000 REM BY MICHAEL A. ROSE
4000 I(1)=0:PRINT"YOU THROW THE CATNIP INTO A PIT. THE PANTHER TUMBLES IN AFTER IT.":PRINT:O(4)=0:GOTO200
