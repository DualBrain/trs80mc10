0 CLS:DIMC1,CC,P,K:GOTO10
1 C1=1:CC=32
2 IFMID$(M$,CC,1)<>""ANDMID$(M$,CC,1)<>" "THENCC=CC-1:GOTO2
3 PRINTMID$(M$,C1,CC-C1):C1=CC+1:CC=C1+31:IFC1<=LEN(M$)THEN2
4 RETURN
10 DIM A$(13),M$(30),N(13),S(13),E(13),W(13),O(11),I(11)
20 FOR A=1 TO 12
30 READ A$(A)
40 NEXT A: REM THIS READS IN ROOM DESCRIPTION DATA
50 FOR A=1 TO 12
60 READ N(A),S(A),E(A),W(A)
70 NEXT A: REM THIS READS IN ROOM VECTORS. SEE ARTICLE
80 0(1)=12:0(2)=6:0(3)=5:0(4)=4:REM 1=CATNIP, 2=PLANT CLIPPERS, 3=WOLVESBANE, AND 4=THE PANTHER
90 FOR A=1 TO 6:READ M$(A):NEXT A
100 M=24:REM YOU HAVE 24 MOVES
110 R=1:REM YOU START OUT IN ROOM #1
120 HR=12:MI=0: REM IT'S 12 PM, WITH 0 MINUTES
130 CLS
140 GOSUB 1600: REM THIS SUBROUTINE DISPLAYS THE GAME INSTRUCTIONS
150 GOSUB 1800: REM THIS SUBROUTINE DESCRIBES THE GAME SITUATION
200 REM THE PROGRAM PROPER STARTS HERE
210 M$=A$(R):GOSUB1:PRINT:REM THIS DISPLAYS THE ROOM MESSAGE OF THE ROOM YOU'RE IN.
220 REM LINES 230 THROUGH 260 CHECK THE OBJECT LIST TO SEE IF YOU'RE IN A ROOM CONTAINING AN OBJECT. MORE ON THIS NEXT ISSUE.
230 IF O(1)=R THEN PRINT "THERE IS CATNIP HERE": PRINT
240 IF O(2)=R THEN PRINT "THERE ARE PLANT CLIPPERS HERE":PRINT
250 IF O(3)=R THEN PRINT "THERE IS WOLVESBANE HERE":PRINT
260 IF O(4)=R THEN M$="THERE IS A PANTHER HERE THAT GUARDS A PASSAGE TO THE WEST":GOSUB1:PRINT
300 REM FOLLOWING IS THE TIME KEEPING SECTION.
310 REM THE VARIABLES "HR" AND "MI" ARE FOR THE HOURS AND MINUTES.
320 MI=MI+15: IF MI=60 THEN MI=0:HR=HR+1: IF HR > 12 THEN HR=1
330 IF MI=0 THEN M$=M$(HR):GOSUB1: PRINT
340 IF HR=6 THEN PRINT:GOTO 1470
350 REM FOLLOWING ARE THE DISPLAYS FOR THE TIME AND THE ROUTINE ASKING WHAT YOU WANT TO DO
360 PRINT RIGHT$(STR$(HR),2);":";RIGHT$(STR$(MI),2);:IF MI=0 THEN PRINT CHR$(8)CHR$(8)"00";
370 INPUT " COMMAND";C$:CLS
380 IF C$="GO" THEN PRINT:INPUT "GO WHERE? ";C$:PRINT:GOTO 430
390 IF C$="GET" THEN 600
400 IF C$="CUT" THEN 800
410 IF C$="THROW" THEN 700
420 IF C$="EAT" THEN 900
430 REM LINES 440 THROUGH 550 ARE PART OF THAT MOVING AROUND ROUTINE FROM THE MAY ISSUE. NOT VERY COMPLICATED, HUH?
440 IF C$="N" AND N(R) > 0 THEN R=N(R): GOTO 200
450 IF C$="NORTH" AND N(R)> 0 THEN R=N(R): GOTO 200
460 IF C$="S" AND S(R) > 0 THEN R=S(R): GOTO 200
470 IF C$="SOUTH" AND S(R) > 0 THEN R=S(R): GOTO 200
480 IF C$="E" AND E(R) > 0 THEN R=E(R): GOTO 200
490 IF C$="EAST" AND E(R) > 0 THEN R=E(R): GOTO 200
500 IF O(4)=R AND C$="WEST" THEN M$="SORRY, THE PANTHER WON'T LET YOU PASS... ALIVE!!!":GOSUB1:PRINT:GOTO 200
510 IF O(4)=R AND C$="W" THEN M$="SORRY, THE PANTHER WON'T LET YOU PASS... ALIVE!!!":GOSUB1:PRINT:GOTO 200
520 IF C$="W" AND W(R) > 0 THEN R=W(R): GOTO 200
530 IF C$="WEST" AND W(R) > 0 THEN R=W(R): GOTO 200
540 REM *** THIS IS IF THE COMMAND IS ILLEGAL ***
550 PRINT "I CANNOT DO THAT":PRINT:GOTO 200
600 REM THIS ROUTINE GETS THINGS FOR YOU. MORE TOO ON THIS NEXT ISSUE.
610 INPUT "WHAT DO YOU WANT TO GET";C$:PRINT
620 IF C$="CATNIP" AND O(1)=R THEN O(1)=0:I(1)=1:PRINT "YOU GOT IT":PRINT:GOTO 200
630 IF C$="CLIPPERS" AND O(2)=R THEN O(2)=0:I(2)=1:PRINT "YOU GOT IT":PRINT:GOTO 200
640 IF C$="WOLVESBANE" AND O(3)=R AND WC=1 THEN O(3)=0:I(3)=1: PRINT "YOU GOT IT":PRINT:GOTO 200
650 IF C$="WEREWOLF" AND R=9 THENM$="OH NO, YOU WOKE HIM UP AND HERE HE COMES.":GOSUB1:PRINT:PRINT "YOU LOSE":END
660 IF C$="PANTHER" AND R=4 THEN PRINT "THE PANTHER GETS YOU":PRINT:PRINT"YOU LOSE.":END
670 PRINT "I CANNOT":PRINT:GOTO 200
700 REM YOU'LL NEED TO THROW THE CATNIP AND THIS TELLS THE PROGRAM WHERE AND HOW
710 INPUT "WHAT DO YOU WANT TO THROW";C$:PRINT
720 IFC$="CATNIP"ANDI(1)=1ANDO(4)=RTHENI(1)=0:GOTO3000
730 PRINT:PRINT "YOU CAN'T DO THAT HERE":GOTO 200
800 REM SUBROUTINE FOR CUTTING
810 IF I(2)=0 THEN GOTO 850
820 INPUT "WHAT AM I TO CUT";C$:PRINT
830 IF C$="WOLVESBANE" AND R=5 THEN WC=1:PRINT"THE WOLVESBANE IS CUT!":PRINT:GOTO 200
840 PRINT"I CANNOT CUT THAT":PRINT:GOTO 200
850 M$="I CANNOT CUT WITHOUT THE CLIPPERS.":GOSUB1:PRINT:GOTO 200
900 REM THIS IS THE EATING ROUTINE
910 IF I(3)=1 THEN 930
920 M$="I DON'T HAVE ANY WOLVESBANE.":GOSUB1:PRINT:GOTO200
930 REM THIS IS THE WINNING ROUTINE
940 CLS:M$="A SURGE OF ENERGY RUSHES THROUGH YOUR BODY AND YOU FEEL REJUVENATED.":GOSUB1
950 PRINT:M$="YOU'RE CURED AND YOU'VE WON THIS GAME.":GOSUB1
960 PRINT:M$="UNFORTUNATELY, BECAUSE OF ALL THE COMMOTION A WEREPIG RUSHES FROM THE BUSHES, NIPS YOU ON THE LEG AND FLEES."
970 GOSUB1:END
1000 REM ROOM DESCRIPTIONS
1010 DATA "YOU ARE AT A MOSS PATCH. TO THE EAST, THERE IS FOREST. TO THE WEST, A SMALL PATH. AND TO THE NORTH, A CAVE ENTRANCE."
1020 DATA "YOU ARE IN A FOREST. TO THE NORTH AND EAST ARE IMPASSIBLE MOUNTAINS. TO THE SOUTH THERE SEEMS TO BE A PATH."
1030 DATA "YOU ARE ON A PASS THROUGH THE MOUNTAINS. PEAKS TOWER ON EITHER SIDE, BUT TO THE SOUTH, THE PATH CONTINUES ON."
1040 DATA "YOU HAVE REACHED A GRASSY PLATEAU. A FEW SHEEP CRINGE IN FEAR, AND RUN AWAY AS YOU APPROACH."
1050 DATA "YOU ARE IN A SMALL DARK ALCOVE."
1060 DATA "YOU HAVE COME UPON A DENSE SWAMP. MOVING ANYWHERE BUT BACK TO THE WEST IS IMPOSSIBLE."
1070 DATA "YOU ARE AT THE SHORE OF A SMALL, BUT DEEP LAKE. SWIMMING IS IMPOSSIBLE. THERE IS A PATH LEADING EAST."
1080 DATA "YOU ARE AT AN INTERSECTION. YOU CAN GO EAST, WEST OR SOUTH. THE PATH SEEMS TO HAVE BEEN USED BY A LARGE ANIMAL."
1090 DATA "YOU ARE IN THE DEN OF A WEREWOLF. IT IS HERE EITHER UNCONSCIOUS OR SLEEPING. IT HAS A LARGE BUMP ON ITS HEAD."
1100 DATA "YOU ARE AT A CAVE ENTRANCE. THE INTERIOR OF THE CAVE STRETCHES NORTH. TO THE SOUTH IS A MOSS PATCH."
1110 DATA "YOU ARE IN A VERY SMALL CAVE. IT TURNS (TIGHTLY) TO THE EAST AND TO THE SOUTH."
1120 DATA "YOU HAVE REACHED THE END OF THE CAVE. THERE IS BARELY ENOUGH ROOM TO TURN AROUND AND GET OUT."
1200 REM DATA FOR DIRECTIONS
1210 DATA 10,0,2,8
1220 DATA 0,3,0,1
1230 DATA 2,4,0,0
1240 DATA 3,0,0,5
1250 DATA 0,0,4,0
1260 DATA 0,0,0,7
1270 DATA 8,0,6,0
1280 DATA 0,7,1,9
1290 DATA 0,0,8,0
1300 DATA 11,1,0,0
1310 DATA 0,10,12,0
1320 DATA 0,0,0,11
1400 REM YOU'RE TURNING INTO A WEREWOLF DESCRIPTIONS
1410 DATA "YOU SUDDENLY FEEL YOU NEED A SHAVE... ALL OVER!!!!"
1420 DATA "YOU SCRATCH AT A FLEA ON YOUR NECK... WITH YOUR LEFT FOOT."
1430 DATA "YOU HAVE AN INTENSE CRAVING FOR A MILK BONE."
1440 DATA "YOU'RE GETTING ANGRY...YOU WANT TO KILL AND MAIM."
1450 DATA "THE SKY IS BEGINNING TO GET LIGHT. YOU HAVE AN INTENSE DESIRE TO HOWL."
1460 DATA "YOU DROP TO ALL FOURS AND TUCK YOUR TAIL BETWEEN YOUR LEGS."
1470 REM YOU'VE LOST
1480 M$="YOU TURN INTO A WEREWOLF AND BEGIN HOWLING AT THE RISING SUN.":GOSUB1
1490 PRINT
1491 M$="YOU ARE NETTED BY THE LOCAL DOGCATCHER AND AFTER BEING KEPT FOR THREE DAYS, YOU'RE SENT TO THE LOCAL ZOO.":GOSUB1
1500 END
1600 REM INSTRUCTIONS
1610 PRINT "THIS GAME USES SINGLE COMMANDS."
1620 PRINT "THE VOCABULARY CONSISTS OF THE"
1630 PRINT "WORDS GO, GET, CUT, EAT,"
1640 PRINT "THROW AND THE DIRECTIONS N, S,
1650 PRINT "E AND W WHICH STAND FOR NORTH,"
1660 PRINT "SOUTH, EAST AND WEST"
1670 PRINT "RESPECTIVELY."
1680 PRINT "IT IS MIDNIGHT AND  YOU HAVE"
1690 PRINT "UNTIL SIX IN THE MORNING TO"
1700 PRINT "SAVE YOURSELF. GOOD LUCK!"
1710 PRINT:PRINT "PRESS enter TO CONTINUE";:INPUTA$
1720 RETURN
1800 CLS:PRINT "THE LAST THING YOU REMEMBER IS"
1810 PRINT "HITTING A LARGE DOGLIKE CREATURE";
1820 PRINT "ON THE HEAD WITH YOUR SILVER"
1830 PRINT "CANE. LOOKING UP YOU SEE A FULL"
1840 PRINT "MOON. IN THE DISTANCE YOU HEAR"
1850 PRINT "A CHURCH BELL CHIME MIDNIGHT."
1870 PRINT "LOOKING DOWN YOU SEE A WOLF-TAG.";
1880 PRINT "ON IT ARE THE WORDS:"
1900 PRINT "CAUTION... THIS ANIMAL HAS BEEN"
1910 PRINT "INOCULATED WITH WEREWOLF SIMPLEX";
1920 PRINT "II. IF BITTEN, TAKE WOLVESBANE"
1930 PRINT "IMMEDIATELY (IF NOT SOONER)."
1950 PRINT "HIS NAME IS LARRY."
1960 PRINT "IT IS MIDNIGHT."
1970 PRINT:PRINT@480,"PRESS enter TO CONTINUE";:INPUTA$
1980 CLS
1990 RETURN
2000 REM BY MICHAEL A. ROSE
3000 M$="YOU THROW THE CATNIP INTO A PIT. THE PANTHER TUMBLES IN AFTER IT.":GOSUB1:PRINT:O(4)=0:GOTO 200
