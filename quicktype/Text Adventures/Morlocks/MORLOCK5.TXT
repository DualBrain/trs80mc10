0 CLS:CLEAR450:DIMC1,CC,M$,A(6,10),A$(6,10):C1=RND(-(PEEK(9)*256+PEEK(10))):X1=RND(6):Y1=RND(10):GOTO40
1 C1=1:CC=32
2 IFMID$(M$,CC,1)<>""ANDMID$(M$,CC,1)<>" "THENCC=CC-1:GOTO2
3 PRINTMID$(M$,C1,CC-C1):C1=CC+1:CC=C1+31:IFC1<=LEN(M$)THEN2
4 RETURN
5 PRINT:PRINT"HIT enter TO CONTINUE":GOSUB4970:CLS:RETURN
10 DL=0:PRINTCHR$(8)" ";
15 DL=DL+1:IFDL=1THENPRINTCHR$(8)CHR$(127);
20 IFDL=30THENPRINTCHR$(8)" ";
25 IFDL=60THENDL=0
30 I$=INKEY$:IFI$=""THEN15
35 PRINTCHR$(8)" ";:RETURN
40 PRINT@9,"THE CAVERN OF":FORCC=16384TO16415:POKECC,PEEK(CC)-64:NEXT
45 PRINT@98,"* T H E   M O R L O C K S *":PRINT@128+32+2,"ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒ"
50 X3=RND(6):Y3=RND(10):IFX3=2ANDY3=8THEN50
60 X=1:Y=1
70 FORX2=1TO6:FORY2=1TO10:READA$(X2,Y2):NEXTY2,X2
80 PRINT:PRINT:PRINT"DO YOU WANT TO SEE THE RULES?"
90 GOSUB4970:PRINT:IFFL=0THEN370
100 CLS:PRINT"------* RULES OF THE GAME *-----"
120 PRINT"APRIL 18, 1934. YOU ARE IN THE"
130 PRINT"JUNGLE. YOU ARRIVE AT THE ENTRY"
140 PRINT"OF A CAVE.":PRINT
150 PRINT"THIS IS THE DEN OF HORRIBLE"
160 PRINT"MORLOCKS WHERE THEY KEEP ALL"
170 PRINT"THEIR PRISONERS. YOU DECIDE"
180 PRINT"TO ATTEMPT TO RESCUE THEM"
185 PRINT"AND DESCEND...":GOSUB5
200 PRINT"AT EACH STAGE THE COMPUTER WILL"
210 PRINT"DESCRIBE THE ROOM YOU ARE IN"
220 PRINT"AND YOU MAY THEN ALSO BE POSED"
230 PRINT"A QUESTION WHICH YOU CAN ANSWER"
240 PRINT"WITH A YES OR NO (Y OR N).":GOSUB5
260 PRINT"YOU WILL THEN BE ABLE TO CHOOSE"
270 PRINT"A DIRECTION YOU WANT TO GO:"
280 PRINT"N, S, E, OR W.":PRINT
310 PRINT"TO GET AN INVENTORY OF ITEMS"
320 PRINT"YOU ARE CARRYING, TYPE I."
330 PRINT"THEY CAN BE HELPFUL OR HARMFUL."
340 PRINT:PRINT
350 PRINTTAB(11)"GOOD LUCK!":PRINT:GOSUB5
370 GOTO550
380 XX=X:YY=Y
390 PRINT
400 PRINT"WHICH DIRECTION DO YOU CHOOSE?"
410 GOSUB10
420 D$=I$:IFD$="I"THENPRINTD$:GOTO5040
430 IFD$<>"N"ANDD$<>"S"ANDD$<>"E"ANDD$<>"W"THEN410
440 F1=1
450 FORZZ=1TOLEN(A$(X,Y)):IFMID$(A$(X,Y),ZZ,1)=D$THENF1=0
460 NEXTZZ
470 IFF1=1THENPRINT:PRINT"YOU CANNOT GO THAT DIRECTION":X=XX:Y=YY:GOTO400
490 IFD$="N"THEND$="NORTH":Y=Y+1
500 IFD$="S"THEND$="SOUTH":Y=Y-1
510 IFD$="W"THEND$="WEST":X=X-1
520 IFD$="E"THEND$="EAST":X=X+1
525 SOUND200,1
530 PRINTD$
540 PRINT"--------------------------------";
550 DD$=" "
560 FORZZ=1TOLEN(A$(X,Y)):G$=MID$(A$(X,Y),ZZ,1):GOSUB5440:DD$=DD$+G$+" ":NEXTZZ
570 PRINT"POSSIBLE DIRECTIONS":PRINT:PRINTDD$:PRINT
580 IFNOT(X=1ANDY=1)THEN660
590 M$="A ROOM THAT IS COVERED IN BLOOD. THREE CORPSES ARE SPREAD ON THE GROUND.":GOSUB1
600 IFCOTHEN660
620 M$="A KNIFE IS STILL PLANTED IN THE BREAST OF ONE OF THEM.":GOSUB1
640 PRINT"DO YOU TAKE THE KNIFE?"
650 GOSUB4970:IFFLTHENCO=1
660 IFNOT(X=1ANDY=2)THEN710
670 M$="A MORLOCK RUNS TOWARDS YOU PREPARING TO ATTACK...":GOSUB1
690 IFCO=1THENPRINT"YOU KILL IT WITH YOUR KNIFE!":GOTO710
700 M$="YOU'VE BEEN STRANGLED. THIS IS THE END...":GOSUB1:GOTO5210
710 IFNOT(X=1ANDY=3)THEN770
720 M$="A SKELETON SITS IN A CORNER OF THIS AREA. HE ASKS YOU YOUR NAME!":GOSUB1
740 PRINT"DO YOU RESPOND TO HIM?"
750 GOSUB4970:IFFLTHENM$="YOU FEEL YOURSELF TURNING INTO A MORLOCK!":GOSUB1:GOTO5210
760 PRINT"NOTHING HAPPENS ..."
770 IFNOT(X=1ANDY=4)THEN860
780 M$="A BEAUTIFUL GIRL STANDS BEFORE YOU. SHE IS A SLAVE.":GOSUB1
790 IFBI=1THEN860
810 M$="SHE SAYS 'TAKE THIS GEM, IT WILL HELP YOU IN YOUR QUEST.'":GOSUB1
830 PRINT"DO YOU TAKE THIS JEWEL?"
840 GOSUB4970:IFFL=1THENBI=1:M$="SHE LOOKS AT YOU WITHOUT SAYING ANYTHING, BUT HER EYES IMPLORE YOU.":GOSUB1
850 IFBI=0THENM$="SHE SITS LOOKING SAD, PERHAPS BECAUSE SHE BELIEVES THAT WITHOUT THE JEWEL YOU ARE RISKING YOUR LIFE.":GOSUB1
860 IFNOT(X=1ANDY=5)THEN910
870 M$="A TABLE TOP. ON IT A MESSAGE TELLS YOU NOT TO FEAR THE NOISE, SNAKES AND THAT THE TREASURE MUST BE OPEN.":GOSUB1
910 IFNOT(X=1ANDY=6)THEN970
920 M$="SOME INSCRIPTIONS ARE PAINTED ON THE WALLS AS WELL AS DESIGNS THAT SEEM PREHISTORIC.":GOSUB1
950 PRINT"EXAMINE THE INSCRIPTIONS?"
960 GOSUB4970:IFFL=1THENPRINT"NOTHING SPECIAL."
970 IFNOT(X=1ANDY=7)THEN1040
980 PRINT"ELVES DANCE AROUND A WELL..."
990 M$="THEY ARE VERY HAPPY AND INVITE YOU TO WATCH.":GOSUB1
1010 PRINT"DO YOU WATCH?"
1020 GOSUB4970:IFFL=1THENM$="THEY RUSH AT YOU CHEERFULLY...":GOSUB1:GOTO5210
1030 PRINT"THEY CONTINUE THEIR DANCE..."
1040 IFNOT(X=1ANDY=8)THEN1110
1050 M$="A VAMPIRE STANDS BEFORE YOU. HE IS DETERMINED TO MAKE A GOOD MEAL OF YOU...":GOSUB1
1070 IFAI=1THENM$="HE NOTICED THE GARLIC THAT YOU HOLD AND DROPS DEAD...":GOSUB1:GOTO1110
1080 M$="DESPITE A STRONG RESISTANCE (I.E. YOUR TOUGH SKIN) HE BITES YOU AND SUCKS YOUR BLOOD. IT'S OVER, GOODBYE...":GOSUB1
1100 GOTO5210
1110 IFNOT(X=1ANDY=9)THEN1210
1120 M$="YOU SUDDENLY FIND YOURSELF FACE TO FACE WITH YOUR PERFECT DOUBLE WHO ASKS IF YOU'RE OKAY.":GOSUB1
1150 PRINT"ARE YOU OKAY?"
1160 GOSUB4970:IFFL=1THENM$="HE RESPONDS: ME TOO, IT'S GOING WELL.'":GOSUB1:GOTO1210
1170 M$="YOU SEE YOUR DOUBLE AGE RAPIDLY! LOOKING AT YOUR HANDS YOU UNDERSTAND THAT YOU ARE DOING THE SAME!":GOSUB1
1190 M$="TEN SECONDS LATER YOU'VE BECOME A SKELETON...":GOSUB1:GOTO5210
1210 IFNOT(X=1ANDY=10)THEN1300
1220 M$="A CRYSTAL ORB IN WHICH YOU CAN SEE A VIEW OF THE JUNGLE.":GOSUB1
1230 IFMR=0THEN1300
1240 M$="WILL YOU TRY TO BREAK THE ORB WITH YOUR HAMMER IN A BID TO ESCAPE?":GOSUB1
1260 GOSUB4970:IFFL=0THEN1300
1270 M$="IN THIS JUNGLE THERE IS AN UNBREATHABLE ATMOSPHERE, YOU DIE OF SUFFOCATION.":GOSUB1
1290 GOTO5210
1300 IFNOT(X=2ANDY=1)THEN1360
1310 M$="A VOICE TELLS YOU 'PRESS THE GREEN BUTTON TO KNOW MORE.'":GOSUB1
1330 PRINT"DO YOU TAP THE BUTTON?"
1340 GOSUB4970:IFFL=1THENM$="SHE RESPONDS 'IT JUST SAVED YOUR LIFE.'":GOSUB1
1350 IFFL=0THENM$="WATER FILLS THE ROOM, THE EXITS ARE BLOCKED. YOU ARE DROWNED...":GOSUB1:GOTO5210
1360 IFNOT(X=2ANDY=2)THEN1410
1370 M$="YOU ARE IN A LONG CORRIDOR. THE WALLS ARE LINED WITH TORCHES.":GOSUB1
1390 PRINT"WILL YOU TAKE ONE?"
1400 GOSUB4970:IFFL=1THENTR=1
1410 IFNOT(X=2ANDY=3)THEN1450
1420 IFTR=0THENM$="A DARK CAVE. YOU CANNOT FIND AN EXIT. YOU STAY ENTOMBED FOREVER...":GOSUB1:GOTO5210
1430 M$="A HUGE ROOM LIT DIMLY BY YOUR TORCH.":GOSUB1
1450 IFNOT(X=2ANDY=4)THEN1550
1460 M$="A STRANGE MACHINE... INSIDE, THERE IS A RED LEVER.":GOSUB1
1480 PRINT"TRY TO USE THIS MACHINE?"
1490 GOSUB4970:IFFL=0THEN1550
1500 M$="IT CAN DISAPPEAR AND REAPPEAR IN ANOTHER ROOM AT RANDOM...":GOSUB1
1530 FORJH=1TO1500:NEXTJH:X=RND(5)+1:Y=RND(9)+1
1540 GOTO540
1550 IFNOT(X=2ANDY=5)THEN1620
1560 M$="YOU ARE IN A ROOM WHERE YOU FIND AN ANVIL AND TOOLS.":GOSUB1
1580 IFMR=1THEN1620
1590 PRINT"YOU NOTICE A HAMMER."
1600 PRINT"DO YOU TAKE THIS HAMMER?"
1610 GOSUB4970:IFFL=1THENMR=1
1620 IFNOT(X=2ANDY=6)THEN1690
1630 M$="A LARGE GREENHOUSE FILLED WITH FLOWERS YOU HAVE NEVER SEEN BEFORE...":GOSUB1
1640 PRINT"DO YOU LINGER HERE?"
1660 GOSUB4970:IFFL=0THEN1690
1670 M$="THE DOOR OF THE DUNGEON OPENS UNDER YOUR FEET. TOO BAD...!":GOSUB1
1680 GOTO5210
1690 IFNOT(X=2ANDY=7)THEN1740
1700 M$="YOU ARE IN A ROOM WHERE YOU FIND A TRAPDOOR.":GOSUB1
1710 PRINT"DO YOU TRY TO OPEN IT?"
1730 GOSUB4970:IFFL=1THENM$="SOME RATS COME OUT...":GOSUB1
1740 IFNOT(X=2ANDY=8)THEN1790
1750 M$="YOU NOTICE A NEWSPAPER FROM 1835 WHICH CONTAINS A CLUE LEADING YOU TO A SECRET PASSAGE.":GOSUB1
1770 PRINT"DO YOU FOLLOW IT?"
1780 GOSUB4970:IFFL=1THENX=X3:Y=Y3:GOTO540
1790 IFNOT(X=2ANDY=9)THEN1850
1800 M$="TWO MORLOCKS ARE ABOUT TO POUNCE ON YOU!":GOSUB1
1810 IFTR=1THENM$="BUT THEY ARE TOO AFRAID OF THE FIRE OF YOUR TORCH.":GOSUB1:GOTO1850
1820 IFCO=1THENM$="DESPITE YOUR KNIFE, THEY MANAGE TO KILL YOU.":GOSUB1:GOTO5210
1830 IFMR=1THENM$="YOU KILL THE FIRST WITH YOUR HAMMER BUT THE SECOND IS TOO STRONG FOR YOU.":GOSUB1:GOTO5210
1840 M$="UNARMED, YOU COULD NOT DO ANYTHING... YOU ARE DEAD!":GOSUB1
1850 IFNOT(X=2ANDY=10)THEN1890
1860 M$="A ROBOT SAYS TO YOU 'AIAHABAYAOADATAY'":GOSUB1
1870 IFCS=1THENM$="YOU HAVE THE KEY FOR THE SECRET CODE, BUT SOMEONE ARRIVES, YOU MUST FLEE WITHOUT TRANSLATING IT.":GOSUB1
1875 IFCS=0THENM$="OF COURSE, WITHOUT THE KEY CODE, IT IS IMPOSSIBLE TO DECIPHER THE MESSAGE...":GOSUB1
1890 IFNOT(X=3ANDY=1)THEN1980
1900 M$="YOU ARE IN A ROOM WHERE THREE WOLVES ATTACK YOU.":GOSUB1
1920 IFTR=1THENM$="FORTUNATELY, THEY ARE AFRAID OF YOUR TORCH.":GOSUB1:GOTO1980
1930 M$="YOU REALIZE THE LEASHES ATTACHED TO THEM ARE NOT LONG ENOUGH TO KEEP THEM FROM REACHING YOU! "
1940 M$=M$+"THEY DEVOUR YOU... TOO BAD FOR YOU!":GOSUB1
1950 GOTO5210
1980 IFNOT(X=3ANDY=2)THEN2010
1990 M$="YOU ARE IN A ROOM WITH A DEEP WELL.":GOSUB1
2000 IFTR=1THENM$="YOU TRIP AND DROP YOUR TORCH IN THE WATER.":GOSUB1:TR=0
2010 IFNOT(X=3ANDY=3)THEN2110
2020 PRINT"A HENRI II BUFFET"
2030 IFAI=1THEN2070
2040 M$="A CLOVE OF GARLIC HAS BEEN LEFT HERE.":GOSUB1
2050 PRINT"DO YOU TAKE THE GARLIC?"
2060 GOSUB4970:IFFL=1THENAI=1
2070 IFAZ=1THEN2110
2080 M$="THERE IS ALSO A BOOK ENTITLED: 'COOKING FROM A TO Z'":GOSUB1
2090 PRINT"DO YOU TAKE THIS BOOK?"
2100 GOSUB4970:IFFL=1THENAZ=1
2110 IFNOT(X=3ANDY=4)THEN2220
2120 M$="YOU ARE IN A DARK ROOM WITH A CHEST WITH A HEAVY PADLOCK":GOSUB1
2150 PRINT"DO YOU WANT TO OPEN IT?"
2160 GOSUB4970:IFFL=0THEN2220
2170 IFMR=0THENPRINT"YOU CAN'T OPEN IT..."
2180 IFMR=1THENM$="HURRAY! THE TRUNK IS OPEN BUT MORLOCKS STARTLED BY THE NOISE BURST INTO THE "
2190 IFMR=1THENM$=M$+"ROOM AND YOU SLIT YOUR THROAT.":GOSUB1:GOTO5210
2220 IFNOT(X=3ANDY=5)THEN2300
2230 M$="A FOUNTAIN IN THE SHAPE OF A LION.":GOSUB1
2240 IFBI=0THENFL=0:GOTO2290
2250 M$="YOU NOTICE A SMALL HOLE THAT WOULD PERFECTLY FIT YOUR JEWEL.":GOSUB1
2270 PRINT"SET THE JEWEL IN THE HOLE?"
2280 GOSUB4970:IFFL=1THENM$="THE FOUNTAIN SEEMS TO SPEAK. YOU HEAR A VOICE SAYING 'YOU'RE LUCKY'":GOSUB1
2285 PRINT"RETRIEVE YOUR JEWEL?":GOSUB4970
2290 IFFL=0THENM$="YOU'RE HIT BY A JET OF BOILING WATER... YOU ARE DEAD.":GOSUB1:GOTO5210
2300 IFNOT(X=3ANDY=6)THEN2360
2310 M$="YOU ARE IN A ROOM WITH OIL ALL OVER THE FLOOR. THERE IS A STRONG SMELL OF GAS.":GOSUB1
2330 PRINT"DO YOU LINGER IN THIS ROOM?"
2340 GOSUB4970:IFFL=1ANDTR=1THENM$="THERE'S A POWERFUL EXPLOSION (YOUR TORCH!). YOU'RE DEAD.":GOSUB1
2350 PRINT"YOU SEE NOTHING OF INTEREST."
2360 IFNOT(X=3ANDY=7)THEN2460
2370 M$="A MORLOCK COOK LOOKS AT YOU STRANGELY...":GOSUB1
2390 IFAZ=0THEN2440
2400 M$="SHE TEARS YOUR COOKBOOK FROM YOUR HANDS AND LOOKS AT THE PAGE ON HOMMEAUX CARROTS.":GOSUB1
2410 M$="NEEDLESS TO SAY, YOU WILL DIE.":GOSUB1:GOTO5210
2440 M$="THERE IS A GLIMMER OF DISAPPOINTMENT IN HER BIG BLACK EYES...":GOSUB1
2460 IFNOT(X=3ANDY=8)THEN2500
2470 M$="A VOICE TELLS YOU 'YOU HAVE BEFORE YOU A POTION.'":GOSUB1
2480 PRINT"DO YOU DRINK IT?"
2490 GOSUB4970:IFFL=1THENPRINT"NOTHING..."
2500 IFNOT(X=3ANDY=9)THEN2550
2510 M$="A LONG ROOM WHERE MANY SKELETONS ARE CRAMMED. SMELLS VERY BAD.":GOSUB1
2520 M$="APART FROM THAT, THERE ARE ALSO SOME SPIDERS.":GOSUB1
2550 IFNOT(X=3ANDY=10)THEN2620
2560 M$="A LONG CORRIDOR... A MAN ASKS: 'HAVE YOU THE MESSAGE?'":GOSUB1
2580 IFME=0THENM$="AS YOU DO NOT, HE CALLS THE GUARDS WHO SLAUGHTER YOU...":GOSUB1:GOTO5210
2590 M$="FORTUNATELY, YOU SHOW HIM AND HE WHISPERS IN YOUR EAR":GOSUB1
2600 M$="'IT'S A HARD LIFE BEING AN ARTIST.'":GOSUB1
2620 IFNOT(X=4ANDY=1)THEN2690
2630 M$="YOU ARE IN A LIBRARY WITH BOOKS FULL OF DUST...":GOSUB1
2650 IFMETHEN2690
2660 M$="YOU NOTICE AN INCOMPREHENSIBLE MESSAGE.":GOSUB1
2670 PRINT"WILL YOU MEMORIZE IT?"
2680 GOSUB4970:IFFL=1THENME=1
2690 IFNOT(X=4ANDY=2)THEN2740
2700 M$="A CELL WITH SLAVES BOUND IN IRON CHAINS... "
2710 M$=M$+"THEY BEG YOU TO DELIVER THEM.":GOSUB1
2720 PRINT"DO YOU TRY?"
2730 GOSUB4970:IFFL=1THENM$="HOW? MAYBE BY BITING THE CHAINS?":GOSUB1
2740 IFNOT(X=4ANDY=3)THEN2790
2750 M$="A SMALL EMPTY ROOM. IN A CORNER IS A SMALL PACKAGE.":GOSUB1
2770 PRINT"WILL YOU OPEN IT?"
2780 GOSUB4970:IFFL=1THENM$="UNFORTUNATELY, IT IS BOOBY-TRAPPED, YOU ARE SHREDDED IN THE BLAST.":GOSUB1:GOTO5210
2790 IFNOT(X=4ANDY=4)THEN2830
2800 M$="YOU ARE INSIDE A SMALL DIMLY LIT CHAPEL. ON THE GROUND, SOME DEAD RATS...":GOSUB1
2830 IFNOT(X=4ANDY=5)THEN2950
2840 M$="YOU ARE IN FRONT OF A HUGE TREE TRUNK. GIVING IT A KICK, YOU REALIZE THAT IT IS HOLLOW.":GOSUB1
2880 PRINT"WILL YOU LOOK MORE CLOSELY?"
2890 GOSUB4970:IFFL=0THEN2950
2900 M$="THERE ARE THE REMAINS OF A BEEHIVE...":GOSUB1
2910 IFMI=1THEN2950
2920 PRINT"WITH A LITTLE HONEY."
2940 PRINT"DO YOU TAKE SOME?":GOSUB4970:IFFL=1THENMI=1
2950 IFNOT(X=4ANDY=6)THEN3040
2960 M$="AN EMPTY ROOM. SUDDENLY YOU FEEL A KNIFE AT YOUR THROAT. YOU ARE ORDERED TO HAND OVER ALL YOUR GOLD.":GOSUB1
2990 IFGO=0THENM$="YOU DO NOT HAVE ANY, YOUR ATTACKER KILLS YOU.":GOSUB1:GOTO5210
3000 PRINT"YOU DO..."
3010 M$="YOUR ATTACKER SAYS: 'YOU'RE LUCKY!' HE RUNS AWAY...":GOSUB1:GO=0
3040 IFNOT(X=4ANDY=7)THEN3100
3050 M$="A HUGE BEAR IS READY TO ATTACK YOU.":GOSUB1
3060 IFMI=0THENM$="HE JUMPS ON YOU AND EATS YOU...":GOSUB1:GOTO5210
3070 M$="LUCKILY FOR YOU, IT'S THE HONEY HE EATS AND NOT YOU.":GOSUB1:MI=0
3100 IFNOT(X=4ANDY=8)THEN3160
3110 PRINT"A SMALL ROOM WITH A CHEST."
3120 IFGO=1THEN3160
3130 PRINT"THERE'S GOLD IN IT."
3140 PRINT"DO YOU TAKE IT?"
3150 GOSUB4970:IFFL=1THENGO=1
3160 IFNOT(X=4ANDY=9)THEN3230
3170 M$="A ROOM WITH AN MC-10 ON A TABLE SITUATED IN THE MIDDLE OF THE ROOM. ON ITS SCREEN IS WRITTEN:":GOSUB1
3200 D=ABS(X-X1)+ABS(Y-Y1)
3210 M$="ONLY"+STR$(D)+" MORE ROOM(S) AND YOU WIN. THERE IS ALSO A SOFTWARE GAME...":GOSUB1
3230 IFNOT(X=4ANDY=10)THEN3290
3240 M$="A HUGE IDOL... ON AN ALTAR OF SACRIFICE, A STATUE OF A MORLOCK.":GOSUB1
3260 PRINT"DO YOU TAKE IT WITH YOU?"
3280 GOSUB4970:IFFL=1THENST=1
3290 IFNOT(X=5ANDY=1)THEN3380
3300 M$="A MUDDY HALLWAY, LIT ONLY BY THE DAYLIGHT COMING A FROM FIREPLACE WITH AN OPENNING TO THE OUTSIDE.":GOSUB1
3330 PRINT"DO YOU LOOK IN THE FIREPLACE?"
3340 GOSUB4970:IFFL=0THEN3380
3350 M$="THE MORLOCKS SEIZE YOU WHILE YOU ARE LAYING ON THE STONE SURFACE... GOODBYE!":GOSUB1
3360 GOTO5210
3380 IFNOT(X=5ANDY=2)THEN3480
3390 M$="A LARGE BANQUET HALL (ODD IN MORLOCK DEN). THERE IS A GOOD SMELL OF FOOD. YOU FEEL TIRED...":GOSUB1
3430 PRINT"DO YOU SIT?"
3440 GOSUB4970:IFFL=0ORTR=0THEN3480
3450 M$="YOU DOZE OFF. YOUR TORCH IGNITES THE ROOM. YOU PERISH IN A BEAUTIFUL FIRE...":GOSUB1:GOTO5210
3480 IFNOT(X=5ANDY=3)THEN3550
3490 M$="A PLAYPEN FULL OF SHELLS AND SAND...":GOSUB1
3509 IFSB=1THEN3529
3510 PRINT"DO YOU TAKE A BIT OF SAND?"
3520 GOSUB4970:IFFL=1THENSB=1
3529 IFCQ=1THEN3550
3530 PRINT"DO YOU TAKE ONE OF THE SHELLS?"
3540 GOSUB4970:IFFL=1THENCQ=1
3550 IFNOT(X=5ANDY=4)THEN3640
3560 M$="YOU ARE IN FRONT OF A LARGE CLOCK THAT STILL SEEMS TO WORK AS A TICK CAN BE HEARD AS THE PENDULUM MOVES."
3570 GOSUB1
3600 PRINT"DO YOU KEEP WATCHING?"
3610 GOSUB4970:IFFL=0THEN3640
3620 M$="YOU REALIZE THAT THE HANDS MOVE IN REVERSE!":GOSUB1
3640 IFNOT(X=5ANDY=5)THEN3720
3650 M$="A SALON WITH CHAIRS ARRANGED AROUND A TABLE. (ODD FOR MORLOCKS!)":GOSUB1
3655 IFCS=1THENGOTO3720
3660 M$="IN THE CENTRE OF THE TABLE THERE'S A SHEET OF PAPER ON WHICH IS WRITTEN: 'SECRET CODE KEY'":GOSUB1
3700 PRINT"DO YOU TAKE THE PAPER?"
3710 GOSUB4970:IFFL=1THENCS=1
3720 IFNOT(X=5ANDY=6)THEN3810
3730 M$="AN EMPTY ROOM EXCEPT FOR A FEW COCKROACHES...":GOSUB1
3740 IFCS=0THEN3810
3750 M$="YOU HEAR A SCREAM BEHIND YOU: 'THE CODE KEY HAS DISAPPEARED!'":GOSUB1
3770 M$="A MAN SURPRISES YOU, HE SAYS YOU'RE A THIEF. HE KILLS YOU AND TAKES THE CODE KEY.":GOSUB1
3800 GOTO5210
3810 IFNOT(X=5ANDY=7)THEN3900
3820 M$="A MAN, WHO LOOKS CHINESE, SAYS TO YOU: 'LOGIC IS THE ART OF IDENTIFYING WHAT IS HIDDEN.'":GOSUB1
3840 PRINT"DO YOU AGREE?"
3850 GOSUB4970:IFFLTHEN3890
3860 M$="HE SAYS, 'BE DAMNED!' TENS OF MORLOCKS SURGE INTO THE ROOM AND KILL YOU.":GOSUB1:GOTO5210
3890 M$="HE SAYS, 'YOU ARE VERY WISE'":GOSUB1
3900 IFNOT(X=5ANDY=8)THEN3960
3910 PRINT"A PHONOGRAPH...":IFDI=1ORRG=1THEN3960
3920 M$="THERE IS ALSO TINO ROSSI'S DEBUT ALBUM.":GOSUB1
3940 PRINT"DO YOU TAKE THE RECORD?"
3950 GOSUB4970:IFFL=1THENDI=1
3960 IFNOT(X=5ANDY=9)THEN3990
3970 M$="NOTHING, EXCEPT FETTERS, CERTAINLY FOR FUTURE SLAVES.":GOSUB1
3990 IFNOT(X=5ANDY=10) THEN4100
4000 M$="A TRANQUIL UNDERGROUND LAKE, THE WATER LOOKS VERY DIRTY. THERE IS ALSO A REVOLVER ON THE OTHER SIDE.":GOSUB1
4010 M$="IT IS POSSIBLE TO FETCH IT BY FOLLOWING THE LAKE'S EDGE.":GOSUB1
4050 PRINT"DO YOU DO THAT?"
4060 GOSUB4970:IFFL=0THEN4100
4070 M$="YOU SLIP ON A ROCK AND YOU FALL IN THE WATER. IT'S SO COLD THAT YOU ARE FROZEN IN THREE SECONDS.":GOSUB1
4090 GOTO5210
4100 IFNOT(X=6ANDY=1)THEN4190
4110 M$="BEHIND YOU, YOU HEAR THE SOUND OF A SNAKE. YOU DO NOT HAVE TIME TO SAVE YOURSELF. IT BITES YOU ...":GOSUB1
4140 IFTR=1ANDCO=1THEN4160
4150 M$="YOU DIE IN TERRIBLE CONVULSIONS. FAREWELL...":GOSUB1:GOTO5210
4160 M$="YOU CAUTERIZE THE WOUND WITH YOUR TORCH AND YOUR KNIFE. IT HURTS BUT YOU SURVIVE.":GOSUB1
4190 IFNOT(X=6ANDY=2)THEN4210
4200 M$="THE MONKS' CLOTHES ON HANGERS.":GOSUB1
4210 IFNOT(X=6ANDY=3)THEN4300
4220 M$="AN ASSEMBLY OF MONKS. THEY ALL CARRY TORCHES IN HAND. THEY ARE SITTING.":GOSUB1
4240 IFTR=1THEN4280
4250 M$="ONE APPROACHES YOU. IT HAS A SKELETON FACE! IT SHOVES A DAGGER IN YOUR SIDE...":GOSUB1:GOTO5210
4280 M$="THEY CRY TOGETHER: 'GREETINGS BROTHER!'":GOSUB1
4300 IFNOT(X=6ANDY=4)THEN4400
4310 M$="AN OLD RUSTY HANDLE. YOU CAN PULL TOWARDS YOU.":GOSUB1
4330 PRINT"DO YOU DO THAT?"
4340 GOSUB4970:IFFL=0THEN4400
4350 M$="PART OF THE FLOOR OPENS BENEATH YOUR FEET. YOU FALL INTO A PIT FULL OF SNAKES AND SPIDERS. "
4370 M$=M$+"YOU CANNOT GO BACK...":GOSUB1:GOTO5210
4400 IFNOT(X=6ANDY=5)THEN4460
4410 PRINT"A ROOM FULL OF STONES."
4420 IFDI=0THEN4460
4430 M$="SUDDENLY, YOU STUMBLE AND DROP YOUR TINO ROSSI RECORD. IT BREAKS.":GOSUB1
4450 DI=0
4460 IFNOT(X=6ANDY=6)THEN4500
4470 M$="AN EMPTY ROOM WITH DIRTY GREY WALLS. A PARTICULARLY NAUSEOUS ODOR MAKES YOU THINK A SEWER.":GOSUB1
4500 IFNOT(X=6ANDY=7)THEN4620
4510 M$="A LABORATORY WITH MANY PIECES OF EQUIPMENT, GLASS TUBES AND MULTITUDE OF JARS FILLED WITH STRANGE LIQUIDS...":GOSUB1
4520 IFRG=1THEN4620
4540 M$="A MAN IN A WHITE COAT APPROACHES AND SAYS: 'DO YOU HAVE MY FAVOURITE RECORD?'":GOSUB1
4560 IFDI=0THENPRINT"YOU DO NOT...":GOTO4600
4570 M$="HE SEES THE TINO ROSSI RECORD AND TRIES TO GRAB IT.":GOSUB1
4580 PRINT"DO YOU RELEASE IT?"
4590 GOSUB4970:IFFL=1THENDI=0:RG=1:GOTO4620
4600 M$="HE RELEASES A KIND OF GAS IN THE ROOM.":GOSUB1
4610 PO=1
4620 IFNOT(X=6ANDY=8)THEN4660
4630 M$="A LARGE ROOM FULL OF DUST. THERE ARE SOME DISFIGURED BODIES COVERED IN BLOOD...":GOSUB1
4660 IFNOT(X=6ANDY=9)THEN4750
4670 IFTR=1THEN4710
4680 M$="A ROOM THAT'S SO GLOOMY YOU DON'T EVEN SEE THE MORLOCK WHO SETS UPON YOU AND KILLS YOU.":GOSUB1
4690 GOTO5210
4710 M$="A DARK ROOM, LIT JUST ENOUGH BY YOUR TORCH THAT YOU SPOT A MORLOCK WHO WANTS TO ATTACK YOU. HE FLEES.":GOSUB1
4750 IFNOT(X=6ANDY=10)THEN4770
4760 M$="A ROOM WITH AN ELAVATOR. DO YOU ATTEMPT TO USE IT?":GOSUB1
4762 GOSUB4970:IFFL=0THENGOTO4770
4764 M$="THE ELEVATOR STARTS BY ITSELF... IT GOES DOWN, DOWN...":GOSUB1
4765 FORT=1TO1300:NEXTT:PRINT:M$="IT STOPS... THE DOOR OPENS... IN HELL!":GOSUB1
4766 GOTO5210
4770 IFX1=XANDY1=YTHENFORT=1TO1500:NEXTT:GOTO4790
4780 GOTO380
4790 F$="********************************"
4800 PRINTF$;"*            YOU WIN!          *";F$:FORT=1TO2500:NEXTT
4810 M$="LOOKING AT THE ROOM WHERE YOU ARE, YOU NOTICE A MESSAGE CONTAINING A MAGIC FORMULA TO MAKE THE MORLOCKS, "
4820 M$=M$+"AS WELL AS THE OTHER DENIZENS OF THE CAVE, HARMLESS! YOU ALSO NOTICE THE KEY TO OPEN ALL THE PASSAGES!":GOSUB1
4890 IFBI=0THEN4920
4900 M$="YOU FREE THE GIRL, WHO BECOMES THE LOVE OF YOUR LIFE!":GOSUB1
4920 IFPO=0THEN4950
4930 M$="BUT A STRANGE DISEASE TAKES YOU IN A FEW WEEKS!...":GOSUB1
4950 M$="A SECRET PASSAGE LEADS TO THE OUTSIDE.":GOSUB1
4960 GOTO5210
4970 GOSUB10:A$=I$:IFA$<>"N"ANDA$<>"Y"ANDA$<>CHR$(13)THEN4970
4990 PRINT
5000 IFA$="Y"ORA$=CHR$(13)THENFL=1:A$="YES"
5010 IFA$="N"THENFL=0:A$="NO"
5020 PRINT"->";A$
5030 RETURN
5040 PRINT:PRINT"------- INVENTORY ITEMS --------"
5060 IFCOTHENPRINT"A KNIFE"
5070 IFBITHENPRINT"A GEM"
5080 IFTRTHENPRINT"A TORCH"
5090 IFMRTHENPRINT"A HAMMER"
5100 IFAITHENPRINT"A CLOVE OF GARLIC"
5110 IFAZTHENPRINT"A BOOK: THE KITCHEN FROM A TO Z"
5120 IFMETHENPRINT"A CODED MESSAGE"
5130 IFMITHENPRINT"SOME HONEY"
5140 IFGOTHENPRINT"SOME GOLD"
5150 IFSTTHENPRINT"A STATUE OF A MORLOCK"
5160 IFSBTHENPRINT"A LITTLE SAND"
5170 IFCQTHENPRINT"A SEASHELL"
5180 IFCSTHENPRINT"THE KEY TO THE SECRET CODE"
5190 IFDITHENPRINT"A TINO ROSSI RECORD"
5200 GOTO390
5210 PRINT
5220 PRINT"DO YOU WANT TO TRY AGAIN?"
5230 GOSUB4970:IFFL=1THENRUN
5240 CLS:PRINT:PRINTTAB(12)"GOODBYE!"
5250 END
5260 DATA NE,NS,NSE,NSE
5270 DATA NS,NSE,NSE,NSE
5280 DATA NSE,S
5290 DATA EW,NE,EWS,W
5300 DATA E,NW,SEW,NEW
5310 DATA NSEW,S
5320 DATA NW,NSEW,SEW,NE
5330 DATA NSW,SE,NW,NSW
5340 DATA NSEW,S
5350 DATA E,EW,NEW,WS
5360 DATA NE,NSEW,NSW,SE
5370 DATA NEW,SE
5380 DATA NEW,NSEW,SEW,NE
5390 DATA NSEW,NSW,NS,NSEW
5400 DATA NSEW,SEW
5410 DATA NW,SW,NW,NSW
5420 DATA NSW,NS,NS,SW
5430 DATA NW,SW
5440 IFG$="W"THENG$="WEST"
5450 IFG$="E"THENG$="EAST"
5460 IFG$="S"THENG$="SOUTH"
5470 IFG$="N"THENG$="NORTH"
5480 RETURN
