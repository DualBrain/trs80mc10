0 CLEAR1000:DIMC1,C2:GOTO50
1 C1=1:C2=32
2 IFMID$(M$,C2,1)<>""ANDMID$(M$,C2,1)<>" "THENC2=C2-1:GOTO2
3 PRINTMID$(M$,C1,C2-C1):C1=C2+1:C2=C1+31:IFC1<=LEN(M$)THEN2
4 RETURN
5 GOTO1410
6 GOTO3630
8 M$="OK"
9 GOSUB1:GOTO540
50 DIMO$(52),OB(52),R$(32),R(32),DI$(32):BA=0:EG=0:GG=0
60 AD=0:CC=0:UH=0:PS=0:MP=0:WG=0:EI=0:CL=0:DF=0:DS=0:DD=0:AI=0:PA=0:PR=0:MC=0:ME=1:AM=0:CM=0:FB=0:AA=0:NO=52:NR=32
90 FORI=1TO32:READR$(I):NEXT:FORI=1TO32:READDI$(I):NEXT:FORI=1TONO:READOB(I):NEXT:FORI=1TONO:READO$(I):NEXT
150 DATA"A DARK, DAMP AND GLOOMY PIT. RAYS OF LIGHT SHINING DOWN FROM ABOVE ARE THE ONLY SOURCE OF LIGHT."
160 DATA"A SMALL NARROW OPENING.","A TUNNEL.","A LARGE UNDERGROUNG CAVERN."
190 DATA"AN UNDERGROUND HOLLOW.","AN OPEN FIELD.","A YARD.","AN OLD ABANDONED SHACK."
230 DATA"A SMALL, DINGY BEDROOM.","A KITCHEN.","A SMALL SENTRY POST.","NEXT TO A MOAT."
270 DATA"INSIDE A MERCHANT'S TENT.","A RIVER BANK.","AN EERIE GRAVEYARD."
300 DATA"NEAR A BOTTOMLESS CHASM.","A SMALL SANDY CLEARING.","A CLIFF RIDDEN AREA."
330 DATA"A LARGE CLOSET.","A LONG FEATURELESS HALL.","INSIDE AN ANCIENT ARCHIVE."
360 DATA"A COURTYARD OF THE CASTLE.","THE GUARDS ROOM","A SMALL SECRET ROOM."
390 DATA"A WEAPON ROOM.","AN OLD SMELLY DUNGEON.","A BLOOD STAINED TORCHURE CHAMBER."
420 DATA"A SECRET CAVE.","ON TOP OF A LADDER.","A DRAGON'S LAIR."
430 DATA"A ROOM WITH WALLS OF SOLID GOLD.","A BIG HOLE, WITH WALLS OF SOLID GOLD"
475 DATA "N ","S ","E W ","E W ","E ","N S E D ","S ","E W S "
480 DATA "E ","W ","S ","W ","N W ","E ","S ","N ","W "
485 DATA "E ","W ","N ","W ","N S E W ","W ","N ","E S "
490 DATA "W U ","E ","S ","D ","N E ","W ","U "
500 VI$="VISIBLE ITEMS: ":CLS:GOTO6180
540 A$="":F$="":S$="":IFR=2THENEG=EG+1:IFEG=10ANDGG<>1THEN7620
550 IFR=30ANDDD<>1THENFB=FB+1
560 IFFB<>3THEN610
561 SH=SH+1
570 IFOB(44)<>-1THENM$="I HAVE BEEN BURNT BY A FIRE BREATHING DRAGON!":GOSUB1:GOTO7620
580 IFOB(44)=-1THENM$="THE DRAGON SENDS A BREATH OF FIRE AT ME, BUT IT REFLECTS OFF MY SHIELD.":GOSUB1:FB=0
590 IFSH=2THENM$="I DON'T KNOW IFI CAN HOLD THE SHIELD FOR MUCH LONGER, BECAUSE IT'S VERY HOT!":GOTO9
600 IFSH=3THENM$="THE SHIELD IS TOO HOT TO HOLD, SO I DROP IT!":GOSUB1:OB(44)=R:GS=1:GOSUB8090:GOTO6
610 INPUT"COMMAND";A$
620 IFLEN(A$)>15THENM$="BE SENSIBLE!":GOTO9
630 IFA$=""THEN7310
650 IFA$<>""THENA$=A$+" "
660 FORI=1TOLEN(A$)
670 IFMID$(A$,I,1)<>" "THENNEXT
680 F$=LEFT$(A$,I-1)
690 FORI=LEN(A$)-1TO1STEP-1
700 IFMID$(A$,I,1)<>" "THENNEXT
710 S$=MID$(A$,I+1,LEN(A$)-I-1)
720 IFF$<"A"ORF$>"Z"THEN7310
730 C1=ASC(A$)-64:IFC1<1ORC1>26THEN750
731 ONC1GOTO740,750,790,1020,1270,2000,2070,3100,3100,3300,3510,3610,3740,3840,3970,4150
732 ONC1-16GOTO4400,4470,4690,5340,5520,5890,5890,6050,7310,7310
750 IFF$<>"BUY"THEN7310
770 GOTO3020
790 IFF$<>"CLIMB"THEN910
810 FORI=1TONO:IFS$=O$(I)THEN830
811 NEXT:GOTO7310
830 IFOB(I)=-1THEN7360
840 IFOB(I)<>RTHEN7360
850 IFS$=O$(1)ANDOB(1)=1THENCL=1:GOTO7250
860 IFS$=O$(1)THEN7250
870 IFS$=O$(49)THENM$="THE WALLS ARE FAR TOO SLIPPERY.":GOTO9
880 IFF$<>"CHOP"THEN1010
890 FORI=1TONO:IFS$=O$(I)THEN940
900 NEXT:GOTO7310
910 IFOB(8)=-1ANDBA=0THEN950
920 IFOB(8)<>-1THEN7360
930 IFBA=1THENM$="TRY EXAMINING AXE!":GOTO9
940 IFR=6ANDS$=O$(3)ANDAD<>1THENM$="THE VIBRATIONS LOOSEN THE APPLE, AND IT FALLS TO THE GROUND.":AD=1:BA=1:GOTO9
950 IFR=20ANDS$=O$(30)THENM$="I MAKE A HOLE BIG ENOUGH TO GET THROUGH.":CC=1:BA=1:GOTO9
960 IFR=7ANDS$=O$(35)THENM$="YOU HAVE FUN, CHOPPING THE FENCE INTO SPLINTERS":GOSUB1:OB(35)=0:OB(48)=7:BA=1:GOTO6
970 IFR=6ANDS$=O$(3)THENM$="THE VIBRATIONS CAUSED BY THE AXE, CAUSE THE APPLE TO FALL.":AD=1:BA=1:GOTO9
980 IFR=1ORR=2ANDS$=O$(1)THENM$="YOU VIOLENTLY CHOP THE LADDER INTO SPLINTERS.":GOSUB1:LD=1:BA=1:OB(1)=0:OB(50)=R:GOTO6
990 GOTO7580
1000 IFF$<>"CUT"THEN7310
1010 GOTO5160
1020 IFF$<>"D"THEN1070
1040 IFR=6THEN6180
1050 IFR=29ANDSR=1THEN6180
1060 IFR=29ANDSR=2THEN6210
1070 IFF$<>"DROP"THEN1140
1080 FORI=1TONO:IFS$=O$(I)THEN1100
1090 NEXT:GOTO7310
1100 IFOB(I)<>-1THEN7340
1110 IFS$=O$(31)THENWG=0
1120 IFS$=O$(26)THEN4280
1130 OB(I)=R:EI=EI-1:GOTO6
1140 IFOB(9)<>-1THEN7360
1160 IFF$<>"DIG"THEN7310
1170 IFR<>15THEN1220
1180 IFDF=1THEN7500
1190 IFSA<>1THENM$="GHOST WON'T LET ME!":GOTO9
1200 OB(13)=15:OB(15)=15:DF=1
1210 GOTO7520
1220 IFR<>17THEN7500
1230 IFDS=1THEN7500
1240 OB(10)=17: DS=1
1250 GOTO7520
1270 IFF$<>"E"THEN1460
1280 ONRGOTO5,5,6210,6240,6270,6510,5,6450,6390,5,5,5,5,6540,5,5,5,6660,6760,5,5,6790,5,5,6850,5,6940
1410 IFR<>30THEN1440
1420 IFDD<>1THENM$="DRAGON WON'T LET ME!":GOTO9
1430 IFDD=1THEN7140
1440 GOTO7460
1460 IFF$<>"EXAM"THEN1500
1470 FORI=1TONO:IFS$=O$(I)THEN1500
1480 NEXT
1490 GOTO7310
1500 IFOB(I)<>RANDOB(I)<>-1THEN7360
1501 GOTO1510
1510 IFS$=O$(1)THENM$="IT'S FAIRLY SHORT.":GOTO9
1520 IFS$<>O$(38)THEN1550
1530 IFR=16ORR=17THENM$="IT'S A 2-METRE WIDE BOTTOMLESS CHASM.":GOSUB1
1531 GOTO7360
1550 IFS$=O$(2)THENM$="HE GIVES YOU A BIG GRIN, WHILST LICKING HIS CHOPS":GOTO9
1560 IFS$=O$(18)THENM$="IT'S GOT SOMETHING WRITTEN ON IT.":GOTO9
1570 IFS$=O$(19)THENM$="IT LOOKS QUITE VALUABLE.":GOTO9
1580 IFS$=O$(44)ANDSH=0THENM$="IT'S GOT A SENSE OF STRENGTH ABOUT IT!":GOTO9
1590 IFS$=O$(44)ANDSH>1THENM$="THE SHIELD IS RED HOT!":GOTO9
1600 IFS$=O$(3)ANDAA=0THEN1610
1601 GOTO1620
1610 M$="THE TREE IS PETRIFIED, YET THERE IS A RED APPLE ON ONE OF IT'S BRANCHES.":GOSUB1:OB(4)=R:AA=1:GOSUB8090:GOTO6
1620 IFS$=O$(3)ANDAA=1ANDAD=0THEN1610
1630 IFS$=O$(3)ANDAD=1THENM$="IT'S PETRIFIED!":GOTO9
1640 IFS$="SYMBOL"THENM$="IT RESEMBLES A GHOST.":GOTO9
1650 IFS$=O$(37)THENM$="IT'S RUSTY":GOTO9
1660 IFS$=O$(4)THENM$="IT'S A BIG, RED, HEATHLY, LOOKING APPLE.":GOTO9
1670 IFS$=O$(21)THENM$="IT'S STUCK TO THE WALL":GOTO9
1680 IFS$=O$(22)THENM$="IT'S A SMELLY DECAYED CORPSE.":GOTO9
1690 IFS$=O$(51)THENM$="IT'S THE TREASURE OF ELGON!":GOTO9
1700 IFS$=O$(36)THENM$="IT'S A SHARPENING STONE.":GOTO9
1710 IFS$=O$(24)THENM$="THERE'S SOMETHING WRITTEN ON IT.":GOTO9
1720 IFS$=O$(6)THENM$="IT LOOKS DESERTED.":GOTO9
1730 IFS$=O$(26)THENM$="IT SEEMS STURDY.":GOTO9
1740 IFS$=O$(7)ANDMS=0THENM$="HE LOOKS POOR.":GOTO9
1750 IFS$=O$(7)ANDMS=1THENM$="HE'S DEAD! AND I DON'T THINK HIS CONDITION WILL GET ANY BETTER!":GOTO9
1760 IFS$<>O$(20)THEN1790
1770 IFAI<>1THENM$="THERE IS A SMALL ROUND HOLE IN THE MIDDLE.":GOTO9
1780 M$="THERE IS AN ANKH INSERTED IN THE MIDDLE OF IT.":GOTO9
1790 IFS$=O$(8)THEN1980
1800 IFS$=O$(29)THENM$="IT HAS A VERY STRONG CURRENT.":GOTO9
1810 IFS$<>O$(30)THEN1840
1820 IFCC<>1THENM$="IT'S AN OLD WOODEN CLOSET, AND IT'S LOCKED":GOTO9
1830 M$="IT'S GOT A HOLE CHOPPED IN IT.":GOTO9
1840 IFS$=O$(31)THENM$="THEY ARE READING GLASSES.":GOTO9
1850 IFS$=O$(10)THENM$="IT'S GOT A STRANGE SYMBOL THAT PROTRUDES FROM IT.":GOTO9
1860 IFS$=O$(32)THENM$="IT'S STEEL AND IT FEELS VERY THICK.":GOTO9
1870 IFS$=O$(11)THENM$="HE IS BIG, AND LOOKS LIKE HE COULD HAVE GREAT STRENGTH.":GOTO9
1880 IFS$<>O$(35)THEN1910
1890 IFPA=1THEN7420
1900 M$="IT HAS A LOOSE PLANK, NAILED ON WITH ONE NAIL.":GOTO9
1910 IFS$=O$(12)THENM$="IT IS AN OLD GRAVE COVERED WITH WEEDS.":GOTO9
1920 IFS$=O$(34)THENM$="IT HAS A CLAWED HEAD.":GOTO9
1930 IFS$=O$(38)THENM$="THE CHEST LOOKS VERY OLD AND YOU SENSE IT IS FULL OF TREASURE.":GOTO9
1940 IFS$=O$(13)THENM$="THEY LOOK LIKE THEY ARE HUMAN.":GOTO9
1950 IFS$=O$(14)THENM$="IT IS A RING WITH A SMALL THIN SCREW COMING OFF IT.":GOTO9
1960 IFS$=O$(33)THENM$="IT'S A SENTRY POST!":GOTO9
1970 GOTO7420
1980 IFBA<>1THENM$="IT'S SHARP":GOTO9
1990 IFBA=1THENM$="IT'S BLUNT.":GOTO9
2000 IFF$<>"FEED"THEN7310
2020 IFS$<>O$(2)THEN7580
2030 IFOB(4)<>-1THEN7360
2040 IFR<>2THEN7360
2050 M$="THE GRUE HAS LIVED FOR OVER 100 YEARS AND ALONG THE WAY, HE HAS LEARNT HOW TO FEED HIMSELF!":GOTO9
2070 IFF$<>"GET"THEN2550
2090 IFEI>4THENM$="I'M CARRYING TOO MUCH!":GOTO9
2100 FORI=1TONO:IFS$=O$(I)THEN2110
2101 NEXT:GOTO7310
2110 IFOB(I)<>-1THEN2120
2111 GOTO2140
2120 IFS$<>O$(26)THEN2170
2130 IFPR=1ORPA=0ANDR=7THENM$="HOW?":GOTO9
2140 IFPA=1ANDOB(26)=RTHEN2540
2150 IFR<>7THEN7380
2160 IFOB(26)=-1THEN7480
2170 IFS$=O$(52)THEN2180
2171 GOTO2200
2180 IFR=31ANDMC=1THEN7400
2190 M$="WHAT?":GOTO9
2200 IFOB(I)<>RTHEN7380
2210 IFS$=O$(36)THENPRINT"IT'S HEAVY!":GOSUB8090:GOTO2540
2220 IFS$=O$(1)THEN2540
2230 IFS$=O$(48)THEN2540
2240 IFS$=O$(51)THEN7190
2250 IFS$=O$(44)ANDGS=0THEN2540
2260 IFS$=O$(44)ANDGS=1THENM$="TOO HOT!":GOTO9
2270 IFS$=O$(43)THENPRINT"I FEEL POWER WITHIN IT":GOSUB8090:GOTO2540
2280 IFS$=O$(8)THEN2540
2290 IFS$=O$(9)ANDMP<>1THENM$="MERCHANT WANTS SOMETHING.":GOTO9
2300 IFS$=O$(9)ANDMP=1THEN2540
2310 IFS$=O$(18) OR S$=O$(38)THENM$="IT'S TOO HEAVY!":GOTO9
2330 FORX=10TO11:IFS$=O$(X)THEN2540
2331 NEXT
2340 FORY=13TO15:IFS$=O$(Y)THEN2540
2341 NEXT
2350 IFS$=O$(18)THEN7400
2360 IFS$=O$(12)THEN7400
2370 IFS$=O$(19)THEN2540
2380 IFS$=O$(26)THEN2540
2390 IFS$=O$(27)THEN2540
2400 IFS$=O$(31)THEN2540
2410 IFS$=O$(7)ANDMS=0THENM$="HE AVOIDS YOU SAYING 'ARE YOU SOME KIND OF WEIRDO?'":GOTO9
2420 IFS$=O$(7)ANDMS=1THENM$="I DON'T WANT TO PICK UP A BODY WITH IT'S GUTS ALL OVER THE PLACE!":GOTO9
2430 IFS$=O$(34)THEN2540
2440 IFS$=O$(35)ANDUH=1THEN2540
2450 IFS$=O$(37)ANDPS=1THEN2540
2460 IFS$<>O$(22)THEN2490
2470 M$="NO THANKS!":GOTO9
2490 IFS$=O$(24)THENM$="IT'S STUCK!":GOTO9
2500 IFS$=O$(4)ANDAA=0THENM$="I CAN'T SEE ANY APPLE AROUND HERE!":GOTO9
2510 IFS$=O$(4)ANDAD<>1THENM$="I CAN'T REACH IT!":GOTO9
2520 IFS$=O$(4)ANDAD=1THENOB(4)=-1:GOTO6
2530 GOTO7400
2540 OB(I)=-1:EI=EI+1:GOTO6
2550 IFF$<>"GO"THEN2920
2570 IFS$<>O$(5)THEN2630
2580 IFR<>2THEN7360
2590 IFGG<>1THEN2790
2600 IFOB(1)=-1THENM$="SOMETHING WON'T FIT!":GOTO9
2610 IFS$=O$(5)ANDR<>2THEN7360
2620 GOTO6240
2630 FORI=1TONO:IFS$=O$(I)THEN2640
2631 NEXT:GOTO7360
2640 IFS$="PLANK"THEN2750
2650 IFS$=O$(52)ANDR=31ANDMC=1THEN7290
2660 IFOB(I)<>RTHEN7360
2670 IFS$=O$(6)ANDR=7THEN6390
2680 IFS$=O$(25)THEN6940
2690 IFS$=O$(17)THEN6880
2700 IFS$=O$(1)THEN830
2710 IFS$=O$(42)THEN6820
2720 IFS$=O$(29)THENM$="I'VE BEEN SWEPT UNDER BY THE CURRENT, AND DROWN.":GOSUB1:GOTO7620
2730 IFS$=O$(23)THEN7000
2740 IFS$<>O$(26)THEN2830
2750 IFPR<>1THEN7600
2760 IFR=14THEN6600
2770 IFR=15THEN6570
2780 GOTO7600
2790 IFGG<>1THENM$="GRUE WON'T LET ME!":GOTO9
2800 GOTO6240
2810 IFAI<>1THENM$="I DON'T KNOW WHAT YOU MEAN.":GOTO9
2820 GOTO7000
2830 IFS$<>O$(33)THEN2860
2840 IFR<>12THEN7360
2850 GOTO6480
2860 IFS$<>O$(30)THEN2890
2870 IFCC<>1THENM$="ONLY GHOSTS CAN WALK THROUGH DOORS!":GOTO9
2880 IFCC=1THEN6730
2890 IFS$<>O$(32)THEN7310
2900 IFPS=1ANDDO=1THEN7060
2901 GOTO7360
2920 IFF$<>"GIVE"THEN7310
2940 FORI=1TONO:IFS$=O$(I)THEN2960
2950 NEXT:GOTO7310
2960 IFS$<>O$(4)THEN3010
2970 IFR=13THEN3060
2980 IFR<>2THEN7360
2990 IFOB(4)<>-1THEN7360
3000 M$="TO WHOM?":GOTO9
3010 IFS$<>O$(19)THEN3050
3020 IFR<>13THEN7360
3030 IFOB(19)<>-1THEN7360
3040 CM=1:GOTO3000
3050 IFS$<>O$(4)THEN7600
3060 IFOB(4)<>-1THEN7360
3070 M$="TO WHOM?":ME=1:AM=1:GOTO9
3080 GOTO7600
3100 IFF$<>"I"THEN3200
3120 Z=0:FORI=1TONO:IFOB(I)=-1THENZ=Z+1
3130 NEXT:IFZ=0THEN7340
3150 M$="I AM CARRYING THE FOLLOWING:":GOSUB1:M$="":FORI=1TONO
3170 IFOB(I)=-1THENM$=M$+O$(I)+", "
3180 NEXT:C1=LEN(M$):M$=LEFT$(M$,C1-2)+".":GOSUB1:GOTO540
3200 IFF$<>"INSERT"THEN7310
3210 FORI=1TONO:IFS$=O$(I)THEN 3230
3220 NEXT:GOTO7310
3230 IFS$<>O$(14)THEN7580
3240 IFOB(14)<>-1THEN7360
3250 IFR<>18THEN7360
3260 M$="IT FITS PERFECTLY IN THE HOLE.":GOSUB1:GOSUB8090:OB(14)=0:AI=1:GOTO6
3300 IFF$<>"JUMP"THEN7310
3320 IFR<>29THEN3370
3330 IFCL=1THEN6330
3340 M$="I JUMP UP, AND LUCKILY LAND BACK ON THE LADDER JUMPING WHILST ON A LADDER IS NOT A SAFE THING TO DO!":CL=0:GOTO9
3370 IFR=16THENM$="I JUMP ACROSS THE CHASM, AND JUST MAKE IT TO THE OTHER SIDE.":GOSUB1:GOSUB8090:GOTO6660
3410 IFR<>17THEN3460
3420 IFOB(36)=-1THENM$="THE WEIGHT OF THE STONE, SHORTENS THE JUMP, AND I FALL DOWN THE PIT.":GOSUB1:GOTO7620
3430 M$="I JUMP ACROSS THE CHASM, AND JUST MAKE IT TO THE OTHER SIDE.":GOSUB1:GOSUB8090:GOTO6630
3460 IFR=1THENM$="I JUMP AS FAR AS I COULD, BUT I END UP A FEW INCHES SHORT FROM THE TOP.":GOTO9
3480 IFR<>2THENM$="WHEE!":GOTO9
3490 M$="I HIT MY HEAD ON THE ROOF.":GOTO9
3510 IFF$<>"KILL"THEN7310
3530 IFS$=O$(7)ANDR=13ANDMS=0THEN3570
3540 IFS$=O$(46)ANDR=30ANDDD=0THEN3580
3550 IFS$=O$(2)ANDR=2ANDGG=0THEN3560
3551 GOTO7600
3560 IFS$<>O$(2)ANDR<>2THEN3570
3561 GOTO3590
3570 IFS$<>O$(7)ANDR<>13THEN3580
3571 GOTO3590
3580 IFS$<>O$(46)ANDR<>30THEN7580
3590 M$="HOW?":GOTO9
3610 IFF$<>"LOOK"THEN7310
3630 CLS:M$=R$(R):GOSUB1:M$=VI$:FORI=1TONO:IFOB(I)=RTHENM$=M$+O$(I)+", "
3680 NEXT:C1=LEN(M$):M$=LEFT$(M$,C1-2)+".":GOSUB1:GOSUB9010:GOSUB9000:GOTO540
3740 IFF$<>"MOVE"THEN7310
3760 FORI=1TONO:IFO$(I)=S$THEN3770
3761 NEXT:GOTO7310
3770 IFS$<>O$(38)THEN3810
3780 IFR<>31THEN7360
3790 IFMC=1THEN3800
3791 M$="AFTER MOVING THE CHEST, YOU DISCOVER A SMALL HOLE UNDER IT.":GOSUB1:GOSUB8090:OB(52)=31:MC=1:GOTO6
3800 M$="OK, I MOVE IT BACK OVER THE HOLE.":GOSUB1:OB(52)=0:MC=0:GOSUB8090:GOTO6
3810 IFS$=O$(36)THEN8
3820 FORI=1TONO:IFS$=O$(I)THEN7580
3821 NEXT:GOTO7310
3840 IFF$<>"N"THEN7310
3860 IFR=1THEN6210
3870 IFR=16THEN6600
3880 IFR=13THEN6330
3890 IFR=20THEN6820
3900 IFR=24THEN6790
3910 IFR=6THEN6360
3920 IFR<>30THEN3950
3930 IFDD<>1THENM$="DRAGON WON'T LET ME!":GOTO9
3940 GOTO6300
3950 IFR=22THEN6910
3960 GOTO7460
3970 IFF$<>"OPEN"THEN7310
3990 FORI=1TONO:IFS$=O$(I)THEN4010
4000 NEXT:GOTO7310
4010 IFS$<>O$(21)THEN4070
4020 IFBO=1THENM$="IT'S ALREADY OPEN!":GOTO9
4030 IFR<>11THEN7360
4040 IFBU<>1THENM$="IT'S LOCKED!":GOTO9
4050 OB(16)=11:BO=1:GOTO6
4070 IFS$<>O$(32)THEN4120
4080 IFR<>5THEN7360
4090 IFPS<>1THEN7360
4100 DO=1:GOTO8
4120 IFS$<>O$(38)THEN7580
4130 IFR<>31THEN7360
4140 M$="IT WON'T BUDGE!":GOTO9
4150 IFF$<>"PAY"THEN4220
4170 IFS$<>O$(7)THEN4250
4180 IFMP=1ANDOB(19)=-1THENOB(19)=0:GOTO8
4190 IFR<>13THEN7360
4200 IFOB(19)<>-1THEN7360
4210 OB(19)=0:MP=1:M$="THE MERCHANT OFFERS YOU THE SHOVEL.":GOTO9
4220 IFF$<>"PUT"THEN4330
4230 FORI=1TONO:IFS$=O$(I)THEN4260
4240 NEXT:GOTO7310
4250 GOTO7580
4260 IFS$<>O$(26)THEN4320
4270 IFOB(26)<>-1THEN7360
4280 IFR<>14THEN4320
4290 M$="THE PLANK IS PLACED OVER THE RIVER.":GOSUB1:GOSUB8090:OB(41)=15:OB(26)=R:PR=1
4320 OB(I)=R:GOTO6
4330 IFF$<>"PULL"THEN4380
4340 IFS$<>O$(16)THEN4390
4350 M$="YOU HEAR A LOUD THUD, BUT NOTHING APPEARS TO HAPPEN.":OB(42)=12:DB=1:GOTO9
4380 IFF$<>"PLACE"THEN7310
4390 GOTO4230
4400 IFF$<>"QUIT"THEN7310
4420 PRINT"SURE YOU WANT TO QUIT <Y/N>?"
4440 Z$=INKEY$:IFZ$="Y"THEN7630
4450 IFZ$="N"THEN8
4451 GOTO4440
4470 IFF$<>"READ"THEN4600
4490 FOR I=1 TO NO:IFS$=O$(I)THEN4510
4500 NEXT:GOTO7310
4510 IFS$<>O$(24)THEN4560
4520 IFWG<>1THEN7360
4530 IFR<>1THEN7360
4540 PRINT:M$="IF YOU ARE STUMPED,THENTHE MAGIC WORD IS IN ALPHABETICAL; AND 5":GOSUB1
4550 M$="LONG; AND DON'T FORGET FIRST ONE IS ALSO THE LAST...":GOTO9
4560 IFS$<>O$(18)THEN4580
4570 IFR<>21 OR WG<>1THEN7360
4580 M$="THE BOOK HAS ONE WORD WRITTEN IN IT: 'RACK'":GOSUB1:PRINT:OB(17)=21:GOSUB8090
4590 GOTO6
4600 IFF$<>"RESTORE"THEN7310
4610 IFS$<>"GAME"THEN7310
4620 OPEN "I",#1,"ELGON.SAV"
4630 FOR I=1 TO NO:INPUT #1,OB(I):NEXT
4640 INPUT #1,R
4650 INPUT #1,BA,GG,DD,CL,CC,AD,EI,DF,SA,DS,AI,PR,PA,MP,UH,PS,EG
4660 INPUT #1,DO,YS,BO,BU,SA,CR,WG,CM,AM,ME,SH,GS,MS,AA,MC,AD
4670 CLOSE #1
4680 GOTO6
4690 IFF$<>"S"THEN4810
4710 IFR=22THEN6760
4720 IFR=6THEN6540
4730 IFR=2THEN6180
4740 IFR=7THEN6330
4750 IFR=8THEN6360
4760 IFR=11THEN6510
4770 IFR=25THEN6820
4780 IFR=15THEN6630
4790 IFR=28THEN6700
4800 GOTO7460
4810 IFF$<>"SLAY"THEN4900
4820 IFS$<>O$(46)THEN4870
4830 IFR<>30THEN7360
4840 IFOB(43)<>-1THEN7360
4850 IFDD=1THENM$="WHAT DRAGON?":GOTO9
4860 GOTO5690
4870 IFS$<>O$(7)THEN7610
4880 IFOB(43)<>-1 OR R<>13THEN7360
4890 MS=1:M$="YOU SLAY THE POOR MERCHANT TO DEATH.":GOTO9
4900 IFF$<>"SHOW"THEN4990
4910 FORI=1TONO:IFS$=O$(I)THEN4930
4920 NEXT:GOTO7310
4930 IFS$<>O$(10)THEN5460
4940 IFOB(10)<>-1THEN7360
4950 IFR<>15THEN7440
4960 M$="THE GHOST LETS OUT A LOUD SHRIEK, AS HE DISINTEGRATES.":GOSUB1:GOSUB8090
4970 OB(11)=0:SA=1
4980 GOTO6
4990 IFF$<>"SAY"THEN5090
5000 IFS$<>"ACKRA"THEN5080
5010 IFCR=1THEN5080
5020 IFR<>18THEN5080
5030 IFAI<>1THEN5080
5040 M$="A RAY OF LIGHT SHINES DOWN FROM THE SKY, REFLECTS THROUGH THE ANKH AND OPENS A SMALL CRAWLWAY IN THE CLIFF.":GOSUB1
5050 OB(23)=18:CR=1:GOSUB8090:GOTO6
5080 M$=S$:GOTO8
5090 IFF$<>"SHARPEN"THEN5150
5100 IFS$<>O$(8)THEN7580
5110 IFOB(8)<>-1THEN7360
5120 IFR<>28THEN7360
5130 M$="THE AXE IS AS SHARP AS IT WAS ORIGINALLY.": BA=0:GOTO9
5140 GOTO7580
5150 IFF$<>"SAW"THEN5210
5160 IFS$<>O$(37)THEN7580
5170 IFOB(27)<>-1THEN7360
5180 IFR<>5THEN7360
5190 IFPS=1THENM$="I HAVE ALREADY DONE THAT!":GOTO9
5200 M$="THE PADLOCK IS SAWN EASLIY, AND IT FALLS TO THE GROUND": PS=1:GOTO9
5210 IFF$<>"SHAKE"THEN5250
5220 IFS$<>O$(3)THEN7580
5230 IFR<>6THEN7360
5240 M$="I SHAKE THE TREE AS HARD AS I CAN, BUT IT IS AS HARD AS A ROCK--THE APPLE DOESN'T EVEN BUDGE.":GOTO9
5250 IFF$<>"SAVE"THEN7310
5260 IFS$<>"GAME"THEN7310
5270 SV$="ELGON.SAV"
5280 OPEN "O",#1,"ELGON.SAV"
5290 FOR I=1 TO NO: WRITE #1,OB(I):NEXT
5300 WRITE #1,R
5310 WRITE #1,BA,GG,DD,CL,CC,AD,EI,DF,SA,DS,AI,PR,PA,MP,UH,PS,EG
5320 WRITE #1,DO,YS,BO,BU,SA,CR,WG,CM,AM,ME,SH,GS,MS,AA,MC,AD
5330 CLOSE #1:GOTO8
5340 IFF$<>"TO"THEN7310
5360 IFAM=1THEN5380
5370 IFCM=1THEN5480
5371 GOTO5440
5380 IFME<>1THEN5430
5390 IFOB(4)<>-1THEN7360
5400 IFR<>13THEN7360
5410 IFMP=1THENOB(4)=0:AM=0:ME=0:GOTO8
5420 IFMP<>1THENM$="THE MERCHANT OFFERS YOU THE SHOVEL.":MP=1:OB(4)=0:ME=0:AM=0:GOTO9
5430 FOR I=1 TO NO:IFS$=O$(I)THEN5440
5431 NEXT:GOTO7580
5440 IFS$<>O$(2)THEN7310
5450 IFOB(4)<>-1THEN7360
5460 IFR<>2THEN7360
5470 GG=1:OB(4)=0:OB(2)=0:M$="THE GRUE DISAPPEARS INTO THE WALL.":GOSUB1:GOSUB8090:GOTO6
5480 IFS$<>O$(7)THEN7580
5490 IFR<>13THEN7360
5500 IFMP=1THENOB(19)=0:CM=0:GOTO8
5510 IFOB(19)<>-1THEN7360
5511 M$="THE MERCHANT OFFERS YOU THE SHOVEL":MP=1:OB(19)=0:CM=0:GOTO9
5520 IFF$<>"U"THEN5580
5540 IFR=32THEN7140
5550 IFR<>26THEN7460
5560 GOTO6850
5580 IFF$<>"USE"THEN5760
5590 FORI=1TONO:IFS$=O$(I)THEN5610
5600 NEXT:GOTO7310
5610 IFS$<>O$(34)THEN5710
5620 IFOB(34)<>-1THEN7360
5630 IFPA=1THEN7600
5640 IFR<>7THEN7440
5650 PA=1:OB(26)=7:PRINT"OK":GOTO6
5690 M$="YOU SLAY THE DRAGON WITH MIGHTY FORCE, AND HE SHRIVELS UP INTO NOTHING.":GOSUB1:GOSUB8090:DD=1:OB(46)=0:GOTO6
5710 IFS$<>O$(43)THEN5740
5720 IFR=13ANDMS=0THEN4890
5730 IFR=30ANDDD=0THEN5690
5740 M$="IT DOESN'T WORK":GOTO9
5760 IFF$<>"UNLOCK"THEN5840
5770 FORI=1TONO:IFS$=O$(I)THEN5790
5780 NEXT:GOTO7310
5790 IFS$<>O$(21)THEN7580
5800 IFOB(15)<>-1THEN7360
5810 IFR<>11THEN7360
5820 IFBU=1THENM$="IT'S ALREADY UNLOCKED!":GOTO9
5830 BU=1:GOTO8
5840 IFF$<>"UNWEAR"THEN7310
5850 IFS$<>O$(31)THENM$="IT WOULD BE HARD EVEN PUTTING THAT ON":GOTO9
5860 IFOB(31)<>-1ANDWG<>0THENM$="I'M NOT WEARING THEM.":GOTO9
5870 WG=0:GOTO8
5890 IFF$<>"W"THEN6060
5920 IFR=3THEN6270
5930 IFR=4THEN6300
5940 IFR=8THEN6420
5950 IFR=10THEN6390
5960 IFR=26THEN6970
5970 IFR=23THEN6910
5980 IFR=21THEN6820
5990 IFR=12THEN6330
6000 IFR=13THEN6570
6010 IFR=17THEN6700
6020 IFR=22THEN6510
6030 IFR=19THEN6760
6040 IFR=31THEN7060
6050 GOTO7460
6060 IFF$<>"WEAR"THEN6130
6070 FOR I=1 TO NO:IFS$=O$(I)THEN6090
6080 NEXT:GOTO7310
6090 IFS$<>O$(31)THEN6120
6100 IFOB(31)<>-1THEN7360
6110 WG=1:GOTO8
6120 GOTO7600
6130 IFF$<>"WITH"THEN7310
6140 GOTO5590
6180 R=1:GOTO6
6210 R=2:GOTO6
6240 R=3:GOTO6
6270 R=4:GOTO6
6300 R=5:GOTO6
6330 R=6:GOTO6
6360 R=7:GOTO6
6390 R=8:GOTO6
6420 R=9:GOTO6
6450 R=10:GOTO6
6480 R=11:GOTO6
6510 R=12:GOTO6
6540 R=13:GOTO6
6570 R=14:GOTO6
6600 R=15:GOTO6
6630 R=16:GOTO6
6660 R=17:OB(47)=17:GOTO6
6700 R=18:GOTO6
6730 R=19:GOTO6
6760 R=20:GOTO6
6790 R=21:GOTO6
6820 R=22:GOTO6
6850 R=23:GOTO6
6880 R=24:GOTO6
6910 R=25:GOTO6
6940 R=26:GOTO6
6970 R=27:GOTO6
7000 R=28:GOTO6
7030 R=29:GOTO6
7060 R=30:IFDD=1THEN3630
7090 CLS:M$=R$(R):GOSUB1:PRINTVI$:PRINT:FORI=1TONO:IFOB(I)=RTHENPRINTO$(I):GOTO7100
7091 NEXT
7100 GOSUB9010:GOSUB9000
7110 IFOB(44)<>-1THENM$="I HAVE BEEN BURNT BY A FIRE BREATHING DRAGON!":GOSUB1:GOTO7620
7120 IFOB(44)=-1THENM$="THE DRAGON SENDS A BREATH OF FIRE AT ME, BUT IT REFLECTS OFF MY SHIELD.":FB=0:GOTO9
7130 GOTO540
7140 R=31:OB(38)=R:GOTO6
7190 PRINT:PRINT"FANTASTIC! YOU SOLVED IT!":GOTO7630
7250 SR=R:R=29:GOTO6
7290 R=32:OB(51)=R:GOTO6
7310 M$="I DON'T UNDERSTAND!":GOTO9
7340 M$="I'M NOT CARRYING ANYTHING!":GOTO9
7360 M$="I CAN'T DO THAT...YET!":GOTO9
7380 M$="I CAN'T SEE IT HERE.":GOTO9
7400 M$="IT'S BEYOND MY POWER TO DO THAT.":GOTO9
7420 M$="I SEE NOTHING SPECIAL.":GOTO9
7440 M$="NOTHING HAPPENS.":GOTO9
7460 M$="I CAN'T GO THAT WAY!":GOTO9
7480 M$="I ALREADY HAVE THAT.":GOTO9
7500 M$="I FOUND NOTHING!":GOTO9
7520 M$="I FOUND SOMETHING!":GOSUB1:GOSUB8090:GOTO6
7580 M$="I CAN'T DO THAT.":GOTO9
7600 M$="SORRY.":GOTO9
7620 PRINT"I'M DEAD!"
7630 PRINT"PLAY AGAIN (Y/N)?"
7650 A$=INKEY$
7700 IFA$="Y"THENRUN
7710 IFA$="N"THENEND
7720 GOTO7650
7780 DATA 2,2,6,0,2,7,13,7,13,0,15,15,0,27,0,0,0,21,4,18,11,23,0,1,23,0,19,0,14,20,9,5,12,10,7,28,5,31,16,15,0,0,25,24,0
7781 DATA 30,17,0,1,0,0,0
7790 DATA"LADDER","GRUE","TREE","APPLE","TUNNEL","SHACK","MERCHANT","AXE","SHOVEL","AMULET","GHOST","GRAVE","BONES"
7791 DATA"ANKH","KEY","LEVER","PASSAGE","BOOK","COIN","STUMP","BOX","SKELETON","CRAWLWAY","SIGN","STAIRS","PLANK"
7792 DATA"SAW","HOLE","RIVER","CLOSET"
7800 DATA"GLASSES","DOOR","POST","HAMMER","FENCE","STONE","PADLOCK","CHEST","CHASM","RIVER","PLANK","DRAWBRIDGE"
7801 DATA"SWORD","SHIELD","VAULT","DRAGON","CHASM","SPLINTERS","PIT","SPLINTERS","TREASURE","HOLE"
9000 FORC1=1TO32:PRINT"-";:NEXT:RETURN
9010 PRINT"SOME OBVIOUS EXITS ARE: ":M$=DI$(R):GOSUB1:RETURN
