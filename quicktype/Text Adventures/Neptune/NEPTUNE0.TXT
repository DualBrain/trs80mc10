10 REM NEPTUNE'S CAVERN'S -- A TEXT ADVENTURE LISTED IN
20 REM THE BOOK "WRITE YOUR OWN PROGRAM:A DATABASE ADVENTURE GAME"
30 REM BY STEVE RODGERS AND MARCUS MILTON ORGINALLY PUBLISHED
40 REM IN 1985.  PORTED TO BLITZBASIC IN 2003 BY KEITH WEATHERBY II
50 NN=14 REM NUMBER OF NOUNS
60 DIM T$(24):REM ROOM DESCRIPTION PART 1
70 DIM D$(24):REM ROOM DESCRIPTION PART 2
80 DIM EX(24,4):REM EXITS
90 DIM NO$(NN):REM NOUN DESCRIPTIONS
100 DIM VB$(VV):REM VERB DESCRIPTIONS
110 DIM OB$(10):REM OBJECT DESCRIPTIONS
120 DIM OBI(10):REM OBJECT INDEX
130 REM NEPTUNE'S CAVERN
200 GOSUB5000:REM INITIALIZE
300 GOSUB1000:REM INPUT
400 GOSUB2000:REM SORT
500 IF (WF <> 1) AND (LF <> 1) THEN GOTO30
600 GOTO5500:REN END
700 END
1000 REM **** INPUT ****
1010 VB$(0)="":NO$(0)="":R$=""
1020 R$=INPUT("WHAT DO YOU DO NEXT? "):R$=UPPER$(R$):CLS:LOCATE(0, 0)
1030 FOR I=1 TO LEN(R$)
1040 IF MID$(R$, I, 1)=" " THEN VB$(0)=LEFT$(R$, 3):NO$(0)=MID$(R$, I + 1, 3):I=LEN(R$)
1050 NEXT
1060 IF NO$(0) <> "" THEN RETURN
1070 R$=LEFT$(R$, 3):IF R$="QUI" THEN END
1080 IF R$="NOR" OR  R$="SOU" OR R$="EAS" OR R$="WES" THEN VB$(0)="GO ":NO$(0)=R$:RETURN
1090 IF R$="HEL" OR R$="INV" OR R$="LOO" OR R$="QUI" THEN NO$(0)="DOO":VB$(0)=R$:RETURN
1100 PRINT "I DON'T UNDERSTAND THAT. "
1110 GOTO1010
2000 REM **** SORT ****
2010 VI=0:NI=0
2020 FOR I=1 TO VV
2030 IF VB$(0)=LEFT$(VB$(I), 3) THEN VI=I:I=VV
2040 NEXT
2050 FOR I=1 TO NN
2060 IF NO$(0)=NO$(I) THEN NI=I:I=NN
2070 NEXT
2080 IF NI=0 OR VI=0 THEN PRINT "I DON'T UNDERSTAND THAT.":RETURN
2090 SELECT VI 
CASE 1 GOTO3000 
CASE 2 GOTO3100 
CASE 3 GOTO3200 
CASE 4 GOTO3300 
CASE 5 GOTO3400 
CASE 6 GOTO3500 
CASE 7 GOTO3620 
CASE 8 GOTO3700
CASE 9 GOTO3800
CASE 10 GOTO3900 
CASE 11 GOTO4000 
END SELECT

3000 REM **** GO ****
3010 IF NI > 4 THEN PRINT "GO WHERE?":RETURN
3020 IF EX(CP, NI)=0 THEN PRINT "NO EXIT THAT WAY":RETURN
3030 IF CP=16 AND (NI=1 OR NI=4) AND FF=0 THEN LF=1:RETURN
3040 CP=EX(CP, NI):GOSUB4000
3050 RETURN

3100 REM **** GET ****
3110 IF NI=6 AND CP=13 THEN PRINT "IT'S TOO HEAVY!":RETURN
3120 IF NI < 8 THEN PRINT "DON'T BE SILLY":RETURN
3130 IF OBI(NI - 7)=99 AND NO$(0) <> "HAN" THEN PRINT "YOU'VE ALREADY GOT IT!":RETURN
3140 FOR I=7 TO 10
3150 IF(OBI(I)=CP AND NI=14) THEN NI=I + 7:HC=HC + 1:I=10
3160 NEXT
3170 IF OBI(NI - 7)=CP THEN PRINT "O.K.":OBI(NI - 7)=99:IN=IN + 1:RETURN
3180 PRINT "IT ISN'T HERE!":RETURN

3200 REM **** DROP ****
3210 IF NI < 8 THEN PRINT "DON'T BE SILLY":RETURN
3220 FOR I=7 TO 10
3230 IF(OBI(I)=99 AND  NI=14) THEN NI=I + 7:HC=HC - 1:I=10
3240 NEXT
3250 IF OBI(NI - 7) <> 99 THEN PRINT "YOU HAVEN'T GOT IT!":RETURN
3260 PRINT "O.K.":OBI(NI - 7)=CP:IN=IN - 1
3270 IF CP=19 AND NI=13 THEN WF=1
3280 RETURN

3300 REM **** CUT ****
3310 IF OBI(1) <> 99 THEN PRINT "YOU'VE NOTHING SHARP ENOUGH!":RETURN
3320 IF NI <> 11 THEN PRINT "YOU CAN'T CUT THAT!":RETURN
3330 IF CP <> 5 THEN PRINT "YOU CAN'T DO THAT":RETURN
3340 IF CF=1 THEN PRINT "YOU'VE ALREADY DONE THAT!":RETURN
3350 PRINT "THE SEAWEED FALLS AWAY TO REVEAL AN OPEN WINDOW."
3360 OBI(4)=5:EX(5, 2)=11:CF=1
3370 D$(5)=LEFT$(D$(5), 19) + "AN OPEN WINDOW IN IT":RETURN 

3400 REM **** WEAR****
3410 IF FF=1 AND NO$(0)="FLI" THEN PRINT "YOU'VE ALREADY GOT THEM ON!":RETURN
3420 IF OBI(2)=99 AND NO$(0)="FLI" THEN FF=1:OB$(2)=0:PRINT "THEY FIT NICELY!":RETURN
3430 IF NI=14 AND HC > 0 THEN PRINT "THAT'S REALLY SILLY!":RETURN
3440 PRINT "YOU CAN'T WEAR THAT!":RETURN

3500 REM **** GIVE****
3510 IF CP=10 AND NI=11 THEN GOTO3550
3520 IF CP=7 AND NI=12 THEN GOTO3580
3530 IF CP=20 THEN PRINT "THAT WON'T DO ANY GOOD!":RETURN
3540 PRINT "NOTHING HERE WANT'S IT":RETURN
3550 IF OBI(4) <> 99 THEN PRINT "YOU HAVEN'T GOT IT":RETURN
3560 OBI(4)=0:OBI(3)=10
3570 PRINT "SOMETHING GLINTS IN THE CORNER":RETURN
3580 IF OBI(5) <> 99 THEN PRINT "YOU HAVEN'T GOT IT":RETURN
3590 OBI(5)=0:EX(7, 2)=13
3600 PRINT "THE FISH SNATCHES THE BONE AND RETIRES TO A CORNER"
3610 T$(7)="YOU ARE IN A CAVERN. IN THE CORNER A"
3615 D$(7)="BONEFISH IS CHEWING A THIGH-BONE":RETURN

3620 REM **** UNLOCK       ****
3630 IF OBI(3) <> 99 THEN PRINT "YOU HAVEN'T EVEN GOT A KEY!":RETURN
3640 IF NI=5 AND (CP=3 OR CP=9) THEN PRINT "THERE ISN'T EVEN A KEYHOLE!":RETURN
3650 IF CP <> 13 OR NI <> 6 THEN PRINT "YOU CAN'T DO THAT":RETURN
3660 IF UF=1 THEN PRINT "IT'S ALREADY UNLOCKED!":RETURN
3670 UF=1:OBI(7)=13:PRINT "THE KEY TURNS EASILY"
3680 T$(13)="YOU ARE IN A ROOM WITH AN OPEN CHEST IN THE MIDDLE."
3690 D$(13)="":RETURN

3700 REM **** USE ****
3710 IF CP <> 20 OR NI <> 14 THEN PRINT "YOU CAN'T DO THAT HERE":RETURN
3720 IF HC < 4 THEN PRINT "YOU HAVEN'T GO ENOUGH PAIRS!":RETURN
3730 PRINT "THE OCTOPUS CAN'T MOVIE.  HE ISN'T AMUSED!" 
3740 FOR I= 7 TO 10:OBI(I)=0:NEXT
3750 HC=0:EX(20, 4)=19
3760 T$(20)="YOU ARE IN A CORRIDOR WITH A CURRENT GOING WEST"
3770 D$(20)="A COLORFUL, MANACLED OCTOPUS SITS SULKING. ":RETURN

3800 REM **** INVENTORY    ****
3810 PRINT:PRINT "YOU ARE CARRYING:" 
3820 IF IN=0 THEN PRINT "NOTHING!":RETURN
3830 FOR I=1 TO 10
3840 IF OBI(I)=99 THEN PRINT "A " + OB$(I)
3850 NEXT:RETURN

3900 REM **** HELP****
3910 PRINT:PRINT "THESE ARE THE VERBS YOU MAY USE:" 
3920 FOR I=1 TO VV:PRINT VB$(I):NEXT
3930 PRINT:PRINT "(YOU NEED ONLY TYPE THE FIRST THREE LETTERS)":RETURN


4000 REM **** LOOK****
4010 PRINT:PRINT T$(CP):PRINT D$(CP):IF CP=22 THEN LF=1:RETURN
4020 FOR I=1 TO 10
4030 IF I=5 AND CP=15 THEN GOTO4050
4040 IF OBI(I)=CP THEN PRINT "A " + OB$(I) + " IS HERE"
4050 NEXT
4060 PRINT:PRINT "EXITS:"
4070 IF EX(CP, 1) > 0 THEN PRINT "NORTH  " 
4080 IF EX(CP, 2) > 0 THEN PRINT "SOUTH  "
4090 IF EX(CP, 3) > 0 THEN PRINT "EAST   "
4100 IF EX(CP, 4) > 0 THEN PRINT "WEST   "
4110 PRINT:PRINT:RETURN

5000 REM **** INITIALIZE   ****
5010 CLS:LOCATE(0, 0):RESTORE L6000
5020 PRINT "NEPTUNE'S CAVERNS"
5030 PRINT "YOU HAVE FOUND THE MAGIC PLUG THAT"
5040 PRINT "BELONGS AT THE BOTTOM OF THE SEA AND"
5050 PRINT "DECIDE TO REPLACE IT BEFORE THE WATER"
5060 PRINT "DRAINS AWAY.  WITH YOUR SCUBA GEAR YOU"
5070 PRINT "DIVE INTO THE OCEAN AND BEGIN YOUR "
5080 PRINT "ADVENTURE..."
5090 PRINT "(IF YOU NEED ASSISTANCE TYPE HELP)"
5100 REM INIT VARIABLES - NN, VV, CP, IN, HC, WF, LF, UF, FF, CF
5101 NN=14:REM NUMBER OF NOUNS
5102 VV=12:REM NUMBER OF VERBS
5103 CP=3:REM CHARACTER POSITION (ROOM)
5104 IN=1:REM NUMBER OF INVENTORY ITEMS
5105 HC=0:REM NUMBER OF HANDCUFFS
5106 WF=0:REM WIN FLAG
5107 LF=0:REM LOST FLAG
5108 UF=0:REM UNLOCK FLAG
5109 FF=0:REM FLIPPER FLAG
5110 CF=0:REM CUT FLAG
5111 VI=0:REM VERB INDEX
5112 NI=0:REM NOUN INDEX
5113 REM CREATE ARRAYS - T$, D$, EX, NO$, VB$ 
5120 REM CREATE THE REST OF THE ARRAYS - OB$, OBI
5130 FOR I=1 TO 24
5140 IF I=17 OR I=18 OR I=23 OR I=24 THEN T$(I)=T$(12):D$(I)=D$(12):GOTO5160
5150 READ T$(0), D$(0):T$(I)=T$(0):D$(I)=D$(0)
5160 NEXT
5170 FOR I=1 TO 24:FOR J=1 TO 4:READ EX(I, J):NEXT:NEXT
5180 FOR I=1 TO 10:READ OB$(I), OBI(I):NEXT
5190 FOR I=1 TO NN:READ NO$(I):NEXT
5200 FOR I=1 TO VV:READ VB$(I):NEXT
5210 GOSUB4000:RETURN:REM LOOK
5500 REM **** END GAME     ****
5510 IF CP=22 THEN PRINT "WITH A SNAP, THE SHARK BITES OFF YOUR HEAD ":GOTO5570
5520 IF CP=16 THEN PRINT "YOU HAVE STEPPED ON A POISONOUS SEA-URCHIN"
5530 IF CP=16 THEN PRINT "YOU DIE A HORRIBLE DEATH.":GOTO5570
5540 PRINT "WITH A 'THUNK' THE PLUG DROPS INTO THE "
5550 PRINT "HOLE AND THE SWIRLING WATERS GROW STILL"
5560 PRINT "CONGRATULATIONS! YOU SAVED THE SEAS! " 
5570 R$=UPPER(INPUT$("DO YOU WISH TO PLAY AGAIN? "))
5580 IF LEFT$(R$, 1)="Y" THEN GOTO10
5590 RETURN
6000 REM **** DESCRIPTIONS ****
6010 DATA "YOU ARE ON THE SEABED.  THE WAY WEST IS "
6011 DATA "BLOCKED BY A HIGH CORAL REEF."
6020 DATA "YOU ARE ON THE SEABED.  TO THE SOUTH A "
6021 DATA "BARNACLED WALL TOWERS ABOVE YOU."
6030 DATA "YOU ARE IN FRONT OF A WOODEN DOOR.  YOU "
6031 DATA "CAN SEE NO HANDLE."
6040 DATA "YOU ARE ON THE SEABED.  TO THE SOUTH A "
6041 DATA "BARNACLED WALL TOWERS ABOVE YOU."
6050 DATA "YOU ARE ON THE SEABED.  TO THE SOUTH A BARNACLED "
6051 DATA "WALL HAS A SQUARE PATCH OF SEAWEED GROWING ON IT."
6060 DATA "YOU ARE ON THE SEABED.  TO THE SOUTH IS A "
6061 DATA "BARNACLED WALL.  A CLIFF BLOCKS THE WAY EAST."
6070 DATA "YOU ARE IN A LONG, LOW CAVERN.  AT THE FAR "
6071 DATA "END A LARGE BONEFISH IS SWIMMING AROUND."
6080 DATA "YOU ARE IN A BRIGHTLY LIT CHAMBER.  THE WALLS, "
6081 DATA "FLOOR, AND ROOM GRLOW IN SHIMMERING LIGHT."
6090 DATA "YOU ARE IN A DIMLY LIT CAVERN WITH A HUGE "
6091 DATA "DOOR AT THE FAR END.  YOU CAN SEE NO HANDLE."
6100 DATA "YOU ARE IN A ROOM FULL OF HUNGRY SEAHORSES. "
6101 DATA "THEY NUZZLE YOUR HAND IN A FRIENDLY MANNER."
6110 DATA "YOU ARE IN A SMALL ROOM.  THE NORTH WALL HAS A SMALL "
6111 DATA "WINDOW IN IT THROUGH WHICH YOU CAN SEE THE SEABED"
6120 DATA "YOU ARE IN AN AMAZINGLY SQUARE ROOM.  THE WALLS, "
6121 DATA "FLOOR AND ROOM ARE ALL SQUARE AS ARE ALL THE EXITS. "
6130 DATA "YOU ARE IN A TINY LITTLE ROOM THAT IS OCCUPIED "
6131 DATA "BY A CHEST INSCRIBED WITH THE INITIALS D.J."
6140 DATA "YOU ARE IN A COLD, MURKY ROOM.  GREY MUD SWIRLS "
6141 DATA "AROUND YOU AND YOU FEEL A FAINT CURRENT TO THE EAST."
6150 DATA "YOU ARE IN A GLOOMY AND EERIE PLACE.  ALL AROUND "
6151 DATA "YOU ARE THE BONES OF LONG DEAD EXPLORERS!"
6160 DATA "YOU ARE IN A SQUARE ROOM.   THE SOUTH EXIT HAS THE WORDS 'DO NOT ENTER!'"
6161 DATA "ABOVE IT.  THE NORTH AND WEST DOORWAYS ARE CRAWLING WITH SEA URCHINS."
6170 DATA "YOU ARE IN A CIRCULAR ROOM WITH A VERY STRONG CURRENT THAT "
6171 DATA "SWIRLS AROUND THE ROOM AND DOWN A HOLE IN THE FLOOR."
6180 DATA "YOU ARE IN A CORRIDOR WITH A STRON GCURRENT GOING WEST.  YOUR WAY "
6181 DATA "IS BLOCKED BY TTHE ARMS OF A LARGE RAINBOW COLORED OCTOPUS."
6190 DATA "YOU'RE IN A SHIPWRECKED CAPTAIN'S CABIN. "
6191 DATA "YOU FEEL THE FLOW OF WATER TO THE WEST."
6200 DATA "YOU SEE A RUSH OF SWIRLING WATER AND "
6201 DATA "FACE THE JAWS OF A GREAT WHITE SHARK."
6210 REM **** EXITS ****
6220 DATA  0, 0, 2, 0, 0, 0, 3, 1, 0, 0, 4, 2, 0, 0, 5, 3
6230 DATA  0, 0, 6, 4, 0, 0, 0, 5, 0, 0, 8, 0, 0, 14, 0, 7
6240 DATA  0, 15, 0, 0, 0, 16, 0, 0, 5, 17, 12, 0, 0, 18, 0, 11 
6250 DATA  7, 0, 0, 0, 8, 0, 15, 0, 9, 21, 16, 14, 10, 22, 17, 15
6260 DATA 11, 23, 18, 16, 12, 24, 0, 17, 0, 0, 20, 0, 0, 0, 21, 0
6270 DATA 15, 0, 0, 20, 0, 0, 0, 0, 17, 0, 24, 0, 18, 0, 0, 23
6300 REM **** OBJECTS ****
6310 DATA "KNIFE", 2
6320 DATA "PAIR OF FLIPPERS", 6
6330 DATA "KEY", 0
6340 DATA "CLUMP OF SEAWEED", 0
6350 DATA "ROTTEN OLD BONE", 15
6360 DATA "MAGIC PLUG", 99
6370 DATA "YELLOW PAIR OF HANDCUFFS", 0
6380 DATA "GREEN PAIR OF HANDCUFFS", 9
6390 DATA "RED PAIR OF HANDCUFFS", 17
6400 DATA "BLUE PAIR OF HANDCUFFS", 11
6500 REM **** NOUNS        ****
6510 DATA "NOR", "SOU", "EAS", "WES", "DOO", "CHE", "WIN", "KNI", "FLI", "KEY", "SEA", "BON", "PLU", "HAN"
6520 REM **** VERBS        ****
6530 DATA "GO ", "GET", "DROP", "CUT", "WEAR", "GIVE", "UNLOCK", "USE", "INVENTORY", "HELP", "LOOK", "QUIT"
