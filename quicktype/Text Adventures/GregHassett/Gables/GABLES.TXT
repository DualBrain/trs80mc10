1 CLEAR50
2 DEFINTA-Z:DEFSTRA,R,Y
3 CLS:PRINTCHR$(23)
4 PRINT"   THE HOUSE OF SEVEN GABLES"
5 PRINT
6 PRINT"        BY GREG HASSETT"
7 A="I DON'T SEE IT HERE.":R="DON'T BE RIDICULOUS.":Y="I DON'T KNOW THAT WORD.":CP=2:POKE16553,255:GOTO255
8 GOTO247
9 IFRND(100)=50ANDZZ>2THEN215
10 IFRND(40)=1ANDCP<>2ANDCP<>5ANDCP<>21ANDCP<>41THEN223
11 GOSUB235:T=T+1:IFT=50OB$(27)="A TARNISHED OLD COMPASS"
12 IFNO>40NO=NO-40
13 IFVB<>25ANDVB>10ANDNO$=""INPUT"GIVE ME AN OBJECT";NO$:GOSUB244
14 IFNO$<>""ANDNO=0ANDVB=1PRINT"I DON'T UNDERSTAND THAT!":GOTO11
15 IFT>175MI=MI-10
16 IFT>225MI=MI-15
17 IFNO=0ANDNO$<>""PRINTY:GOTO11
18 IFVB<>1THEN32
19 IFVB=1ANDNO$=""INPUT"WHERE";NO$:GOSUB244
20 IFNO>40NO=NO-40
21 IFNO<7THEN24ELSEAB%=OB(NO,0)
22 IF(AB%>10ANDAB%<18)ORABS(CP-AB%)>3ORCP=2ORAB%=0PRINT"I DON'T KNOW HOW TO APPLY THAT WORD HERE.":GOTO11
23 CP=AB%:GOTO8
24 IFOB(27,0)<>-1PRINT"I CAN'T UNDERSTAND COMPASS POINTS WITHOUT A COMPASS!":GOTO11
25 IFP(CP,NO-1)=0PRINT"THERE IS NO WAY TO GO IN THAT DIRECTION.":GOTO11
26 IFCP=2ANDNO=2PRINT"THE DOOR IS LOCKED. I THINK I CAN GET BY IT, THOUGH.":GOTO11
27 IFCP=6ANDNO=3ANDDF=0PRINT"THE DOOR TO THAT WAY IS CLOSED.":GOTO11
28 IFCP=9ANDOB(9,0)=9ANDNO=1PRINT"THE VAMPIRE WON'T LET ME GO NORTH!":GOTO11
29 IFCP=38ANDNO=6ANDOB(18,0)<>0P(5,0)=2
30 IFCP=5ANDFF=1ANDP(5,0)=2ANDNO=1CLS:PRINT"YOU MADE IT!":GOTO230
31 CP=P(CP,NO-1):GOTO8
32 IFVB<>2THEN48
33 IFNO$=""INPUT"GET WHAT";NO$:GOSUB244
34 IFOB(NO,0)=-1PRINT"I'M ALREADY CARRYING THAT!!!":GOTO11
35 IFNO=7ANDCP=2PRINT"IT IS IMPOSSIBLE TO REMOVE.":GOTO11
36 IFNO=0PRINTY:GOTO11
37 IFOB(NO,0)<>CPPRINTA:GOTO11
38 IFNO=31PRINTR:GOTO11
39 IFNO=9PRINT"YOU CAN'T BE SERIOUS!":GOTO11
40 IFNO=40PRINT"I CANNOT PULL IT OUT OF ITS SOCKET.":GOTO11
41 IFNO=14PRINT"MY, THAT'S HEAVY. TOO HEAVY TO CARRY, IN FACT.":GOTO11
42 IFNO=16PRINT"IT IS IN A BIG POT THAT I CAN'T LIFT.":GOTO11
43 IFNO=22PRINT"BE SERIOUS.":GOTO11
44 IFNO=26PRINT"THE CAT VANISHED, SAYING ";CHR$(34);"I WILL RETURN...";CHR$(34):OB(26,0)=RND(40):GOTO11
45 IFZZ>5PRINT"I CAN'T CARRY ANY MORE. CLUE: DROP SOMETHING.":GOTO11
46 ZZ=ZZ+1
47 OB(NO,0)=-1:PRINT"YOU GOT IT.":GOTO11
48 IFVB=3THEN8
49 IFVB<>4THEN53
50 ZZ=0:PRINT"CURRENTLY I AM CARRYING:":FORI=8TOLO:IFOB(I,0)=-1PRINTOB$(I):ZZ=ZZ+1
51 NEXTI:IFZZ=0PRINT"NOTHING!"
52 GOTO11
53 IFVB<>5THEN58
54 J=0:FORI=1TOLO:IFOB(I,0)=5J=J+OB(I,1)
55 NEXTI:J=J+MI
56 PRINT"OUT OF A MAXIMUM OF 160 POINTS, YOU HAVE";J;"POINTS."
57 GOTO11
58 IFVB<>6THEN65
59 IFNO$=""INPUT"DROP WHAT";NO$:GOSUB244
60 IFOB(NO,0)<>-1PRINT"I AM NOT HOLDING IT.":GOTO11
61 PRINT"ALL RIGHT."
62 ZZ=ZZ-1
63 OB(NO,0)=CP
64 GOTO11
65 IFVB=7PRINT"I CANNOT GIVE YOU HELP. IT IS AGAINST MY RELIGION.":GOTO11
66 IFVB<>8THEN73
67 CLS:INPUT"PRESS <ENTER> WHEN CASSETTE PREPARATIONS ARE MADE ";XX$
68 FORI=8TOLO:IFOB$(I)=""THEN70
69 PRINT#-1,OB(I,0)
70 NEXTI
71 PRINT#-1,CP,FF,ZZ,T,P(5,0),DF
72 GOTO11
73 IFVB<>9THEN80
74 CLS:INPUT"PRESS <ENTER> WHEN CASSETTE PREPARATIONS ARE MADE ";XX$
75 FORI=8TOLO:IFOB$(I)=""THEN77
76 INPUT#-1,OB(I,0)
77 NEXTI
78 INPUT#-1,CP,FF,ZZ,T,P(5,0),DF
79 GOTO8
80 IFVB<>10THEN84
81 INPUT"DO YOU REALLY WANT TO QUIT NOW";QN$
82 QN$=LEFT$(QN$,1):IFQN$="N"THEN11
83 GOTO230
84 IFVB<>11THEN91
85 IFOB$(NO)=""ANDNO<>7PRINTR:GOTO11
86 IFNO=7THEN89
87 IFOB(NO,0)<>-1PRINTA:GOTO11
88 IFNO<>7PRINT"I CAN'T RING ";OB$(NO);"!!":GOTO11
89 IFCP<>2PRINT"I SEE NO BELL HERE!":GOTO11
90 PRINT"THE DOOR FLIES OPEN AND I AM SWEPT INSIDE, BUT THEN THE DOOR
VANISHES LEAVING NO VISIBLE WAY OUT!":FF=1:CP=5:GOTO11
91 IFVB<>12THEN98
92 IFNO=8ANDOB(10,0)<>0PRINT"IT IS ALREADY FULL.":GOTO11
93 IFOB(NO,0)<>-1PRINT"I'M NOT HOLDING IT RIGHT NOW.":GOTO11
94 IFNO<>8ANDNO<>29PRINT"I CANT FILL THAT!":GOTO11
95 IFCP<>3PRINT"THERE IS NOTHING TO FILL IT WITH HERE.":GOTO11
96 IFNO=29PRINT"NO, I VALUE MY LIFE AND THAT FLUID THAT WAS IN THERE
MIGHT REACT WITH THE WATER...":GOTO11
97 IFNO=8PRINT"YOUR BUCKET IS NOW FULL.":OB(8,0)=0:OB(10,0)=-1:GOTO11
98 IFVB<>13THEN104
99 IFNO<>27ANDNO<>26ANDNO<>9ANDNO<>13ANDNO<>31PRINT"IT ISN'T ALIVE!":GOTO11
100 IFOB(NO,0)<>CPPRINTA:GOTO11
101 IFNO=9PRINT"I DON'T THINK IT WOULD APPROVE.":GOTO11
102 IFNO=27PRINT"IT DOESN'T TAKE TOO FAVORABLY TO THE IDEA.":GOTO11
103 PRINT"YOU'LL HAVE TO BE MORE SPECIFIC.":GOTO11
104 IFVB<>14THEN110
105 IFNO=0PRINT"THROW WHAT?":GOTO11
106 IFOB(NO,0)<>-1PRINT"I'M NOT HOLDING IT!":GOTO11
107 IFNO<>24ANDNO<>10THEN60
108 IFCP=38ANDNO=10PRINT"THE WITCH HAS VANISHED!":OB(18,0)=38:PRINT"HER HAT REMAINS.":OB(31,0)=0:OB(10,0)=0:OB(8,0)=-1:GOTO11
109 GOTO60
110 IFVB<>15THEN116
111 IFNO=0PRINT"SHOW WHAT?":GOTO11
112 IFOB(NO,0)<>-1PRINT"I'M NOT CARRYING IT!":GOTO11
113 IFNO<>11PRINT"NOTHING HAPPENED.":GOTO11
114 IFOB(9,0)<>25PRINT"NOTHING EXCITING HAPPENED.":GOTO11
115 PRINT"THE VAMPIRE SCURRIES OFF SAYING:
YOU HAVEN'T SEEN THE END OF ME YET!!!":OB(9,0)=9:GOTO11
116 IFVB<>16THEN123
117 IFNO<>9ANDNO<>13ANDNO<>31PRINT"THAT IS STUPID.":GOTO11
118 IFOB(NO,0)<>CPPRINTA:GOTO11
119 IFOB(23,0)<>-1PRINT"I DON'T HAVE ANY FOOD, STUPID!":GOTO11
120 IFNO=9ANDOB(23,0)<>-1PRINT"HE ONLY LIKES BANANAS.":GOTO11
121 IFNO=9PRINT"HE WHARFS DOWN YOUR BANANA AND EATS THE PEEL, TOO!":OB(23,0)=0:GOTO11
122 PRINT"SHE GRABS THE BANANA AND THROWS IT INTO THE BREW.":OB(23,0)=0:GOTO11
123 IFVB<>17THEN129
124 IFOB(NO,0)<>-1ANDOB(NO,0)<>CPPRINT"I DON'T SEE IT HERE.":GOTO11
125 IFNO=11PRINT"I DON'T WANT TO HAVE BAD BREATH.":GOTO11
126 IFNO<>12ANDNO<>23PRINTR:GOTO11
127 IFOB(NO,0)<>-1PRINT"I'M NOT CARRYING IT.":GOTO11
128 PRINT"MUNCH MUNCH MUNCH. GOOD.":OB(23,0)=0:OB(24,0)=-1:GOTO11
129 IFVB<>18THEN141
130 IFNO=0PRINT"JE NE COMPRENDS PAS. (I DON'T UNDERSTAND).":GOTO11
131 IFNO<>14ANDNO<>22ANDNO<>33PRINT"THAT IS BEYOND MY CAPABILITIES.":GOTO11
132 IFCP<>25ANDNO=14PRINT"I SEE NO COFFIN TO OPEN HERE!":GOTO11
133 IFCP=25ANDOB(9,0)=CPANDNO=14PRINT"THE VAMPIRE WON'T LET ME!":GOTO11
134 IFNO=14ANDCP=25PRINT"THE COFFIN IS OPEN":CF=1:IFOB(35,0)=0OB(35,0)=25:GOTO11ELSE11
135 IFNO<>22THEN139
136 IFCP=2PRINT"IT IS LOCKED. MAYBE THESE PEOPLE(?) ARE FRIENDLY, SO TRY
RINGING THE BELL.":GOTO11
137 IFCP=6PRINT"I CANNOT. THERE IS NO DOORKNOB AND NO KEYHOLE. HOWEVER, IT
IS QUITE POSSIBLE THAT I CAN GET BY IT.":GOTO11
138 PRINT"I SEE NO DOOR HERE.":GOTO11
139 IFOB(33,0)=-1PRINT"TRY UNFOLD AIRPLANE."ELSEPRINT"I HAVE NO AIRPLANE."
140 GOTO11
141 IFVB<>20THEN150
142 IFNO=22AND(CP=2ORCP=6)THEN145
143 IFNO=0PRINT"CHOP WHAT? I DON'T UNDERSTAND!":GOTO11
144 IFOB(NO,0)<>CPPRINT"I DON'T SEE IT HERE.":GOTO11
145 IFOB(39,0)<>-1INPUT"WITH WHAT, MY BARE HANDS";BH$:IFLEFT$(BH$,1)="Y"PRINT"I DON'T KNOW KARATE!":GOTO11
146 IFOB(39,0)<>-1PRINT"ALL RIGHT THEN!":GOTO11
147 IFNO<>22PRINT"THAT IS BEYOND MY POWER.":GOTO11
148 IFCP<>2ANDCP<>6PRINT"I SEE NO DOOR HERE.":GOTO11
149 PRINT"THE DOOR IS CHOPPED TO PEICES.":DF=1:GOTO11
150 IFVB<>19THEN158
151 IFNO<>22ANDNO<>38ANDNO<>14PRINT"THAT IS RIDICULOUS.":GOTO11
152 IFNO=14ANDCP<>25PRINT"I SEE NO COFFIN TO CLOSE HERE.":GOTO11
153 IFNO=14ANDCP=25PRINT"THE COFFIN IS CLOSED.":CF=0:GOTO11
154 IFNO=22ANDCP=2PRINT"IT IS ALREADY CLOSED.":GOTO11
155 IFNO=22ANDCP=5PRINT"THE DOOR VANISHED, REMEMBER?":GOTO11
156 IFNO=22ANDCP=6ANDDF=1PRINT"CLOSING A BUNCH OF WOOD SCRAPS IS BEYOND BY POWER.":GOTO11
157 IFNO=22PRINT"I SEE NO DOOR HERE.":GOTO11
158 IFVB<>21THEN163
159 IFNO=0PRINTY:GOTO11
160 IFNO<>15PRINT"WHY DO YOU KEEP TELLING ME TO DO DUMB THINGS?":GOTO11
161 IFOB(15,0)<>-1PRINT"I'M NOT HOLDING THEM!":GOTO11
162 PRINT"OKAY...
POOF! THE CHEMICALS THRANSFORMED THEMSELVES INTO AN ORANGE
BUBBLING LIQUID.":CF=1:GOTO11
163 IFVB<>22THEN179
164 IFNO<>16ANDNO<>10ANDNO<>28ANDNO<>15PRINT"YOU CAN'T BE SERIOUS.":GOTO11
165 IFNO=16ANDCP=38PRINT"I DON'T THINK I SHOULD. (THERE'S AN OLD SHOE IN IT!)":GOTO11
166 IFNO=10ANDOB(10,0)=-1PRINT"OKAY! GULP GULP GULP.":OB(10,0)=0:OB(8,0)=-1:GOTO11
167 IFNO=28ANDOB(28,0)=-1PRINT"ALL RIGHT. POOF! OH MY! WHERE AM I?? OH MY! ALL MY POSSESIONS
WERE LOST IN THE DENSE CLOUD OF GREASY BLACK SMOKE."
168 IFNO<>28OROB(28,0)<>-1THEN177
169 FORI=8TOLO
170 IFOB$(I)=""THEN173
171 IFOB(I,0)<>-1ANDOB(I,O)<>5THEN173
172 OB(I,0)=RND(38)
173 NEXTI
174 OB(28,0)=0:OB(29,0)=-1
175 ZZ=1
176 GOTO11
177 IFNO=15PRINT"I DON'T WANT TO TRY THAT.":GOTO11
178 PRINT"I CAN'T DO THAT YET.":GOTO11
179 IFVB<>23THEN185
180 IFNO=22AND(CP=2ORCP=6)THEN182
181 IFOB(NO,0)<>-1ANDOB(NO,0)<>CPPRINT"I DON'T SEE IT HERE.":GOTO11
182 IFNO<>9ANDNO<>13ANDNO<>31PRINT"I DON'T WANT TO HIT THE ";NO$;".":GOTO11
183 IFNO=9PRINT"HIT HIM YOURSELF! I'M SURE NOT GONNA!":GOTO11
184 PRINT"YUK! SHE'S GOT WARTS ALL OVER HER!":GOTO11
185 IFVB=24PRINT"ALL RIGHT...";NO$:GOTO11
186 IFVB=25PRINT"YOU HAVE USED";T;"TURNS SO FAR.":GOTO11
187 IFVB<>26THEN193
188 IFNO<>10ANDNO<>28ANDNO<>15PRINT"DON'T BE DUMB.":GOTO11
189 IFNO=10ANDOB(10,0)=-1PRINT"YOUR BUCKET IS EMPTY AND THE GROUND IS WET.":OB(10,0)=0:OB(8,0)=-1:GOTO11
190 IFNO=28PRINT"YOU ARE STUCK WITH AN EMPTY TEST TUBE NOW.":OB(28,0)=0:OB(29,0)=-1:GOTO11
191 IFNO=15ANDOB(15,0)=-1PRINT"THE CHEMICALS REACTED WITH THE DUST AND I GOT LOST IN A THICK
CLOUD OF ORANGE SMOKE. I SUGGEST YOU 'LOOK'.":CP=12:GOTO11
192 PRINT"I AM NOT CARRYING IT RIGHT NOW.":GOTO11
193 IFVB=27PRINT"I'M NOT ROCKY, YOU KNOW.":GOTO11
194 IFVB<>29THEN198
195 IFNO<>33PRINT"GIVE ME A BREAK!":GOTO11
196 IFOB(33,0)<>-1PRINT"I'M NOT HOLDING AN AIRPLANE!":GOTO11
197 PRINT"OK.":OB(33,0)=0:OB(34,0)=-1:GOTO11
198 IFVB<>28THEN205
199 IFOB(NO,0)<>CPANDOB(NO,0)<>-1PRINT"I DON'T SEE IT AND I'M NOT HOLDING IT.":GOTO11
200 IFNO<>34ANDNO<>32ANDNO<>19PRINT"THERE IS NO WRITING THERE.":GOTO11
201 IFOB(NO,0)<>-1PRINT"I DON'T HAVE IT WITH ME.":GOTO11
202 IFNO=32PRINT"IT SAYS:
NOTTUB SSERP":GOTO11
203 IFNO=34PRINT"IT READS: REMEMBER THE WIZARD OF OZ.":GOTO11
204 PRINT"THEY ARE WRITTEN IN WITCHISH.":GOTO11
205 IFVB=30PRINT"TRY OPEN.":GOTO11
206 IFVB<>31THEN212
207 IFOB(NO,0)<>CPPRINTA:GOTO11
208 IFNO<>9ANDNO<>31PRINT"BE SERIOUS.":GOTO11
209 IFOB(35,0)<>-1PRINT"I DON'T HAVE A KNIFE!":GOTO11
210 IFNO=31PRINT"THE KNIFE VANISHES WHEN IT TOUCHES HER.":OB(35,0)=0:GOTO11
211 PRINT"THE VAMPIRE DIES AND THEN VANISHES.":OB(9,0)=0:GOTO11
212 IFNO<>40PRINT"THAT'S IDIOTIC.":GOTO11
213 IFCP<>37PRINT"I SEE NO BUTTON HERE.":GOTO11
214 CP=38:GOTO8
215 PRINT"OH MY! A GHOST POPS OUT AND SAYS:
THROW ME SOME TREASURE!":GOSUB235
216 IFVB<>16ORNO<>13THEN218
217 IFOB(23,0)=0PRINT"I HAVE NO FOOD."ELSEPRINT"HE EATS THE BANANA AND VANISHES.":OB(23,0)=0:OB(24,0)=CP:GOTO11
218 IFVB<>14PRINT"HE IS GETTING MAD!":GOSUB235
219 IFVB<>14PRINT"HE HAS KILLED ME!":GOTO230
220 IFOB(NO,0)<>-1PRINT"I'M NOT HOLDING IT.":GOSUB235:GOTO218
221 IFOB(NO,1)=0PRINT"HE GROWLS:
NOT GOOD ENOUGH! AND THROWS IT BACK.":GOSUB235:GOTO218
222 PRINT"HE SNATCHES THE TREASURE AND RUNS AWAY.":ZZ=ZZ-1:OB(NO,0)=0:GOTO11
223 PRINT"HEAVENS! A ONE EYED GHOUL POPPED OUT!"
224 INPUT"WHAT AM I TO DO";CM$:GOSUB236
225 IFVB<>14ORNO<>15THEN229
226 IFOB(15,0)<>-1PRINT"I DON'T HAVE THEM!":GOTO229
227 IFCF=0PRINT"THEY WEREN'T MIXED!":GOTO229
228 PRINT"THE GHOUL MELTS TO NOTHING.":OB(15,0)=CP:GOTO11
229 PRINT"THE GHOUL KILLS ME!":GOTO230
230 J=0:FORI=8TOLO:IFOB(I,0)=5J=J+OB(I,1)
231 NEXTI
232 J=J+MI
233 PRINT"OUT OF A MAXIMUM OF 160 POINTS, YOU SCORED";J;"POINTS,
USING";T;"TURNS.":PRINT:INPUT"DO YOU WISH TO PLAY THIS ADVENTURE AGAIN";AG$:AG$=LEFT$(AG$,1):IFAG$="Y"THENRUNELSEIFAG$<>"N"THEN230
234 END
235 INPUT"****** WHAT SHOULD I DO";CM$
236 NO$="":VB$="":VB=0:NO=0
237 LC=LEN(CM$):FORI=1TOLC:IFMID$(CM$,I,1)<>" "VB$=VB$+MID$(CM$,I,1):NEXTI
238 V1$=VB$
239 VB$=LEFT$(VB$,3)
240 FORI=1TONV:IFVB$(I)=VB$VB=I:GOTO242ELSENEXTI
241 VB=1:NO$=VB$:GOTO244
242 IFLEN(V1$)+1>=LEN(CM$)NO=0:RETURN
243 NO$=RIGHT$(CM$,LEN(CM$)-1-LEN(V1$))
244 NO$=LEFT$(NO$,3):FORI=1TONN:IFNO$=NO$(I)THEN246ELSENEXTI
245 NO=0:RETURN
246 NO=I:RETURN
247 CLS
248 PRINTP$(CP)
249 FORI=8TOLO:IFOB(I,0)=CPPRINT"THERE IS ";OB$(I);" HERE."
250 NEXTI
251 FL=0:IFOB(27,0)<>-1THEN253ELSEPRINT"I CAN GO -- ";:FORI=0TO5:IFP(CP,I)<>0PRINTNO$(41+I);"  ";:FL=1
252 NEXTI:IFFL=0PRINT"NOWHERE!"ELSEPRINT
253 PRINT"----------------------------------------------------------------";
254 GOTO9
255 NN=46:LO=40:P=40:NV=32:DIM NO$(NN),OB$(LO),OB(LO,1),P$(P),P(P,5),VB$(NV)
256 P$(1)="I'M AT THE SECOND GABLE.":DATA0,4,0,0,0,0
257 P$(2)="I'M OUTSIDE A HOUSE WITH SEVEN GABLES.
THERE IS A DOORBELL BESIDE THE DOOR.":DATA0,5,0,0,0,0
258 P$(3)="I'M IN THE KITCHEN. THERE IS A SINK WITH A WORKING
SPICKET HERE.":DATA0,6,0,0,0,0
259 P$(4)="I'M IN THE GUEST ROOM. (I DON'T WANT TO BE A PERMANENT GUEST!)":DATA1,0,5,8,0,0
260 P$(5)="I'M IN THE LIVING ROOM.":DATA0,9,6,4,0,0
261 P$(6)="I'M IN THE DINING ROOM.":DATA3,0,7,5,0,0
262 P$(7)="I'M AT THE BOTTOM OF A STAIRWELL.":DATA0,0,0,6,29,0
263 P$(8)="I'M AT THE FIRST GABLE. IT OVERLOOKS A HUGE YARD OF TALL GRASS.":DATA0,0,4,0,0,0
264 P$(9)="I'M AT THE TOP OF A STAIRCASE.":DATA5,0,0,10,0,20
265 P$(10)="I'M IN A DARK STORAGE CLOSET. A HOLLOW VOICE SAYS:
MIX THEM.":DATA0,0,9,0,0,0
266 P$(11)="I'M IN A MAD SCIENTIST'S LABORATORY.":DATA0,0,0,0,0,12
267 P$(12)="I'M IN A MAZE OF HALLWAYS.":DATA0,16,12,12,11,15
268 DATA0,0,0,0,0,0,0,0,0,0,0,0
269 P$(15)=P$(12):DATA15,17,15,16,0,0
270 P$(16)=P$(12):DATA15,16,12,16,16,16
271 P$(17)=P$(12):DATA15,0,18,0,0,0
272 P$(18)="I'M AT THE WEST END OF THE HALLWAY":DATA19,0,23,17,0,0
273 P$(19)="I'M IN A DUNGION. A CRUDE NOTE ON THE WALL READS:
YOU CAN'T GET OUT WITHOUT KILLING ME FIRST!  --WITCHY-POO":DATA0,18,19,19,19,19
274 P$(20)="I'M AT THE BOTTOM OF A STAIRCASE.":DATA0,23,0,0,9,0
275 DATA0,0,0,0,0,0
276 P$(22)="I'M IN A BARREN LIBRARY.":DATA0,24,0,0,0,0
277 P$(23)="I'M IN THE CENTER OF A LONG E/W HALLWAY":DATA20,0,24,18,0,0
278 P$(24)="I'M AT THE EAST END OF THE HALLWAY.":DATA22,0,25,23,0,0
279 P$(25)="I'M IN DRACULA'S CHAMBER":DATA0,0,0,24,0,0
280 P$(26)="I'M IN A BROOM CLOSET. EXCITING.":DATA0,0,0,27,0,0
281 P$(27)="I'M AT THE FOURTH GABLE":DATA28,0,0,26,0,0
282 P$(28)="I'M AT GABLE FIVE.":DATA0,27,29,0,0,0
283 P$(29)="I'M AT THE TOP OF A STAIRCASE.":DATA0,0,0,28,0,32
284 P$(30)="I'M IN A BEDROOM.":DATA0,0,31,33,0,0
285 P$(31)="I'M IN A DUSTY OLD CHAMBER. LOOKS LIKE A DILAPIDATED BEDROOM.":DATA0,34,32,30,0,0
286 P$(32)="I'M AT THE SIXTH GABLE.":DATA0,0,0,31,0,0
287 P$(33)="I'M AT THE THIRD GABLE.":DATA0,0,34,30,0,0
288 P$(34)="I'M AT ONE END OF A HALLWAY.":DATA31,40,35,0,0,0
289 P$(35)="I'M IN THE MIDDLE OF A HALLWAY.":DATA0,0,36,34,0,0
290 P$(36)=P$(34):DATA32,0,37,35,0,0
291 P$(37)="I'M IN A ROOM WITH A PEDESTAL IN THE MIDDLE. ATOP
THE PEDESTAL IS A LARGE BUTTON.":DATA0,0,0,36,0,39
292 P$(38)="I'M IN THE WITCH'S CHAMBER.":DATA0,0,0,0,0,5
293 P$(39)="I'M AT THE SEVENTH GABLE":DATA37,0,0,0,0,0
294 P$(40)="I'M AN A MAMOTH ART GALLERY.":DATA34,0,0,0,0,0
295 OB$(8)="AN EMPTY BUCKET":DATA6,0:OB$(9)="A MEAN LOOKING VAMPIRE":DATA25,0:OB$(10)="A BUCKET OF WATER":DATA0,0
296 OB$(11)="SOME FRESH GARLIC":DATA3,0
297 OB$(14)="A COFFIN":DATA25,0:OB$(15)="SOME CHEMICALS":DATA11,0
298 OB$(16)="A POT OF WITCHES' BREW":DATA38,0
299 OB$(18)="A BLACK HAT":DATA0,50:OB$(19)="SOME VALUABLE RECIPIES":DATA26,15
300 OB$(20)="SOME SILVER CANDLESTICKS":DATA6,10
301 OB$(23)="A BANANA":DATA3,0:OB$(24)="A BANANA PEEL":DATA0,0
302 OB$(25)="A SPARKLING DIAMOND":DATA39,15:OB$(26)="A BLACK CAT":DATA1,0
303 OB$(27)="A SHINY COMPASS":DATA2,0
304 OB$(28)="A TEST TUBE OF FLUID":DATA33,0:OB$(29)="AN EMPTY TEST TUBE":DATA0,0
305 OB$(30)="A BEAUTIFUL ROSE":DATA29,10:OB$(31)="AN UGLY WITCH":DATA38,0:OB$(32)="A BOOK TITLED PRIMEVAL WITCHCRAFT":DATA22,0
306 OB$(33)="A PAPER AIRPLANE":DATA32,0:OB$(34)="A PIECE OF PAPER WITH WRITING ON IT":DATA0,0
307 OB$(35)="SULTON'S DAGGER":DATA0,35:OB$(36)="A PRICELESS REMBRANT":DATA40,20
308 OB$(39)="AN OLD RUSTY AXE":DATA15,5
309 FORI=1TOP:FORJ=0TO5:READP(I,J):NEXTJ,I
310 FORI=8TOLO
311 IFOB$(I)=""THEN313
312 READOB(I,0),OB(I,1)
313 NEXTI
314 DATANOR,SOU,EAS,WES,UP,DOW,BEL,BUC,VAM,WAT,GAR,FOO,GHO,COF,CHE,BRE,CUP,HAT,REC,CAN,KEY,DOO,BAN,PEE,DIA,CAT,COM,FLU,TUB,ROS,WIT,BOO,AIR,PAP,DAG,REM,CAG,GAT,AXE,BUT,N,S,E,W,U,D
315 FORI=1TONN:READNO$(I):NEXTI
316 DATAGO,GET,LOO,INV,SCO,DRO,HEL,SAV,LOA,QUI,RIN,FIL,KIL,THR,SHO,FEE,EAT,OPE,CLO,CHO,MIX,DRI,HIT,SAY,TUR,DUM,FIG,REA,UNF,UNL,STA,PRE
317 FORI=1TONV:READVB$(I):NEXTI
318 GOTO8
