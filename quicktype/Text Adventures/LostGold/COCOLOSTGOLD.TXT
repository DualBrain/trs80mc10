0 CLS:CLEAR1000:DIMC1,CC,M$:CC=RND(-TIMER):GOTO1000
1 C1=1:CC=32
2 IFMID$(M$,CC,1)<>"" AND MID$(M$,CC,1)<>" " THENCC=CC-1:GOTO2
3 PRINTMID$(M$,C1,CC-C1):C1=CC+1:CC=C1+31:IFC1<=LEN(M$) THEN2
4 RETURN
6 IFSGN(L)=1 THENTM$=MID$(STR$(L),2):RETURN
7 RETURN
1000 PRINT@0,"### A RIDER FANTASY CREATION ###";
1010 PRINT@42,"ADVENTURE # 1";:PRINT@70,"LOST DUTCHMAN'S GOLD.";
1020 PRINT@128,"WORLD COPYRIGHT 1979";:PRINT@160,"BY BOB LIDDIL AND TERI LI";:PRINT@192,"ALL RIGHTS RESERVED.";
1030 FORZZ=1 TO2500:NEXT
1060 CLS
1080 M$="WELCOME TO THE SEARCH FOR THE LOST DUTCHMAN'S GOLD. "
1081 M$=M$+"I'M THE GHOST OF BACK-PACK SAM, YER GUIDE ON THIS ADVENTURE.":GOSUB1
1090 M$="YA CAN USE TWO WORD COMMANDS TO FIND CLUES 'N MOVE 'BOUT. GET, GO, PUT, DROP, "
1100 M$=M$+"SCORE, INVENTORY, 'N 'BOUT 100 MORE WORDS MAKE SENSE TA ME.":GOSUB1
1101 M$="ASK FER HELP IF YA GET STUMPED. I HOPE YA DON'T END UP A GHOST, LIKE ME!!":GOSUB1
1110 PRINT"              GOOD LUCK!"
1130 DIMN$(26,7),O$(32,3),R$(46):B=0:IN=B:LN=50:LM=LN:X1=B:X2=B:T$="":M1$=T$:M2$=T$:QM$=T$:U=B:U1=B:IM=2:BD=21:L=1
1140 FORL=B TO26:FORI=B TO7:READ N$(L,I):NEXT I,L
1150 FORI=B TO32:READO$(I,0),O$(I,1),O$(I,2):O$(I,3)=" ":NEXT
1160 FORI=B TO46:READR$(I):NEXT
1170 PRINT:INPUT"HIT 'ENTER' TO START";ZX:L=1:CLS
1180 GOSUB6:IF((O$(12,2)=TM$ OR O$(12,3)="1") AND CL>0) OR L<15 THEN1230
1190 PRINT"IT'S TOO DARK TA SEE!":IFL=15 AND O$(6,3)<>"1" AND O$(4,3)<>"1" THENPRINT"YA HEAR A STRANGE NOISE!"
1200 GOTO1560
1210 T$="OK":RETURN
1220 B=1:PRINT"YA SEE: ":RETURN
1230 IFCL=1 THENLN=LN-1:IFLN<1 THENCL=0
1240 L1=L
1250 B=0:M$="YER "+N$(L,0)+".":GOSUB1:T$="":FORJ=0 TO32:IFVAL(O$(J,2))<>L THEN1370
1260 IFB=0 THEN GOSUB1220
1270 IFO$(J,3)="-7" OR O$(J,3)="-2" THENT$=T$+"BURNT "
1280 IFJ<>1 OR VAL(O$(1,2))<>L THEN1310
1290 IFO$(1,3)="" THENT$=T$+"UNTIED ":GOTO1310
1300 IFO$(1,3)="-9" THENT$=T$+"DEAD ":DM=1
1310 IFO$(J,3)="-3" THENT$=T$+"SMASHED "
1320 IFL<>26 OR J<>32 THEN 1350
1330 IFU1<>0 THENT$=T$+"UN"
1340 T$=T$+"LOCKED "
1350 IFCL=1 AND J=12 THENT$=T$+"LIT "
1360 T$=T$+O$(J,0)+", "
1370 NEXTJ:IFT$<>"" THENM$=LEFT$(T$,LEN(T$)-2)+".":GOSUB1
1380 T$="":IFU1=1 AND L=1 THENM$="OPEN TRAP DOOR.":GOSUB1
1390 IFN$(L,1)="" THEN1500
1400 IFB=0 THEN GOSUB1220
1410 IFL<>16 THEN1440
1420 IFU<>0 THENPRINT"UN";
1430 PRINT"LOCKED ";:GOTO1470
1440 IFL<>17 THEN1470
1450 IFU=1 THENPRINT"UNLOCKED ";:GOTO1470
1460 PRINT"BLOCKED ";
1470 PRINTN$(L,1)"."
1480 IFL=17 AND U=0 THENN$(L,2)=" "+RIGHT$(N$(L,2),3)
1490 IFL=16 AND U=1 THENN$(L,2)="E"+RIGHT$(N$(L,2),3)
1500 IFN$(L,2)="" THEN1560
1510 M$="OBVIOUS EXITS: ":FORI=1 TO4:P$=MID$(N$(L,2),I,1):IFP$="N" THENM$=M$+"NORTH "
1520 IFP$="S" THENM$=M$+"SOUTH "
1530 IFP$="E" THENM$=M$+"EAST "
1540 IFP$="W" THENM$=M$+"WEST "
1550 NEXT:GOSUB1
1560 B=0:PRINT"================================";
1570 IFL=9 AND O$(22,2)="11" THENM$="THAR'S NOISE UP AHEAD. SOUNDS LIKE INJUNS.":GOSUB1
1580 IFL=9 AND O$(22,2)="" THENO$(22,2)="11"
1590 INPUT" --NOW WHAT";QM$
1600 IFLN<10 AND CL=1 THENM$="YER RUNNIN' LOW ON KEROSENE.":GOSUB1
1610 REM
1620 IFQM$<>"SCORE" THEN1680
1630 IFL<>6 THENT$="YA GET NOTHIN' FOR BEIN' HERE!":GOTO3820
1640 Z=0:FORI=14 TO17:IFO$(I,2)="6" THENZ=Z+1
1650 NEXT
1660 M$="YA GOT"+STR$(Z)+" TREASURES, TOT'LIN"+STR$((Z/4)*100)+"%":GOSUB1:IFZ=4 THENPRINT"YA MADE IT!":GOTO4130
1670 T$="YA MISSED SUN TREASURE!":GOTO3820
1680 IFCL=0 AND L=15 AND O$(6,3)<>"1" THENM$="YE GAD! YA BEEN BIT BY A RATTLER. YER DEAD.":GOSUB1:GOTO4130
1690 IFL=17 AND U=1 AND RND(0)*9+1<2 THEN1695
1691 GOTO1700
1695 M$="HOLEY CRAP! WOWIE! IT'S A CAVE IN! THE IRON DOOR'S BLOCKED, YER TRAPPED!":GOSUB1:U=0
1700 IFQM$="HELP" THENX=VAL(N$(L,3)):ONX GOSUB4150,4160,4170,4180,4200,4220,4230,4240,:GOTO1180
1710 IFLEFT$(QM$,2)="GO" THENX1=LEN(QM$):QM$=RIGHT$(QM$,X1-2):QM$="GOO"+QM$
1720 X1=LEN(QM$):IFX1<3 THENT$="WHAT?":GOTO3820
1730 X3=0:FOR X2=3 TOX1:M1$=MID$(QM$,X2,1):IFM1$=" " THENX3=X2:X2=X1
1740 NEXTX2:X2=X3:IFX3=0 THENX2=X1
1750 P$=LEFT$(QM$,3):IFX2<>X1 AND X2+3>X1 THEN1990
1760 II=-1:FORI=0 TO42:IFP$=R$(I) THENII=I
1770 NEXT:IFII>-1 THENI=II:GOTO1810
1780 IFX1=X2 THENT$="WHAT?":GOTO3820
1790 T$="SORRY, BUT YA CAN'T "+LEFT$(QM$,X2):IFX2<X1 THENT$=T$+RIGHT$(QM$,X1-X2)
1800 T$=T$+ ".":GOTO3820
1810 IFI<8 AND X1=X2 THENONI+1 GOSUB3970,3970,3970,4130,2060,3980,3840,3940:GOTO3820
1820 JJ=-1:M1$=MID$(QM$,X2+1,3):FORJ=0 TO32:IFM1$=LEFT$(O$(J,0),3) THENJJ=J
1830 NEXT:J=JJ:IFJJ>-1 THEN2000
1840 IFM1$="IRO" THEN1960
1850 IFI=36 THEN2400
1860 IFN$(L,1)="" THEN1930
1870 Z=1:A=Z:X1=LEN(N$(L,1)):FORX2=5 TOX1:IFMID$(N$(L,1),X2,1)<>CHR$(46) AND X2<>X1 THEN1920
1880 M2$=MID$(N$(L,1),Z,3):IFM2$=M1$ THEN1900
1890 Z=X2+2:A=A+1:GOTO1920
1900 X2=X1:B=A:IFI=10 THENPRINT"YA SEE NOTHIN' SPECIAL.":GOTO1240
1910 GOTO2000
1920 NEXT
1930 FORX=43 TO46:IFM1$=R$(X) THENX=46:GOTO2040
1940 NEXT
1950 IFO$(4,3)="1" OR O$(6,3)="1" THENIFI=23 AND M1$="SEL" THENPRINT"OK":PRINT"YER DEAD.":GOTO4130
1960 IFI=32 THEN GOSUB2220:GOTO3820
1970 IFI=33 THEN GOSUB2340:GOTO3820
1980 T$="I CAN'T TELL WHATCHA WANT.":GOTO3820
1990 T$="I MUST BE DUMB, YA DON'T MAKE SENSE.":GOTO3820
2000 IFI<9 THEN2040
2010 IFJ<33 AND J>-1 THENB=VAL(O$(J,2)):IFB<>L AND B>0 THEN3810
2020 IFJ>32 THENJ=29
2030 IFI-8>14 THEN2032
2031 ONI-8 GOSUB2820,3040,3040,3040,2820,3440,3440,3440,3440,3640,3640,2820,2100,2100:GOTO2040
2032 ON(I-8)-14 GOSUB2580,2570,2570,2380,2660,2750,2780,2170,2170,2220,2340,2820,2370,2400,3440,3430,2980,3000,3640,4250
2040 IFI<9 THENONI+1 GOSUB2420,2420,2420,4130,2060,2420,3840,3940,2980
2050 GOTO3820
2060 IFO$(3,3)<>"1" THEN3810
2070 PRINT"OK":FORI=13 TO17:IFVAL(O$(I,2))=-L THENT$="EUREKA! WE STRUCK GOLD?!?":GOSUB6:O$(I,2)=TM$:RETURN
2080 NEXT
2090 T$="DAG NAB IT! THAR'S NOTHIN' HERE!":RETURN
2100 IFJ<>23 THEN2570
2110 IFL=14 THENO$(23,2)="15":GOTO1210
2120 IFL=15 THENO$(23,2)="16":GOTO1210
2130 IFL=16 AND U=1 THENO$(23,2)="17":GOTO1210
2140 IFL=17 THENO$(23,2)="19":GOTO1210
2150 IFL=19 THENO$(23,2)="23":O$(23,3)="-3"
2160 GOTO1210
2170 IFJ=3 OR J=4 OR J=6 OR J=8 OR J=9 OR J=11 OR J=12 OR J=13 OR J=19 OR J=27 OR J=28 THEN2190
2180 GOTO2560
2190 IFO$(J,3)="1" THENIN=IN-1
2200 O$(J,3)="-3":GOSUB6:O$(J,2)=TM$:GOTO1210
2210 T$="DON'T HAFTA.":RETURN
2220 IFL<>16 THEN2260
2230 IFU<>0 THEN2210
2240 IFO$(10,3)="1" THENU=1:GOTO1210
2250 GOTO3810
2260 IFL<>26 THEN2300
2270 IFU1<>0 THEN2210
2280 IFO$(10,3)="1" THENU1=1:GOTO1210
2290 GOTO3810
2300 IFO$(1,3)<>"1" THEN2330
2310 IFO$(0,3)="2" THENO$(0,3)="":GOSUB6:O$(0,2)=TM$:GOTO1210
2320 GOTO2560
2330 IFCL=1 AND O$(12,3)="1" THENCL=0:GOTO1210
2340 IF(L=16 OR L=17) AND U=1 THENU=0:GOTO1210
2350 IFL=26 AND U1=1 THENU1=0:GOTO1210
2360 GOTO2560
2370 T$="YA HEAR NOTHIN' SPECIAL.":RETURN
2380 IFL<>11 OR O$(9,3)<>"1" THEN2980
2390 T$="THEY TOOK THE FIRE WATER 'N RAN.":IN=IN-1:O$(9,2)="":O$(9,3)="":O$(22,2)="":RETURN
2400 IFM1$="YOH" THENT$="TURKEY!":M$="SORRY, YER IN THE WRONG ADVENTURE.":GOSUB1:GOTO3820
2410 GOSUB1210:T$=T$+CHR$(13)+RIGHT$(QM$,X1-X2)+".":GOTO3820
2420 IFB=0 THEN2470
2430 L=VAL(N$(L,B+3)):IFO$(1,3)="1" AND (L=1 OR L=6 OR L=15) THENT$="HE WON'T GO.":L=L1:RETURN
2440 IFL=17 AND U=0 THENL=L1:GOTO3810
2450 IFL1=26 AND L=1 AND U1=0 THENL=L1:GOTO3810
2460 GOTO2570
2470 IFU1=1 AND L=1 THENL=26:GOTO1210
2480 IFU1=1 AND M1$="TRA" AND L=26 THENL=1:GOTO1210
2490 FORX1=1 TO4:M2$=MID$(N$(L,2),X1,1):IFM2$<>LEFT$(M1$,1) THENNEXTX1:GOTO2520
2500 FORX2=43 TO46:IFM1$<>R$(X2) THENNEXTX2:GOTO2520
2510 L=VAL(N$(L,X1+3)):IFL<>0 THEN2570
2520 IFL=23 THENT$="IT'S TOO SLIPPERY.":RETURN
2530 IFL=25 THENL=26:RETURN
2540 IFL=26 THENL=25:RETURN
2550 IFL=19 THENL=23:PRINT"NIGH BROKE M' NECK!":RETURN
2560 T$="SORRY, BUT YA CAN'T DO THAT.":RETURN
2570 IF(L=1 OR L=6 OR L=15) AND O$(1,3)="1" THENT$="MULE WON'T BUDGE.":L=L1:RETURN
2575 B=0:GOTO1210
2580 IFO$(4,3)<>"1" AND O$(6,3)<>"1" THEN2980
2590 IFJ<>1 THEN2630
2600 Z=0:IFO$(J,3)="1" THENZ=99
2610 O$(J,3)="-9":GOSUB6:O$(1,2)=TM$
2620 IFZ=99 THENIN=IN-1
2630 IFJ=22 THENPRINT"YA GOT ONE!":PRINT"BUT THE REST GOT YOU!":GOTO4130
2640 IFJ=9 THENT$="YA HIT ONE!":RETURN
2650 GOTO1210
2660 IFO$(18,3)<>"1" THEN3810
2670 IFJ<0 THEN1790
2680 IFJ=4 OR (J>5 AND J<11) OR (J>14 AND J<18) OR J=22 OR J=29 OR J>30 THEN2560
2690 IFO$(J,3)="1" THENIN=IN-1
2700 O$(J,3)="-7":GOSUB6:O$(J,2)=TM$
2710 IFJ>0 THEN2570
2720 IFO$(0,3)<>"-7" THEN 2570
2730 GOSUB6:O$(0,2)=TM$:FORJ=5 TO21:IFO$(J,2)="-1" THENO$(J,3)="-7"
2740 NEXT:GOTO2570
2750 IFJ<>12 THEN2660
2760 IFO$(J,3)<>"1" OR O$(18,3)<>"1" THEN2660
2770 CL=1:GOTO1210
2780 IFJ=13 THENM$="YEEECH! IT'S KEROSENE! YA JUST POISONED YERSELF.":GOSUB1:GOTO4130
2790 IFJ<>9 THEN2560
2800 M$="WHEEEE! YA GOT PLASTERED AND LOST A DAY.":GOSUB1:IFCL=1 THENLN=LN-10
2810 RETURN
2820 IFJ>32 OR J<0 THEN2860
2830 B=VAL(O$(J,2)):IFO$(J,3)="2" OR B=L OR B=-1 THEN2860
2840 IFJ<22 THEN3810
2850 T$="IT AIN'T HERE.":RETURN
2860 IFJ>21 OR J=1 OR J=-1 THENT$="WHO YOU THINK YA IS, PAUL BUNYON?!!":RETURN
2870 IFO$(J,3)="-7" THEN T$="IT'S BURNT UP 'N RUINED.":RETURN
2880 IFIN>4 THENT$="YA CAN'T! YER HANDS 'R FULL.":RETURN
2890 IFO$(J,3)="1" THENPRINT"YA ALREADY GOT IT!":RETURN
2900 IFVAL(O$(J,1))=0 AND I<>16 THEN2930
2910 IN=IN+1:IFO$(J,2)="-1" THENIM=IM-1:O$(J,2)="":O$(J,3)="1":GOTO2930
2920 O$(J,3)="1":T$="OK":O$(J,2)=""
2930 IFJ<>9 AND J<>11 AND J<>2 THEN1210
2940 IFJ=9 THENIFO$(8,2)="-6" THENO$(8,2)="6"
2950 IFJ=11 THENIFO$(12,2)="-8" THENO$(12,2)="8"
2960 IFJ=2 THENIFO$(18,2)="-14" THENO$(18,2)="14"
2970 RETURN
2980 IFJ<0 THENT$="WHAT!?":RETURN
2985 IFO$(J,3)<>"1" THENT$="YA DON'T HAVE IT!":RETURN
2990 T$="OK":IN=IN-1:GOSUB6:O$(J,2)=TM$:O$(J,3)="":RETURN
3000 IFO$(5,3)<>"1" THEN3810
3010 IFL=7 THENL=8:GOTO1210
3020 IFL=12 THENL=13
3030 GOTO1210
3040 GOSUB1210:IFJ+1>14 THEN3042
3041 ONJ+1 GOTO3060,3100,3110,3130,3140,3150,3160,3130,3130,3170,3190,3200,3220,3230:GOTO3050
3042 ON(J+1)-14 GOTO3130,3130,3130,3130,3130,3250,3270,3290,3310,3320,3330,3350,3130,3370,3130,3130,3130,3390,3130
3050 GOTO3130
3060 IFVAL(O$(0,3))<1 THENT$="THEY'S LUMPY OL' LEATHER SADDLEBAGS.":RETURN
3070 PRINT"THEY HOLD:":IFIM=0 THENPRINT"NOTHIN'.":T$="":RETURN
3080 FORI=6 TO22:IFO$(I,2)="-1" THENPRINT O$(I,0)
3090 NEXT:T$="":RETURN
3100 T$="YA SEE A WEARY OL' GREY MULE."
3101 IFDM=1 THENT$="YA SEE A DEAD OL' GREY MULE."
3102 RETURN
3110 T$="IT'S AN OL' TORN SACK.":IFO$(18,2)="-14" THENO$(18,2)="14"
3120 RETURN
3130 T$="YA SEE NOTHIN' SPECIAL.":RETURN
3140 T$="IT'S AN OLD WINCHESTER SINGLE-SHOT.":RETURN
3150 T$="LOOKS LIKE A MAP TA THE LOST DUTCHMAN'S MINE! THAR'S A SKETCH OF SOME BOULDERS.":RETURN
3160 T$="NOTHIN' SPECIAL, JUS' A LOADED SIX SHOOTER.":RETURN
3170 IFO$(8,2)="-6" THENT$="LOOKS LIKE SOMETHIN'S 'HIND THE BOTTLES.":O$(8,2)="6":RETURN
3180 GOTO3130
3190 T$="THAR'R 3 KEYS, TIED WITH A LEATHER STRAP":RETURN
3200 IFO$(12,2)="-8" THENT$="LOOKS LIKE THAR'S GLASS UNDER 'UM.":O$(12,2)="8":RETURN
3210 GOTO3130
3220 T$="IT'S AN OL' KEROSENE LAMP, FULL TA THE '"+STR$(LN)+" TURNS LEFT ' MARK.":RETURN
3230 IFI=11 THENT$="IT SEZ 'KEROSENE.'":RETURN
3240 T$="THAR'S WRITING ON IT.":RETURN
3250 IFO$(20,2)="-17" THENO$(20,2)="17":T$="IT'S A MESSAGE!":RETURN
3260 GOTO3130
3270 IFI=11 THENT$="IT SEZ: BRING TREASURES TO SALOON, SAY 'SCORE.'":RETURN
3280 GOTO3240
3290 IFI=11 THENT$="IT SEZ: HAVEN'T YOU ANYTHING BETTER TO DO?":RETURN
3300 GOTO3240
3310 T$="THEY'S A SAVAGE LOOKIN' BAND, 'N THEY SEEN YA!":RETURN
3320 T$="JUS' AN OL' CART FULL O' ROCKS.":RETURN
3330 IFO$(5,2)="-25" THENO$(5,2)="1":T$="THAR'S SOMETHIN' HERE!!":RETURN
3340 GOTO3130
3350 IFI=11 THENT$="IT SEZ: WELCOME TO FRONTIER TOWN.":RETURN
3360 GOTO3240
3370 IFO$(10,2)="-6" THENO$(10,2)="6":T$="THAR'S A SET OF KEYS THAR!":RETURN
3380 GOTO3130
3390 B=INT(RND(0)*3+1):ONB GOTO3400,3410,3420
3400 T$="IT'S A BARREL CACTUS.":RETURN
3410 T$="IT'S A CHOLLA CACTUS.":RETURN
3420 T$="IT'S A SAGUARO CACTUS.":RETURN
3430 IFJ=4 AND I=38 THEN3570
3440 IFJ<>1 OR VAL(O$(1,3))<0 THENT$="SORRY, BUT THAT AIN'T POSSIBLE.":RETURN
3450 IFI<>38 THEN3480
3460 IFO$(1,3)="" THENT$="TENDERFOOT! YA HAFTA LEAD 'IM FIRST.":RETURN
3470 GOTO3570
3480 IFO$(1,3)="1" THEN2980
3490 IFO$(7,3)="1" THENIFI<>16 THENT$="TRY 'LEAD.':RETURN
3500 ONI-13 GOTO3510,3520,3530,3560
3510 T$="HE GOT AWAY FROM YA.":RETURN
3520 T$="YER TOO SLOW, HE GOT AWAY.":RETURN
3530 IFO$(7,3)<>"1" THENT$="BAD LUCK, YA TRIPPED AND HE RAN OFF.":O$(1,3)="":GOSUB6:O$(1,2)=TM$:RETURN
3535 IFO$(7,3)="1" THENT$="OK":O$(1,3)="1":GOSUB6:O$(1,2)="":RETURN
3540 IFO$(1,3)="" THEN2880
3550 GOTO2980
3560 T$="HE BUCKED YA OFF.":RETURN
3570 INPUT"WITH WHAT";QM$:IFJ=4 THEN3610
3580 IFLEFT$(QM$+" ",3)<>"SAD" THENT$="IT FELL OFF. HE GOT AWAY.":O$(1,3)="":IN=IN-1:GOSUB6:O$(1,2)=TM$:RETURN
3590 IFO$(0,3)="1" THENO$(0,3)="2":IN=IN-1:GOTO2570
3600 GOTO2980
3610 IFLEFT$(QM$+" ",3)<>"BUL" THEN2560
3620 IFO$(8,3)<>"1" OR O$(4,3)<>"1" THEN2980
3630 GOTO1210
3640 IFO$(J,3)<>"1" THEN2980
3650 INPUT"ON OR IN WHAT? (IE 'ON TABLE')  ";QM$:P$=LEFT$(QM$+" ",2):IFP$<>"IN" AND P$<>"ON" THENT$="HUH?":RETURN
3660 M1$=MID$(QM$,4,3)
3670 IFP$<>"ON" THEN3720
3680 IFM1$="MUL" THEN3590
3690 IFJ<>13 THEN2980
3700 IFLM=0 THENT$="IT'S EMPTY.":RETURN
3710 LM=0:GOTO2980
3720 IFM1$<>"SAD" THEN3760
3730 IFJ<5 OR J=19 OR J=22 THENT$="SORRY, IT DON'T FIT.":RETURN
3740 IFIM>4 THENT$="THE BAGS 'R FULL.":RETURN
3745 IFO$(1,3)<>"1" AND O$(0,3)="2" THENT$="MULE'S GOT 'EM. HE AIN'T HERE.":RETURN
3750 O$(J,2)="-1":O$(J,3)="":IN=IN-1:IM=IM+1:T$="OK":RETURN
3760 IFM1$="MUL" THENT$="YA GOT KICKED!":RETURN
3770 IFM1$<>"LAN" THEN2560
3780 IFO$(13,3)<>"1" OR O$(12,3)<>"1" THEN2980
3790 IFLM=50 THENLN=LN+50:LM=0:GOTO1210
3800 T$="JAR'S EMPTY.":RETURN
3810 T$="YA CAN'T DO THAT... YET!"
3820 M$=T$:GOSUB1:T$="":GOTO1180
3830 REM INV
3840 PRINT"YA GOT WITH YA:":T$="":M$="":IFIN=0 AND O$(1,3)<>"1" THENT$="NOTHIN'":RETURN
3850 B=VAL(O$(0,3)):X1=B:IFB<>1 THEN3880
3860 PRINT O$(0,0);", CONTAINING: ":FORI=1 TO22:IFO$(I,2)="-1" THENPRINT"   ";O$(I,0):X1=2
3870 NEXT:IFX1<>2 THENPRINT"   NOTHIN'!"
3880 IFO$(1,3)="1" THENPRINT"THE MULE, WHICH YER LEADIN.":IFB=2 THENPRINT"   (CARRYIN' SADDLEBAGS)"
3890 X1=0:FORI=2 TO22:IFVAL(O$(I,3))<1 THEN3920
3900 IFCL=1 AND I=12 THENM$=M$+"LIT "
3910 M$=M$+O$(I,0)+", "
3920 NEXT:IFM$<>"" THENT$=LEFT$(M$,LEN(M$)-2)+"."
3930 RETURN
3940 IFL=19 THENFORZZ=16 TO1 STEP-1:SOUNDZZ*2,1:PRINT:NEXT:M$="YA FELL 100 FEET 'N LANDED ON THE ROCKS. YER DEAD.":GOSUB1:GOTO4130
3950 IFJ=23 OR J=32 OR M1$="P" THENT$="OK":RETURN
3960 GOTO2560
3970 T$="WHERE?":RETURN
3980 IFL=25 THENL=26:GOTO1210
3990 IFL=26 THENL=25
4000 GOTO1210
4010 DATA ,,,,,,,,IN A MINER'S SHACK,WINDOW. DOOR,,1,3,3,,,IN A DESERT,ROAD. MOUNTAINS. DESERT
4011 DATA WNES,2,4,2,2,2,ON A DIRT PATH,MINER'SHACK. ROAD. MOUNTAINS," W ",5,1,4,2,2
4020 DATA ON A DIRT ROAD,MOUNTAINS. PATH. DESERT. TOWN,NE S,2,7,3,2,5,IN A GHOST TOWN
4021 DATA SALOON," N ",1,6,4,,,IN A SALOON,,W   ,1,5,,,
4030 DATA AT THE SUPERSTITION MOUNTAINS,ROAD. DESERT,"S N ",4,4,2,2,2,AT WEAVER'S NEEDLE,,NS  ,1,9,7,,
4040 DATA IN A NARROW DEFILE,BUSHES. CAVES,"  NS",1,11,10,12,8,IN A SMALL CAVE,,E   ,1,9,,,,BEHIND A BUSH
4041 DATA BUSH,W   ,1,9,,,,IN A BOX CANYON,BUSHES. TREES. BOULDERS,"   S",4,12,12,12,9
4050 DATA IN FRONT OF A HIDDEN MINE,MINE SHAFT,ESNW,8,14,12,12,12,IN THE MOUTH OF A DIM MINE,MINE SHAFT
4051 DATA EW  ,1,15,13,,,IN A MINE,DARK TUNNEL,EW,6,16,14,,
4060 DATA AT THE END OF A TUNNEL,IRON DOOR," W  ",1,17,15,,,IN A LARGE CHAMBER,IRON DOOR,WNES,1,16,19,18,20
4061 DATA IN A MAZE OF TUNNELS,,NSEW,6,18,18,18,18,IN FRONT OF A PIT,DARK HOLE," S  ",7,23,17,,
4070 DATA AT AN INTERSECTION,," SNW",6,,22,17,21,IN A DEAD END TUNNEL,,E   ,8,20,,,,IN A BLOCKED TUNNEL,
4071 DATA N   ,8,20,,,,AT THE BOTTOM OF A SHAFT,WALLS,S   ,8,24,,,
4080 DATA IN A TUNNEL,,NS  ,6,23,25,,,AT THE END OF A TUNNEL,LADDER," N  ",6,26,24,,,AT THE TOP OF A LADDER,,,6,1,25,,
4090 DATA SADDLEBAGS,2,1,MULE,,3,BURLAP SACK,4,14,SHOVEL,4,1,RIFLE,4,1,MAP,1,-25,GUN,1,-1,CARROTS,1,-1
4091 DATA BOX OF RIFLE BULLETS,1,-6,WHISKEY BOTTLES,2,6,KEYS,1,-6,PILE OF BONES (MINE),2,8
4100 DATA LANTERN,2,-8,JAR OF LIQUID,1,-11,*SPANISH COINS*,1,-10,*TOURQUOISE*,1,-23
4101 DATA #SILVER#,1,-21,#GOLD#,1,-22,MATCHES,1,-14,CRATES,3,17,NOTE,1,-17,PAPER,1,6,INDIANS,,11
4110 DATA ORE CART,,14,BED,,1,SIGN,,5,BROKEN GLASS,,6,TABLES,,6,CHAIRS,,6,ROCKS,,10,WOODEN RAILS,,15
4111 DATA CACTUS,,2,TRAP DOOR,,26
4120 DATA GOO,ENT,RUN,QUI,DIG,CLI,INV,JUM,DRO,GET,EXA,REA,LOO,MOV,CAT,CHA,LEA,RID,PUT,PLA,PIC,PUS,PUL,SHO,OPE
4121 DATA CLO,GIV,BUR,LIG,DRI,BRE,HIT,UNL,LOC,TAK,LIS,SAY,FEE,LOA,UNT,FOL,POU,EAT,NOR,SOU,EAS,WES
4130 PRINT:PRINT:INPUT"DO YA WANNA TRY AGAIN";QM$:IF LEFT$(QM$+" ",1)="Y" THEN RUN
4131 END
4140 REM HELP
4150 PRINT"TRY EXAM'NIN THIN'S.":RETURN
4160 PRINT"ROADS GO PLACES.":RETURN
4170 PRINT"MAYBE THE TRAIL GOES SOMEWHAR.":RETURN
4180 IFO$(5,3)<>"1" THENPRINT"YA GOT A MAP?":RETURN
4190 PRINT"TRY 'FOLLOW.'":RETURN
4200 IFVAL(O$(1,2))=L THENPRINT"THE MULE LOOKS THIN.":RETURN
4210 GOTO4150
4220 PRINT"KEEP GOIN'.":RETURN
4230 PRINT"IT'S SLIPRY, BUTCHA MIGHT MAKE IT DOWN.":RETURN
4240 PRINT"THIS HERS'S A MINE, YA KNOW.":RETURN
4250 IFJ=7 THENT$="THEY TASTE PURTY GOOD.":RETURN
4260 T$="CAN'T DO THAT... WEIRDO!":RETURN
4270 INPUT"SURE ";QM$:IFLEFT$(QM$+" ",1)<>"Y" THEN1590
4280 GOTO1590
4290 END
