3 'LOST DUTCHMAN'S GOLD BY TERI LI (SOFTSIDE)
5 'MODIFIED (AG) TO SAVE GAME IN PROGRESS (9/15/80)
10 CLS:CLEAR1000:DEFINTA-L,U-Z:DEFSTRM-T:DIMN(7),O(32,3),R(46):M=STRING$(63,"="):POKE16553,255:B=0:IN=B:LN=50:LM=LN:X1=B:X2=B:T="":M1=T:M2=T:QM=T:U=B:U1=B:IM=2
20 GOSUB2330
30 ONERRORGOTO2320
40 FORL=BTO26:FORI=BTO7:READN(I):NEXTI,L
50 FORI=BTO32:READO(I,0),O(I,1),O(I,2):O(I,3)="":NEXTI
60 FORI=BTO46:READR(I):NEXT
70 L=1
80 CLS:PRINT@128,"":GOSUB2340:IFGS=1THENCLS:GOTO170
100 IFL<15OR(CL>0AND(O(12,3)="1"ORO(12,2)=STR$(L)))THEN150ELSEPRINT@0,"ITS TOO DARK TO SEE !!";CHR$(30):IFL=15THENIFO(6,3)<>"1"ANDO(4,3)<>"1"THENPRINT"YOU HEAR A STRANGE NOISE !"
110 GOTO350
120 T="OK":RETURN
140 B=1:PRINT"  YOU SEE :";CHR$(30):PRINT:RETURN
150 IFCL=1THENLN=LN-1:IFLN<1THENCL=0
160 RESTORE:FORL1=0TOL:FORI=0TO7:READN(I):NEXTI,L1:L1=L
170 B=0:PRINT@0,CHR$(30):PRINT:PRINT:PRINT:PRINT:PRINT:PRINT@64,"YOU ARE "N(0)".";:FORJ=0TO32:IFVAL(O(J,2))<>LTHEN240ELSEIFB=0GOSUB140
180 IFO(J,3)="-7"ORO(J,3)="-2"THENPRINT"BURNT ";
190 IFJ=1THENIFVAL(O(1,2))=LTHENIFO(1,3)=""THENPRINT"UNTIED ";:ELSEIFO(1,3)="-9"THENPRINT"DEAD ";
200 IFO(J,3)="-3"THENPRINT"SMASHED ";
210 IFL=26ANDJ=32THENIFU1=0THENPRINT"LOCKED ";:ELSEPRINT"UNLOCKED ";
220 IFCL=1ANDJ=12THENPRINT"LITE ";
230 PRINTO(J,0)". ";
240 NEXTJ
250 IFU1=1ANDL=1THENPRINT"OPEN TRAP DOOR. ";
260 IFN(1)=""THENPRINT:GOTO330ELSEIFB=0THENGOSUB140
270 IFL=16THENIFU=0THENPRINT"LOCKED ";:ELSEPRINT"UNLOCKED ";:N(2)="E"+RIGHT$(N(2),3)
280 IFL=17THENIFU=1THENPRINT"UNLOCKED ";:ELSEPRINT"BLOCKED ";:N(2)=" "+RIGHT$(N(2),3)
285 IFU1=1ANDU=0THENPRINT"CAVED IN ";:N(2)=" "+RIGHT$(N(2),3):N(4)=""
290 PRINTN(1);"."
330 PRINT:IFN(2)=""THEN350ELSEPRINT"OBVIOUS EXITS ARE : ";:FORI=1TO4:P=MID$(N(2),I,1):IFP="N"THENPRINT"NORTH ";:ELSEIFP="S"THENPRINT"SOUTH ";ELSEIFP="E"THENPRINT"EAST ";:ELSEIFP="W"THENPRINT"WEST ";
340 NEXT:PRINT
350 B=0:PRINTM
360 IFL=9ANDO(22,2)="11"THENPRINT@960,"THAR'S NOISE UP AHEAD - - SOUNDS LIKE INJUNS."
370 IFL=9ANDO(22,2)=""THENO(22,2)="11"
390 PRINT@960,"      What do you want to do now";:INPUTQM
410 IFLN<10ANDCL=1THENPRINT"YOU'RE RUNNIN' LOW ON KEROSENE."
420 IFQM<>"SCORE"THEN445ELSEIFL<>6THENT="YOU CAN'T, YOU'RE IN THE WRONG LOCATION !":GOTO1900:ELSEZ=0:FORI=14TO17:IFO(I,2)=" 6"THENZ=Z+1
430 NEXT
440 PRINT"YOU HAVE"Z"TREASURES, TOTALING"(Z/4)*100"%":IFZ=4THENPRINT"YOU MADE IT !!!":FORI=1TO500:NEXT:FORI=1TO100:PRINT@RND(1023),"YOU MADE IT !!!!":NEXT:GOTO2290:ELSET="YOU MISSED SOME OF THE TREASURE.":GOTO1900
445 IFLEFT$(QM,3)="SAV"GOTO2400
450 IFCL=0ANDL=15ANDO(6,3)<>"1"ANDO(4,3)<>"1"THENPRINT"OH NO!
YOU'VE BEEN BITTEN BY A RATTLESNAKE.

YOU'RE DEAD.":GOTO2290
460 IFL=17ANDU=1THENIFRND(9)<2THENPRINT" ! ! ! CAVE-IN ! ! !
THE IRON DOOR IS BLOCKED,
YOU CAN'T GET OUT ! !":U=0
480 IFQM="HELP"THENX=VAL(N(3)):ONXGOSUB2150,2160,2170,2180,2190,2200,2210,2220:GOTO100
490 X1=LEN(QM):IFX1<3THENT="WHAT?":GOTO1900:ELSEFORX2=3TOX1:M1=MID$(QM,X2,1):IFM1<>CHR$(32)THENNEXTX2:X2=X1
500 P=LEFT$(QM,3):IFX2<>X1ANDX2+3>X1THEN610ELSEFORI=0TO42:IFP<>R(I)THENNEXT:IFX1=X2THENT="WHAT?":GOTO1900:ELSET="SORRY, BUT YOU CANNOT "+LEFT$(QM,X2):IFX2<X1THENT=T+RIGHT$(QM,X1-X2)+".":GOTO1900:ELSET=T+".":GOTO1900
520 IFI<8ANDX1=X2THENONI+1GOSUB2010,2010,2010,2290,680,1010,1920,2000:GOTO1900
540 M1=MID$(QM,X2+1,3):FORJ=0TO32:IFM1=LEFT$(O(J,0),3)THEN630ELSENEXTJ:IFM1="IRO"THEN580ELSEIFI=36THEN940
550 IFN(1)<>""THENZ=1:A=Z:X1=LEN(N(1)):FORX2=5TOX1:IFMID$(N(1),X2,1)<>CHR$(46)ANDX2<>X1THENNEXTELSEM2=MID$(N(1),Z,3):IFM2<>M1THENZ=X2+2:A=A+1:NEXTX2:ELSEB=A:IFI=10THENPRINT"YOU SEE NOTHING SPECIAL.":GOTO160ELSE630
560 FORX=43TO46:IFM1=R(X)THEN650ELSENEXT
570 IFO(4,3)="1"ORO(6,3)="1"THENIFI=23ANDM1="SEL"THENPRINT"OK":GOTO2290
580 IFI=32GOSUB780:GOTO1900
590 IFI=33GOSUB860:GOTO1900
600 T="I DON'T KNOW WHAT YOU WANT.":GOTO1900
610 T="I MUST BE DUMB, i DON'T UNDERSTAND YOU.":GOTO1900
630 IFI<9THEN650ELSEIFJ<33THENB=VAL(O(J,2)):IFB<>LANDB>0THEN1890ELSEELSEJ=29
640 ONI-8GOSUB1250,1390,1390,1390,1250,1700,1700,1700,1700,1820,1820,1250,700,700,1080,1060,1060,920,1150,1190,1210,730,730,780,860,1390,900,940,1700,1690,1320,1340,1820,2300
650 IFI<9THENONI+1GOSUB970,970,970,2290,680,1010,1920,2000,1320
660 GOTO1900
680 IFO(3,3)<>"1"THEN1890ELSEPRINT"OK":FORI=13TO17:IFVAL(O(I,2))=-LTHENT=" - - EUREKA!  WE STRUCK GOLD ?!? ? !":O(I,2)=STR$(L):ELSENEXT:T="DAG NAB IT! THERE'S NOTHIN' HERE!"
690 RETURN
700 IFJ<>23THEN1060ELSEIFL=14THENO(23,2)="15"ELSEIFL=15THENO(23,2)="16"ELSEIFL=16ANDU=1THENO(23,2)="17"ELSEIFL=17THENO(23,2)="19"ELSEIFL=19THENO(23,2)="23":O(23,3)="-3"
710 GOTO120
730 IFJ=3ORJ=4ORJ=6ORJ=8ORJ=9ORJ=11ORJ=12ORJ=13ORJ=19ORJ=27ORJ=28THENO(J,3)="-3":GOTO120:ELSE1050
770 T="ITS ALREADY UNLOCKED.":RETURN
780 IFL=16THENIFU=0THENIFO(10,3)="1"THENU=1:GOTO120:ELSE1890:ELSE770
800 IFL=26THENIFU1=0THENIFO(10,3)="1"THENU1=1:RETURN:ELSE1890:ELSE770
820 IFO(1,3)="1"THENIFO(0,3)="2"THENO(0,3)="":O(0,2)=STR$(L):GOTO120:ELSE1050
840 IFCL=1ANDO(12,3)="1"THENCL=0:GOTO120
860 IF(L=16ORL=17)ANDU=1THENU=0:GOTO120
870 IFL=26ANDU1=1THENU1=0:GOTO120
880 GOTO1050
900 T="YOU CAN HEAR NOTHING SPECIAL.":RETURN
920 IFL<>11ORO(9,3)<>"1"THEN1320ELSEPRINT"THEY TOOK THE FIRE WATER,
AND RAN OFF.":IN=IN-1:O(9,2)="":O(9,3)="":O(22,2)="":RETURN
940 IFM1="YOH"T="YOU'RE IN THE WRONG ADVENTURE, TURKEY!":GOTO1900
950 GOSUB120:T=T+"
"+RIGHT$(QM,X1-X2)+".":GOTO1900
970 IFB<>0THENL=VAL(N(B+3)):IFO(1,3)="1"AND(L=1ORL=6ORL=15)THENT="THE MULE WON'T GO.":L=L1:RETURN:ELSEIFL=17ANDU=0THENL=L1:GOTO1890:ELSEIFL1=26ANDL=1ANDU1=0THENL=L1:GOTO1890:ELSE1060
980 IFU1=1ANDL=1THENL=26:GOTO120
990 IFU1=1ANDM1="TRA"ANDL=26THENL=1:RETURN
1000 FORX1=1TO4:M2=MID$(N(2),X1,1):IFM2<>LEFT$(M1,1)THENNEXTX1:ELSE:FORX2=43TO46:IFM1<>R(X2)THENNEXTX2ELSEL=VAL(N(X1+3)):IFL<>0THEN1060
1010 IFL=23THENT="ITS TOO SLIPPERY.":RETURN
1020 IFL=25THENL=26:RETURN
1030 IFL=26THENL=25:RETURN
1040 IFL=19THENL=23:T="WOW, ALMOST SLIPPED AND FELL.":RETURN
1050 T="SORRY, BUT YOU CAN'T DO THAT.":RETURN
1060 B=0:GOTO120
1080 IFO(4,3)<>"1"ANDO(6,3)<>"1"THENT="WITH WHAT?":RETURNELSEIFJ<>1THEN1110ELSEIFO(J,3)="1"THENZ=99ELSEZ=0
1090 O(J,3)="-9":O(1,2)=STR$(L)
1100 IFZ=99THENIN=IN-1
1110 IFJ=22THENPRINT"YOU HIT ONE OF'UM.

BUT THE REST KILLED YOU.":GOTO2290
1120 IFJ=9THENT="YOU HIT ONE.":RETURN
1130 GOTO120
1150 IFO(18,3)<>"1"THEN1890ELSEIFJ=4OR(J>5ANDJ<11)OR(J>14ANDJ<18)ORJ=22ORJ=25ORJ=29ORJ>30THEN1050ELSEO(J,3)="-7":O(J,2)=STR$(L)
1160 IFO(0,3)="-7"THENO(0,2)=STR$(L):FORJ=5TO21:IFO(J,2)="-1"THENO(J,3)="-7":NEXT:ELSENEXT
1170 GOTO1060
1190 IFJ<>12THEN1150ELSEIFO(J,3)<>"1"ORO(18,3)<>"1"THEN1150ELSECL=1:GOTO120
1210 IFJ=13THENPRINT"YECH !
ITS KEROSENE.
YOU JUST POISONED YOURSELF.":GOTO2290
1220 IFJ=9THENT="WHEEEEEE !
YOU GOT PLASTERED AND LOST A DAY.":IFCL=1THENLN=LN-10:RETURN:ELSERETURN
1230 GOTO1050
1250 IFJ<33THENB=VAL(O(J,2)):IFO(J,3)<>"2"ANDB<>LANDB<>-1THENIFJ<22THEN1890ELSET="IT'S NOT HERE.":RETURN
1260 IFJ>21ORJ=1THENT="? JUST WHO DO YOU THINK YOU ARE, PAUL BUNYON?
YOU'RE NOT STRONG ENOUGH.":RETURN
1270 IFO(J,3)="-7"THENT="ITS BURNT UP AND RUINED.":RETURN
1280 IFIN>4THENT="YOU CAN'T, YOUR HANDS ARE FULL.":RETURN:ELSEIFO(J,3)="1"THENPRINT"YOU ALREADY HAVE IT!":RETURN:ELSEIFVAL(O(J,1))<>0ORI=16THENIN=IN+1:IFO(J,2)="-1"THENIM=IM-1:O(J,2)="":O(J,3)="1"ELSEO(J,3)="1":O(J,2)=""
1290 IFJ<>9ANDJ<>11ANDJ<>2THEN120ELSEIFJ=9THENIFO(8,2)="-6"THENO(8,2)="6":ELSE:RETURN:ELSEIFJ=11THENIFO(12,2)="-8"THENO(12,2)="8"ELSE:RETURN:ELSEIFJ=2THENIFO(18,2)="-14"THENO(18,2)="14"
1300 RETURN
1320 IFO(J,3)<>"1"THENT="YOU DON'T HAVE IT !":RETURN:ELSET="OK":IN=IN-1:O(J,3)="":O(J,2)=STR$(L):RETURN
1340 IFO(5,3)<>"1"THEN1890
1345 IFN(4)="THEN1050
1350 IFL=7THENL=8
1360 IFL=12THENL=13
1370 GOTO120
1390 GOSUB120:ONJ+1GOTO1400,1430,1440,1460,1470,1480,1490,1460,1460,1500,1510,1520,1530,1540,1460,1460,1460,1460,1460,1560,1570,1580,1590,1600,1610,1620,1460,1630,1460,1460,1460,1640,1460
1400 IFVAL(O(0,3))<1THENT="THEY ARE AN OLD PAIR OF LEATHER SADDLEBAGS.
THEY APPEAR TO HAVE SOMETHING IN THEM,
MAYBE YOU SHOULD GET'UM.":RETURN
1410 PRINT"THEY CONTAIN ";:FORI=6TO22:IFO(I,2)="-1"THENPRINTO(I,0)". ";
1420 NEXT:T="":RETURN
1430 T="YOU SEE AN OLD, WEARY GREY MULE.":RETURN
1440 T="IT'S AN OLD TORN SACK.":IFO(18,2)="-14"THENO(18,2)="14"
1450 RETURN
1460 T="YOU CAN SEE NOTHING SPECIAL.":RETURN
1470 T="IT'S AN OLD WINCHESTER SINGLE-SHOT.":RETURN
1480 T="IT APPEARS TO BE A MAP TO THE LOST DUTCHMAN MINE,
THERE'S A DRAWING OF SOME BOULDERS.":RETURN
1490 T="NOTHING SPECIAL, JUST A LOADED SIX-SHOOTER.":RETURN
1500 IFO(8,2)="-6"THENT="THERE APPEARS TO BE SOMETHING BEHIND THE BOTTLES.":O(8,2)="6":RETURN:ELSE1460
1510 T="THERE ARE THREE KEYS,
TIED TOGETHER WITH A LEATHER STRAP.":RETURN
1520 IFO(12,2)="-8"THENT="IT LOOKS LIKE THERE'S GLASS UNDER 'UM.":O(12,2)="8":RETURN:ELSE1460
1530 T="ITS AN OLD KEROSENE LANTERN.  THE KEROSENE LEVEL
IS BESIDE A MARK THAT SAYS '"+STR$(LN)+"'TURNS LEFT.":RETURN
1540 IFI=11THENT="IT SAYS 'KEROSENE'.":RETURN
1550 T="THERE'S WRITING ON IT.":RETURN
1560 IFO(20,2)="-17"THENO(20,2)="17":T="ITS A MESSAGE !":RETURN:ELSE1460
1570 IFI=11THENT="IT SAYS :
BRING TREASURES TO SALOON,
AND SAY
SCORE.":RETURN:ELSE1550"
1580 IFI=11THENT="IT SAYS :
WATCH FOR OTHER RIDER FANTASY CREATIONS  ADVENTURES!":RETURN:ELSE1550
1590 T="THEY ARE A SAVAGE LOOKING BAND.
AND THEY'VE SEEN YOU.":RETURN
1600 T="ITS JUST AN OLD EMPTY ORE CART.":RETURN
1610 IFO(5,2)="-25"THENO(5,2)="1":T="THERE'S SOMETHIN' HERE !!":RETURN:ELSE1460
1620 IFI=11THENT="IT SAYS :
WELCOME TO
FRONTIERTOWN.":RETURN:ELSE1550
1630 IFO(10,2)="-6"THENO(10,2)="6":T="THERE'S A SET OF KEYS THERE!":RETURN:ELSE1460
1640 B=RND(3):ONBGOTO1650,1660,1670
1650 T="ITS A BARREL CACTUS.":RETURN
1660 T="ITS A CHOLLA CACTUS.":RETURN
1670 T="ITS A SAGUARO CACTUS.":RETURN
1690 IFJ=4ANDI=38THEN1780
1700 IFJ<>1ORVAL(O(1,3))<0T="SORRY, BUT THAT'S NOT POSSIBLE.":RETURN
1710 IFI=38THENIFO(1,3)=""THENT="TENDERFOOT! YOU HAVE'TA LEAD 'IM FIRST.":RETURN:ELSE1780
1720 IFO(1,3)="1"THEN1320ELSEIFO(7,3)="1"THENIFI<>16T="TRY LEAD.":RETURN
1730 ONI-13GOTO1740,1750,1760,1770
1740 T="YOU'RE TOO SLOW, HE GOT AWAY.":RETURN
1750 T="THE MULE GOT AWAY FROM YOU.":RETURN
1760 IFO(7,3)<>"1"THENT="TOUGH LUCK, YOU TRIPPED AND THE MULE RAN OFF.":O(1,3)="":O(1,2)=STR$(L):RETURN:ELSEIFO(1,3)=""THENGOTO1280ELSE1320
1770 T="THE MULE BUCKED YOU OFF.":RETURN
1780 INPUT"WITH WHAT";QM:IFJ=4THEN1800ELSEIFLEFT$(QM,3)<>"SAD"T="IT FELL OFF.
THE MULE GOT AWAY.":O(1,3)="":IN=IN-1:RETURN
1790 IFO(0,3)="1"THENO(0,3)="2":IN=IN-1:GOTO1060:ELSE1320
1800 IFLEFT$(QM,3)="BUL"THENIFO(8,3)<>"1"ORO(4,3)<>"1"THEN1320ELSE120ELSE1050
1820 IFO(J,3)<>"1"THEN1320ELSEINPUT"ON OR IN WHAT? (IE. ON TABLE)";QM:P=LEFT$(QM,2):IFP<>"IN"ANDP<>"ON"THENT="HUH?":RETURN
1830 M1=MID$(QM,4,3)
1840 IFP="ON"THENIF"MUL"=M1THEN1790:ELSEIFJ=13THENIFLM=0THENT="ITS EMPTY.":RETURN:ELSELM=0:GOTO1320:ELSE1320
1850 IF"SAD"=M1THENIFJ<5ORJ=19ORJ=22THENT="SORRY, IT WON'T FIT.":RETURN:ELSEIFIM>4THENT="THE "+O(0,0)+" ARE FULL.":RETURNELSEO(J,2)="-1":O(J,3)="":IN=IN-1:IM=IM+1:T="OK":RETURN
1860 IF"MUL"=M1THENT="YOU GOT KICKED !":RETURN
1870 IF"LAN"=M1THENIFO(13,3)="1"ANDO(12,3)="1"THENIFLM=50THENLN=LN+50:LM=0:GOTO120:ELSET="ITS EMPTY!":RETURN:ELSE1320
1880 GOTO1050
1890 T="YOU CAN'T DO THAT... YET!"
1900 PRINTT:T="":GOTO100
1920 PRINT"YOU HAVE WITH YOU : ";:IFIN=0ANDO(1,3)<>"1"THENT="NOTHING.":RETURN
1930 B=VAL(O(0,3)):X1=B:IFB=1THENPRINT:PRINTO(0,0)", WHICH CONTAIN; ";:FORI=1TO22:IFO(I,2)="-1"THENPRINTO(I,0)". ";:X1=2:NEXT:ELSENEXT:IFX1<>2THENPRINT"NOTHING!";
1940 IFO(1,3)="1"THENPRINT:PRINT"THE "O(1,0)", WHICH YOU'RE LEADING. ";:IFB=2THENPRINT"AND IS CARRYING THE SADDLEBAGS.";
1950 PRINT:X1=0:FORI=2TO22:IFVAL(O(I,3))<1THEN1970ELSEIFCL=1ANDI=12THENPRINT"LITE ";
1960 PRINTO(I,0)". ";
1970 NEXT
1980 T="":RETURN
2000 IFL=19THENCLS:PRINTCHR$(23)"
YOU HAVE FALLEN 100FEET.

YOU LANDED ON ROCKS.

YOU ARE DEAD.
":GOTO2290:ELSEIFJ=23ORJ=32ORM1="P"THENT="OK":RETURN:ELSE1050
2010 T="WHERE?":RETURN
2060 DATA,,,,,,,,IN A MINER'S SHACK,WINDOW. DOOR,,1,3,3,,,IN A DESERT,ROAD. MOUNTAINS. DESERT,WNES,2,4,2,2,2,ON DIRT PATH,MINER'S SHACK. ROAD. MOUNTAINS," W  ",5,1,4,2,2
2070 DATAON A DIRT ROAD,MOUNTAINS. PATH. DESERT. TOWN IN THE DISTANCE,NE S,2,7,3,2,5,IN A GHOST TOWN,SALOON," N  ",1,6,4,,,IN A SALOON,,W   ,1,5,,,
2080 DATAAT THE BASE OF THE SUPERSTITION MOUNTAINS,ROAD. DESERT,S N ,4,4,2,2,2,AT THE BASE OF WEAVER'S NEEDLE,,NS  ,1,9,7,,
2090 DATAIN A NARROW DEFILE,BUSHES. CAVES,"  NS",1,11,10,12,8,IN A SMALL CAVE,,E   ,1,9,,,,BEHIND A BUSH,BUSH,W   ,1,9,,,,IN A BOX CANYON,BUSHES. TREES. BOULDERS,"   S",4,12,12,12,9
2100 DATAIN FRONT OF A PARTIALLY HIDDEN MINE,MINE SHAFT,ESNW,8,14,12,12,12,IN THE ENTRANCE OF A DIMLY LITE MINE,MINE SHAFT,EW  ,1,15,13,,,IN THE MINE,DARK TUNNEL,EW  ,6,16,14,,
2110 DATAAT THE END OF THE TUNNEL,IRON DOOR," W  ",1,17,15,,,IN A LARGE CHAMBER,IRON DOOR,WNES,1,16,19,18,20,IN A MAZE OF TUNNELS,,NSEW,6,18,18,18,18,IN FRONT OF A PIT,DARK HOLE," S  ",7,23,17,,
2120 DATAAT A TUNNEL INTERSECTION,," SNW",6,,22,17,21,IN A DEAD END TUNNEL,,E   ,8,20,,,,IN A BLOCKED TUNNEL,,N   ,8,20,,,,AT THE BOTTOM OF A SHAFT,WALL,S   ,8,24,,,
2130 DATAIN A TUNNEL,,NS  ,6,23,25,,,AT THE END OF THE TUNNEL,LADDER," N  ",6,26,24,,,AT THE TOP OF LADDER,,,6,1,25,,
2150 PRINT"TRY EXAMING THINGS.":RETURN
2160 PRINT"ROADS USUALLY GO PLACES.":RETURN
2170 PRINT"MAYBE THE TRAIL GOES SOMEWHERE.":RETURN
2180 IFO(5,3)<>"1"THENPRINT"DO YOU HAVE THE MAP?":RETURN:ELSEPRINT"TRY 'FOLLOW'.":RETURN
2190 IFVAL(O(1,2))=LTHENPRINT"MAYBE THE MULE IS HUNGRY.":RETURN:ELSEGOTO2150
2200 PRINT"KEEP GOING.":RETURN
2210 PRINT"ITS SLIPPERY, BUT YOU MIGHT BE ABLE TO CLIMB DOWN.":RETURN
2220 PRINT"THIS IS A MINE, YOU KNOW.":RETURN
2240 DATASADDLEBAGS,2,1,MULE,,3,BURLAP SACK,4,14,SHOVEL,4,1,RIFLE,4,1,MAP,1,-25,GUN,1,-1,CARROTS,1,-1,BOX OF RIFLE BULLETS,1,-6,WHISKEY BOTTLES,2,6,KEYS,1,-6,PILE OF BONES (MINE),2,8
2250 DATALANTERN,2,-8,JAR OF LIQUID,1,-11,* SPANISH COINS *,1,-10,* TOURQUOISE *,1,-23,# SILVER #,1,-21,# GOLD #,1,-22,MATCHES,1,-14,CRATES,3,17,NOTE,1,-17,PAPER,1,6,INDIANS,0,11
2260 DATAORE CART,,14,BED,,1,SIGN,,5,BROKEN GLASS,,6,TABLES,,6,CHAIRS,,6,ROCKS,,10,WOODEN RAILS,,15,CACTUS,,2,TRAP DOOR,,26
2270 REM *** COMMAND TABLE ***
2280 DATAGO ,ENT,RUN,QUI,DIG,CLI,INV,JUM,DRO,GET,EXA,REA,LOO,MOV,CAT,CHA,LEA,RID,PUT,PLA,PIC,PUS,PUL,SHO,OPE,CLO,GIV,BUR,LIG,DRI,BRE,HIT,UNL,LOC,SEA,LIS,SAY,FEE,LOA,UNT,FOL,POU,EAT,NOR,SOU,EAS,WES
2290 PRINT@960,"DO YOU WANT TO PLAY ANOTHER GAME";:INPUTQM:IFLEFT$(QM,1)="Y"THENRUNELSECLS:END
2300 IFJ=7THENIFO(8,3)="1"THENT="THEY TASTE PRETTY GOOD.":RETURN
2310 T="CAN'T DO THAT...
WIERDO !":RETURN
2320 RESUME160
2330 PRINTCHR$(23)"### RIDER FANTASY CREATIONS ###":PRINTTAB(10)"ADVENTURE # 1":PRINTTAB(4)"THE LOST DUTCHMAN'S GOLD.

WORLD COPYRIGHT 1979,
  BY TERI LI,
ALL RIGHTS RESERVED.
	":RETURN
2340 PRINT"      WELCOME TO THE SEARCH FOR THE LOST DUTCHMAN'S GOLD.
I AM THE GHOST OF BACKPACK SAM  -  YOUR GUIDE ON THIS ADVENTURE.YOU MAY USE TWO WORD COMMANDS TO FIND CLUES AND EXECUTE MOVEMENT
SUCH WORDS AS GET, GO, PUT, DROP, SCORE, INVENTORY ";
2350 PRINT"AND ABOUT A
HUNDRED MORE ARE AVAILABLE TO YOU.  HELP  WILL GIVE YOU HINTS
WHEN YOU ARE STUCK.  GOOD LUCK  AND I HOPE YOU DON'T END UP A
GHOST LIKE ME.":PRINT
2360 INPUT"WHAT IS YOUR NAME";NA:IFNA=""THEN2360ELSEIFLEN(NA)>8THENNA=LEFT$(NA,8)
2365 NF=NA+"/DUT"
2370 INPUT"DO YOU WANT TO RESUME A SAVED GAME";NB:IFNB=""THEN2370ELSEIFLEFT$(NB,1)="Y"THENGOTO2500
2390 CLS:RETURN
2400 PRINT"SAVING GAME...":OPEN"O",1,NF
2430 FORY=0TO7:PRINT#1,N(Y):NEXT
2440 FORY=0TO32:FORV=0TO3:PRINT#1,O(Y,V):NEXTV,Y
2450 FORV=0TO46:PRINT#1,R(V):NEXT
2460 PRINT#1,B:PRINT#1,IN:PRINT#1,LN:PRINT#1,LM
2470 PRINT#1,L:PRINT#1,CL:PRINT#1,J:PRINT#1,Z
2490 PRINT#1,U:PRINT#1,U1:PRINT#1,IM:CLOSE:GOTO1900
2500 OPEN"I",1,NF:FORY=0TO7:INPUT#1,N(Y):NEXT
2540 FORY=0TO32:FORV=0TO3:INPUT#1,O(Y,V):NEXTV,Y
2550 FORV=0TO46:INPUT#1,R(V):NEXT
2560 INPUT#1,B,IN,LN,LM,L,CL,J,Z,U,U1,IM
2570 CLOSE:GS=1:RETURN

