5 CLS:PRINT@232,"PHAROAH'S CURSE":GOSUB50:CLS
20 CLEAR 400
25 GOTO100
50 FORX=1TO1000:A$=INKEY$:IFA$=""THENNEXT
55 RETURN
100 '**************************
110 '*  PHAROAH'S  CURSE      *
120 '**************************
130 BG=3:BX=3:FL=3:KY=3:T=3:W=3:DG=3:GR=3
150 GOSUB190:GOTO1000
189 !!! entry corridor !!!
190 D=10:D$="NYNY":GOSUB9000:PRINT@320,"YOU ARE IN THE ENTRY CORRIDOR.":P=1:RETURN
199 [[[ antechamber ]]]
200 D=18:D$="NYYY":GOSUB9000:PRINT@320,"YOU ARE NOW IN THE ANTECHAMBER";:PRINT@384,"THE ROOM APPEARS TO HAVE BEEN   RANSACKED.";:P=2:RETURN
209 %%% annex %%%
210 P=3:D=18:D$="YYNN":GOSUB9000:PRINT@320,"YOU ARE IN THE ANNEX.":IFBX=3THENPRINT@384,"THERE IS A BOX AT 'B'.":PRINT@135,"b";:RETURN:ELSERETURN
219 ))) prep. room (((
220 P=4:D=18:D$="NNNY":GOSUB9000:PRINT@384,"YOU ARE IN THE ROOM OF PREPAR-  ATION FOR THE AFTERLIFE. IT ALSOAPPEARS TO HAVE BEEN RANSACKED.";:IFFL=3THENPRINT@320,"THERE IS A FLUTE AT 'F'.":PRINT@180,"f";:RETURN:ELSERETURN
229 ::: devot. to gods :::
230 P=5:D=18:D$="YNNY":GOSUB9000:PRINT@384,"YOU ARE IN THE ROOM OF DEVOTION TO THE GODS.";:RETURN
239 &&& boat rm. &&&
240 P=6:D=18:D$="YYYN":GOSUB9000:PRINT@320,"THERE IS A LARGE BOAT IN THIS RM";:PRINT@384,"THE HIEROGLYPHICS READ -- 'FOR  THE PASSAGE TO THE OTHERWORLD.  --FROM THE PHAROAH'S DEVOTED     SERVANT, RAN-SECH.";
245 PRINT@47,CHR$(193);:PRINT@77,STRING$(2,195)CHR$(199)STRING$(2,195);:PRINT@111,CHR$(197);:PRINT@135,CHR$(197)CHR$(203)CHR$(195)STRING$(5,128)CHR$(197)STRING$(5,128)CHR$(195)CHR$(199)CHR$(202);:PRINT@168,CHR$(205)STRING$(3,207)STRING$(3,195);
247 PRINTCHR$(199)STRING$(3,195)STRING$(3,207)CHR$(206);:PRINT@201,CHR$(196)CHR$(205)STRING$(9,207)CHR$(206)CHR$(200);:PRINT@236,STRING$(7,204);:RETURN
249 ### minor treasury ###
250 P=7:D=14:D$="NNNY":GOSUB9000:PRINT@288,"YOU ARE IN THE MINOR TREASURY.  IT HAS BEEN RANSACKED.";:IFKY=3THENPRINT@384,"THERE IS A KEY AT 'K'.":PRINT@140,"k";:RETURN:ELSERETURN
259 -=: pyramid room :=-
260 P=8:D=18:D$="NYYN":GOSUB9000:PRINT@320,"YOU ARE IN THE PYRAMID ROOM.":PRINT@384,"A LARGE PYRAMID FILLS THE ROOM.";
265 FORX=1TO7:PRINT@X*31+16,STRING$(X*2,207);:NEXT:RETURN
269 @@@ passage of life @@@
270 P=9:D=18:D$="NYNY":GOSUB9000:PRINT@320,"YOU ARE IN THE PASSAGE OF LIFE.":PRINT@384,"HIEROGLYPHS ON THE WALLS TELL OFTHE LIFE OF THE PHAROAH, KEMEN- RATUR.":RETURN
279 ___ burial chamber ___
280 P=10:D=18:D$="NNYN":GOSUB9000:PRINT@320,"YOU ARE IN THE BURIAL CHAMBER.":PRINT@384,"A LARGE SARCOPHAGUS FILLS THE   ROOM.  HIEROGLYPHS WARN OF SURE AND SUDDEN DEATH FOR THOSE WHO  OPEN IT.";:RETURN
289 >>> dead rm <<<
290 P=11:D=18:D$="YYYY":GOSUB9000:PRINT@320,"THIS ROOM HAS TWO SKELETONS.":PRINT@384,"THE SKELETONS ARE AT THE PLACES MARKED 'S'.";:PRINT@130,"s";:PRINT@156,"s";:RETURN
299 *** major treasury ***
300 P=12:D=18:D$="YNNN":GOSUB9000:PRINT@320,"YOU'RE IN THE MAJOR TREASURY.":IFDG=3THENPRINT@384,"YOU ARE ATTACKED BY TWO HUGE    DOGS !!!":PRINT@142,"d";:PRINT@145,"d";
305 IFT=3ANDDG=1THENPRINT@210,"t";:PRINT@384,"YOU CAN SEE THE GOLD TABLETS AT 'T'."STRING$(60,32);:RETURN:ELSERETURN
309 """ snake room """
310 C=3:P=13:D=18:D$="NYNY":GOSUB9000:PRINT@320,"YOU'RE IN THE SNAKE ROOM.":PRINT@384,"THERE IS A COBRA AT 'C'."
311 FORX1=1TO100:Y=RND(8)*32:X=RND(28)+1:PRINT@X+Y,"c";:NEXT:RETURN
319 $$$ shrine $$$
320 P=14:D=18:D$="YYYY":GOSUB9000:PRINT@320,"YOU ARE IN THE SHRINE.":PRINT@384,"THERE IS A STATUE AT 'S'.";:PRINT@175,"s";:RETURN
329 /// gate to eternal life //
330 P=15:D=14:D$="NYNY":GOSUB9000:PRINT@288,"YOU ARE IN THE ROOM OF THE GATE TO ETERNAL LIFE.":PRINT@384,"AN INSCRIPTION ON THE EAST DOOR READS-ONLY ONE WITHOUT MALICE ORGREED MAY PASS INTO THE AFTER-  LIFE.";:RETURN
339 @@@@  end corridor @@@@
340 P=16:D=10:D$="NYNY":GOSUB9000:PRINT@288,"YOU ARE IN THE EXIT CORRIDOR.";:RETURN
349 #=#=# watery grave #=#=#
350 P=17:FORX=0TO14:PRINT@X*32,STRING$(32,175):FORY=1TO10:NEXTY,X:CLS3:PRINT@230,"YOU WERE DROWNED !!";:GOSUB50:PRINT@480,;:CLS:END
359 +-+-+ dust bowl +-+-+
360 P=18:CLS:FORX=0TO14:PRINT@X*32,STRING$(32,207):FORY=1TO10:NEXTY,X:CLS5:PRINT@224,"YOU SUFFOCATE IN A CHOKING CLOUDOF DUST !!";:SCREEN0,1:GOSUB50:PRINT@480,;:CLS:END
369 *:*:* exit sequence *:*:*
370 P=19:IFBX=1ORBG=1ORFL=1ORKY=1ORW=1THENPRINT@320,STRING$(191,32);:PRINT@320,"A CAVE-IN ...":PRINT:PRINT"SORRY.":END
375 CLS:IFT=1THENPRINT@224,"CONGRATULATIONS !":PRINT"YOU'VE RETRIEVED THE GOLD":PRINT"TABLETS OF THE PHAROAH":PRINT"KEMEN-RATUR":PRINT:PRINT"THIS MAKES YOU A FIRST CLASS    ADVENTURER !!":PRINT:END
380 PRINT@224,"CONGRATULATIONS !!":PRINT:PRINT"YOU'VE ESCAPED THE TOMB OF THE  PHAROAH AND ELUDED THE CURSE.":PRINT:PRINT"HOWEVER, YOU DIDN'T RETRIEVE    THE GOLD TABLETS.":PRINT:PRINT"THIS QUALIFIES YOU AS A NOVICE  ADVENTURER.":PRINT:END
1000 PRINT@352,STRING$(32,32);:PRINT@352,"COMMAND - ";:LINE INPUT CD$
1002 IFP=12ANDDG=3THEN1110
1004 IFP=13ANDC=3THENIFCD$<>"W"THEN1150
1010 IFCD$="INVENT"THEN2000
1012 IFINSTR("NSEW",CD$)<>0THENA$=CD$:GOTO2520
1020 IFCD$="LEAVE"THEN2500
1030 IFLEFT$(CD$,3)="GET"THEN3000
1040 IFLEFT$(CD$,4)="OPEN"THEN3500
1050 IFLEFT$(CD$,6)="SEARCH"THEN4000
1060 IFLEFT$(CD$,4)="DROP"THEN4500
1100 '*** SPECIFIC VERBS
1110 IFCD$="THROW BOOMERANG" ANDP=12ANDDG=3ANDBG=1THENPRINT@384,"THE DOGS TURN INTO STATUES AS   THE BOOMERANG DISAPPEARS IN A   FLASH OF LIGHT!!":DG=1:BG=2:GOSUB50:GOSUB305:GOTO1000
1120 IFCD$="THROW BOOMERANG"ANDBG=1THENCLS:PRINT@320,"THE BOOMERANG FLIES AWAY FROM   YOU, THEN RETURNS AND CHOPS OFF YOUR HEAD!!":END
1130 IFP=12ANDDG=3THENPRINT@384,"THE DOGS RUSH UP AND BREAK YOUR NECK !!!":END
1140 IFCD$="DRINK WINE"THENIFW=1THENPRINT@384,"THE WINE IS DRY AND A POWDER    AFTER 4000 YEARS !!"STRING$(70,32);:W=2:GOTO1000:ELSEPRINT@384,"GOOD IDEA! I COULD USE SOME.    BUT, UNFORTUNATELY, WE DON'T    HAVE ANY."STRING$(30,32);:GOTO1000
1150 IFCD$="PLAY FLUTE"ANDP=13ANDC=3THENPRINT@384,"THE COBRAS SEPARATE AND CLEAR A PATH FOR YOU TO WALK THROUGH THEROOM."STRING$(50,32);:C=1:GOSUB10000:GOTO1000
1160 IFP=13ANDC=3THENPRINT@384,"YOU DIE A HORRIBLE DEATH FROM   THE COBRA'S BITES !":END
1170 IFCD$="PLAY FLUTE"THENPRINT@384,"AAH, MUSIC TO THE EARS!"STRING$(102,32);:GOTO1000
1180 IFCD$="READ BOOMERANG"THENPRINT@384,STRING$(127,32);:PRINT@384,"IT SAYS - FOR PROTECTION AGAINSTEVIL SPIRITS IN THE PASSAGE TO  THE AFTERLIFE.";:GOTO1000
1200 IFCD$="SHOW AUTHOR"THENCLS:PRINT@228,"CONCIEVED AND WRITTEN BY:":PRINT:PRINTTAB(10)"TIM KOONCE":GOSUB50:NP=P:GOTO2550
1210 IFCD$="LOOK"THENNP=P:GOTO2550
1990 PRINT@384,STRING$(127,32);:PRINT @384,"I DON'T KNOW HOW TO "CD$".":GOTO1000
1999 ***INVENTORY***
2000 PRINT@384,STRING$(127,32);:PRINT@384,;
2010 IFBG=1THENPRINT"BOOMERANG  ";
2020 IFBX=1THENIFKY<>1THENPRINT"LOCKED BOX  ";:ELSEPRINT"BOX  ";
2030 IFFL=1THENPRINT"FLUTE    ";
2040 IFKY=1THENPRINT"KEY    ";
2050 IFT=1THENPRINT"GOLD TABLETS  ";
2060 IFW=1THENPRINT"WINE BOTTLE  ";
2065 IFGR=1THENPRINT"GOLD RING";
2070 PRINT:GOTO1000
2499 ***LEAVE***
2500 PRINT@352,STRING$(31,32);:PRINT@352,"WHICH DIRECTION (N,S,E,W) ?";
2510 A$=INKEY$:IFA$=""THEN2510
2520 DN=INSTR("NESW",A$):IFDN=0THEN1000
2530 AP=P*8+DN*2-9
2540 NP=VAL(MID$("00020000000503010204000000000003060000020807050000000006000906000011000800001100101312091100000000140011171518130016001400190015",AP,2))
2545 IFNP=0THEN1000
2550 P=NP:ONP GOSUB190,200,210,220,230,240,250,260,270,280,290,300,310,320,330,340,350,360,370
2560 GOTO1000
2999 ***GET***
3000 '** ASSUME THAT "GET" FOLLOWED BY ONE SPACE
3010 Q$=MID$(CD$,5):IFQ$="BOX"ANDP=3ANDBX=3THENBX=1:GOTO3300
3020 IFQ$="FLUTE"ANDP=4ANDFL=3THENFL=1:GOTO3300
3030 IFQ$="TABLETS"ANDP=12ANDT=3THENT=1:GOTO3300
3040 IFQ$="KEY"ANDP=7ANDKY=3THENKY=1:GOTO3300
3050 IFQ$="RING"ANDP=6ANDGR=3THENGR=1:GOTO3300
3200 PRINT@384,STRING$(127,32);:PRINT@384,"I CAN'T GET "Q$".";:GOTO1000
3300 PRINT@384,STRING$(127,32);:PRINT@384,"OK, YOU'VE GOT THE "Q$".";:GOTO1000
3499 ***OPEN***
3500 Q$=MID$(CD$,6)
3510 IF Q$="BOX"THENPRINT@384,STRING$(127,32);:IFKY=3THENPRINT@384,"THE BOX IS LOCKED."STRING$(2,13):GOTO1000:ELSEIFKY=1THENPRINT@384,"THE BOX HOLDS A BOTTLE OF WINE  AND A CEREMONIAL BOOMERANG WITH INSCRIPTIONS ON THE SIDES.";:BG=1:W=1:GOTO1000
3520 IFLEFT$(Q$,5)="SARCO"ANDP=10THENIFOS=0THENPRINT@384,STRING$(127,32);:PRINT@384,"I WOULDN'T IF I WERE YOU.":OS=1:GOTO1000:ELSEPRINT@384,STRING$(127,32);:PRINT@384,"OKAY, YOU'RE DEAD.":END
3530 IFQ$="DOOR"THENPRINT@384,"OK."STRING$(188,32);:GOTO1000
3540 PRINT@384,STRING$(127,32);:PRINT@384,"I CAN'T OPEN "Q$".";:GOTO1000
3999 ***SEARCH***
4000 Q$=MID$(CD$,8)
4010 IFQ$="STATUE"ANDP=14THENPRINT@384,STRING$(127,32);:PRINT@384,"THE BASE READS-I AM THE SUN GOD.ONLY THROUGH ME CAN ANYONE FIND THE AFTERLIFE.";:GOTO1000
4020 IFLEFT$(Q$,8)="SKELETON"ANDP=11THENPRINT@384,STRING$(127,32);:PRINT@384,"THEY APPEAR TO BE THE SKELETONS OF 18TH CENTURY POACHERS. THEY  DIED OF BROKEN NECKS.";:GOTO1000
4030 IFQ$="BOX"ANDBX=1THENPRINT@384,STRING$(127,32);:PRINT@384,"THE BOX IS LOCKED.":GOTO1000
4040 IFQ$="PYRAMID"THENPRINT@384,STRING$(127,32);:PRINT@384,"A PLAQUE READS:   CONCEIVED AND WRITTEN BY:  TIM KOONCE.";:GOTO1000
4050 IFQ$="BOAT"ANDP=6ANDGR=3THENPRINT@384,STRING$(127,32);:PRINT@384,"THERE IS A GOLD RING HERE.":GOTO1000
4100 PRINT@384,STRING$(127,32);:PRINT@384,"THERE IS NOTHING UNUSUAL ABOUT  THE "Q$".";:GOTO1000
4499 ***DROP***
4500 Q$=MID$(CD$,6)
4510 IFQ$="BOOMERANG"ANDBG=1THENBG=3:GOTO4700
4520 IFQ$="BOX"ANDBX=1THENBX=3:GOTO4700
4530 IFQ$="FLUTE"ANDFL=1THENFL=3:GOTO4700
4540 IFQ$="KEY"ANDKY=1THENKY=3:GOTO4700
4550 IFQ$="TABLETS"ANDT=1THENT=3:GOTO4700
4560 IFQ$="WINE"ANDW=1THENW=3:GOTO4700
4570 IFQ$="RING"ANDGR=1THENGR=3:GOTO4700
4600 PRINT@384,STRING$(127,32);:PRINT@384,"YOU DON'T HAVE A "Q$".";:GOTO1000
4700 PRINT@384,STRING$(127,32);:PRINT@384,"YOU'VE DROPPED THE "Q$".";:GOTO1000
9000 CLS:PRINT@0,CHR$(207)STRING$(30,204)CHR$(207);:FORX=1TOD/2-1:PRINTCHR$(207)STRING$(30,128)CHR$(207);:NEXT:PRINTCHR$(207)STRING$(30,195)CHR$(207);
9010 FORX=1TO4:IFMID$(D$,X,1)="Y"THENONX GOSUB9030,9040,9050,9060
9020 NEXT:RETURN
9030 PRINT@12,STRING$(8,207);:RETURN
9040 FORX1=D*8-18TOD*8+78STEP32:PRINT@X1,CHR$(207);:NEXT:RETURN
9050 PRINT@D*16+12,STRING$(8,207);:RETURN
9060 FORX1=D*8-47TOD*8+49STEP32:PRINT@X1,CHR$(207);:NEXT:RETURN
10000 '**parting of cobras
10010 FORX=1154TO1181:A=PEEK(X):B=PEEK(X+32):POKEX,128:POKEX+32,128
10020 IFPEEK(X-32)=128THENPOKEX-32,A
10030 IFPEEK(X+64)=128THENPOKEX+64,B
10040 NEXT:RETURN
