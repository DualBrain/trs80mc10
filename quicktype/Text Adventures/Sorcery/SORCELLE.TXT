5 POKE12314,0:EXEC54316
10 CLS
20 PRINT:PRINT"           ***************" 
30 PRINT"           *             *"
40 PRINT"           * SORCELLERIE *"
50 PRINT"           *             *"
60 PRINT"           ***************" 
70 PRINT:PRINT:PRINT"JUSQU'EN 2036, LE MONDE VIVAIT EN PAIX, PUIS DES TROUBLES COMMENCERENT A REMUER LA SOCIETE ."
90 PRINT"22 JUIN 2037 :REAPPARITION DE SORCIERS AUX POUVOIRS TERRIFIANTS . PEU NOMBREUX  ILS ETAIENT FACILEMENT MAITRISES ."
100 PRINT"NOEL 2037 : COUP DE FORCE DES SORCIERS .TOUTES LES ARMEES SONT ANEANTIES ."
105 PRINT"LA DICTATURE DES SORCIERS EST PROCLAMEE."
110 PRINT"FEVRIER 2038 :LES SORCIERS PEUVENT A PRESENT BRISER LA VOLONTE DES RESISTANTS ."
120 PRINT"CONSCIENTS DU DANGER, DES DIZAINES DE PERSONNES ONT ESSAYE DE COMBATTRE CE      POUVOIR MALEFIQUE, MAIS EN VAIN ."
140 PRINT@1600,"APPUYEZ SUR UNE TOUCHE ";:INPUT QM:CLS
150 PRINT"APRES 6 ANS DE RECHERCHES, VOUS TROUVEZ ENFIN UN INDICE : L'ARME ABSOLUE SE     TROUVE DANS UN CHATEAU ."
160 PRINT"QUELLE EST DONC CETTE ARME ABSOLUE ?    A VOUS DE LE DECOUVRIR !"
170 GOTO 200
190 RETURN
200 PRINT"VOICI QUELQUES ELEMENTS POUR VOUS AIDER DANS VOTRE QUETE :"
210 PRINT
220 PRINT"     N POUR ALLER AU NORD ."
230 PRINT"     S POUR ALLER AU SUD ."
240 PRINT"     O POUR ALLER A L'OUEST ."
250 PRINT"     E POUR ALLER A L'EST ."
260 PRINT"   INV POUR L'INVENTAIRE ."
270 PRINT"   QUI POUR QUITTER ."
280 PRINT:PRINT"POUR LE RESTE, A VOUS DE JOUER !        PRESSEZ ENTER";:INPUTQM
290 DIM LI$(24,7),OB$(30,2),VE$(32)
300 FOR I=1 TO 24:FOR J=1 TO 7
310 READ LI$(I,J):PRINTLI$(I,J);:NEXTJ:PRINT:NEXTI
320 DATA DEVANT LA PORTE DU CHATEAU,,aEaa,,2,,
330 DATA DANS L'ENTREE DU CHATEAU,,aESO,,3,7,1
340 DATA DANS LE GRAND SALON,MEUBLES DE LUXE.,aESO,,4,8,2
350 DATA DANS UN COULOIR MAGNIFIQUE,,NESO,6,5,9,3
360 DATA DANS LA SALLE DE BAIN,EVIER.,aaaO,,,,4
370 DATA DANS UNE CHAMBRE SOMPTUEUSE,LIT.,aaSa,,,4,
380 DATA DANS LE PETIT SALON,,NESa,2,8,11,
390 DATA DANS LA CUISINE,MEUBLES.,NaSO,3,,13,7
400 DATA DANS LA CHAMBRE D'AMIS,LIT.PORTE.,aaaa,,,,
410 DATA DANS UN ATELIER,,aaaO,,,,24
420 DATA DANS UN COULOIR,,NEaa,7,12,,
430 DATA DANS UN LONG COULOIR DE PIERRE,,aEaO,,13,,11
440 DATA DANS UN COULOIR,,NaaO,8,,,12
450 DATA DANS UNE PIECE SECRETE,,NEaa,12,15,,
460 DATA DANS UNE SALLE DE TORTURE !,INSTRUMENTS DE TORTURE.,aaaO,,,,14
470 DATA AU GRENIER,,aEaa,,17,,
480 DATA DANS UNE GRANDE PIECE ILLUMINEE PAR DES BOUGIES,,aEaO,,18,,16
490 DATA DANS UNE PETITE PIECE SOMBRE,,aaaO,,,,17
500 DATA SUR UNE CORNICHE,LE VIDE.,aaSa,,,20,
510 DATA DANS UNE PIECE AU SOUS-SOL,,NESa,19,21,22,
520 DATA DANS UN ASCENSEUR,,aaaa,,,,
530 DATA DANS UNE SALLE OU TRAINENT DES SQUELETTES,,NaSa,20,,23,
540 DATA DANS UNE SALLE ABANDONNEE DEPUIS LONGTEMPS,,Naaa,22,,,
550 DATA DANS UNE GROTTE,,aEaO,,10,,9
560 FOR I=1 TO 30:FOR J=1 TO 2:READ OB$(I,J):NEXT J:NEXT I
570 DATA PILE,-6,AMPOULE,3,LAMPE,-3,GANTS,5,GATEAU,8,TABLEAU,9
580 DATA SCIE,10,FIOLE,10,CORDE,15,PARCHEMIN,17,ECHELLE,23,DRAPS,6,PLAFOND,0
590 DATA GROTTE,-9,FENETRE,14,BARREAU,-14,STATUE,16,FENTE,18,CHANDELIERS,4
600 DATA NORD,0,SUD,0,EST,0,OUEST,0,BOUTON,-12,LIT,0,ANNEAU,14
610 DATA CHIENS,22,MUR,0,PORTE,0,PASSAGE,-12
620 FORI=1TO32:READVE$(I):NEXTI
630 DATA "VA",ENT,MON,DES,REG,EXA,OBS,PRE,SAI,SAI,DEV,LAC,POS,LAN
640 DATA JET,OUV,FER,SCI,EMB,POU,LIS,MAN,DON,ATT,ALL,INS,APP,VIS,DET,ETE,VA,BOI
650 PI=0:LA=0:GA=0:AL=0:PA=0:CO=0:BA=0:FE=0:CR=0:DR=0:EM=0:NO=0
660 L=1:FI=0:LE=0
670 CLS
680 IFL=24ANDLA=0THEN3770
690 IFL=17ANDEM=0THEN3780
700 IFL=23ANDCH=0THEN3790
710 PRINT"VOUS ETES ";LI$(L,1):PRINT
720 Q1=1
730 PRINT"VOUS VOYEZ : ";LI$(L,2);:IFLI$(L,2)=""THENQ1=0
740 IF L=9 AND AC=0 THEN PRINT" VOUS ENFERMANT DANS LA PIECE.";:GOTO760
750 IFL=9ANDAC<>0THENPRINT" RONGEE PAR L'ACIDE.";
760 Q2=0
770 FORI=1TO30:IFVAL(OB$(I,2))<>LTHEN900
780 PRINTOB$(I,1);".";:Q2=1
790 IFI=2ANDAL=0THENPRINT" ALLUMEE.";:GOTO900
800 IFI=3ANDLA=0THENPRINT" ETEINTE.";:GOTO900
810 IFI=3ANDLA<>0THENPRINT" ALLUMEE.";:GOTO900
820 IFI=8ANDFI=0THENPRINT" PLEINE.";:GOTO900
830 IFI=8ANDFI<>0THENPRINT" CASSEE.";:GOTO900
840 IFI=15ANDFE=0THENPRINT" FERMEE.";:GOTO900
850 IFI=15ANDFE<>0THENPRINT" OUVERTE.";:GOTO900
860 IFI=16ANDBA<>0THENPRINT" SCIE.";:GOTO900
870 IFI=18THENPRINT" DANS LE MUR.";:GOTO900
880 IFI=27ANDCH=0THENPRINT" AFFAMES.";:GOTO900
890 IFI=27ANDCH<>0THENPRINT" MORTS.";
900 NEXTI:IFQ1=0ANDQ2=0THENPRINT"RIEN DE SPECIAL.":PRINT:GOTO920
910 PRINT
920 PRINT"DIRECTIONS POSSIBLES: ";
930 FORI=1TO4:DI$=MID$(LI$(L,3),I,1)
940 IFDI$="N"THENPRINT"NORD.";
950 IFDI$="S"THENPRINT"SUD.";
960 IFDI$="E"THENPRINT"EST.";
970 IFDI$="O"THENPRINT"OUEST.";
980 NEXTI:PRINT
990 PRINT
1000 PRINT"----------------------------------------"
1020 IFL=21THEN3800
1030 PRINT@880,;
1040 PRINT"QUE FAITES-VOUS ?";:GOSUB 1200
1050 IF RIGHT$(R$,1)=" "THENR$=LEFT$(R$,LEN(R$)-1):GOTO1050
1060 IF R$=""THEN1040
1070 IF R$="O"ORR$="E"ORR$="N"ORR$="S"THENM$=R$:GOTO1490
1080 IFLEFT$(R$,3)="QUI" THEN CLS:END
1090 IF LEFT$(R$,3)="INV"THEN1290
1100 X=0:FORI=LEN(R$)TO1STEP-1
1110 IF MID$(R$,I,1)=" "ORMID$(R$,I,1)="'"THENX=I:I=1
1120 NEXTI:IFX=0THEN1190
1130 V$=LEFT$(R$,X-1):M$=MID$(R$,X+1,4)
1140 V=0:FORI=1TO32:IFLEFT$(V$,3)=VE$(I)THENV=I:I=32
1150 NEXTI
1160 M=0:FORI=1TO30:IFM$=LEFT$(OB$(I,1),4)THEN:M=I:I=30
1170 NEXT I
1180 IFV=0THENGOTO1190
1181 IFV>23GOTO1184
1182 ONVGOTO1380,1380,1530,1640,1820,1820,1820,2290,2290,2290,2460,2530,2530,2560,2560,2650,2760,2850,2910,2940,3050,3090,3140 
1184 ON(V-23)GOTO3220,3310,3350,2940,3440,3500,3580,1380,3620
1190 PRINT:PRINT"ESSAYEZ AUTRE CHOSE !":GOTO1040
1200 R$=" "
1210 A$=INKEY$:IFA$=""THEN1210
1220 IFASC(A$)<>13THEN1250
1230 IF LEN(R$)>1THENR$=RIGHT$(R$,LEN(R$)-1):RETURN
1240 R$="":RETURN
1250 IF ASC(A$)=8ANDLEN(R$)>1THENR$=LEFT$(R$,LEN(R$)-1):PRINTA$;" ";A$;:GOTO1210
1260 IFA$=" "ORA$="'"THEN1280
1270 IF ASC(A$)<65ORASC(A$)>91 THEN1210
1280 PRINTA$;:R$=R$+A$:GOTO1210
1290 IFNO=0THENPRINT:PRINT"VOUS NE POSSEDEZ RIEN.":GOTO1040
1300 PRINT:PRINT"VOUS PORTEZ :":PRINT:FORI=1TO12:IFOB$(I,2)<>"100"THEN1370
1310 PRINT OB$(I,1);".";
1320 IFI=3ANDLA=0THENPRINT" ETEINTE.";:GOTO1370
1330 IFI=3ANDLA<>0THENPRINT" ALLUMEE.";:GOTO1370
1340 IFI<>8THEN1370
1350 IFFI=0THENPRINT" PLEINE.";:GOTO1370
1360 PRINT" CASSEE.";
1370 NEXTI:PRINT:GOTO1040
1380 IFM<>30THEN1410
1390 IFL<>12ORVAL(OB$(30,2))<0THENPRINT:PRINT"OU VOYEZ-VOUS UN ";OB$(30,1);" ? ":GOTO1040
1400 L=14:GOTO670
1410 IFM<>14THEN1440
1420 IFL<>9ORVAL(OB$(14,2))<0THENPRINT:PRINT"OU VOYEZ-VOUS UNE ";OB$(14,1);" ? ":GOTO1040
1430 L=24:GOTO670
1440 IFM<>29THEN1480
1450 IFL<>9THENPRINT:PRINT"DONNEZ UNE DIRECTION SVP.":GOTO1040
1460 IFAC=0THENPRINT:PRINT"IMPOSSIBLE ! ELLE EST FERMEE.":GOTO1040
1470 L=4:GOTO670
1480 IFM<20ORM>23THEN1520
1490 FORI=1TO4
1500 IFLEFT$(M$,1)=MID$(LI$(L,3),I,1)THENL=VAL(LI$(L,I+3)):GOTO670
1510 NEXTI
1520 PRINT:PRINT"VOUS NE POUVEZ PAS ALLER PAR LA !":GOTO1040
1530 IF L<>6ANDL<>19THENPRINT:PRINT"VOUS NE POUVEZ PAS MONTER ICI !":GOTO1040
1540 IF L<>6 THEN 1590
1550 IFM<>11THENPRINT:PRINT"VOUS DEVEZ AVOIR TROP BU !":GOTO1040
1560 IF OB$(11,2)="100"THENPRINT:PRINT"POSEZ D'ABORD VOTRE ";OB$(11,1):GOTO1040
1570 IF VAL(OB$(11,2))<>LTHENPRINT:PRINT"QUELLE ";OB$(11,1);" ?":GOTO1040
1580 L=16:GOTO670
1590 IFM=12THENL=14:GOTO670
1600 IFM<>11 THENPRINT:PRINT"VOUS DEVRIEZ VOUS REPOSEZ UN PEU.":GOTO1040
1610 IFOB$(11,2)="100"THENPRINT:PRINT"ET SI VOUS POSIEZ VOTRE ";OB$(11,1);" ?":GOTO1040
1620 L=14:GOTO670
1640 IFL<>14ANDL<>16THENPRINT:PRINT"VOUS NE POUVEZ PAS DESCENDRE ICI.":GOTO1040
1650 IFL=14THEN1680
1660 IFM<>11THENPRINT:PRINT"NE DITES PAS N'IMPORTE QUOI !":GOTO1040
1670 L=6:GOTO670
1680 IFM<>11ANDM<>9ANDM<>12THENPRINT:PRINT"REPOSEZ VOUS DONC UN PEU...":GOTO1040
1690 IFFE=0THENPRINT:PRINT"IMPOSSIBLE : LA";OB$(15,1);"EST FERMEE.":GOTO1040
1700 IF BA=0THENPRINT:PRINT"UN ";OB$(16,1);" VOUS EMPECHE DE PASSER !":OB$(16,2)="14":FORZ=1TO2000:NEXTZ:GOTO670
1710 IFM=9ORM=12THEN1750
1720 IFOB$(11,2)="100"ORVAL(OB$(11,2))=LTHENPRINT:PRINT"VOUS NE POUVEZ PAS.":GOTO1040
1730 IFOB$(11,2)="19"THENL=19:GOTO670
1740 PRINT:PRINT"QUELLE ";OB$(11,1);" ?":GOTO1040
1750 IF M=12 THENGOTO1790
1760 IFVAL(OB$(9,2))<>LANDOB$(9,2)<>"100"THENPRINT:PRINT"QUELLE ";OB$(9,1);" ?":GOTO1040
1770 IFCR=0THENPRINT:PRINT"LA ";OB$(9,1);" CASSE !":PRINT"VOUS VOUS ECRASEZ AU FOND DU RAVIN":GOTO3670
1780 PRINT:PRINT"VOUS PASSEZ PAR LA ";OB$(15,1);" ET ALLEZ VOUS ECRASEZ AU FOND DU RAVIN.":GOTO3670
1790 IFVAL(OB$(12,2))<>LANDOB$(12,2)<>"100"THENPRINT:PRINT"QUEL ";OB$(12,1);" ?":GOTO1040
1800 IF DR=0THENL=19:GOTO670
1810 GOTO1780
1820 IFM=0THEN2280
1830 IFVAL(OB$(M,2))=0ORVAL(OB$(M,2))=100ORVAL(OB$(M,2))=LTHEN1850
1840 PRINT:PRINT"JE NE VOIS PAS CA ICI !":GOTO1040
1850 IFM>23THENGOTO1852
1851 ONMGOTO1860,1870,1890,2280,1920,1930,2280,1940,1960,1970,2280,2280,1980,2000,2020,2070,2100,2110,2120,2280,2280,2280,2280
1852 ON(M-23)GOTO2130,2150,2180,2190,2210,2240,2270
1860 PRINT:PRINT"VOUS N'EN AVEZ JAMAIS VU ?":GOTO1040
1870 IF AL=0 THENPRINT:PRINT"ELLE EMET UNE LUMIERE AVEUGLANTE.":GOTO1040
1880 PRINT:PRINT"ELLE EST ETEINTE.":GOTO1040
1890 IF OB$(1,2)<>"100"THENPRINT:PRINT"IL MANQUE UNE ";OB$(1,1):GOTO1040
1900 PRINT:PRINT"ELLE EST ALLUMEE.":GOTO1040
1910 PRINT:PRINT"ELLE EST ALLUMEE.":GOTO1040
1920 PRINT:PRINT"IL EST AU CHOCOLAT.":GOTO1040
1930 PRINT:PRINT"C'EST UN PORTRAIT DE SATAN.":GOTO1040
1940 IFFI=0THENPRINT:PRINT"ELLE EST PLEINE DE LIQUIDE.":GOTO1040
1950 PRINT:PRINT"ELLE EST BRISEE.":GOTO1040
1960 PRINT:PRINT"ELLE A L'AIR ANCIENNE.":GOTO1040
1970 PRINT:PRINT"IL Y A QUELQUE CHOSE MARQUE DESSUS.":GOTO1040
1980 IFL<>6THEN2280
1990 PRINT:PRINT"IL Y A UN TROU ASSEZ GRAND POUR Y PASSER.":GOTO1040
2000 REM
2010 PRINT:PRINT"IL FAIT ASSEZ SOMBRE A L'INTERIEUR.":GOTO1040
2020 IFFE=0THENPRINT:PRINT"ELLE EST FERMEE.":GOTO2040
2030 PRINT:PRINT"ELLE EST OUVERTE."
2040 IF VAL(OB$(16,2))<0THENPRINT"VOUS VOYEZ EGALEMENT UN ";OB$(16,1):OB$(16,2)="14":GOTO2060
2050 PRINT"IL Y A UN ";OB$(16,1):IFBA<>0THENPRINT" SCIE";
2060 PRINT:PRINT"VOUS VOYEZ AUSSI UN RAVIN TRES PROFOND .":GOTO1040
2070 REM
2080 IFBA=0THENPRINT:PRINT"IL A L'AIR SOLIDE.":GOTO1040
2090 PRINT:PRINT"IL EST SCIE.":GOTO1040
2100 PRINT:PRINT"C'EST UNE STATUE DE SATAN.":GOTO1040
2110 PRINT:PRINT"ELLE EST ETROITE,HORIZONTALE,ET DANS LE MUR.":GOTO1040
2120 PRINT:PRINT"ILS SONT EN OR INCRUSTE DE DIAMANTS.":GOTO1040
2130 REM
2140 PRINT:PRINT"VOUS N'EN AVEZ JAMAIS VU ?":GOTO1040
2150 IFL=9 THEN2280
2160 IFOB$(1,2)<>"-6"THENPRINT:PRINT"C'EST UN LIT A BALDAQUINS.":GOTO1040
2170 PRINT:PRINT"C'EST UN LIT A BALDAQUINS.":PRINT"VOUS DECOUVREZ UNE ";OB$(1,1);" CACHEE DESSOUS !":FORZ=1TO2000:NEXTZ
2171 OB$(1,2)="6":GOTO670
2180 PRINT:PRINT"IL EST ACCROCHE DANS LE MUR.":GOTO1040
2190 IF CH=0THENPRINT:PRINT"ILS ONT L'AIR MECHANTS.":GOTO1040
2200 PRINT:PRINT"ILS SONT MORTS.":GOTO1040
2210 IFL<>12THEN2280
2220 IF OB$(24,2)="12"THEN2280
2230 PRINT:PRINT"IL EST FENDU DE HAUT EN BAS.":PRINT"VOUS REMARQUEZ EGALEMENT UN ";OB$(24,1):OB$(24,2)="12":FORZ=1TO2000:NEXTZ
2231 GOTO670
2240 IFL<>9THEN2280
2250 IFAC=0THENPRINT:PRINT"ELLE EST EN ACIER.":GOTO1040
2260 PRINT:PRINT"ELLE EST RONGEE PAR L'ACIDE.":GOTO1040
2270 REM
2280 PRINT:PRINT"VOUS NE VOYEZ RIEN DE SPECIAL.":GOTO1040
2290 IFM=24THEN2940
2300 IFM=2THEN2400
2310 IFNO=4THENPRINT:PRINT"VOUS PORTEZ TROP D'OBJETS !":GOTO1040
2320 IFM=0THENPRINT:PRINT"PRENDRE QUOI ???":GOTO1040
2330 IF OB$(M,2)="100"THENPRINT:PRINT"VOUS L'AVEZ DEJA !":GOTO1040
2340 IFVAL(OB$(M,2))<>LTHENPRINT:PRINT"JE NE VOIS PAS CA ICI !":GOTO1040
2350 IFM=9THEN2420
2360 IFM=12THEN2440
2370 IFM>12THENPRINT:PRINT"VOUS NE POUVEZ PAS PRENDRE CA !":GOTO1040
2380 IFM=6 THEN OB$(14,2)="9"
2390 OB$(M,2)="100":NO=NO+1:GOTO670
2400 IFAL<>0THEN2390
2410 PRINT:PRINT"VOUS OUBLIEZ DE FAIRE QUELQUE CHOSE !":GOTO1040
2420 IFCR<>0THEN2410
2430 GOTO2390
2440 IFDR<>0THEN2410
2450 GOTO2390
2460 IFM<>2THENPRINT:PRINT"CA NE SE DEVISSE PAS !":GOTO1040
2470 IFL<>3THENPRINT:PRINT"ON NE PEUT RIEN DEVISSER ICI !":GOTO1040
2480 IFAL<>0THENPRINT:PRINT"C'EST DEJA FAIT.":GOTO1040
2490 IFOB$(4,2)<>"100"THENPRINT:PRINT"IMPOSSIBLE : VOUS VOUS BRULEZ .":GOTO1040
2500 IFOB$(3,2)<>"-3"THENAL=1:GOTO670
2510 AL=1:PRINT:PRINT"ELLE ETAIT RELIE A UN DISPOSITIF ELECTRONIQUE ET QUELQUE CHOSE EST TOMBE.":OB$(3,2)="3"
2520 FORZ=1TO4000:NEXTZ:GOTO670
2530 IFM=0THENPRINT:PRINT"LACHER QUOI ???":GOTO1040
2540 IF OB$(M,2)<>"100"THENPRINT:PRINT"VOUS NE L'AVEZ PAS AVEC VOUS.":GOTO1040
2550 OB$(M,2)=STR$(L):NO=NO-1:PRINT:PRINT"OK":FORZ=1TO1000:NEXTZ:GOTO670
2560 IFM<>8THEN2530
2570 IFL<>9THEN2530
2580 IFOB$(8,2)<>"100"THENPRINT:PRINT"VOUS NE L'AVEZ PAS.":GOTO1040
2590 IFFI<>0THEN2530
2600 PRINT:PRINT"OU ? ";:GOSUB1200
2610 IF LEFT$(R$,4)<>LEFT$(OB$(29,1),4)THENPRINT:PRINT"EN RETOMBANT ELLE SE BRISE.":FI=1:OB$(8,2)="9":NO=NO-1:GOTO670
2620 PRINT:PRINT"LA PORTE EST RONGEE PAR L'ACIDE QUE CONTENAIT LA FIOLE ."
2630 NO=NO-1
2640 FI=1:AC=1:OB$(8,2)="9":FORZ=1TO 2000:NEXTZ:GOTO670
2650 IFM<>15 ANDM<>29THENPRINT:PRINT"CA NE S'OUVRE PAS !":GOTO1040
2660 IFM=29THEN2700
2670 IFL<>14THENPRINT:PRINT"QUELLE ";OB$(15,1);" ?":GOTO1040
2680 IFFE<>0THENPRINT:PRINT"VOUS L'AVEZ DEJA FAIT.":GOTO1040
2690 FE=1:PRINT:PRINT"OK":FORZ=1TO1000:NEXTZ:GOTO670
2700 IFL=24ORL=19THENPRINT:PRINT"QUELLE ";OB$(29,1);" ?":GOTO1040
2710 IFL<>9THENPRINT:PRINT"ELLE EST DEJA OUVERTE !":GOTO1040
2720 IFAC<>0THEN2740
2730 PRINT:PRINT"IMPOSSIBLE, IL N'Y A PAS DE POIGNEE ET ELLE A L'AIR SOLIDE.":GOTO1040
2740 PRINT:PRINT"VOUS NE POUVEZ PAS, MAIS IL Y A UN TROU ASSEZ GRAND POUR PASSER."
2750 GOTO1040
2760 IFM<.15ANDM<.29THENPRINT:PRINT"CA NE SE FERME PAS.":GOTO1040
2770 IFM=29THEN2810
2780 IFL<>14 THENPRINT:PRINT"QUELLE ";OB$(15,1);" ?":GOTO1040
2790 IFFE=0THENPRINT:PRINT"VOUS L'AVEZ DEJA FAIT !":GOTO1040
2800 FE=0:PRINT:PRINT"OK":FORZ=1TO1000:NEXTZ:GOTO670
2810 IFL=24 ORL=19THENPRINT:PRINT"QUELLE ";OB$(29,1);" ?":GOTO1040
2820 IFL<>9THENPRINT:PRINT"N'OUBLIEZ PAS QUE VOUS ETES ENFERME ...":GOTO1040
2830 IFAC=0THENPRINT:PRINT"VOTRE ETAT MENTAL DOIT ATTEINDRE LA COTE D'ALERTE A MON AVIS ...":GOTO1040
2850 IFOB$(7,2)<>"100"THENPRINT:PRINT"AVEC LES DENTS ?":GOTO1040
2860 IFM<>16THENPRINT:PRINT"CA NE SE SCIE PAS .":GOTO1040
2870 IFL<>14OROB$(16,2)="-14"THENPRINT:PRINT"QUEL ";OB$(16,1);" ?":GOTO1040
2880 IFBA<>0THENPRINT:PRINT"VOUS L'AVEZ DEJA FAIT !":GOTO1040
2890 IF FE=0THENPRINT:PRINT"VOUS OUBLIEZ DE FAIRE QUELQUE CHOSE...":GOTO1040
2900 PRINT:PRINT"OK":BA=1:FORZ=1TO1000:NEXTZ:GOTO670
2910 IFM<.17THENPRINT:PRINT"DROLE D'IDEE...":GOTO1040
2920 IFL<>16THENPRINT:PRINT"QUELLE ";OB$(17,1);" ?":GOTO1040
2930 PRINT:PRINT"OK":EM=1:GOTO1040
2940 IFM<>6ANDM<>24THENPRINT:PRINT"CA NE DONNE RIEN.":GOTO1040
2950 IFM=24THEN3010
2960 IFOB$(6,2)="100"OR(OB$(14,2)="9"ANDOB$(6,2)="9")THENPRINT:PRINT"IL NE SE PASSE RIEN.":GOTO1040
2970 IFVAL(OB$(6,2))<LTHENPRINT:PRINT"QUEL ";OB$(6,1);" ?":GOTO1040
2980 IFL<>9THENPRINT:PRINT"IL NE SE PASSE RIEN.":GOTO1040
2990 PRINT:PRINT"UNE ";OB$(14,1);"ETAIT CACHEE DERRIERE !"
3000 FOR  Z=1TO1000:NEXTZ:OB$(14,2)="9":GOTO670
3010 IFL<>12OROB$(24,2)="-12"THENPRINT:PRINT"QUEL ";OB$(24,1);" ?":GOTO1040
3020 IFOB$(30,2)="12"THENPRINT:PRINT"IL NE SE PASSE RIEN":GOTO1040
3030 OB$(30,2)="12":PRINT:PRINT"CA DECLENCHE L'OUVERTURE D'UN ";OB$(30,1);" !"
3040 FORZ=1TO1500:NEXTZ:GOTO670
3050 IFM<>10THENPRINT:PRINT"CA NE SE LIT PAS.":GOTO1040
3060 IFOB$(10,2)<>"100"ANDVAL(OB$(10,2))<>LTHENPRINT:PRINT"QUEL ";OB$(10,1);" ?":GOTO1040
3070 IFLE=0THENPRINT:PRINT"C'EST ECRIT DANS UNE LANGUE ETRANGE QUI NE RESSEMBLE A RIEN DE CONNU .":GOTO1040
3080 GOTO3720
3090 IFM<>5THENPRINT:PRINT"LES HUMAINS ONT DE DROLES D'IDEES !":GOTO1040
3100 IFOB$(5,2)<>"100" AND VAL(OB$(5,2))<>LTHENPRINT:PRINT"QUEL ";OB$(5,1);" ?":GOTO1040
3110 PRINT:PRINT"LES POISONS ONT PARFOIS BON GOUT..."
3120 PRINT"CELUI-CI ETAIT PARTICULIEREMENT EFFICACE . VOUS ETES FOUDROYE !":GOTO3670
3140 IFM<>5 THENPRINT:PRINT"VOUS ETES TRES GENEREUX !":GOTO1040
3150 IF OB$(5,2)<>"100"THENPRINT:PRINT"VOUS NE LE PORTEZ PAS AVEC VOUS.":GOTO1040
3160 IFL<>22 THENPRINT:PRINT"C'EST TRES GENEREUX DE VOTRE PART.":GOTO1040
3170 PRINT:PRINT"A QUI ? ";:GOSUB1200:PRINT
3180 IFLEFT$(OB$(27,1),4)<>LEFT$(R$,4)THENPRINT:PRINT"LE MOINS QUE L'ON PUISSE DIRE, C'EST QUE VOUS ETES GENEREUX !":GOTO1040
3190 PRINT:PRINT"ILS SE JETTENT DESSUS ! CE SERA LEUR DERNIER REPAS : ILS MEURENT EN POUSSANT DESHURLEMENTS A GLACER UN MORT !"
3200 OB$(5,2)="0":CH=1:NO=NO-1
3210 FORZ=1TO2000:NEXTZ:GOTO670
3220 IFM<>9ANDM<>12THENPRINT:PRINT"CA NE S'ATTACHE PAS.":GOTO1040
3230 IF (M=9ANDCR=1)OR(M=12ANDDR=1)THENPRINT:PRINT"C'EST DEJA FAIT.":GOTO1040
3240 IFOB$(M,2)<>"100"THENPRINT:PRINT"VOUS NE L'AVEZ PAS.":GOTO1040
3250 IFL<>14THENPRINT:PRINT"VOUS NE POUVEZ PAS L'ATTACHER ICI.":GOTO1040
3260 PRINT:PRINT"OU ?";:GOSUB1200:PRINT
3270 IFLEFT$(R$,4)<>LEFT$(OB$(26,1),4)THENPRINT:PRINT"VOUS NE POUVEZ PAS L'ATTACHER LA !":GOTO1040
3280 NO=NO-1
3290 PRINT:PRINT"OK":OB$(M,2)="14":IFM=9THENCR=1:GOTO1040
3300 DR=1:GOTO1040
3310 IFM<>3THENPRINT:PRINT"CA NE S'ALLUME PAS !":GOTO1040
3315 IFOB$(3,2)<>"100"THENPRINT:PRINT"VOUS N'AVEZ PAS DE LAMPE SUR VOUS .":GOTO1040
3320 IFOB$(1,2)<>"100"THENPRINT:PRINT"IMPOSSIBLE : IL N'Y A PAS DE ";OB$(1,1);".":GOTO1040
3330 IFLA<>0THENPRINT:PRINT"C'EST DEJA FAIT.":GOTO1040
3340 LA=1:PRINT:PRINT"OK":GOTO1040
3350 IFM<.10THENPRINT:PRINT"VOUS NE POUVEZ PAS.":GOTO1040
3360 IFOB$(10,2)<>"100"THENPRINT:PRINT"VOUS NE L'AVEZ PAS AVEC VOUS.":GOTO1040
3370 IFL<>18THENPRINT:PRINT"JE NE VOIS PAS OU VOUS POURRIEZ LE FAIRE !";:GOTO1040
3380 IFLE<>0THENPRINT:PRINT"VOUS L'AVEZ DEJA FAIT.":GOTO1040
3390 PRINT:PRINT"OU ?";:GOSUB1200:PRINT
3400 IFLEFT$(R$,4)<>LEFT$(OB$(18,1),4)THENPRINT:PRINT"VOUS NE POUVEZ PAS LE METTRE ICI .":GOTO1040
3410 PRINT:PRINT"IL EST ASPIRE VERS L'INTERIEUR ,ET QUELQUES MINUTES PLUS TARD IL RESSORT ."
3420 LE=1:NO=NO-1
3430 OB$(10,2)="18":FORZ=1TO1000:NEXTZ:GOTO670
3440 IFM<>2THENPRINT:PRINT"CA NE SE VISSE PAS.":GOTO1040
3450 IFL<>3THENPRINT:PRINT"ON NE PEUT RIEN VISSER ICI.":GOTO1040
3460 IFAL=0THENPRINT:PRINT"ELLE EST DEJA SUR SON SUPPORT.":GOTO1040
3470 IFOB$(2,2)<>"100"THENPRINT:PRINT"VOUS NE L'AVEZ PAS AVEC VOUS.":GOTO1040
3480 PRINT:PRINT"EN LA REVISSANT VOUS VOUS ELECTROCUTEZ !"
3490 GOTO 3670
3500 IFM<>9ANDM<>12THENPRINT:PRINT"CA NE SE DETACHE PAS.":GOTO1040
3510 IF VAL(OB$(9,2))<>LAND OB$(9,2)<>"100"THENPRINT:PRINT"QUELLE ";OB$(9,1);" ?":GOTO1040
3520 IF VAL(OB$(12,2))<>LANDOB$(12,2)<>"100"THENPRINT:PRINT"QUEL ";OB$(12,1);" ?":GOTO1040
3530 IFM=9AND CR=0THENPRINT:PRINT"ELLE N'EST PAS ATTCHEE.":GOTO1040
3540 IFM=12ANDDR=0THENPRINT:PRINT"IL N'EST PAS ATTACHE.":GOTO1040
3550 IFL<>14THENPRINT:PRINT"VOUS NE POUVEZ PAS LE FAIRE D'ICI.":GOTO1040
3560 PRINT:PRINT"OK":IFM=9THENCR=0:GOTO1040
3570 DR=0:GOTO1040
3580 IFM<.3THENPRINT:PRINT"AH BON ??!":GOTO1040
3590 IFOB$(3,2)<>"100"THENPRINT:PRINT"VOUS NE LA PORTEZ PAS SUR VOUS .":GOTO1040
3600 IFLA=0THENPRINT:PRINT"ELLE EST DEJA ETEINTE .":GOTO1040
3610 PRINT:PRINT"OK":LA=0:GOTO1040
3620 IFM<>8THENPRINT:PRINT"VOUS DITES N'IMPORTE QUOI !":GOTO1040
3630 IFOB$(8,2)<>"100"ANDVAL(OB$(8,2))<>LTHENPRINT:PRINT"QUELLE ";OB$(8,1(;" ?":GOTO1040
3640 IFFI<>0THENPRINT:PRINT"ELLE EST CASSE : IL N'Y A PLUS RIEN DEDANS.":GOTO1040
3650 PRINT:PRINT"C'ETAIT DE L'ACIDE . . ."
3660 PRINT"JE VOUS LAISSE DEVINER CE QUI VOUS ARRIVE !"
3670 PRINT:PRINT:PRINT"VOUS AVEZ FAILLI A VOTRE MISSION. VOUS ETES VRAIMENT NUL ."
3680 PRINT:PRINT"SERIEZ-VOUS PRET A RECOMMENCER ?"
3690 A$=INKEY$:IFA$=""THEN3690
3700 IFA$="O"THEN RUN
3710 CLS:PRINT@985,"P O U L E    M O U I L L E E   !":FORZ=1TO3000:NEXTZ:END
3720 CLS:PRINT"C'EST LE TEXTE D'UN SORT !"
3730 PRINT:PRINT"CE SORTILEGE VOUS A PERMIS DE RAYER DE LA SURFACE DE LA TERRE TOUS LES SORCIERS!"
3750 PRINT:PRINT:PRINT"VOUS MERITEZ VRAIMENT LE TITRE DE SUPERMAN DU XXI EME SIECLE !"
3760 END
3770 PRINT:PRINT"CETTE ";OB$(14,1);" EST SI SOMBRE ,QUE VOUS ETES TOMBE, ET VOUS ETES ROMPU LE COU.":GOTO3670
3780 PRINT:PRINT"UN ECLAIR VENU DE NULLE PART VOUS FOUDROIE":GOTO3670
3790 PRINT:PRINT"LES "OB$(27,1);" NE FONT QU'UNE BOUCHEE DE VOUS !":GOTO3670
3800 PRINT:PRINT"CET ASCENSEUR N'A PAS DE BOUTON."
3810 PRINT"MAIS IL SE MET EN MARCHE TOUT SEUL !"
3820 PRINT"APRES QUELQUES INSTANTS, IL S'ARRETE ET LA PORTE S'OUVRE SUR . . .L'ENFER !"
3830 GOTO 3670
