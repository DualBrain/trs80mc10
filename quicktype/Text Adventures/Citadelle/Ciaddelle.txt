1 CLEAR152:SCREEN4:COLOR0,1,2:CLS:PRINT"CITADELLE":LOCATE0,2:PRINT"2-l'Espadon de Kranz":LOCATE18,3:PRINT"1984-LORICIELS":TS=TIME
2 LOCATE5,7:PRINT"PRESSEZ LA TOUCHE LOCK":LOCATE10,8:PRINT"PUIS ESPACE":LOCATE0,14:PRINT"Adaptation Sanyo PHC-25";:LOCATE15,15:PRINT"2012 David BOURG";:GOSUB496
3 SCREEN2:COLOR2,0,1:NB=1:AC=10:DO=5:PV=90:SO=5:PC=1:KEY1,"OUVRE ":KEY2,"PRENDS ":KEY3,"REGARDE ":KEY4,"FOUILLE ":GOSUB480:P=1:GOSUB43:GOTO17
4 PRINTA$;:RETURN
5 COLOR1,0,0:PRINTA$:RETURN
6 COLOR5,0,0
7 PRINTA$:RETURN
8 GOTO7:REM CITADELLE-2 SANYO PHC-25 12527 1744 515
9 GOSUB495
17 CONSOLE9,15:CLS:COLOR3,0,0:GOSUB516
19 IFA$="O"THENA$="OUE"
20 IFA$="E"THENA$="EST"
21 IFA$="N"THENA$="NOR"
22 IFA$="S"THENA$="SUD"
23 LO=LEN(A$):IFLO<3THEN17
24 COLOR0,0,0:PRINT:MI=0:FORA=1TOLO
25 B$=MID$(A$,A,1):IFB$=" "THENMI=A
26 NEXTA:IFMI=0THENX$=A$:M=0:MI=4:GOTO30
27 W$=RIGHT$(A$,LO-MI):Y$=LEFT$(W$,3):IFY$="SOU"ORY$="CEL"ORY$="CAC"ORY$="OUB"ORY$="ETU"ORY$="ESC"ORY$="LAB"ORY$="CHA"THENY$="SAL"
28 M=0:FORA=1TO40:IFY$=M$(A)THENM=A
29 NEXTA:IFM=0THENA$=U$:GOSUB6:PRINTW$;".":GOTO9
30 X$=LEFT$(A$,MI-1):Z$=LEFT$(X$,3)
31 V=0:FORA=1TO33:IFZ$=V$(A)THENV=A
32 NEXTA:IFV=0THENA$=U$:GOSUB6:PRINTX$;".":GOTO9
33 IFI=1ANDV<>25THEN500
34 IFV<10THEN41
35 IFV=24ANDM=33ANDP(7)=0THEN286
36 IFV=24ANDM=38ANDP(12)=0THEN514
40 ONPGOTO70,80,90,100,110,120,130,140,150,160,170,180,190,200,210,220
41 ONVGOTO375,380,386,394,377,377,377,377,370
42 PC=P:P=T(P,V-4):IFP=0THEN498
43 CONSOLE0,15:CLS:COLOR1,2,0:CONSOLE1,15:LOCATE19,0:PRINT"L I E U :";:COLOR1,0,0:PRINTP
45 COLOR5,0,0:A$=S$
55 ONPGOSUB400,405,410,415,420,425,430,435,440,445,450,455,405,465,470,475
56 PRINT:COLOR0,0,1:A$="Objets :":GOSUB4
57 FORA=27TO40:IFP(A-26)=PTHENA$=N$(A-26)+" ":GOSUB4
58 NEXTA
59 PRINT:COLOR2,0,0:A$="Issues :":GOSUB4:IFT(P,1)>0THENA$=" Nord":GOSUB4
60 IFT(P,2)>0THENA$=" Sud":GOSUB4
61 IFT(P,3)>0THENA$=" Est":GOSUB4
62 IFT(P,4)>0THENA$=" Ouest":GOSUB4
63 IFQ(1)<>PANDQ(2)<>PANDQ(3)<>PANDQ(4)<>PANDQ(5)<>PANDQ(7)<>PANDQ(8)<>PTHENMO=0:RETURN
64 PRINT:MO=0:COLOR3,0,0:A$="Monstres :":GOSUB4
65 FORA=1TO8:IFQ(A)=PTHENA$=Q$(A)+" ":GOSUB4:MO=A
66 NEXTA:RETURN
70 IF(V=1ORV=13)ANDM=1THEN230
71 IFV=10ANDM=2THENT(1,3)=7:GOTO231
72 IF(V=1ORV=13)ANDM=25THEN271
79 GOTO506
80 IF(V=1ORV=12ORV=13)ANDM=1THEN232
81 IFV=10ANDM=3THEN233
83 IFV=12ANDM=4THEN235
84 IFV=12ANDM=5THEN236
85 IFV=10ANDM=6THEN238
86 IF(V=1ORV=13)ANDM=6THEN240
89 GOTO506
90 IFV=13ANDM=7THEN242
91 IF(V=14ORV=11)ANDM=8THEN243
92 IFV=1ANDM=1THEN244
93 IFV=1ANDM=25THEN245
94 IF(V=15ORV=31)ANDM=9THEN246
99 GOTO506
100 IFV=16ANDM=10THEN248
101 IFV=17ANDM=10THEN250
102 IFV=12ANDM=10THEN252
103 IFV=10ANDM=2THEN255
104 IFV=1ANDM=10THEN517
105 IFV=13ANDM=10THEN256
106 IF(V=29ORV=30ORV=32)ANDM=10THEN257
109 GOTO506
110 IFV=10ANDM=2THEN504
119 GOTO506
120 IF(V=12ORV=18)ANDM=12THEN259
121 IF(V=1ORV=13)ANDM=26THEN291
122 IF(V=1ORV=13)ANDM=25THEN271
129 GOTO506
130 IFV=12ANDM=5THEN261
131 IFV=18ANDM=13THEN262
132 IF(V=1ORV=13)ANDM=25THEN271
139 GOTO506
140 IFV=19ANDM=14THEN263
141 IFV=20ANDM=15THEN266
149 GOTO506
150 IFV=1ANDM=25THEN272
151 IFV=21ANDM=16THEN273
159 GOTO506
160 IF(V=1ORV=13)ANDM=2THEN275
161 IFV=33ANDM=36THEN276
162 IFV=10ANDM=2THEN278
163 IFV=22ANDM=17THEN280
169 GOTO506
170 IFV=1ANDM=18THEN282
171 IFV=23ANDM=19THEN284
179 GOTO506
180 IFV=19ANDM=20THEN288
181 IFV=25THEN292
189 GOTO506
190 IFV=10ANDM=6THEN231
191 IFV=12ANDM=6THEN294
199 GOTO506
200 IFV=22ANDM=17THEN296
201 IFV=10ANDM=2ANDP(3)=0THEN297
202 IF(V=26ORV=10ORV=20)ANDM=15THEN298
209 GOTO506
210 IFV=1ANDM=21THEN300
211 IFV=13ANDM=21THEN302 
212 IFV=27ANDM=22THEN304
213 IFV=28ANDM=24THEN306
214 GOTO506
220 IF(V=1ORV=12ORV=13)ANDM=1THEN232
221 IFV=10ANDM=3THEN511
222 IF(V=1ORV=12ORV=13)ANDM=18THEN281
229 GOTO506
230 A$="Il est ecrit:  ici,  domine  la magie.":GOSUB5:GOTO9
231 A$="Elle n'est pas verrouillee.":GOSUB520:GOTO9
232 A$=R$+"un tiroir.":GOSUB5:GOTO9
233 IFP(6)<>-2THEN503
234 A$=R$+N$(6):P(6)=2:GOSUB519:GOTO9
235 A$="Dans l'une d'elles, un squelette":GOSUB5:GOTO9
236 IFP(2)<>-2THEN499
237 A$=R$+N$(2):GOSUB519:P(2)=2:GOTO9
238 IFOA=1THEN503 
239 OA=1:A$="La porte resiste puis cede.":GOSUB520:GOTO9
240 IFOA<>1THEN508
241 A$="Sur l'envers de la porte, il estgrave: LA  TORTURE  FAIT  TOUT  AVOUER.":GOSUB5:GOTO9
242 A$="L'une d'elles a les yeux":GOSUB5:A$="en verre.":GOSUB5:GOTO9
243 A$="Le bras droit de l'autre se leve":BL=1:GOSUB5:GOTO9
244 A$="Le portrait  d'une  jeune  femmeaux genereux atours.ISABELLE estinscrit sur le cadre.":GOSUB5:GOTO9
245 A$="Des traces de sang menent au murouest.":GOSUB5:GOTO9
246 IFBL<>1THEN500
247 A$="Un passage s'ouvre a l'ouest.":GOSUB520:T(3,4)=4:GOTO9
248 IFQ(6)<>4THEN500
249 A$="Il n'a meme pas combattu!":GOSUB5:CG=1:GOTO9
250 IFCG<>1THEN500
251 A$="Sous la torture, il dit:":GOSUB5:A$="La colonne conduit a la colonne.A bout de force, il meurt.":GOSUB5:Q(6)=0:CG=0:MG=1:GOTO9
252 IFMG<>1THEN517
253 IFP(10)<>-4THEN499
254 A$="Vous trouvez "+N$(10):P(10)=4:GOSUB519:GOTO9
255 A$=R$+"un courant d'air fetide.":T(4,2)=5:T(5,1)=4:GOSUB520:GOTO9
256 A$="Il a une blessure profonde.":GOSUB5:GOTO9
257 IFCG<>1THEN517
258 A$="Il ne veux rien dire.":GOSUB5:GOTO9
259 IFP(3)<>-6THEN499
260 A$="Sous  la  paille, un  trou dans lequel vous apercevez "+N$(3):GOSUB519:P(3)=6:GOTO9
261 A$="Alors que vous posez votre main sur lui, son crane s'eleve & ditsans guerrier, point de victoire":GOSUB5:GOTO9
262 A$="A l'envers est grave:   DANS LE PUITS, LA CLEF DE LA G....":GOSUB5:GOTO9
263 IFP(9)<>0THEN500
264 IFP(4)<>-8THEN499
265 A$=R$+N$(4):GOSUB519:P(4)=8:GOTO9
266 IFFG=1THEN503
267 IFP(9)=0THENA$="Vous forcez  la grille  avec la barre.":FG=1:GOTO270
268 PV=PV-30:IFPV>0THENA$="Au prix d'un effort surhumain,  vous ecartez deux barreaux.":FG=1:GOTO270
269 A$="Vous epuisez vos forces en vain.Prisonnier, vous mourrez de soif":GOSUB5:GOTO361
270 T(8,4)=9:GOSUB520:GOTO9
271 A$="C'est humide et glissant!":GOSUB5:GOTO9
272 A$="C'est un malefice!Du sang suintedu sol  et coule pour former un cercle magique.":GOSUB5:CE=1:GOTO9
273 IFCE<>1THEN500
274 A$="La magie vous transporte en un  autre lieu.":P=10:GOSUB520:GOSUB495:GOSUB43:GOTO378
275 A$=R$+"un emplacement rond a la place de la poignee.":GOSUB5:GOTO9
276 IFP(10)<>0THEN502
277 A$="La  medaille  tourne  puis vous entendez un claquement.":OP=1:P(10)=-10::NB=NB-1:GOSUB5:GOTO9
278 IFOP<>1THEN508
279 A$="Vous ouvrez la porte.":T(10,4)=11:GOSUB520:GOSUB495:GOTO9
280 A$="La magie vous transporte en un  autre lieu.":P=14:GOSUB520:GOSUB43:GOTO378
281 A$="Le dossier dissimule un escalier":T(16,3)=5:T(5,2)=16:GOSUB5:GOTO9
282 A$=R$+"une bosse sur la toile del'assise.":GOSUB5:GOTO9
284 IFP(5)<>-11THEN499
285 A$="Au fond, vous trouvez "+N$(5)+" et "+N$(7):GOSUB519:P(5)=11:P(7)=11:GOTO9
286 IFP(7)<>0THEN502
287 A$="'sorti du puits, mettez le...'  La suite est illisible.":GOSUB5:GOTO9
288 IFI=1THEN503
289 IFP(5)<>0ORP(6)<>0THENA$="Vous  n'avez  pas  l'equipement pour vous risquer dans le puits.":GOSUB5:GOTO9
290 P(5)=12:P(6)=12:NB=NB-2:A$="Vous placez la corde, le grappinet descendez...   Au fond, vous trouvez "+N$(1):GOSUB519:P(1)=12:I=1:GOTO9
291 A$="Vous ne voyez pas le fond.":GOSUB5:GOTO9
292 IFI<>1THEN500
293 A$="Vous posez le heaume sur le borddu puits et remontez.":GOSUB5:I=0:GOTO9
294 A$="Vous trouvez un missel.  Sur la couverture il est ecrit 'la mainse joint a la main'.":GOSUB5:GOTO9
296 IFP(1)<>0THENP=10:GOSUB43:GOTO378
297 T(14,4)=13:A$="Vous ouvrez la porte.":GOSUB520:GOTO9
298 IFP(1)<>0THENA$="Impossible...":GOSUB6:GOTO9
299 A$="Le heaume se met a briller.  La herse se leve devant moi.":GOSUB520:T(14,1)=15:GOTO9
300 A$="Une voix venant de la sculpture dit: trouvez la formule.":GOSUB5:GOTO9
302 A$="Ses deux mains sont en or.":GOSUB5:GOTO308
304 IFK<>1THENA$="Les mains bougent un peu, mais  sont bloquees...":GOSUB5:GOTO9
305 A$="Au centre de la piece, dans une lumiere aveuglante, apparait":GOSUB5:A$=N$(8)+".":GOSUB5:P(8)=15:GOTO9
306 K=1:A$="Vous invoquez KRANZ.":GOSUB5:GOTO9
308 A$="Sur le socle est ecrit: KRANZ.":GOSUB5:GOTO9
309 A$="LA DEUXIEME EPREUVE EST RESOLUE.COMBATTEZ MAINTENANT POUR LE":GOSUB5:A$="SCEPTRE D'IRUN.":GOSUB5:END
310 PM=RND(1)*40+10:AM=RND(1)*15:DM=RND(1)*15
311 GOSUB495:CONSOLE0,15:CLS:COLOR3,0,1:CONSOLE5,15:LOCATE10,0:PRINT "C O M B A T":COLOR2,0,1
312 LOCATE11,1:A$="<A>TTAQUE":GOSUB7:LOCATE11,2:A$="<D>EFENSE":GOSUB7:LOCATE11,3:A$="<S>ORTILEGE":GOSUB7:LOCATE11,4:A$="<F>UITE":GOSUB7
313 PLAY"V10L64O2AGR64AR64O3L8CR64L64O2AGR64AR64O3L4D#","V10L64O3EDR64ER64L8GR64L64EDR64ER64L4O3A#"
320 GOSUB495:LOCATE5,5:CLS:LOCATE5,5:GOSUB516
321 IFA$="A"THEN326
322 IFA$="D"THEN328
323 IFA$="S"THEN351
324 IFA$="F"THEN357
325 GOTO320
326 A1=2:A2=4:A3=-14:GOTO330
328 A1=0:A2=0:A3=0
330 TC=INT(RND(1)*20+1):IF(18-TC)<AMTHEN336
331 A$="Vous l'avez rate.":GOSUB7:IFAM<5THENA$="Il est tres resistant.":GOSUB7
332 TC=INT(RND(1)*20+1):IF(20-TC)<AC-A2THEN341
333 A$="Le monstre vous a rate.":GOSUB7
334 IFA3<>0THEN346
335 GOTO320
336 A$="Vous l'avez touche.":GOSUB7:D=INT(RND(1)*DO-A2-A3+1)
337 IFD>8THENA$="C'est un coup de maitre.":GOSUB7
338 PM=PM-D:IFPM<0THENA$="Le monstre tombe mort.":GOSUB7:GOTO348
339 B=RND(1):IFB<.5THENA$="Vous avez l'avantage.":GOSUB7:PRINT:GOTO330
340 GOTO332
341 A$="Vous etes touche.":GOSUB7:D=INT(RND(1)*DM+1)
342 IFD>8THENA$="Attention, il est tres fort.":GOSUB7
343 PV=PV-D:IFPV<0THEN358
344 B=RND(1):IFB<.5THENA$="Attention, il a l'avantage.":GOSUB7:GOTO332
345 GOTO320
346 B=RND(1):IFB<.5THEN336
347 GOTO320
348 IFPM<-3THENA$="et disparait.":GOSUB7:Q(MO)=0:GOTO350
349 Q(MO)=INT(RND(1)*13+1)
350 PR=PR+1:GOSUB495:RETURN
351 IFSO=0THENA$="Vous n'avez plus aucun sort.":GOSUB7:GOTO332
352 SO=SO-1:A$="Vous lancez un sort.":GOSUB7:B=RND(1)
353 IFB<.25THENA$="Le monstre contre le sort.":GOSUB7:PRINT:GOTO332
354 D=8:A$="Le sort atteind le monstre.":GOSUB7:PM=PM-D
355 IFPM<0THENA$="et le foudroie.":GOSUB7:PRINT:PR=PR+1:Q(MO)=0:GOSUB495:RETURN
356 GOTO320
357 P=PC:GOSUB43:GOTO9
358 LOCATE0,10:CLS
359 A$="JE SUIS LE SEUL MAITRE DE CETTE CITADELLE. AINSI FINISSENT CEUX QUI OSENT M'AFFRONTER.":GOSUB7:PRINT
360 A$="HA!HA! VOS RESTES NE SERONT QUE LA MARQUE DE MON POUVOIR.":GOSUB7:PLAY"T100V10L16O3FCO2G#O3CO2G#FL3D","T100V8L16O2FCO1G#O2CO1G#FL3D"
361 LOCATE15,9:A$="G A M E   O V E R":LOCATE4,13:A$="Voulez-vous rejouer ":GOSUB4:INPUTA$
362 IFLEFT$(A$,1)="O"THENRUN
363 CLS:END
370 CONSOLE9,15:CLS:O=0:X=0:PRINT"Vous possedez ":PRINT
371 FORA=27TO40:IFP(A-26)=0THENA$=N$(A-26)+" ":GOSUB4:O=O+1
372 NEXTA
373 IFO=0THENA$="Votre courage pour seul bien.":LOCATE1,11:COLOR7,0,0:GOSUB7
374 GOTO9
375 IFM<>0THENGOTO40
376 GOSUB43:GOTO9
377 GOSUB42:IFMO=6THEN379
378 IFMO<>0THENGOSUB310:GOSUB43:GOTO378
379 GOTO9
380 LOCATE0,9:A$="EXPERIENCE :          ":GOSUB4:PRINTPR*100:A$="VIE :                 ":GOSUB4:PRINTPV
381 A$="ATTAQUE :             ":GOSUB4:PRINTDO
382 A$="DEFENSE :             ":GOSUB4:PRINTAC
383 A$="NOMBRE DE SORTS :     ":GOSUB4:PRINTSO
384 A$="TEMPS DE JEU : "+STR$(INT((TIME-TS)/21600))+" minutes":GOSUB4
385 GOTO9
386 IFNB=6ORM<27THEN501
387 IFP(M-26)<>PTHEN500
388 P(M-26)=0:NB=NB+1
389 IFM=29ANDP=6THENP=8:A$="Vous glissez dans l'oubliette!":GOSUB8:GOSUB495:GOSUB43:GOTO378
390 IFM=34THEN309
391 IFM=39THENDO=15
392 IFM=40THENAC=AC-5
393 A$="Vous prenez l'objet.":GOSUB5:GOTO9
394 IFNB=0ORM<27THEN502
395 IFP(M-26)<>0THEN500
396 P(M-26)=P:NB=NB-1
397 IFM=39THENDO=5
398 IFM=40THENAC=AC+5
399 A$="Vous posez l'objet sur le sol.":GOSUB5:GOTO9
400 A$=A$+" souterrain.":GOSUB8:A$=R$+"une table de pierre.":GOSUB8:A$="A l'est"+P$:GOSUB8:RETURN
405 A$=A$+"e chambre. Il ya deux couches, une table et unearmoire.":GOSUB8:RETURN
410 A$=A$+" couloir. Deux grandes statues se dressent de  part et d'autre d'un tableau.":GOSUB8:RETURN
415 A$=A$+"e salle des":GOSUB8:A$="supplices. Du sang sec couvre":GOSUB8:A$="sol et murs. Au sud"+P$:GOSUB8:COLOR3,0,0:A$=R$+Q$(6)
416 IFMG=1THENA$=A$+" mort."
417 GOSUB8:RETURN
420 A$=A$+" escalier qui  mene aux oubliettes.":GOSUB8:A$="A l'ouest une cellule. Au nord"+P$:GOSUB8:RETURN
425 A$=A$+"e cellule.":GOSUB8:A$=R$+"de la paille sur le sol.":GOSUB8:RETURN
430 A$=A$+" cachot.":GOSUB8:A$="Dans un coin sombre est assis unsquelette au cote d'une planche.":GOSUB8:RETURN
435 A$=A$+"e oubliette.":GOSUB8:A$="Le sol est dalle.":GOSUB8:A$="A l'ouest, une grille.":GOSUB8:RETURN
440 A$=A$+" couloir.":GOSUB8:A$="Le sol est etonnamment lisse.":GOSUB8:RETURN
445 A$=A$+"e salle ronde.":GOSUB8:A$="Au centre se dresse une colonne.A l'ouest, une porte.":GOSUB8:RETURN
450 A$=A$+"e etuve. "+R$:GOSUB4:A$="une armoire, un fauteuil et une":GOSUB4:A$="table. A l'est"+P$:GOSUB8:RETURN
455 A$=A$+"e cour fermee.":GOSUB8:A$=R$+"un puits en son centre.":GOSUB8:RETURN
465 A$=A$+"e salle sombre":GOSUB8:A$="Vous distinguez au centre, une  colonne, au nord, une grille et ":GOSUB4:A$="a l'ouest"+P$:GOSUB8:RETURN
470 A$=A$+"e salle d'arme.Sur le mur nord, une sculpture. Au sud, une grille.":GOSUB8:RETURN
475 A$=A$+" laboratoire d'alchimie. Il y a une table et unfauteuil.":GOSUB8:RETURN
480 U$="Je ne comprends pas :":S$="Vous etes dans un":R$="Il y a ":Q$="Il n'y a ":P$=", une porte."
481 DATA3,2,,5,1,,,,,1,,,,,3,,,,1,6,,,5,,,,,1,,,,,,16,,12,,9,,,,,10,,,,9,,,,14,,,,,,,14,,,9,,,
482 DIMT(16,4):FORA=1TO16:FORB=1TO4:READT(A,B):NEXTB,A
483 DATAREG,SCO,PRE,POS,NOR,SUD,EST,OUE,INV,OUV,ENF,FOU,EXA,APP,BAI,CAP,TOR,SOU,DES,FOR,ENT,TOU,DEC,LIS,REM,AVA,JOI,DIS,INT,QUE,ABA,PAR,PLA
484 DIMV$(33):FORA=1TO33:READV$(A):NEXTA
485 DATATAB,POR,TIR,COU,SQU,ARM,STA,YEU,BRA,GUE,SAL,PAI,PLA,DAL,GRI,CER,COL,FAU,TOI,PUI,SCU,MAI,MIS,KRA,SOL,TRO
486 DIMM$(40):FORA=1TO26:READM$(A):NEXTA
487 DATAHEA,un heaume,-12,ECU,des ecus d'or,-2,CLE,une clef,-6,DIA,un diamant,-8,GRA,un grappin,-11,COR,une corde,-2
488 DATAPAR,un parchemin,-11,ESP,l'espadon de Kranz,-15,BAR,une barre,5,MED,une medaille,-4,CAL,un calice,7,MAN,un manuscrit,-16
489 DATAHAC,une hache,3,BOU,le bouclier d'Oran,0
490 DIMN$(14),P(14):FORA=27TO40:READM$(A),N$(A-26),P(A-26):NEXTA
491 DATAun assassin,2,un magicien,16,une lich,7,un orc,5,une harpie,15,un guerrier,4,un squelette,13,un gnoll,11,un bugbear,9
492 FORA=1TO9:READQ$(A),Q(A):NEXTA
493 RETURN
495 A$="":LOCATE 10,15:COLOR1,0,0:PRINT"Pressez une touche...";
496 K$=INKEY$:IF K$=""THEN496
497 RETURN
498 A$=Q$+"pas d'issue de ce cote.":GOSUB6:P=PC:RETURN
499 A$=Q$+"rien de special.":GOSUB6:GOTO9
500 A$="Vous ne pouvez pas faire cela.":GOSUB6:GOTO9
501 A$="Vous etes trop charge.":GOSUB6:GOTO9
502 A$="Vous ne l'avez pas.":GOSUB6:GOTO9
503 A$="Vous l'avez deja fait.":GOSUB6:GOTO9
504 IFT(5,1)=4THEN503
505 A$="Vous n'y arrivez pas.":GOSUB5:GOTO9
506 IFV=1ORV=13ORV=12THEN499
507 GOTO500
508 A$="C'est ferme.":GOSUB5:GOTO9
511 IFP(12)<>-16THEN499
512 A$=R$+N$(12):P(12)=16:GOSUB5:GOTO9
514 IFP(12)<>0THEN502
515 A$="Vous apprenez de puissants sorts":SO=SO+5:GOSUB5:GOTO9
516 A$="Que faites-vous ":GOSUB4:INPUTA$:RETURN
517 IFMG=1THEN499
518 A$="Il vous defie mais semble faible":GOSUB5:GOTO9
519 PLAY"T100V10L64O5CO6CO7CV8CV6GV4C","T100V10L64O4CO5CO6CGD#CV8CV6GV4C":GOSUB5:RETURN
520 PLAY"V10L8O2FF#GL4G#","V10L8O3CC#DL4D#":GOSUB5:RETURN
9028 CV8CV6GV4C":GOSUB5:RETURN

