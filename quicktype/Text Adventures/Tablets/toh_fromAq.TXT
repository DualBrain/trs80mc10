
20 RESTORE
30 GOTO4440
40 DATA43
50 DATA01N,02S,03E,04W,05UP,06DOWN,07OUT,08ENTE,09READ,10NOTI,11CLIM
60 DATA12TREE,13MAKE,15BAIL,15USE,16TAKE,17DROP,18EGG,19BOTT,20SCYT
70 DATA21RAFT,22LAMP,23MATC,24BOOK,15EMPT,26FIRS,27SECO,28KEY,29FILL
80 DATA15LIGH,30STRI,31ROCK,32BACK,33ABRA,34MOVE,35FLOO,36LOOK,37LIST
90 DATA38QUIT,39HELP,40ROPE,41LOGS,42BREA
100 DATA37,01020409,010302010305,011102020305,0603,020203060403,0405
110 DATA07060608,0507,0301,0111,0203,02110314,0425,0412,011602140405
120 DATA012302150817,03180716,022103190417,01200218,0219,0118,0121
130 DATA03160424
140 DATA02260336,012403130426,0325,013002280724,0127022903290437,0128
150 DATA01310227,0133023003300432,0331,02310437,0233,0534,0311
160 DATA01330228
170 DATA16
180 DATAA BIRD'S EGG,4,AN OLD EMPTY BOTTLE,8,A SCYTHE,8,A RAFT,0
190 DATAA FULL BOTTLE,0,A LAMP,36,A MATCH,13,A LIT LAMP,0,A HEAVY BOOK,25
200 DATAA BALL OF STRING,28,THE FIRST TABLET,35,THE SECOND TABLET,22
210 DATAA RUSTY KEY,10,A BURNT MATCH,0
220 DATASOME LOGS,5,SOME ROPE,5
230 DATA8,A01D11D12*D16*,A06E01*D04*,A11E02*D05*,A21E04*D07*,A14E03*D06*
240 DATAA33E05*D11*,A34E06*D12*,E09*D20*
250 DATA69
260 DATA0910A01*D01*,1112A03*I04K*,1618B01*B01J*,1718D01*C01H01D02*
270 DATA0100A11E02*I12K*,1321B15B16*C15C16H15H16D03G04*,1621B04*B04J*
280 DATA1721D04*C04J*
290 DATA1521A09D04*I10K*,1628B13*B13J*,1728D13*C13J*,1528A06D13*D04E01*
300 DATA0800A06E01*I07K*,1619B02*B02J*,1719D02*C02J*,1620B03*B03J*
310 DATA1720D03*C03J*,2919A09*C02H02B05J*,1519A11D05*D05E02C05H05B02*
320 DATA1520A14D03*D06E03*,1519A21D02*D07E04*,0200A21E04*I22K*
330 DATA1627B12*B12J*,1727D12*C12J*,1522D06*E08D08*
340 DATA3023D07D06E08*C07C06H07H06B14B08J*
350 DATA3023D07D15E07*C07C15H07H15B14E09D20*,1623B14*B14J*
360 DATA1723D14*C14J*,1622B06*B06J*,1722D06*C06J*,1622B08*B08J*
370 DATA1722D08*D14*,1623B07*B07J*,1723D07*C07J*,1131A12*D08*
380 DATA1624B09*B09J*,1724D09*C09J*,0924D09*D09*,0932A01*D10*
390 DATA3300A24*I27K*,1630B10*B10J*,1730D10*C10J*,1520A33D03*D11E05*
400 DATA0100A33E05*I34K*,3435A34*D12E06*,0600A34E06*I35K*,1626B11*B11J*
410 DATA1726D11*C11J*,0800A32D13*I02K*,0800A32C13*D13*
420 DATA3600*K*,3700*A*,3800*L*,3900*D15*
430 DATA0100A14E03*I15K*
440 DATA1640B16*B16J*,1740D16*C16J*,1641B15*B15J*,1741D15*C15J*
450 DATA4221B04*C04H04G15G16J*,1719D05*C05J*,1619B05*B05J*
460 DATA1519D05*C05H05B02J*,1541*E07D08*,3023D07*C07H07B14J*
470 DATA1540A12D16*I13K*
480 DATA0600A13*D08*,1540A13D16*I12K*
490 DATA33,0,52,6,2,205,89,30,201
500 ROOM=1
510 CLS
520 C(9)=0
530 REM UI=USR(0)
540 GOSUB2710
550 IFRO=FFTHENC(9)=1
560 REM POKE14421,CC:UI=USR(0)
570 FORQQ=0TO31:PRINTCHR$(131);:NEXT
580 X=0
590 FORW=1TOO
600 IFO(W)<>ROORN(W)=1THENGOTO630
610 IFX=0THENPRINT:PRINT"THERE IS ALSO:-":X=1
620 PRINTO$(W)
630 NEXT
640 FORL=1TOC
650 T$=C$(L)
660 GOTO1200
670 NEXT
680 PRINT:PRINT"WHAT SHALL I DO NOW?"
690 IFRO=17ANDN(8)=0THENQW=QW+1:IFQW=10THENGOTO4270
700 IFRO=27ANDN(8)=0THENQW=QW+1:IFQW=10THENGOTO4270
710 JJ=0
720 INPUTY$
730 IFY$=""THENGOTO680
740 S$=""
750 X=1
760 W=1
770 IFMID$(Y$,W,1)=" "THENGOTO830
780 IFW/5=INT(W/5)THENGOTO830
790 W$(X)=LEFT$(Y$,W)
800 W=W+1
810 IFW>LEN(Y$)THENGOTO890
820 GOTO770
830 Y$=RIGHT$(Y$,LEN(Y$)-1)
840 IFLEN(Y$)=0THENGOTO890
850 IFASC(Y$)<>32THENGOTO830
860 Y$=RIGHT$(Y$,LEN(Y$)-1)
870 X=X+1
880 GOTO760
890 FORW=1TOX
900 FORX=1TOV
910 Y$=RIGHT$(V$(X),LEN(V$(X))-2)
920 IFW$(W)=Y$THENS$=S$+LEFT$(V$(X),2)
930 NEXT
940 NEXT
950 RI=RO
960 R$=""
970 T$=R$(RO)
980 IFLEFT$(T$,2)=S$THENR$=MID$(T$,3,2)
990 IFR$<>""THENGOTO1030
1000 IFLEN(T$)=0THENGOTO1050
1010 T$=RIGHT$(T$,LEN(T$)-4)
1020 GOTO980
1030 RO=VAL(R$)
1040 IFRI<>ROTHENGOTO510
1050 IFS$=""THENPRINT"I DON'T UNDERSTAND THAT.":GOTO680
1060 IFRO=11ORRO=14ORRO=21ORRO=33THENGOTO1090
1070 IFVAL(S$)<5THENPRINT"I CAN'T GO THAT WAY.":GOTO680
1080 IFLEN(S$)>4THENS$=LEFT$(S$,4)
1090 IFLEN(S$)=2THENS$=S$+"00"
1100 W=0:J$=S$
1110 W=W+1:K=W
1120 IFW>ATHENPRINT"I CAN'T GO THAT WAY.":GOTO680
1130 T$=LEFT$(A$(W),4)
1140 IFT$=S$THENGOTO1180
1150 IFW<ATHENGOTO1110
1160 IFJJ=1THENPRINT"I CAN'T.":GOTO680
1170 PRINT"WHAT!?"CHR$(7):GOTO680
1180 T$=RIGHT$(A$(W),LEN(A$(W))-4)
1190 L=0
1200 P=0
1210 IFASC(T$)=42THENGOTO1360
1220 S$=LEFT$(T$,1)
1230 JJ=1
1240 F$=MID$(T$,2,2)
1250 W=VAL(F$)
1260 IFS$="A"THENGOSUB1590
1270 IFS$="B"THENGOSUB1610
1280 IFS$="C"THENGOSUB1640
1290 IFS$="D"THENGOSUB1660
1300 IFS$="E"THENGOSUB1680
1310 IFS$="F"THENGOSUB1700
1320 IFP=0ANDL<>0THENGOTO670
1330 IFP=0THENW=K:S$=J$:GOTO1110
1340 T$=RIGHT$(T$,LEN(T$)-3)
1350 GOTO1200
1360 T$=RIGHT$(T$,LEN(T$)-1)
1370 S$=LEFT$(T$,1)
1380 F$=MID$(T$,2,2)
1390 P=0
1400 W=VAL(F$)
1410 IFS$="A"THENGOSUB1720
1420 IFS$="B"THENGOSUB1790
1430 IFS$="C"THENGOSUB1840
1440 IFS$="D"THENGOSUB1880
1450 IFS$="E"THENGOSUB1900
1460 IFS$="F"THENGOSUB1920
1470 IFS$="G"THENGOSUB1940
1480 IFS$="H"THENGOSUB1960
1490 IFS$="I"THENGOSUB1980
1500 IFS$="J"THENGOSUB2000
1510 IFS$="K"THENGOSUB2030
1520 IFS$="L"THENGOSUB2050
1530 IFP=255THENGOTO510
1540 IFP=100THENGOTO680
1550 IFL<>0THENGOTO670
1560 IFASC(T$)=42THENGOTO680
1570 T$=RIGHT$(T$,LEN(T$)-3)
1580 GOTO1370
1590 IFW=ROTHENP=1
1600 RETURN
1610 IFO(W)=ROTHENP=1
1620 IFN(W)=1THENP=1
1630 RETURN
1640 IFO(W)<>ROTHENP=1
1650 RETURN
1660 IFN(W)=1THENP=1
1670 RETURN
1680 IFC(W)=1THENP=1
1690 RETURN
1700 IFC(W)=0THENP=1
1710 RETURN
1720 PRINT"I HAVE WITH ME THE FOLLOWING:-"
1730 FORX=1TOO
1740 IFN(X)=1THENPRINTO$(X):P=1
1750 NEXT
1760 IFP=0THENPRINT"**NOTHING**"
1770 P=100
1780 RETURN
1790 IFN(W)=1THENPRINT"I ALREADY HAVE IT!":RETURN
1800 IFQ=3THENPRINT"I CAN'T CARRY ANYTHING ELSE":RETURN
1810 N(W)=1
1820 Q=Q+1
1830 RETURN
1840 N(W)=0
1850 Q=Q-1
1860 O(W)=RO
1870 RETURN
1880 GOSUB2060
1890 RETURN
1900 C(W)=1
1910 RETURN
1920 C(W)=0
1930 RETURN
1940 O(W)=RO
1950 RETURN
1960 O(W)=0
1970 RETURN
1980 RO=W
1990 RETURN
2000 PRINT"OK"
2010 P=100
2020 RETURN
2030 P=255
2040 RETURN
2050 GOTO4320
2060 PRINT:IFW=16THENGOTO4220
2070 IFW<>1THENGOTO2130
2080 PRINT"IT SAYS:-"
2090 PRINT"FOR TOP QUALITY ADVENTURES AND"
2091 PRINT"OTHER  COMPUTER GAMES COME TO:-"
2100 PRINT
2101PRINT"FAWKES COMPUTING,
2102PRINT"41 WOLFRIDGE RIDE, ALVESTON,"
2110 PRINT"BRISTOL BS12 2RA."
2120 RETURN
2130 IFW<>2THENGOTO2170
2140 PRINT"SPLAT!! THE EGG HAS BROKEN AND"
2150 PRINT"BITS ARE SPREAD OUT OVER A"
2151 PRINT"LARGE AREA!!"
2160 RETURN
2170 IFW<>3THENGOTO2190
2180 PRINT"I HAVE BUILT A RAFT OUT OF THE
2181 PRINT"LOGS AND ROPE."
2190 IFW<>4THENGOTO2220
2200 PRINT"THE DOOR OF THE COTTAGE IS NOW"
2210 PRINT"OPEN. I CAN NOW ENTER TO"
2211 PRINT"WHATEVER LIES BEYOND..."
2220 IFW<>5THENGOTO2240
2230 PRINT"THE FIRE IS NOW OUT. I CAN GO"
2231 PRINT"NORTH WHERE THE FLAMES WERE..."
2240 IFW<>6THENGOTO2260
2250 PRINT"THE UNDERGROWTH HAS BEEN"
2251 PRINT"CHOPPED AWAY.I CAN NOW GO"
2252 PRINT"NORTH..."
2260 IFW<>7THENGOTO2280
2270 PRINT"THE WATER HAS BEEN CLEARED. I"
2271 PRINT"CAN NOW PROCEED SOUTH."
2280 IFW<>8THENGOTO2300
2290 PRINT"HOW?"
2300 IFW<>9THENGOTO2340
2310 PRINT"IT SAYS:-"
2320 PRINT"THE WISE ADVENTURER WHO WISHES"
2321 PRINT"TO KNOW THE MAGIC WORD SHOULD"
2330 PRINT"RETURN TO THE START AND READ"
2331 PRINT"THE BACK OF THE NOTICE THERE..."
2340 IFW<>10THENGOTO2370
2350 PRINT"IT SAYS:-"
2360 PRINT"GO TO THE DEVIL'S ROCKS AND SAY"
2361 PRINT"THE MAGIC WORD, ABRACADABRA..."
2370 IFW<>11THENGOTO2390
2380 PRINT"THE COBWEBS HAVE BEEN CHOPPED"
2381 PRINT"DOWN. I CAN NOW GO NORTH..."
2390 IFW<>12THENGOTO2420
2400 PRINT"I HAVE MOVED AWAY PART OF THE"
2401 PRINT"FLOOR. SOME STAIRS LEAD DOWN..."
2410 IFW=16THENGOTO4260
2420 IFW<>13THENGOTO2450
2430 PRINT"AAAARGH! I HAVE BEEN KILLED. I"
2431 PRINT"ENTERED THE PASSAGE WITHOUT THE"
2432 PRINT"KEY!"
2440 GOTO4400
2450 IFW<>14THENGOTO2510
2460 IFRO>16ANDRO<23THENGOTO2500
2470 IFRO>27ANDRO<36THENGOTO2500
2480 N(8)=0:O(8)=RO:Q=Q-1
2490 PRINT"OK":RETURN
2500 PRINT"HOW WILL I SEE, DUMMY?"
2510 IFW<>15THENGOTO2670
2520 PRINT"THIS IS A TOP QUALITY ADVENTURE"
2521 PRINT"FROM FAWKES COMPUTING."
2522 PRINT
2530 IFRO>10THENGOTO2550
2540 PRINT"YOU'LL HAVE TO WORK OUT THIS"
2541 PRINT"C-RAFT-Y ADVENTURE ON YOUR OWN."
2550 IFRO<>11THENGOTO2570
2560 PRINT"DON'T BOTTLE UP YOUR ANGER AT"
2561 PRINT"BEING STUCK - EMPTY IT OUT."
2570 IFRO=14ORRO=33THENPRINT"YOU'LL HAVE TO HACK YOUR WAY":PRINT"OUT ALONE"
2580 IFRO=21THENPRINT"I'M NOT GOING TO BAIL YOU OUT":PRINT"THIS TIME."
2590 IFRO>23ANDRO<27THENPRINT"THERE'S A WHOLE LIBRARY OF":PRINT"CLUES TO BE FOUND"
2600 IFRO=34THENPRINT"I THINK I HAVE GOT YOU FLOORED!"
2610 IFRO=27ORRO=17THENPRINT"SORRY- I CAN'T SHED ANY LIGHT":PRINT"ON THE  PROBLEM"
2620 IFRO=19ORRO=37THENPRINT"YOU ARE GULL-ABLE, UNLIKE THE"
2630 IFRO=19ORRO=37THENPRINT"GLASS SEEMS TO BE. HAVE YOU":PRINT"READ IT?"
2640 IFRO=12ORRO=13THENPRINT"YOU'LL HAVE TO BREAK THIS":PRINT"CRAFTY ONE YOURSELF."
2650 PRINT
2651 PRINT"WORDS I KNOW INCLUDE LOOK,"
2652 PRINT"LIST, TAKE, DROP, USE, READ,"
2660 PRINT"CLIMB, QUIT, HELP, STRIKE,"
2661 PRINT"FILL, AND MANY MANY MORE."
2670 IFW<>20THENRETURN
2680 PRINT"  THERE IS A BONFIRE OF LOGS"
2681 PRINT"BLAZING  CHEERFULLY AWAY."
2690 IFFF=0THENFF=RO
2700 RETURN
2710 IFRO<>1THENGOTO2750
2720 CC=2
2730 PRINT"  I AM AT THE START OF A FOREST"
2731 PRINT"TRAIL. A NOTICE STANDS NEARBY."
2740 PRINT"I CAN GO NORTH AND WEST."
2750 IFRO<>2THENGOTO2790
2760 PRINT"  I AM IN A FOREST CLEARING."
2761 PRINT"SHORT SPRINGY GRASS GROWS"
2770 PRINT"UNDERFOOT."
2780 PRINT"I CAN GO NORTH, SOUTH AND EAST."
2790 IFRO<>3THENGOTO2840
2800 CC=2
2810 PRINT"  I AM STANDING IN THE MIDDLE"
2811 PRINT"OF A CLEARING. A LARGE TREE"
2820 PRINT"GROWS NEARBY."
2830 PRINT"I CAN GO NORTH, SOUTH AND EAST."
2840 IFRO<>4THENGOTO2880
2850 PRINT"  I AM AT THE TOP OF A TALL"
2851 PRINT"TREE. WHAT A VIEW! I CAN ALSO"
2860 PRINT"SEE A BIRD'S NEST."
2870 PRINT"I CAN GO DOWN."
2880 IFRO<>5THENGOTO2920
2890 PRINT"  THE PATH GOES PAST WHERE SOME"
2891 PRINT"TREE TRUNKS HAVE BEEN SAWN UP."
2900 PRINT"THERE IS SOME SAWDUST ON THE"
2901 PRINT"GROUND."
2910 PRINT"I CAN GO SOUTH, EAST OR WEST."
2920 IFRO<>6THENGOTO2960
2930 CC=2
2940 PRINT"  I AM OUTSIDE AN OLD DESERTED"
2941 PRINT"COTTAGE. THE DOOR IS LOCKED."
2950 PRINT"I CAN GO WEST."
2960 IFRO<>7THENGOTO3010
2970 CC=32
2980 PRINT"  I AM INSIDE THE COTTAGE. IT"
2981 PRINT"IS ALL DAMP AND MUSTY."
2990 PRINT"A STAIRWAY LEADS DOWN."
3000 PRINT"I CAN GO OUT."
3010 IFRO<>8THENGOTO3030
3020 PRINT"  I AM IN A CELLAR. IT IS VERY"
3021 PRINT"DARK."
3022 PRINT" A STAIRWAY LEADS UP..."
3030 IFRO<>9THENGOTO3070
3040 CC=43
3050 PRINT"  THE PATH LEADS TO THE EDGE OF"
3051 PRINT"A POND"
3060 PRINT"I CAN GO EAST."
3070 IFRO<>10THENGOTO3100
3080 PRINT"  I AM NOW THE OTHER SIDE OF"
3081 PRINT"THE POND. THE GROUND IS DAMP"
3082 PRINT"HERE."
3090 PRINT"I CAN GO NORTH."
3100 IFRO<>11THENGOTO3150
3110 CC=62
3120 PRINT"  I AM IN AN ENCHANTED"
3121 PRINT"CLEARING. A WALL OF FIRE RAGES"
3130 PRINT"IN FRONT OF ME."
3140 PRINT"I CAN GO SOUTH."
3150 IFRO<>12THENGOTO3190
3160 CC=58
3170 PRINT"  I AM AT THE BASE OF THE MAGIC"
3171 PRINT"ROCK.
3180 PRINT"I CAN GO UP IT, SOUTH, OR EAST."
3190 IFRO<>13THENGOTO3230
3200 PRINT"  I AM AT THE TOP OF THE MAGIC"
3201 PRINT"ROCK. I CAN GET QUITE A NICE"
3210 PRINT"VIEW FROM HERE..."
3220 PRINT"I CAN GO DOWN OR WEST."
3230 IFRO<>14THENGOTO3260
3240 PRINT"  I CAN'T GET ANY FURTHER THIS"
3241 PRINT"WAY AS THE PATH IS VERY"
3242 PRINT"OVERGROWN."
3250 PRINT"I CAN GO WEST."
3260 IFRO<>15THENGOTO3290
3270 PRINT"  I AM IN AN OLD DRIED UP"
3271 PRINT"STREAM BED."
3280 PRINT"I CAN GO NORTH, SOUTH, OR WEST."
3290 IFRO<>16THENGOTO3330
3300 CC=58
3310 PRINT"  I AM BY THE ENTRANCE OF A"
3311 PRINT"POT-HOLE."
3320 PRINT"I CAN GO NORTH OR SOUTH."
3330 IFRO<>17THENGOTO3420
3340 IFN(8)=1THENGOTO3380
3350 PRINT"  IT IS DARK IN HERE. I BETTER"
3351 PRINT"GET SOME LIGHT QUICKLY OR "
3360 PRINT"I MAY BE IN TROUBLE..."
3370 R$(17)="":CC=112:GOTO3420
3380 R$(17)="03180716"
3390 PRINT"  I AM IN A DRAINPIPE-LIKE"
3391 PRINT"TUNNEL."
3400 PRINT"I CAN GO EAST OR OUT."
3410 QW=0:CC=49
3420 IFRO<>18THENGOTO3450
3430 PRINT"  I AM IN A LONG FLAT TUNNEL."
3440 PRINT"I CAN GO SOUTH, EAST, OR WEST."
3450 IFRO<>19THENGOTO3490
3460 PRINT"  I AM IN A HUGE CATHEDRAL-LIKE"
3461 PRINT"CHAMBER. THROUGH THICK GLASS"
3470 PRINT"I CAN SEE THE CHAMBER OF"
3471 PRINT"ECHOES!"
3480 PRINT"I CAN GO NORTH OR SOUTH."
3490 IFRO<>20THENGOTO3530
3500 PRINT" THE CAVE FILTERS OUT TO A"
3501 PRINT"DEAD-END. I CAN'T GET ANY"
3510 PRINT"FURTHER THIS WAY."
3520 PRINT"I CAN GO SOUTH."
3530 IFRO<>21THENGOTO3560
3540 PRINT"  THERE IS A DIP IN THE TUNNEL"
3541 PRINT"WHICH IS FLOODED!!"
3550 PRINT"I CAN GO NORTH."
3560 IFRO<>22THENGOTO3600
3570 PRINT"  I AM IN A SMALL MUSTY"
3571 PRINT"CHAMBER. IT APPEARS TO BE A"
3572 PRINT"DEAD END."
3580 PRINT"I CAN'T GET ANY FUTHER THIS"
3581 PRINT"WAY."
3590 PRINT"I CAN GO NORTH."
3600 IFRO<>23THENGOTO3630
3610 PRINT"  I AM IN A DEEP SIDED RAVINE."
3620 PRINT"I CAN GO EAST OR WEST."
3630 IFRO<>24THENGOTO3670
3640 CC=58
3650 PRINT"  I AM BY THE DEVIL'S ROCKS."
3651 PRINT"THESE FORM A LARGE SANDY CLIFF."
3660 PRINT"I CAN GO SOUTH OR EAST."
3670 IFRO<>25THENGOTO3700
3680 PRINT"  I AM ON A GENTLE HILL-SLOPE."
3690 PRINT"I CAN GO NORTH, EAST, OR WEST."
3700 IFRO<>26THENGOTO3740
3710 PRINT"  HOLY ****! THE PATH LEADS TO"
3711 PRINT"A CLIFF EDGE. I HAD BETTER GET"
3720 PRINT"AWAY FROM HERE QUICK IN CASE"
3721 PRINT"THE PATH CRUMBLES..."
3730 PRINT"I CAN GO EAST."
3740 IFRO<>27THENGOTO3830
3750 IFN(8)=1THENGOTO3790
3760 PRINT"  IT IS DARK IN HERE. I HAD"
3761 PRINT"BETTER GET SOME LIGHT QUICKLY"
3770 PRINT"OR I MAY BE IN TROUBLE..."
3780 R$(27)="":CC=112:GOTO3830
3790 R$(27)="013002280724"
3800 PRINT"  I AM INSIDE A SMALL PASSAGE."
3810 PRINT"I CAN GO NORTH, SOUTH, OR OUT."
3820 QW=0:CC=49
3830 IFRO<>28THENGOTO3870
3840 PRINT"  I AM INSIDE THE WHISPERING"
3841 PRINT"ROOM. I CAN HEAR MY BREATH AND"
3850 PRINT"IT SOUNDS ALL  FUNNY."
3860 PRINT"I CAN GO NORTH, SOUTH, EAST,
3861 PRINT"OR WEST."
3870 IFRO<>29THENGOTO3910
3880 PRINT"  THERE IS A CAVE-IN IN FRONT"
3881 PRINT"OF ME SO I CAN'T GET ANY"
3890 PRINT"FURTHER THIS WAY."
3900 PRINT"I CAN GO NORTH."
3910 IFRO<>30THENGOTO3940
3920 PRINT"  I AM IN A DIMLY-LIT CAVERN."
3930 PRINT"I CAN GO NORTH OR SOUTH."
3940 IFRO<>31THENGOTO3970
3950 PRINT"  I AM AT A JUNCTION OF"
3951 PRINT"PASSAGES."
3960 PRINT"I CAN GO NORTH, SOUTH, EAST."
3961 PRINT"OR WEST."
3970 IFRO<>32THENGOTO4020
3980 PRINT"  I AM AT THE ENTRANCE TO THE"
3981 PRINT"PASSAGE OF DEATH. A LARGE SIGN"
3990 PRINT"SAYS BEWARE ALL YE WHO ENTER"
3991 PRINT"HERE WITHOUT A RUSTY KEY!"
4000 PRINT"I DON'T KNOW IF IT IS A BLUFF.."
4010 PRINT"I CAN ENTER OR GO EAST."
4020 IFRO<>33THENGOTO4060
4030 PRINT"  OOEER! MY WAY IS BLOCKED BY A"
4031 PRINT"MASS  OF LARGE WHITE STICKY "
4040 PRINT"COBWEBS..."
4050 PRINT"I CAN GO SOUTH OR WEST."
4060 IFRO<>34THENGOTO4100
4070 PRINT"  I AM IN A ROOM WITH PATTERNED"
4071 PRINT"SLABS ON THE FLOOR. ONE OF THE"
4080 PRINT"SLABS SEEMS QUITE LOOSE..."
4090 PRINT"I CAN GO SOUTH."
4100 IFRO<>35THENGOTO4130
4110 PRINT"  I AM IN A LITTLE NICHE UNDER"
4111 PRINT"THE FLOOR."
4120 PRINT"I CAN GO UP THE STAIRS."
4130 IFRO<>36THENGOTO4170
4140 PRINT"  I AM UNDERNEATH A TALL PINE"
4141 PRINT"TREE. UNFORTUNATELY THERE ARE"
4150 PRINT"NO BRANCHES LOW DOWN SO I CAN'T"
4151 PRINT"CLIMB IT."
4160 PRINT"I CAN GO EAST."
4170 IFRO<>37THENGOTO4210
4180 PRINT"  I AM IN THE CHAMBER OF"
4181 PRINT"ECHOES. THROUGH THICK GLASS I"
4190 PRINT"CAN SEE THE CATHEDRAL CHAMBER.."
4200 PRINT"I CAN GO NORTH OR SOUTH."
4210 RETURN
4220 FOR QQ=0TO31:PRINT"*";:NEXT
4221 PRINT"CONGRATULATIONS- YOU HAVE WON."
4222 PRINT"YOU HAVE SOLVED THE ADVENTURE!"
4223 FOR QQ=0TO31:PRINT"*";:NEXT
4230 FORA=1TO50
4240 REM SOUND(RND(1)*255,RND(1)*255):POKE12288,RND(1)*255:POKE13312,RND(1)*9
4250 NEXT
4260 GOTO4320
4270 PRINT"HELP! I HAVE FALLEN OVER IN THE"
4271 PRINT"DARK AND BROKEN MY LEG."
4280 PRINT"I CAN'T MOVE!! NO-ONE WILL FIND"
4281 PRINT"ME DOWN HERE AND I AM SURE"
4290 PRINT"I WILL DIE..."
4300 PRINT:PRINT"SORRY..."
4310 GOTO4400
4320 PRINT
4330 IFW=16THENGOTO4400
4340 FORB=1TO5
4341 REM FORA=300TO400
4350 REM SOUND(1,400-A):NEXT
4360 PRINT:PRINT"WHAT A LOAD OF RUBBISH!!"
4370 PRINT:PRINT"QUITTER!!"
4380 REM POKE13312,B
4390 NEXT
4400 PRINT:PRINT:PRINT:PRINT"DO YOU WANT TO PLAY AGAIN?";
4410 Y$=INKEY$
4420 IFY$="Y"THENPRINT:GOTO4430
4421 IFY$="N"THENPRINT:NEW:END
4422 GOTO4410
4430 Y$=INKEY$:IFY$<>""THEN4430
4431 RUN
4440 CLS
4450 PRINTTAB(5)"TABLETS OF HIPPOCRATES"
4460 PRINTTAB(7)"(C) 1984 S.FAWKES"
4470 PRINT
4471 PRINT"  IN 435 B.C. HIPPOCRATES"
4480 PRINT"DISCOVERED A CURE FOR CANCER."
4490 PRINT"THIS WAS WRITTEN ON TWO GOLD"
4500 PRINT"TABLETS WHICH WERE THEN HIDDEN"
4501 PRINT"IN A MAGICAL FOREST AND"
4502 PRINT"FORGOTTEN."
4510 PRINT"  THE WHEREABOUTS OF THIS"
4520 PRINT"FOREST HAVE RECENTLY BEEN"
4530 PRINT"DISCOVERED. IT IS YOUR JOB TO"
4540 PRINT"FIND THE TABLETS AND RETURN"
4541 PRINT"WITH THEM TO THE START..."
4542 GOSUB6000
4550 PRINT:PRINT"  I WILL ACT AS YOUR PUPPET."
4560 PRINT"GUIDE ME WITH SIMPLE SENTENCES"
4570 PRINT"USUALLY OF VERB/ NOUN TYPE,"
4580 PRINT"E.G. TAKE CANDLE. WORDS I KNOW"
4590 PRINT"INCLUDE: N, S, E, W, UP, DOWN,"
4591 PRINT"TAKE, DROP, LOOK, LIST, QUIT,"
4600 PRINT"HELP, READ, CLIMB, MAKE, USE,"
4601 PRINT"MOVE, FILL, AND MANY MANY MORE."
4610 PRINT"YOU ARE ADVISED TO DRAW A MAP"
4611 PRINT"AS YOU  GO ALONG..."
4620 PRINT:PRINT"PRESS ENTER TO CONTINUE";
4630 CLEAR(1000):READV
4640 DIMV$(V)
4650 FORA=1TOV
4660 READV$(A)
4670 NEXT
4680 READR
4690 DIMR$(R)
4700 FORA=1TOR
4710 READR$(A)
4720 NEXT
4730 READO
4740 DIMO$(O):DIMO(O)
4750 FORA=1TOO
4760 READO$(A)
4770 READO(A)
4780 NEXT
4790 READC
4800 DIMC$(C)
4810 FORA=1TOC
4820 READC$(A)
4830 NEXT
4840 READA
4850 DIMA$(A)
4860 FORB=1TOA
4870 READA$(B)
4880 NEXT
4890 DIMN(O)
4900 DIMC(10)
4910 Q=0
4920 QW=0
4930 FORCC=14417TO14425
4940 READUI
4950 REM POKECC,UI
4960 NEXT
4970 QW=0
4980 REM POKE14340,81
4990 REM POKE14341,56
5000 CC=2
5010 INPUTY$
5020 GOTO500
6000 PRINTTAB(6)"<< PRESS SPACE BAR >>";
6010 Y$=INKEY$:IFY$<>" "THEN 6010
6020 Y$=INKEY$:IFY$<>""THEN 6020
6030 PRINT
6040 RETURN
