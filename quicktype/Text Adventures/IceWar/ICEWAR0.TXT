
10 CLS
20 CLEAR300
30 PRINT@233,CHR$(34);"ICE WORLD WAR";CHR$(34);
50 PRINT@489,"BY KIT DOMENICO"
60 PRINT"        MODIFIED FOR COCO"
70 PRINT"        BY J.R. APPLEGATE"
100 READIT
105 PL=6
106 TM=1
107 H1=1
108 H2=1
110 DIM NA$(IT),NM$(IT),LO(IT)
120 FORI=1TOIT
130 READ NA$(I),NM$(I),LO(I)
140 NEXT I
150 READ LO
160 DIM DE$(LO),CO(LO,6),LI(LO)
170 FOR I=0 TO LO-1
180 READ DE$(I),CO(I,1),CO(I,2),CO(I,3),CO(I,4),CO(I,5),CO(I,6),LI(I)
190 NEXT I
210 LC=1
500 CLS
501 IF TM<20 THEN 505
502 IF H2=1 THEN PRINT@235,"I BLED TO DEATH!":GOTO 4295
503 IF H1=1 THEN PRINT@235,"I FROZE TO DEATH!":GOTO 4295
505 IF(LI(LC)=0ANDL=0)OR(L=1ANDLO(10)<>LCANDLO(10)<>99)THENPRINT"IT'S DARK IN HERE!":GOTO 690
510 PRINT "I AM IN ";DE$(LC)
515 IF FO=1 THEN LO(9)=LC
530 PRINT "I CAN SEE ";
535 IS=0
540 FOR I=1 TO IT
550 IF LO(I)<>LC THEN 580
560 IF POS(X)+LEN(NA$(I))+LEN(NM$(I))+1>=64 THEN PRINT:PRINTTAB(5);
570 PRINT NM$(I);""NA$(I);",";:IS=IS+1
580 NEXT I
583 IF IS=0 THEN PRINT"NOTHING AT ALL.";
585 PRINT
590 PRINT"THE EXITS ARE ";
600 FOR I=1 TO 6
610 IF CO(LC,I)=0 THEN 680
620 IF I=1 THEN PRINT"NORTH,";
630 IF I=2 THEN PRINT"EAST,";
640 IF I=3 THEN PRINT"SOUTH,";
650 IF I=4 THEN PRINT"WEST,";
660 IF I=5 THEN PRINT"UP,";
670 IF I=6 THEN PRINT"DOWN.";
680 NEXT I
685 PRINT
690 PRINT STRING$(63,"+")
800 A$="":V$="":N$=""
801 TM=TM+1
802 IF TM>360 THEN 5000
805 INPUT"WHAT SHOULD I DO";A$
810 IF LEN(A$)=1 THEN N$=A$:GOTO 1200
820 V$=LEFT$(A$,3)
830 FOR I=1 TO LEN(A$)
840 IF MID$(A$,I,1)=" "THEN N$=LEFT$(RIGHT$(A$,LEN(A$)-I),3):GOTO 860
850 NEXT I
860 REM
870 IF V$="GO " THEN 1200
871 IF V$="RUN" THEN 1200
872 IF V$="WAL" THEN 1200
880 IF V$="GET" THEN 1400
881 IF V$="TAK" THEN 1400
882 IF V$="GRA" THEN 1400
883 IF V$="CAR" THEN 1400
884 IF V$="MOV" THEN 1400
890 IF V$="DRO" THEN 1700
891 IF V$="PUT" THEN 1700
892 IF V$="THR" THEN 1700
893 IF V$="REM" THEN 1700
900 IF V$="REA" THEN 1900
910 IF V$="WEA" THEN 2000
920 IF V$="CUT" THEN 2200
930 IF V$="LIG" THEN 2400
940 IF V$="SHO" THEN 2600
942 IF V$="BLA" THEN 2600
943 IF V$="FIR" THEN 2600
950 IF V$="BAN" THEN 2800
960 IF V$="STI" THEN 2800
970 IF V$="OPE" THEN 3000
975 IF V$="UNL" THEN 3000
980 IF V$="FIL" THEN 3200
990 IF V$="SAY" THEN 3400
992 IF V$="YEL" THEN 3400
994 IF V$="CAL" THEN 3400
1000 IF V$="INV" THEN 3600
1010 IF V$="LOO" THEN 3800
1020 IF V$="RUB" THEN 4000
1030 IF V$="PRE" THEN 4200
1040 IF V$="PUS" THEN 4200
1050 IF V$="FEE" THEN 4500
1060 IF V$="QUI" THEN CLS:PRINT@200,"":GOTO 4290
1180 PRINT"SORRY. I AM UNABLE TO COMPLY WITH THAT ORDER, IN THAT FORM."
1190 GOTO 800
1200 N$=LEFT$(N$,1)
1205 IF LC=18 OR LC=20 GOSUB 6500
1210 IF N$="N" AND CO(LC,1)<>0 THEN LC=CO(LC,1):GOTO 500
1220 IF N$="E" AND CO(LC,2)<>0 THEN LC=CO(LC,2):GOTO 500
1240 IF N$="S" AND CO(LC,3)<>0 THEN LC=CO(LC,3):GOTO 500
1260 IF N$="W" AND CO(LC,4)<>0 THEN LC=CO(LC,4):GOTO 500
1280 IF N$="U" AND CO(LC,5)<>0 THEN LC=CO(LC,5):GOTO 500
1300 IF N$="D" AND CO(LC,6)<>0 THEN LC=CO(LC,6):GOTO 500
1320 PRINT"I CANNOT GO THAT DIRECTION."
1340 GOTO 800
1400 IF N$="INV" THEN 3600
1405 IF PO>=PL THEN PRINT"I AM CARRYING TOO MUCH ALREADY. I CAN'T PICK ANYTHING ELSE UP.":GOTO 800
1410 FOR I=1 TO IT
1420 IF N$<>LEFT$(NA$(I),3) THEN 1500
1430 IF LO(I)<>LC THEN 1500
1440 IF NA$(I)="SLED" OR LEFT$(NA$(I),4)="GATE" OR NA$(I)="SNOWBEAST" OR NA$(I)="ALIEN" THEN PRINT"NO WAY! THAT'S OUT OF THE QUESTION!":GOTO 800
1450 IF NA$(I)="WIRES" AND LC=26  THEN PRINT"THEY'RE FASTENED SECURELY TO THE LASER MOUNT.":GOTO 800
1455 IF LC=9 AND I=14 AND LO(27)=0 THEN LO(27)=LC
1460 LO(I)=99
1465 PO=PO+1
1470 PRINT"GOT IT.":GOTO 500
1500 NEXT I
1505 PRINT"I CAN'T PICK THAT UP."
1510 GOTO 800
1700 REM
1710 FOR I=1 TO IT
1720 IF LEFT$(NA$(I),3)<>N$ THEN 1800
1730 IF LO(I)<>99 AND LO(I)<>98 THEN 1800
1740 LO(I)=LC:PO=PO-1
1750 IF I=3 THEN H1=1
1760 GOTO 500
1800 NEXT I
1810 PRINT"YOU DON'T HAVE THAT."
1820 GOTO 800
1900 IF LI(LC)=0 AND L=0 THEN PRINT"I CAN'T READ IN THE DARK!":GOTO 800
1905 IF N$="PAN" AND LC=1 THEN PRINT"THE WARNING LIGHTS SAY 'MASSIVE DAMAGE!'":PRINTTAB(23)"'FIRE IMMINENT!'":GOTO 800
1907 IF N$="PAN" AND LC=32 THEN PRINT"IT HAS A BUTTON MARKED 'LAUNCH TO ORBIT'.":GOTO 800
1910 IF N$="PAN" AND LC=17 THEN PRINT"THE WARNING LIGHTS SAY '&%xxx **** !'":GOTO 800
1915 IF N$="BAD" AND (LO(4)=99 OR LO(4)=LC) THEN PRINT"IT SAYS '%##-!+'":GOTO 800
1920 IF N$="GAU" AND LC=11 AND0GA=0 THEN PRINT"IT SAYS 'EMPTY'.":GOTO 800
1925 IF N$="ORD" AND LO(21)<>99 AND LO(21)<>LC THEN PRINT"WHAT ORDERS?":GOTO 800
1930 IFN$="ORD"AND(LO(21)=LCORLO(21)=99) AND LI(LC)=1 THEN PRINTTAB(20)"ATTENTION":PRINT"RETURN THE HASLAN AMBASSADOR AND THE TREATY TO THE ORBITOR":
1935 PRINT"IN ORBIT AROUND THE PLANET. THIS WILL END THE WAR BETWEEN OUR":PRINT"PLANETS.":GOTO 800
1940 IF N$="WAT" AND (LO(2)=99 OR LO(2)=98 OR LO(2)=LC) THEN PRINTTAB(5)"IT SAYS '";(3-INT(TM/90));"ZHORG AND ";((INT(TM/90)+1)*90-TM); "QUAKL' BEFORE THE PLANET DIES.":GOTO 800
1950 IF N$="LET" AND (LO(11)=99 OR LO(11)=LC) AND NM$(11)="AN OPEN " THEN PRINT TAB(5)"IT SAYS 'MORE ADVENTURES ARE COMING. FROM THIS AND OTHER"
1955 PRINT TAB(14)"AUTHORS. ENJOY, ENJOY.'":PRINTTAB(5)"'P.S. THIS IS THE TREATY. TAKE CARE OF IT.'":GOTO 800
1970 PRINT"I CAN'T READ THAT. SORRY."
1980 GOTO 800
2000 IS=0
2005 FOR I=1 TO 5
2010 IF LEFT$(NA$(I),3)<>N$ THEN 2060
2020 IF LO(I)=98 THEN PRINT"I ALREADY HAVE THAT ON.":GOTO 800
2030 IF LO(I)<>99 THEN PRINT"I HAVE TO GET IT, FIRST.":GOTO 800
2040 LO(I)=98:IS=IS+1
2045 IF NA$(I)="PARKA" THEN H1=0
2046 IF NA$(I)="BANDAGE" THEN H2=0
2050 PO=PO-1
2060 NEXT I
2070 IF IS=0 THENPRINT"i CAN'T WEAR THAT."
2090 GOTO 800
2200 IF LO(12)<>99 AND LO(25)<>99 THEN PRINT"WITH WHAT?":GOTO 800
2210 IF N$<>"WIR" AND N$<>"ROD" AND N$<>"KEY" THEN PRINT"THAT WON'T CUT.":GOTO 800
2220 IF N$<>"WIR" THEN 3200
2230 IF LC<>26 THEN PRINT"THEY AREN'T HERE.":GOTO 800
2240 NM$(25)="THREE CUT ":GOTO 500
2400 IF N$="FLA" AND (LO(18)=99 OR LO(18)=LC) THEN PRINT"IT'S BROKEN":GOTO 800
2410 IF N$="GLO" AND (LO(10)=LC OR LO(10)=99) THEN L=1:NM$(10)="A BRIGHT, GLOWING ":GOTO 500
2415 IF N$="GLO" AND (LO(10)<>LC AND LO(10)<>99) THEN PRINT"I DON'T HAVE IT.":GOTO 800
2420 PRINT"SORRY.  THAT DOESN'T LIGHT."
2430 GOTO 800
2600 IF LO(8)<>99 THEN PRINT"WITH WHAT?":GOTO 800
2605 IF N$="BLA" THEN PRINT"TELL ME A TARGET!":GOTO 800
2610 IF N$="GAT" AND (LC=15 OR LC=20) THEN PRINT"B A R O O O m! THERE IS A SHOWER OF SPARKS!":GOTO 800
2615 IF N$="WIR" AND LC=26 PRINT"BBZZZZZT. NOTHING HAPPENS.":GOTO 800
2620 IF N$="GAT" AND LC=30 AND NM$(30)="A CLOSED " THEN NM$(30)="A BLASTED OPEN ":CO(30,2)=31:GOTO 500
2630 FOR I=1 TO IT
2640 IF LO(I)<>LC THEN 2680
2650 IF N$<>LEFT$(NA$(I),3) THEN 2680
2660 IF NA$(I)="RUBBLE" THEN PRINT"YOU ALREADY SHOT THAT.":GOTO 800
2670 NA$(I)="RUBBLE":NM$(I)="A PILE OF ":GOTO 500
2680 NEXT I
2690 PRINT"I CAN'T GET A BEAD ON IT."
2700 GOTO 800
2800 IF (LO(5)<>LC AND LO(5)<>99) THEN PRINT"WITH WHAT?":GOTO 800
2805 IF N$="BAN" THEN PRINT"ON WHAT":INPUT N$:N$=LEFT$(N$,3)
2806 IF N$="CUT" OR N$="WOU" THEN H2=0:LO(5)=98:GOTO 800
2810 IF N$<>"ALI" THEN PRINT"THE BANDAGE WON'T STICK.":GOTO 800
2820 IF (LO(9)<>99 AND LO(9)<>LC) THEN PRINT"HE ISN'T HERE.":GOTO 800
2830 NM$(9)="A GAUDILY DRESSED, FRIENDLY "
2835 LO(5)=0
2840 FO=1
2850 GOTO 500
3000 IF N$<>"KIT" AND N$<>"MED" AND N$<>"POR" AND N$<>"LET" AND N$<>"ORD" AND N$<>"GAT" THENPRINT"IT DOESN'T OPEN":GOTO 800
3005 IF N$="KIT" AND LO(18)=0 AND (LO(17)=99 OR LO(17)=LC) THENLO(18)=LC
3010 IF N$="KIT" AND LO(13)=0 AND (LO(17)=99 OR LO(17)=LC) THEN LO(13)=LC:GOTO 500
3015 IF N$="KIT" AND (LO(17)=99 OR LO(17)=LC) THEN PRINT"IT'S EMPTY.":GOTO 800
3020 IF N$="POR" AND LO(11)=0 AND (LO(20)=99 OR LO(20)=LC) THEN LO(11)=LC:NM$(20)="AN OPEN ":GOTO 500
3025 IF N$="POR" AND (LO(20)=99 OR LO(20)=LC) THEN PRINT"IT'S EMPTY.":GOTO 800
3030 IF N$="LET" AND (LO(11)=99 OR LO(11)=LC) THEN NM$(11)="AN OPEN ":GOTO 500
3040 IF N$="MED" AND (LO(13)=99 OR LO(13)=LC) THEN LO(5)=LC:GOTO 500
3050 IF N$="ORD" AND (LO(21)=99 OR LO(21)=LC) THEN NM$(21)="SOME OPENED ":GOTO 500
3060 IF N$<>"GAT" THEN PRINT"IT ISN'T HERE.":GOTO 800
3065 IF (LC=15 OR LC=20) AND LO(26)<>99 THEN PRINT"I DON'T HAVE THE TOOL.":GOTO 800
3070 IF NM$(26)="A ROUND " AND LC=20 THEN NM$(29)="AN OPEN ":CO(20,2)=21:GOTO 500
3080 IF NM$(26)="A TRIANGULAR " AND LC=15 AND LO(26)=99 THEN NM$(28)="AN OPEN ":CO(15,2)=16:GOTO 500
3090 PRINT"SOMETHING'S WRONG.":GOTO 800
3200 IF LO(27)<>99 THEN PRINT"I HAVE NO TOOL.":GOTO 800:REM FILE ROUTINE
3210 IF N$<>"KEY" AND N$<>"ROD" THEN PRINT"SORRY. THIS OLD FILE WON'T CUT THAT.":GOTO 800
3220 IF LO(26)<>99 AND LO(26)<>LC THEN PRINT"SORRY. IT ISN'T HERE.":GOTO 800
3230 IF NM$(26)="A SQUARe " THEN NM$(26)="A TRIANGULAR ":LO(24)=LC:GOTO 500
3240 IF NM$(26)="A TRIANGULAR "THEN NM$(26)="A ROUND ":LO(24)=LC:GOTO 500
3250 IF NM$(26)="A ROUND " THEN NM$(26)="A CRUMPLED ":LO(24)=LC:NA$(26)="NEEDLE OF METAL":GOTO 500
3260 PRINT"IT DOESN'T DO ANY GOOD ANYMORE."
3270 GOTO 800
3400 PRINT"'OK., ";RIGHT$(A$,LEN(A$)-I);"!'"
3410 PRINT"NO-ONE HEARS ME."
3420 GOTO 800
3600 PO=0
3605 PRINT "I AM CARRYING ";
3610 FOR I=1 TO IT
3620 IF LO(I)<>99 THEN 3680
3630 IF POS(X)+LEN(NA$(I))+LEN(NM$(I))>63 THEN PRINT:PRINTTAB(5);
3640 PRINT NM$(I);NA$(I);",";
3650 PO=PO+1
3680 NEXT I
3690 IF PO=0 THEN PRINT"NOTHING AT ALL.";
3695 PRINT
3700 IS=0
3705 PRINT"I AM WEARING ";
3710 FOR I=1 TO 5
3720 IF LO(I)<>98 THEN 3750
3730 IF POS(X)+LEN(NA$(I))+LEN(NM$(I))>63 THEN PRINT:PRINT TAB(5);
3740 PRINT NM$(I);NA$(I);", ";:IS=IS+1
3750 NEXT I
3760 IF IS=0 THEN PRINT"NOTHING BUT MY UNIFORM." ELSE PRINT
3770 IF H1+H2=0  THEN 3790
3771 IF H2=0 AND LC=1 THEN 3790
3772 PRINT"I AM ";
3774 IF H1=1 AND LC<>1THEN PRINT"COLD, ";
3776 IF H2=1 THEN PRINT"CUT AND BLEEDING."ELSE PRINT
3790 GOTO 800
3800 IF N$="" THEN 500
3810 IF N$="WAT" ORN$="PAN" OR N$="GAU" OR N$="0RD" OR N$="LET" THEN 1900
3880 PRINT"I DON'T SEE ANYTHING OF INTEREST TO YOU THERE."
3890 GOTO 800
4000 FOR I=1 TO IT
4010 IF LEFT$(NA$(I),3)<>N$ THEN 4050
4020 IF LO(I)<>LC AND LO(I)<>99 THEN PRINT"IT'S NOT HERE.":GOTO 800
4030 IF I<>10 THEN PRINT"IT FEELS COLD.":GOTO 800
4040 GOTO 2400
4050 NEXT I
4060 PRINT"SORRY. I CAN'T."
4070 GOTO 800
4200 IF N$<>"BUT" THEN PRINT"NOTHING HAPPENS.":GOTO 800
4210 IF LC<>32 THEN PRINT"iT'S NOT HERE.":GOTO 800
4220 IF LO(22)<>32 THEN PRINT"CLICK.     CLICK. CLICK. NOTHING.":GOTO 800
4230 CLS
4240 PRINT@235,"LIFT-OFF!"
4250 FOR I=1 TO 2000:NEXT I
4260 IF LO(19)=32 AND NM$(25)="THREE CUT " THEN 4300
4270 PRINT@266,"C R A S H ! ! !"
4275 IF NM$(25)="THREE TINY " THEN PRINT"A LASER CUTS YOUR SHIP IN TWO."
4280 PRINT"THE SHIP SPUN IN, OUT OF CONTROL."
4290 PRINT"I AM DEAD. THE PLANET IS DESTROYED BY THE ORBITING SHIP."
4295 PRINT
4296 INPUT"DO YOU WISH TO PLAY AGAIN";A$
4297 IF LEFT$(A$,1)="Y" THEN RUN ELSE NEW
4300 CLS
4301 CLS
4302 PRINT"I HAVE DOCKED WITH THE ORBITING SHIP."
4305 IF LO(11)=32 AND LO(9)=32  THEN 4350
4310 IF (LO(11)<>32 AND LO(11)<>99) THEN PRINT@220,"I DIDN'T BRING THE TREATY."
4320 IF LO(9)<>32 THEN PRINT@220,"I DIDN'T BRING THE AMBASSADOR."
4330 GOTO 4290
4340 REM SUCCESS
4350 PRINT
4360 PRINTTAB(20)"CONGRATULATIONS"
4370 PRINTSTRING$(63,"+")
4380 PRINT:PRINT:PRINT
4390 PRINT"YOU HAVE SAVED TWO PLANETS AND BILLIONS OF LIVES."
4400 PRINT"              THANK YOU."
4410 GOTO 4295
4500 IF LO(6)=LC AND (LO(23)=LC OR LO(23)=99) AND N$="SNO" THEN LO(23)=0:NM$(6)="A FRIENDLY DOCILE ":GOTO 800
4510 PRINT"I CAN'T.":GOTO 800
5000 CLS:REM PLANET IS DESTROYED
5010 PRINT@235,"B O O M !"
5020 FOR I=1 TO 2000:NEXT I
5030 PRINT
5040 PRINT"I TOOK TOO LONG.  THE ORBITTING SHIP BLEW UP THE PLANET."
5050 GOTO4295
6500 IF LC=20 AND LO(1)<>98 THEN PRINT"i CAN'T GO ON. I AM SLIDING IN THE SNOW.":FOR I=1 TO 700:NEXT I:N$="E":RETURN
6510 IF LC=18 AND LO(1)<>98 THEN PRINT"I CAN'T GO ON. i AM SLIPPING IN THE SNOW.":FOR I=1 TO 700:NEXT I:N$="W":RETURN
6520 RETURN
28000 REM THIS IS THE ITEM LIST
28010 REM FORMAT   NOUN,DESCRIPTION,BEGINNING LOCATION
28020 REM IT=NO. OF ITEMS ON LIST
28090 DATA 30
28100 DATA"SNOWSHOES","SOME ALIEN ",11
28110 DATA"WATCH","AN ALIEN ",4
28120 DATA"PARKA","A HEAVY ",2
28130 DATA"BADGE","A GOLD ",16
28140 DATA"BANDAGE","A STICKY ",0
28150 DATA"SNOWBEAST","A WILD, MEAN ",5
28160 DATA"SLED","A STALLED POWER ",7
28170 DATA"BLASTER","A FULLY CHARGED ",2
28180 DATA"ALIEN","A MENACING, WOUNDED ",17
28190 DATA"GLOBE","A DULL, DARK ",22
28200 DATA"LETTER","A SEALED ",0
28210 DATA"CUTTERS","A PAIR OF RUSTY ",17
28220 DATA"MEDIKIT","A CLOSED ",0
28230 DATA"STRAW","A PILE OF MUSTY ",9
28240 DATA"LUMP","A GREY ",18
28250 DATA"SNOWBALL","AN ICEY ",19
28260 DATA"KIT","A SURVIVAL ",1
28270 DATA"FLASHLIGHT","A TWISTED ",0
28280 DATA"AUTOPILOT","A SHUTTLE ",14
28290 DATA"PORTFOLIO","A SEALED ",2
28300 DATA"ORDERS","A SET OF SEALED ",31
28310 DATA"IGNITER","A SHUTTLE ENGINE ",1
28320 DATA"MOSS","SOME DRIED ",6
28330 DATA"POWDER","A PILE OF BRASS ",0
28340 DATA"WIRES","THREE TINY ",26
28350 DATA"ROD","A SQUARE ",10
28360 DATA"FILE","A RUSTY ",0
28370 DATA"GATE WITH A TRIANGULAR KEYHOLE","A CLOSED ",15
28380 DATA"GATE WITH A ROUND KEYHOLE","A CLOSED ",20
28390 DATA"GATE WITH A HEXAGONAL KEYHOLE","A CLOSED ",30
29000 REM LOCATION LIST
29010 REM LO IS NO OF LOCATIONS
29020 REM STRUCTURE   DESCRIPTION,N,E,S,W,U,D,STATE OF LIGHT
29090 DATA 33
29100 DATA "NOWHERE",0,0,0,0,0,0,0
29110 DATA "THE COCKPIT OF A GROUNDED SHIP. THE PANEL IS BLINKING          DIMLY.",2,0,0,0,0,0,1
29120 DATA "A PUBLIC SQUARE IN A CITY OF SQUALID ICE HUTS ON AN       ALIEN WORLD. THERE IS A SHOT UP SHIP TO THE SOUTH. DEBRIS       FROM THE SHIP LITTERS THE SQUARE.",8,15,1,3,0,0,1
29130 DATA "A LONG ABANDONED MARKET SQUARE UNDER A PURPLE SUN.",9,2,4,0,0,0,1
29140 DATA"A CRUMBLED STREET WITH THE BROKEN CITY WALL TO THE SOUTH AND EAST.",3,6,5,0,0,0,1
29150 DATA "A GLARING ICE FIELD WITH A CRUMPLED CITY TO THE NORTH.",4,0,0,0,0,0,1
29160 DATA "A FIELD OF SWEET SMELLING GREEN MOSS, WITH LONG GROOVES IN IT MADE BY SHARP TEETH.",0,7,0,4,0,0,1
29170 DATA"A GLARING ICE FIELD, WITH MELTED PATCHES SMOKING IN THE SUN.",0,0,0,6,11,0,1
29180 DATA "AN ALLEY WITH BROKEN STONE AND ICE WALLS ON BOTH SIDES.",12,0,2,9,0,0,1
29190 DATA "A COLLAPSED ICE HUT WITH A LONG SHELF DOWN ONE WALL.",0,8,3,0,0,0,1
29200 DATA "AN ABANDONED STOREROOM. IT'S MUSTY AIR SMELLS OF DEAD       MOSS.",0,0,15,0,0,0,1
29210 DATA "THE SADDLE OF A SNOWMACHINE.  THERE IS A GAUGE ON THE       PANEL.",0,0,0,0,0,7,1
29220 DATA "THE BOTTOM FLOOR OF A TALL HUT.  A CHILL BLAST CURLS OUT THROUGH A HOLE IN THE ROOF.",0,0,8,0,14,13,0
29230 DATA"A CELLER UNDER THE HUT.  A DIRT SIDE-WALL HAS COLLAPSED.",0,0,0,0,12,0,0
29240 DATA "THE ATTIC OF THE HUT.  a PILE OF DRIED STRAW HAS SETTLED IN ONE CORNER.",0,0,0,0,0,12,0
29250 DATA "A PUBLIC SQUARE WITH A WEATHERED WALL TO THE EAST.",10,0,0,2,0,0,1
29260 DATA "A HUGE FIELD OF WHITE ICE. AN ALIEN LANDER HAS CRASHED       TO THE SOUTH.",0,18,17,15,0,0,1
29270 DATA "IN THE COCKPIT OF AN ALIEN LANDER.  THE PANEL LIGHTS ARE GLOWING DIMLY.",16,0,0,0,0,0,0
29280 DATA "AN ENDLESS EXPANSE OF PINK ICE.",0,19,0,16,0,0,1
29290 DATA "A LONG STRETCH OF ORANGE ICE.",0,20,0,18,0,0,1
29300 DATA "A STRETCH OF RED ICE WITH A CITY WALL TO THE EAST.",0,0,0,19,0,0,1
29310 DATA "AN OPEN SQUARE IN A CITY OF SPIRES.",0,22,0,20,0,0,1
29320 DATA "A TOWERING RED SPIRE.",23,24,0,21,0,0,1
29330 DATA "A SQUAT, GREEN SPIRE.",0,0,22,0,0,0,0
29340 DATA "A CRUMBLING PURPLE SPIRE.",0,28,25,22,0,0,0
29350 DATA "AN ORANGE, PITTED SPIRE.",24,0,26,0,0,0,0
29360 DATA "A PINK SNOWFIELD BESIDE AN ANTI-SHIP LASER, SCANNING     THE SKIES.",25,0,0,0,0,0,1
29370 DATA "A BLUE SPIRE BUILDING.",28,30,0,0,0,0,1
29380 DATA "A GREEN SPIRE BUILDING WITH A GLASS FLOOR.",29,0,27,24,0,0,0
29390 DATA "A YELLOW SPIRE BUILDING.",0,0,28,0,0,0,1
29400 DATA "A FLAT FIELD WITH A GATE TO THE EAST.",0,0,0,27,0,0,1
29410 DATA "AN ICE FIELD WITH AN UNDAMAGED SHIP.",0,0,0,30,32,0,1
29420 DATA "THE INTERIOR OF A SHUTTLE SHIP, WITH A DIMLY LIT PANEL.",0,0,0,0,0,31,1
