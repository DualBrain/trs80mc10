0 CLS:CLEAR500:DIMC1,CC,P,K:LN$="++++++++++++++++++++++++++++++++":GOTO10
1 C1=1:CC=32
2 IFMID$(M$,CC,1)<>""ANDMID$(M$,CC,1)<>" "THENCC=CC-1:GOTO2
3 PRINTMID$(M$,C1,CC-C1):C1=CC+1:CC=C1+31:IFC1<=LEN(M$)THEN2
4 RETURN
5 GOSUB1:GOTO800
10 CLS
30 PRINT@233,CHR$(34)"ICE WORLD WAR"CHR$(34)
50 PRINT@329,"BY KIT DOMENICO"
60 PRINT"        MODIFIED FOR COCO"
70 PRINT"        BY J.R. APPLEGATE"
75 PRINT"        AND FOR THE MC-10"
80 PRINT"          BY JIM GERRIE"
100 READIT
105 PL=6
106 TM=1
107 H1=1
108 H2=1
110 DIMNA$(IT),NM$(IT),LO(IT)
120 FORI=1TOIT
130 READNA$(I),NM$(I),LO(I)
140 NEXTI
150 READLO
160 DIMDE$(LO),CO(LO,6),LI(LO)
170 FORI=0TOLO-1
180 READDE$(I),CO(I,1),CO(I,2),CO(I,3),CO(I,4),CO(I,5),CO(I,6),LI(I)
190 NEXTI
210 LC=1
500 CLS
501 IFTM<20THEN505
502 IFH2=1THENPRINT@235,"I BLED TO DEATH!":GOTO4295
503 IFH1=1THENPRINT@235,"I FROZE TO DEATH!":GOTO4295
505 IF(LI(LC)=0ANDL=0)OR(L=1ANDLO(10)<>LCANDLO(10)<>99)THENPRINT"IT'S DARK IN HERE!":GOTO690
510 M$="I AM IN "+DE$(LC):GOSUB1
515 IFFO=1THENLO(9)=LC
530 M$="I CAN SEE "
535 IS=0
540 FORI=1TOIT
550 IFLO(I)<>LCTHEN 580
570 M$=M$+NM$(I)+NA$(I)+", ":IS=IS+1
580 NEXTI
583 IFIS=0THENM$=M$+"NOTHING OF INTEREST.":GOTO585
584 A$=LEFT$(M$,LEN(M$)-2)+".":M$=A$
585 GOSUB1
590 M$="THE EXITS ARE: "
600 FORI=1TO6
610 IFCO(LC,I)=0THEN680
620 IFI=1THENM$=M$+"NORTH, "
630 IFI=2THENM$=M$+"EAST, "
640 IFI=3THENM$=M$+"SOUTH, "
650 IFI=4THENM$=M$+"WEST, "
660 IFI=5THENM$=M$+"UP, "
670 IFI=6THENM$=M$+"DOWN, "
680 NEXTI
685 A$=LEFT$(M$,LEN(M$)-2)+".":M$=A$:GOSUB1
690 PRINTLN$;
800 A$="":V$="":N$=""
801 TM=TM+1
802 IFTM>360THEN5000
805 INPUT"WHAT SHOULD I DO";A$
810 IFLEN(A$)=1THENN$=A$:GOTO1200
820 V$=LEFT$(A$,3)
830 FORI=1TOLEN(A$)
840 IFMID$(A$,I,1)=" "THEN N$=LEFT$(RIGHT$(A$,LEN(A$)-I),3):GOTO860
850 NEXTI
860 REM VERBS
870 IFV$="GO "THEN1200
871 IFV$="RUN"THEN1200
872 IFV$="WAL"THEN1200
880 IFV$="GET"THEN1400
881 IFV$="TAK"THEN1400
882 IFV$="GRA"THEN1400
883 IFV$="CAR"THEN1400
884 IFV$="MOV"THEN1400
890 IFV$="DRO"THEN1700
891 IFV$="PUT"THEN1700
892 IFV$="THR"THEN1700
893 IFV$="REM"THEN1700
900 IFV$="REA"THEN1900
910 IFV$="WEA"THEN2000
920 IFV$="CUT"THEN2200
930 IFV$="LIG"THEN2400
940 IFV$="SHO"THEN2600
942 IFV$="BLA"THEN2600
943 IFV$="FIR"THEN2600
950 IFV$="BAN"THEN2800
960 IFV$="STI"THEN2800
970 IFV$="OPE"THEN3000
975 IFV$="UNL"THEN3000
980 IFV$="FIL"THEN3200
990 IFV$="SAY"THEN3400
992 IFV$="YEL"THEN3400
994 IFV$="CAL"THEN3400
1000 IFV$="INV"THEN3600
1010 IFV$="LOO"THEN3800
1020 IFV$="RUB"THEN4000
1030 IFV$="PRE"THEN4200
1040 IFV$="PUS"THEN4200
1050 IFV$="FEE"THEN4500
1060 IFV$="QUI"THEN CLS:PRINT@224,;:GOTO4290
1180 M$="SORRY. I AM UNABLE TO COMPLY WITH THAT ORDER, IN THAT FORM."
1190 GOTO5
1200 N$=LEFT$(N$,1)
1205 IFLC=18ORLC=20THENGOSUB6500
1210 IFN$="N"ANDCO(LC,1)<>0THENLC=CO(LC,1):GOTO500
1220 IFN$="E"ANDCO(LC,2)<>0THENLC=CO(LC,2):GOTO500
1240 IFN$="S"ANDCO(LC,3)<>0THENLC=CO(LC,3):GOTO500
1260 IFN$="W"ANDCO(LC,4)<>0THENLC=CO(LC,4):GOTO500
1280 IFN$="U"ANDCO(LC,5)<>0THENLC=CO(LC,5):GOTO500
1300 IFN$="D"ANDCO(LC,6)<>0THENLC=CO(LC,6):GOTO500
1320 M$="I CANNOT GO THAT DIRECTION."
1340 GOTO5
1400 IFN$="INV"THEN3600
1405 IFPO>=PLTHENM$="I AM CARRYING TOO MUCH ALREADY. I CAN'T PICK ANYTHING ELSE UP.":GOTO5
1410 FORI=1TOIT
1420 IFN$<>LEFT$(NA$(I),3)THEN1500
1430 IFLO(I)<>LCTHEN1500
1440 IFNA$(I)="SLED"ORLEFT$(NA$(I),4)="GATE"ORNA$(I)="SNOWBEAST"ORNA$(I)="ALIEN"THEN1442
1441 GOTO1450
1442 M$="NO WAY! THAT'S OUT OF THE QUESTION!":GOTO5
1450 IFNA$(I)="WIRES"ANDLC=26THENM$="THEY'RE FASTENED SECURELY TO THE LASER MOUNT.":GOTO5
1455 IFLC=9ANDI=14ANDLO(27)=0THENLO(27)=LC
1460 LO(I)=99
1465 PO=PO+1
1470 PRINT"GOT IT.":FORC1=1TO1000:NEXT:GOTO500
1500 NEXTI
1505 M$="I CAN'T PICK THAT UP."
1510 GOTO5
1700 REM
1710 FORI=1TOIT
1720 IFLEFT$(NA$(I),3)<>N$THEN1800
1730 IFLO(I)<>99ANDLO(I)<>98THEN1800
1740 LO(I)=LC:PO=PO-1
1750 IFI=3THENH1=1
1760 GOTO500
1800 NEXTI
1810 M$="YOU DON'T HAVE THAT."
1820 GOTO5
1900 IFLI(LC)=0ANDL=0THENM$="I CAN'T READ IN THE DARK!":GOTO5
1905 IFN$="PAN"ANDLC=1THENM$="THE WARNING LIGHTS SAY 'MASSIVE DAMAGE!'":GOSUB1:M$="'FIRE IMMINENT!'":GOTO5
1907 IFN$="PAN"ANDLC=32THENM$="IT HAS A BUTTON MARKED 'LAUNCH TO ORBIT'.":GOTO5
1910 IFN$="PAN"ANDLC=17THENM$="THE WARNING LIGHTS SAY '&%XXX **** !'":GOTO5
1915 IFN$="BAD"AND(LO(4)=99ORLO(4)=LC)THENM$="IT SAYS '%##-!+'":GOTO5
1920 IFN$="GAU"ANDLC=11ANDGA=0THENM$="IT SAYS 'EMPTY'.":GOTO5
1925 IFN$="ORD"ANDLO(21)<>99ANDLO(21)<>LCTHENM$="WHAT ORDERS?":GOTO5
1930 IFN$="ORD"AND(LO(21)=LCORLO(21)=99)ANDLI(LC)=1THEN1932
1931 GOTO1940
1932 M$="ATTENTION: RETURN THE HASLAN AMBASSADOR AND THE TREATY TO THE SHIP "
1933 M$=M$+"IN ORBIT AROUND THE PLANET. THIS WILL END THE WAR.":GOTO5
1940 IFN$="WAT"AND(LO(2)=99ORLO(2)=98ORLO(2)=LC)THEN1942
1941 GOTO1950
1942 M$="IT SAYS '"+STR$(3-INT(TM/90))+" ZHORG AND"+STR$((INT(TM/90)+1)*90-TM)+" QUAKL ' BEFORE THE PLANET DIES.":GOTO5
1950 IFN$="LET"AND(LO(11)=99ORLO(11)=LC)ANDNM$(11)="AN OPEN "THEN1952
1951 GOTO1970
1952 M$="IT SAYS 'MORE ADVENTURES ARE COMING. FROM THIS AND OTHER "
1955 M$=M$+"AUTHORS. ENJOY, ENJOY.' P.S. THIS IS THE TREATY. TAKE CARE OF IT.":GOTO5
1970 M$="I CAN'T READ THAT. SORRY."
1980 GOTO5
2000 M$="":IS=0
2005 FORI=1TO5
2010 IFLEFT$(NA$(I),3)<>N$THEN2060
2020 IFLO(I)=98THENM$="I ALREADY HAVE THAT ON.":GOTO5
2030 IFLO(I)<>99THENM$="I HAVE TO GET IT, FIRST.":GOTO5
2040 LO(I)=98:IS=IS+1
2045 IFNA$(I)="PARKA"THENH1=0
2046 IFNA$(I)="BANDAGE"THENH2=0
2050 PO=PO-1
2060 NEXTI
2070 IFIS=0THENM$="I CAN'T WEAR THAT."
2090 GOTO5
2200 IFLO(12)<>99ANDLO(25)<>99THENM$="WITH WHAT?":GOTO5
2210 IFN$<>"WIR"AND N$<>"ROD"ANDN$<>"KEY"THENM$="THAT WON'T CUT.":GOTO5
2220 IFN$<>"WIR"THEN3200
2230 IFLC<>26THENM$="THEY AREN'T HERE.":GOTO5
2240 NM$(25)="THREE CUT ":GOTO500
2400 IFN$="FLA"AND(LO(18)=99ORLO(18)=LC)THENM$="IT'S BROKEN":GOTO5
2410 IFN$="GLO"AND(LO(10)=LCORLO(10)=99)THENL=1:NM$(10)="A BRIGHT, GLOWING ":GOTO500
2415 IFN$="GLO"AND(LO(10)<>LCANDLO(10)<>99)THEN M$="I DON'T HAVE IT.":GOTO5
2420 M$="SORRY. THAT DOESN'T LIGHT."
2430 GOTO5
2600 IFLO(8)<>99THENM$="WITH WHAT?":GOTO5
2605 IFN$="BLA"THENM$="TELL ME A TARGET!":GOTO5
2610 IFN$="GAT"AND(LC=15ORLC=20)THENM$="B A R O O O M! THERE IS A SHOWER OF SPARKS!":GOTO5
2615 IFN$="WIR"ANDLC=26THENM$="BBZZZZZT. NOTHING HAPPENS.":GOTO5
2620 IFN$="GAT"ANDLC=30ANDNM$(30)="A CLOSED "THEN NM$(30)="A BLASTED OPEN ":CO(30,2)=31:GOTO500
2630 FORI=1TOIT
2640 IFLO(I)<>LCTHEN2680
2650 IFN$<>LEFT$(NA$(I),3)THEN2680
2660 IFNA$(I)="RUBBLE"THENM$="YOU ALREADY SHOT THAT.":GOTO5
2670 NA$(I)="RUBBLE":NM$(I)="A PILE OF ":GOTO500
2680 NEXTI
2690 M$="I CAN'T GET A BEAD ON IT."
2700 GOTO5
2800 IF(LO(5)<>LCANDLO(5)<>99)THENM$="WITH WHAT?":GOTO5
2805 IFN$="BAN"THENINPUT"ON WHAT";N$:N$=LEFT$(N$,3)
2806 IFN$="CUT"ORN$="WOU"THENH2=0:LO(5)=98:GOTO 800
2810 IFN$<>"ALI"THENM$="THE BANDAGE WON'T STICK.":GOTO5
2820 IF(LO(9)<>99ANDLO(9)<>LC)THENM$="HE ISN'T HERE.":GOTO5
2830 NM$(9)="A GAUDILY DRESSED, FRIENDLY "
2835 LO(5)=0
2840 FO=1
2850 GOTO500
3000 IFN$<>"KIT"ANDN$<>"MED"ANDN$<>"POR"ANDN$<>"LET"ANDN$<>"ORD"ANDN$<>"GAT"THENM$="IT DOESN'T OPEN":GOTO5
3005 IFN$="KIT"ANDLO(18)=0AND(LO(17)=99ORLO(17)=LC)THENLO(18)=LC
3010 IFN$="KIT"ANDLO(13)=0AND(LO(17)=99ORLO(17)=LC)THENLO(13)=LC:GOTO500
3015 IFN$="KIT"AND(LO(17)=99ORLO(17)=LC)THENM$="IT'S EMPTY.":GOTO5
3020 IFN$="POR"ANDLO(11)=0AND(LO(20)=99ORLO(20)=LC)THENLO(11)=LC:NM$(20)="AN OPEN ":GOTO500
3025 IFN$="POR"AND(LO(20)=99ORLO(20)=LC)THENM$="IT'S EMPTY.":GOTO5
3030 IFN$="LET"AND(LO(11)=99ORLO(11)=LC)THENNM$(11)="AN OPEN ":GOTO500
3040 IFN$="MED"AND(LO(13)=99ORLO(13)=LC)THENLO(5)=LC:GOTO500
3050 IFN$="ORD"AND(LO(21)=99ORLO(21)=LC)THENNM$(21)="SOME OPENED ":GOTO500
3060 IFN$<>"GAT"THENM$="IT ISN'T HERE.":GOTO5
3065 IF(LC=15ORLC=20)ANDLO(26)<>99THENM$="I DON'T HAVE THE TOOL.":GOTO5
3070 IFNM$(26)="A ROUND "ANDLC=20THENNM$(29)="AN OPEN ":CO(20,2)=21:GOTO500
3080 IFNM$(26)="A TRIANGULAR "ANDLC=15ANDLO(26)=99THENNM$(28)="AN OPEN ":CO(15,2)=16:GOTO500
3090 M$="SOMETHING'S WRONG.":GOTO5
3200 IFLO(27)<>99THENM$="I HAVE NO TOOL.":GOTO5:REM FILE ROUTINE
3210 IFN$<>"KEY"ANDN$<>"ROD"THENM$="SORRY. THIS OLD FILE WON'T CUT THAT.":GOTO5
3220 IFLO(26)<>99ANDLO(26)<>LCTHENM$="SORRY. IT ISN'T HERE.":GOTO5
3230 IFNM$(26)="A SQUARE "THENNM$(26)="A TRIANGULAR ":LO(24)=LC:GOTO500
3240 IFNM$(26)="A TRIANGULAR "THENNM$(26)="A ROUND ":LO(24)=LC:GOTO500
3250 IFNM$(26)="A ROUND "THENNM$(26)="A CRUMPLED ":LO(24)=LC:NA$(26)="NEEDLE OF METAL":GOTO 500
3260 M$="IT DOESN'T DO ANY GOOD ANYMORE."
3270 GOTO5
3400 M$="'OK., "+RIGHT$(A$,LEN(A$)-I)+"!'":GOSUB1
3410 M$="NO-ONE HEARS ME."
3420 GOTO5
3600 PO=0
3605 PRINT"I AM CARRYING:"
3610 M$="":FORI=1TOIT
3620 IFLO(I)<>99THEN3680
3640 M$=M$+NM$(I)+NA$(I)+", "
3650 PO=PO+1
3680 NEXTI
3690 IFPO=0THENM$="NOTHING AT ALL.":GOTO3695
3691 A$=LEFT$(M$,LEN(M$)-2)+".":M$=A$
3695 GOSUB1
3700 IS=0
3705 M$="I AM WEARING "
3710 FORI=1TO5
3720 IFLO(I)<>98THEN3750
3740 M$=M$+NM$(I)+NA$(I)+", ":IS=IS+1
3750 NEXTI
3760 IFIS=0THENM$=M$+"NOTHING BUT MY UNIFORM.":GOTO3765
3761 A$=LEFT$(M$,LEN(M$)-2)+".":M$=A$
3765 GOSUB1
3770 IFH1+H2=0THEN800
3771 IFH2=0ANDLC=1THEN800
3772 M$="I AM "
3774 IFH1=1ANDLC<>1THENM$=M$+"COLD, "
3776 IFH2=1THENM$=M$+"CUT AND BLEEDING."
3790 GOTO5
3800 IFN$=""THEN500
3810 IFN$="WAT"ORN$="PAN"ORN$="GAU"ORN$="0RD"ORN$="LET"THEN1900
3880 M$="I DON'T SEE ANYTHING OF INTEREST TO YOU THERE."
3890 GOTO5
4000 FORI=1TOIT
4010 IFLEFT$(NA$(I),3)<>N$THEN4050
4020 IFLO(I)<>LC AND LO(I)<>99THENM$="IT'S NOT HERE.":GOTO5
4030 IFI<>10THENM$="IT FEELS COLD.":GOTO5
4040 GOTO2400
4050 NEXTI
4060 M$="SORRY. I CAN'T."
4070 GOTO5
4200 IFN$<>"BUT"THENM$="NOTHING HAPPENS.":GOTO5
4210 IFLC<>32THENM$="IT'S NOT HERE.":GOTO5
4220 IFLO(22)<>32THENM$="CLICK.     CLICK. CLICK. NOTHING.":GOTO5
4230 CLS
4240 PRINT@235,"LIFT-OFF!"
4250 FORI=1TO2500:NEXTI
4260 IFLO(19)=32ANDNM$(25)="THREE CUT "THEN4300
4270 PRINT@266,"C R A S H ! ! !"
4275 IFNM$(25)="THREE TINY "THENM$="A LASER CUTS YOUR SHIP IN TWO.":GOSUB1
4280 M$="THE SHIP SPUN IN, OUT OF CONTROL.":GOSUB1
4290 M$="I AM DEAD. THE PLANET IS DESTROYED BY THE ORBITING SHIP.":GOSUB1
4295 PRINT
4296 INPUT"DO YOU WISH TO PLAY AGAIN";A$
4297 IFLEFT$(A$,1)="Y"THENRUN
4298 END
4300 CLS
4302 M$="I HAVE DOCKED WITH THE ORBITING SHIP.":GOSUB1
4305 IFLO(11)=32ANDLO(9)=32THEN4350
4310 IF(LO(11)<>32ANDLO(11)<>99)THENPRINT@224,"I DIDN'T BRING THE TREATY."
4320 IFLO(9)<>32THENPRINT@224,"I DIDN'T BRING THE AMBASSADOR."
4330 GOTO4290
4340 REM SUCCESS
4350 PRINT
4360 PRINTTAB(14)"CONGRATULATIONS!"
4370 PRINTLN$;
4380 PRINT:PRINT:PRINT
4390 M$="YOU HAVE SAVED TWO PLANETS AND BILLIONS OF LIVES.":GOSUB1
4400 PRINTTAB(20)"THANK YOU."
4410 GOTO4295
4500 IFLO(6)=LC AND (LO(23)=LC OR LO(23)=99) AND N$="SNO"THENLO(23)=0:NM$(6)="A FRIENDLY DOCILE ":GOTO 800
4510 M$="I CAN'T.":GOTO5
5000 CLS:REM PLANET IS DESTROYED
5010 PRINT@235,"B O O M !"
5020 FORI=1TO2500:NEXTI
5030 PRINT
5040 M$="I TOOK TOO LONG. THE ORBITTING SHIP BLEW UP THE PLANET.":GOSUB1
5050 GOTO4295
6500 IFLC=20ANDLO(1)<>98THENM$="I CAN'T GO ON. I AM SLIDING IN THE SNOW.":GOSUB1:FORI=1TO1000:NEXTI:N$="E":RETURN
6510 IFLC=18ANDLO(1)<>98THENM$="I CAN'T GO ON. I AM SLIPPING IN THE SNOW.":GOSUB1:FORI=1TO1000:NEXTI:N$="W":RETURN
6520 RETURN
8000 REM ITEM LIST
8010 REM NOUN,DESCRIPTION,BEGINNING LOCATION
8020 REM IT=NO. OF ITEMS ON LIST
8090 DATA 30
8100 DATA"SNOWSHOES","SOME ALIEN ",11
8110 DATA"WATCH","AN ALIEN ",4
8120 DATA"PARKA","A HEAVY ",2
8130 DATA"BADGE","A GOLD ",16
8140 DATA"BANDAGE","A STICKY ",0
8150 DATA"SNOWBEAST","A WILD, MEAN ",5
8160 DATA"SLED","A STALLED POWER ",7
8170 DATA"BLASTER","A FULLY CHARGED ",2
8180 DATA"ALIEN","A MENACING, WOUNDED ",17
8190 DATA"GLOBE","A DULL, DARK ",22
8200 DATA"LETTER","A SEALED ",0
8210 DATA"CUTTERS","A PAIR OF RUSTY ",17
8220 DATA"MEDIKIT","A CLOSED ",0
8230 DATA"STRAW","A PILE OF MUSTY ",9
8240 DATA"LUMP","A GREY ",18
8250 DATA"SNOWBALL","AN ICEY ",19
8260 DATA"KIT","A SURVIVAL ",1
8270 DATA"FLASHLIGHT","A TWISTED ",0
8280 DATA"AUTOPILOT","A SHUTTLE ",14
8290 DATA"PORTFOLIO","A SEALED ",2
8300 DATA"ORDERS","A SET OF SEALED ",31
8310 DATA"IGNITER","A SHUTTLE ENGINE ",1
8320 DATA"MOSS","SOME DRIED ",6
8330 DATA"POWDER","A PILE OF BRASS ",0
8340 DATA"WIRES","THREE TINY ",26
8350 DATA"ROD","A SQUARE ",10
8360 DATA"FILE","A RUSTY ",0
8370 DATA"GATE WITH A TRIANGULAR KEYHOLE","A CLOSED ",15
8380 DATA"GATE WITH A ROUND KEYHOLE","A CLOSED ",20
8390 DATA"GATE WITH A HEXAGONAL KEYHOLE","A CLOSED ",30
9000 REM LOCATION LIST
9010 REM LO IS NO OF LOCATIONS
9020 REM DESCRIPTION,N,E,S,W,U,D,STATE OF LIGHT
9090 DATA 33
9100 DATA"NOWHERE",0,0,0,0,0,0,0
9110 DATA"THE COCKPIT OF A GROUNDED SHIP. THE PANEL IS BLINKING DIMLY.",2,0,0,0,0,0,1
9120 DATA"A CITY OF SQUALID ICE HUTS ON AN ALIEN WORLD. THERE'S A SHOT UP SHIP TO THE SOUTH & DEBRIS ALL OVER.",8,15,1,3,0,0,1
9130 DATA"A LONG ABANDONED MARKET SQUARE UNDER A PURPLE SUN.",9,2,4,0,0,0,1
9140 DATA"A CRUMBLED STREET WITH THE BROKEN CITY WALL TO THE SOUTH AND EAST.",3,6,5,0,0,0,1
9150 DATA"A GLARING ICE FIELD WITH A CRUMPLED CITY TO THE NORTH.",4,0,0,0,0,0,1
9160 DATA"A FIELD OF SWEET SMELLING GREEN MOSS, WITH LONG GROOVES IN IT MADE BY SHARP TEETH.",0,7,0,4,0,0,1
9170 DATA"A GLARING ICE FIELD, WITH MELTED PATCHES SMOKING IN THE SUN.",0,0,0,6,11,0,1
9180 DATA"AN ALLEY WITH BROKEN STONE AND ICE WALLS ON BOTH SIDES.",12,0,2,9,0,0,1
9190 DATA"A COLLAPSED ICE HUT WITH A LONG SHELF DOWN ONE WALL.",0,8,3,0,0,0,1
9200 DATA"AN ABANDONED STOREROOM. IT'S MUSTY AIR SMELLS OF DEAD MOSS.",0,0,15,0,0,0,1
9210 DATA"THE SADDLE OF A SNOWMACHINE. THERE IS A GAUGE ON THE PANEL.",0,0,0,0,0,7,1
9220 DATA"THE BOTTOM FLOOR OF A TALL HUT.  A CHILL BLAST CURLS OUT THROUGH A HOLE IN THE ROOF.",0,0,8,0,14,13,0
9230 DATA"A CELLER UNDER THE HUT. A DIRT SIDE-WALL HAS COLLAPSED.",0,0,0,0,12,0,0
9240 DATA"THE ATTIC OF THE HUT. A PILE OF DRIED STRAW HAS SETTLED IN ONE CORNER.",0,0,0,0,0,12,0
9250 DATA"A PUBLIC SQUARE WITH A WEATHERED WALL TO THE EAST.",10,0,0,2,0,0,1
9260 DATA"A HUGE FIELD OF WHITE ICE. AN ALIEN LANDER HAS CRASHED TO THE SOUTH.",0,18,17,15,0,0,1
9270 DATA"IN THE COCKPIT OF AN ALIEN LANDER. THE PANEL LIGHTS ARE GLOWING DIMLY.",16,0,0,0,0,0,0
9280 DATA"AN ENDLESS EXPANSE OF PINK ICE.",0,19,0,16,0,0,1
9290 DATA"A LONG STRETCH OF ORANGE ICE.",0,20,0,18,0,0,1
9300 DATA"A STRETCH OF RED ICE WITH A CITY WALL TO THE EAST.",0,0,0,19,0,0,1
9310 DATA"AN OPEN SQUARE IN A CITY OF SPIRES.",0,22,0,20,0,0,1
9320 DATA"A TOWERING RED SPIRE.",23,24,0,21,0,0,1
9330 DATA"A SQUAT, GREEN SPIRE.",0,0,22,0,0,0,0
9340 DATA"A CRUMBLING PURPLE SPIRE.",0,28,25,22,0,0,0
9350 DATA"AN ORANGE, PITTED SPIRE.",24,0,26,0,0,0,0
9360 DATA"A PINK SNOWFIELD BESIDE AN ANTI-SHIP LASER, SCANNING THE SKIES.",25,0,0,0,0,0,1
9370 DATA"A BLUE SPIRE BUILDING.",28,30,0,0,0,0,1
9380 DATA"A GREEN SPIRE BUILDING WITH A GLASS FLOOR.",29,0,27,24,0,0,0
9390 DATA"A YELLOW SPIRE BUILDING.",0,0,28,0,0,0,1
9400 DATA"A FLAT FIELD WITH A GATE TO THE EAST.",0,0,0,27,0,0,1
9410 DATA"AN ICE FIELD WITH AN UNDAMAGED SHIP.",0,0,0,30,32,0,1
9420 DATA"THE INTERIOR OF A SHUTTLE SHIP, WITH A DIMLY LIT PANEL.",0,0,0,0,0,31,1
