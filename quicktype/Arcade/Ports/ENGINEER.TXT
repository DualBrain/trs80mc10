10 GOSUB10000:DIMC,F(5):C=RND(-TIMER)
20 FORC=1TO31:B$=B$+CHR$(128):NEXT:C=1+RND(7)
30 PRINTTAB(8)"E N G I N E E R":PRINT
40 PRINT"TO BECOME AN ENGINEER, YOU MUST CONSTRUCT A TRESTLE BRIDGE"
50 PRINT"CONNECTING THE TWO BLOCKS AT THETOP OF THE SCREEN. THIS"
60 PRINT"IS DONE BY PLACING BEAMS IN     ACCORDANCE WITH THE INSPECTOR'S"
70 PRINT"SPECIFICATIONS. JUST MOVE THE   BLINKING GUIDE TO THE POSITION"
80 PRINT"YOU WISH TO PLACE THE BEAM, THENENTER THE DIRECTION YOU WANT"
90 PRINT"TO SET THE BEAM.  YOUR STARTING POSITION MUST BE BELOW THE"
100 PRINT"INSPECTOR'S FEET."
130 PRINTTAB(6)"PRESS enter TO BEGIN";:INPUTZ$:CLS0
140 FORZZ=64TO66:PRINT@ZZ,CHR$(143+(16*(C-1)));:NEXT
145 FORZZ=93TO95:PRINT@ZZ,CHR$(143+(16*(C-1)));:NEXT
150 REM SET CANYON
160 FORX=0TO63:SET(X,28,C):NEXT
170 Y=5:D=57:X=3
180 SET(X+2,Y,C):SET(X+3,Y,C):SET(X+D-2,Y,C):SET(X+D-3,Y,C)
190 SET(X,Y,C):SET(X+D,Y,C):SET(X+1,Y,C):SET(X+D-1,Y,C)
200 Y=Y+1:IFRND(9)>2THENX=X+1:D=D-2
210 IFY=29THEN220
211 GOTO180
220 X=27+RND(9):Y=27
230 GOSUB1030
240 M=RND(25)+20:N=2
250 PRINT@12,"engineer";
260 IFPOINT(M,N+1)THEN280
270 N=N+1:GOTO260
280 N=N-5:IFN<0THENN=0
290 SET(M,N,C):SET(M+1,N,C)
300 REM POSITION BEAM
310 PRINT@480,"A=LEFT S=RIGHT Z=DOWN";
320 FORZZ=1TO50:C$=INKEY$:IFC$<>""THEN330
321 IFZZ<25THENRESET(M,N):RESET(M+1,N)
325 IFZZ>=25THENSET(M,N,C):SET(M+1,N,C)
326 NEXTZZ:GOTO290
330 IFC$="S"THEN380
340 IFC$="A"THEN400
350 IFC$="Z"THEN420
360 GOTO290
370 REM SET BEAM
380 IFPOINT(M+2,N)>0THEN440
390 RESET(M,N):RESET(M+1,N):M=M+2:GOTO290
400 IFPOINT(M-1,N)>0THEN440
410 RESET(M,N):RESET(M+1,N):M=M-2:GOTO290
420 IFPOINT(M,N+1)>0THEN440
430 RESET(M,N):RESET(M+1,N):N=N+1:GOTO290
440 PRINT@480,B$;:PRINT@480,"";:A$=INKEY$
450 IFN<Y THENPRINT@480,"TOO HIGH!";:DA=DA+.5:RESET(M,N):RESET(M+1,N):FORI=1TO1200:NEXT:GOTO240
460 PRINT@480,"BEAM DIRECTION (1-3)";
470 PRINT@380,"1 2 ";:PRINT@412,"^/  ";:PRINT@444,"*-3 ";
480 C$=INKEY$:IFC$=""THEN480
481 D=VAL(C$)
490 IFD<1 OR D>3GOTO440
500 PRINT@380,LEFT$(B$,4);:PRINT@412,LEFT$(B$,4);:PRINT@444,LEFT$(B$,4);
510 DA=DA+1:M1=M:N1=N
520 FORI=1TO9
530 IFM>61 OR N<4 OR M<2THENI=9:NEXT:GOTO620
540 SET(M,N,C):SET(M+1,N,C):SET(M+2,N,C)
545 IFD=1THEN560
550 IFPOINT(M+3,N)THEN770
560 OND GOTO570,580,590,600
570 I=I+RND(2):N=N-1:I1=I1+1:GOTO610
580 M=M+2:N=N-1:GOTO610
590 M=M+2:GOTO610
600 M=M+2:N=N+1
610 NEXTI
620 IFPOINT(M+2,N-1)=C OR D=1THEN770
630 PRINT@480,B$;:M=M1:N=N1
640 REM ILLEGAL PLACEMENT
650 PRINT@480,"END NOT SUPPORTED!";:DA=DA+.5
660 FORI=1TO666:NEXT
670 FORI=1TO9:RESET(M,N):RESET(M+1,N):RESET(M+2,N)
680 OND GOTO690,700,710,720
690 N=N-1:GOTO730
700 M=M+2:N=N-1:GOTO730
710 M=M+2:GOTO730
720 M=M+2:N=N+1
730 IFN<2THEN750
740 NEXTI
750 PRINT@416,"DAY";INT(DA);:PRINT@480,B$;
760 GOTO240
770 PRINT@480,B$;
780 REM MOVE INSPECTOR
790 PRINT@480,"INSPECTION...";
800 IFW=0 THENW=1:GOTO810
801 W=0
810 FORI=1TORND(20)+5
820 GOSUB1050
830 IFW=1THEN880
840 IFPOINT(X-2,Y+1)=0 AND POINT(X,Y+1)=0 AND POINT(X+2,Y+1)=0 THENY=Y+1:GOTO910
850 IFPOINT(X-1,Y)=0 THENX=X-1:GOTO910
860 IFPOINT(X,Y-1)=0 THENY=Y-1:GOTO910
870 GOTO910
880 IFPOINT(X,Y+1)=0 AND POINT(X+3,Y+1)=0 THENY=Y+1:GOTO910
890 IFPOINT(X+3,Y)=0 THENX=X+1:GOTO910
900 IFPOINT(X,Y-1)=0 THENY=Y-1:GOTO910
910 GOSUB1030:IFX<4 OR X>59 THENGOSUB1080
920 NEXTI
930 PRINT@480,B$;
940 PRINT@416,"DAY";INT(DA);
950 REM CHECK FOR COMP. BRIDGE
960 F(4)=0:F(5)=0:FORI=7TO56STEP3:FORJ=4TO5
970 IFPOINT(I,J)THENF(J)=F(J)+1:IFF(4)>16 OR F(5)>16THEN1010
980 NEXTJ
990 NEXTI:GOTO240
1000 PRINT@480,B$;
1010 PRINT@480,"YOU'VE FINISHED! HIT ENTER";:INPUTZ$:RUN
1020 REM SET INSPECTOR
1030 SET(X,Y,C):SET(X+1,Y-1,C):SET(X+2,Y,C):SET(X+1,Y-3,C)
1040 SET(X,Y-2,C):SET(X+1,Y-2,C):SET(X+2,Y-2,C):RETURN
1050 RESET(X,Y):RESET(X+1,Y-1):RESET(X+2,Y):RESET(X+1,Y-3)
1060 RESET(X,Y-2):RESET(X+1,Y-2):RESET(X+2,Y-2):RETURN
1070 GOSUB10003:END
1080 IFW=0 THENW=1:RETURN
1090 W=0:RETURN
10000 IF PEEK(65535)=27 THEN POKE65497,0:GOTO10002 ELSE CLS:INPUT"CAN YOUR COMPUTER HANDLE DOUBLE SPEED (Y/N)";A$
10001 IF A$="Y" THEN POKE65495,0 ELSE IF A$<>"N" THEN10000
10002 CLS:RETURN
10003 IF PEEK(65535)=27 THEN POKE65496,0 ELSE POKE65494,0
10004 RETURN
