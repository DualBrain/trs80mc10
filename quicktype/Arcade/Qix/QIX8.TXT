0 DIMK(255),P(100),X,Y,A,B,Q,R,T,C,P,V,Q(511):GOTO5000
1 V=-1:RETURN
2 V=-32:RETURN
3 V=1:RETURN
4 V=32:RETURN
6 T=T+1:P(T)=P:POKEP,191:RETURN
7 FORC=1TOT:POKEP(C),223:NEXT:T=0:V=0:RETURN
8 V=0:RETURN
10 X=A:Y=B:A=0
11 IFPOINT(X,Y-1)THENQ=(RND(0)*.75+.25):A=1:GOTO13
12 IFPOINT(X,Y+1)THENQ=-(RND(0)*.75+.25):A=1
13 IFPOINT(X-1,Y)THENR=(RND(0)*.75+.25):GOTO21
14 IFPOINT(X+1,Y)THENR=-(RND(0)*.75+.25):GOTO21
16 IFATHEN21
17 Q=(RND(0)*.75+.25)*-SGN(Q):R=(RND(0)*.75+.25)*-SGN(R):GOTO21
20 FORA=1TO5000
21 A=X:B=Y:RESET(X,Y):X=X+R:Y=Y+Q:ONPOINT(X,Y)GOTO10,10,10,200,200,10,10,10:SET(X,Y,1)
22 A=X:B=Y:RESET(X,Y):X=X+R:Y=Y+Q:ONPOINT(X,Y)GOTO10,10,10,200,200,10,10,10:SET(X,Y,1)
23 ONK(ASC(INKEY$+"*"))GOSUB1,2,3,4,50:ONK(PEEK(P+V))GOSUB6,7,8:P=P+V:POKEP,207:NEXT
30 IFSC>HSTHENHS=SC
31 PRINT@0,"SCORE"SC;:PRINT@16,"HIGH";HS;:PRINT@480,"HIT c TO CONTINUE";
32 I$=INKEY$:IFI$<>"C"THEN32
34 GOTO2010
50 Q=0:N=0:X=INT(X/2):Y=INT(Y/2):PRINT@32*Y+X,CHR$(128);
51 Q=Q+1:Q(Q)=32*Y+X+16384
52 IFN=QTHENSC=SC+(315-Q):A=5000:RETURN
53 N=N+1:B=PEEK(Q(N)):IFB=128ORB=239THENGOSUB60
55 GOTO52
60 POKEQ(N),175:IFPEEK(Q(N)-1)=128THENQ=Q+1:Q(Q)=Q(N)-1:POKEQ(Q),239
62 IFPEEK(Q(N)+1)=128THENQ=Q+1:Q(Q)=Q(N)+1:POKEQ(Q),239
63 IFPEEK(Q(N)-32)=128THENQ=Q+1:Q(Q)=Q(N)-32:POKEQ(Q),239
64 IFPEEK(Q(N)+32)=128THENQ=Q+1:Q(Q)=Q(N)+32:POKEQ(Q),239
65 RETURN
200 PRINT@0,"SCORE"SC;:PRINT@16,"HIGH";HS;:PRINT@480,"YOU'RE DEAD!  PLAY AGAIN (y/n)?";
210 I$=INKEY$:SET(X,Y,1):SET(X,Y,4):SET(X,Y,8):SET(X,Y,2):IFI$=""THEN210
220 IFI$="Y"THEN2005
230 IFI$="N"THENEND
240 GOTO210 
2000 K(65)=1:K(87)=2:K(83)=3:K(90)=4:K(32)=5:K(128)=1:K(223)=2:K(191)=3
2005 SC=0
2010 CLS0:FORX=16384TO16415:POKEX,223:NEXT:FORX=16447TO16895STEP32:POKEX,223:NEXT
2020 FORX=16894TO16864STEP-1:POKEX,223:NEXT:FORX=16832TO16416STEP-32:POKEX,223:NEXT
2030 X=13+RND(20):Y=13+RND(10):R=1:Q=1:P=16384+33:POKEP,207:V=0:T=0:P(1)=16384:GOTO20
5000 CLS:PRINTTAB(14)"QIX":PRINT
5010 PRINTTAB(9)"BY JIM GERRIE":PRINT
5030 PRINT"  awsz  = MOVE        YOU = "CHR$(207)
5040 PRINT"  space = END ROUND":PRINT
5050 PRINT"  TRY TO CONFINE THE GREEN"
5060 PRINT"  DOT AS MUCH AS POSSIBLE"
5070 PRINT"  BEFORE ENDING A ROUND."
5080 PRINT"  DON'T LET IT HIT YOU OR"
5090 PRINT"  THE RED TRAIL YOU LEAVE."
5095 PRINT"  HIT COMPLETED WALLS TO"
5096 PRINT"  CONVERT YOUR TRAIL TO WALL.":PRINT:PRINTTAB(6)"HIT ANY KEY TO BEGIN";
5097 FORX=16384TO16415:POKEX,PEEK(X)-64:NEXT:FORX=16447TO16895STEP32:POKEX,PEEK(X)-64:NEXT
5098 FORX=16894TO16864STEP-1:POKEX,PEEK(X)-64:NEXT:FORX=16832TO16416STEP-32:POKEX,PEEK(X)-64:NEXT
6010 IFINKEY$=""THEN6010
6020 GOTO2000
