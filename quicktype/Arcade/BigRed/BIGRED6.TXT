0 CLS:DIMV(4,1):V(1,1)=1:V(2,0)=1:V(3,1)=-1:V(4,0)=-1:Z=RND(-(PEEK(9)*256+PEEK(10))):GOTO40
1 PRINT@8*32+14,"±³°";:PRINT@9*32+14,"½¿¸";:RETURN
3 PRINT@8*32+13,"°³³²";:PRINT@9*32+13,"µ»»¿";:PRINT@10*32+13,"´»³¾";:RETURN
6 PRINT@9*32+12,"€·¼¾½»€";:PRINT@10*32+12,"µ¿³»·¿º";:PRINT@11*32+12,"µ¿€¸¸¿º";:PRINT@12*32+12,"€½»³·¾€";:RETURN
14 PRINT@9*32+10,"€±¿¿¿¿¿¿¿»²";:PRINT@10*32+10,"±¿¿°°¿°°¿¿¿";:PRINT@11*32+10,"µ¿¿°°¿°°¿¿¿";:PRINT@12*32+10,"µ¿¿¼¿¼½¾¼¿¿";
18 PRINT@13*32+10,"´¿º°´°°¸³¿¿";:PRINT@14*32+10,"°´½¿³³³³¿¾¸";:RETURN
20 CLS4:PRINT@0,"BIG RED GOT YOU!";:END
21 PRINT@8*32+14,"€€€";:PRINT@9*32+14,"€€€";:RETURN
23 PRINT@8*32+13,"€€€€";:PRINT@9*32+13,"€€€€";:PRINT@10*32+13,"€€€€";:RETURN
26 PRINT@9*32+12,"€€€€€€€";:PRINT@10*32+12,"€€€€€€€";
28 PRINT@11*32+12,"€€€€€€€";:PRINT@12*32+12,"€€€€€€€";:RETURN
34 PRINT@9*32+10,"€€€€€€€€€€€";:PRINT@10*32+10,"€€€€€€€€€€€";:PRINT@11*32+10,"€€€€€€€€€€€";:PRINT@12*32+10,"€€€€€€€€€€€";
38 PRINT@13*32+10,"€€€€€€€€€€€";:PRINT@14*32+10,"€€€€€€€€€€€";:RETURN
40 PRINT" ******************************"
41 PRINT" * 'BIG RED' FROM DON SCALES' *"
42 PRINT" *     LABYRINTH (C) 1980     *"
43 PRINT" * MC-10 J. GERRIE JUNE 2014  *"
44 PRINT" ******************************"
45 PRINT:PRINT"YOU FIND YOURSELF IN A MAZE WITHA GIANT KILLER TOMATO!":PRINT"YOU MUST FIND THE EXIT BEFORE"
46 PRINT"HE FINDS YOU.  GOOD LUCK.":PRINT
47 PRINT:REM GET MAZE DIMENSION
50 H=10:V=7
70 PRINT "THINKING..."
95 REM CLEAR MAZE ARRAY
100 A=H*V+1:DIMA(A+A)
110 FORI=1TOA+A:A(I)=0:NEXTI
120 Q=0:Z=0:X=RND(H)
125 REM-SAVE MAZE ENTRY POINT
130 A(A)=X
140 A(X)=1:C=2
150 R=X:S=1:GOTO220
155 REM START OF MAZE BUILD ROUTINE
160 IFR<>HTHEN200
170 IFS<>VTHEN190
180 R=1:S=1:GOTO210
190 R=1:S=S+1:GOTO210
200 R=R+1
210 IFA(R+(S-1)*H)=0THEN160
220 IFR-1=0THEN610
230 IFA(R-1+(S-1)*H)<>0THEN610
240 IFS-1=0THEN420
250 IFA(R+(S-2)*H)<>0THEN420
260 IFR=HTHEN320
270 IFA(R+1+(S-1)*H)<>0THEN320
275 REM-LEFT/DOWN/RIGHT
280 X=RND(3)
290 IFX=1THEN960
300 IFX=2THEN1000
310 GOTO1040
320 IFS<>VTHEN350
330 IFZ=1THEN400
340 Q=1:GOTO360
350 IFA(R+S*H)<>0THEN400
355 REM-LEFT/DOWN/UP
360 X=RND(3)
370 IFX=1THEN960
380 IFX=2THEN1000
390 GOTO1110
395 REM-LEFT/DOWN
400 X=RND(2)
410 GOTO370
420 IF R=H GOTO 540
430 IF A(R+1+(S-1)*H)<>0 GOTO 540
440 IF S<>V GOTO 470
450 IF Z=1 GOTO 520
460 Q=1:GOTO 480
470 IF A(R+S*H)<>0 GOTO 520
475 REM LEFT/RIGHT/UP
480 X=RND(3)
490 IF X=1 GOTO 960
500 IF X=2 GOTO 1040
510 GOTO 1110
515 REM-LEFT/RIGHT
520 X=RND(2)
530 GOTO 490
540 IF S<>V GOTO 570
550 IF Z=1 GOTO 960
560 Q=1:GOTO 580
570 IF A(R+S*H)<>0 GOTO 960
575 REM-LEFT/UP
580 X=RND(2)
590 IF X=1 GOTO 960
600 GOTO 1110
610 IF S-1=0 GOTO 820
620 IF A(R+(S-2)*H)<>0 GOTO 820
630 IF R=H GOTO 750
640 IF A(R+1+(S-1)*H)<>0 GOTO 750
650 IF S<>V GOTO 680
660 IF Z=1 GOTO 730
670 Q=1:GOTO 690
680 IF A(R+S*H)<>0 GOTO 730
685 REM-DOWN/RIGHT/UP
690 X=RND(3)
700 IF X=1 GOTO 1000
710 IF X=2 GOTO 1040
720 GOTO 1110
725 REM-DOWN/RIGHT
730 X=RND(2)
740 GOTO 700
750 IF S<>V GOTO 780
760 IF Z=1 GOTO 1000
770 Q=1:GOTO 790
780 IF A(R+S*H)<>0 GOTO 1000
785 REM-DOWN/UP
790 X=RND(2)
800 IFX=1THEN1000
810 GOTO 1110
820 IFR=HTHEN910
830 IFA(R+1+(S-1)*H)<>0THEN910
840 IFS<>VTHEN870
850 IFZ=1THEN1040
860 Q=1:GOTO880
870 IFA(R+S*H)<>0THEN1040
875 REM-RIGHT/UP
880 X=RND(2)
890 IFX=1THEN1040
900 GOTO1110
910 IFS<>VTHEN940
920 IFZ=1THEN160
930 Q=1:GOTO 950
940 IF A(R+S*H)<>0 GOTO 160
950 GOTO 1110
955 REM-LEFT
960 A(R-1+(S-1)*H)=C
970 C=C+1:A(A+R-1+(S-1)*H)=2:R=R-1
980 IF C=A GOTO 1210
990 Q=0:GOTO 220
995 REM-DOWN
1000 A(R+(S-2)*H)=C
1010 C=C+1
1020 A(A+R+(S-2)*H)=1:S=S-1:IFC=ATHEN1210
1030 Q=0:GOTO220
1035 REM-RIGHT
1040 A(R+1+(S-1)*H)=C
1050 C=C+1:IFA(A+R+(S-1)*H)=0THEN1070
1060 A(A+R+(S-1)*H)=3:GOTO1080
1070 A(A+R+(S-1)*H)=2
1080 R=R+1
1090 IFC=ATHEN1210
1100 GOTO610
1105 REM-UP
1110 IFQ=1THEN1170
1120 A(R+S*H)=C:C=C+1:IFA(A+R+(S-1)*H)=0THEN1140
1130 A(A+R+(S-1)*H)=3:GOTO1150
1140 A(A+R+(S-1)*H)=1
1150 S=S+1:IFC=ATHEN1210
1160 GOTO 220
1165 REM-EXIT AT TOP OF SCREEN
1170 Z=1
1180 IFA(A+R+(S-1)*H)=0THEN1200
1190 A(A+R+(S-1)*H)=3:Q=0:GOTO160
1200 A(A+R+(S-1)*H)=1:Q=0:R=1:S=1:GOTO210
1205 REM-MAKE EXIT IF NOT THERE
1210 IFZ<>1THENX=A+RND(H)+(V-1)*H:A(X)=A(X)+1
1295 REM-END OF MAZE BUILD
1610 XX=RND(7):YY=RND(6):ZZ=RND(4):PRINTXX;YY:PRINT:PRINT"PRESS ANY KEY WHEN READY...";
1611 REM PRINT:FORVV=0TOV-1:FORHH=1TOH:PRINTSTR$(A(HH+(VV*V)));:NEXT:PRINT:NEXT
1620 I$=INKEY$:IFI$=""THEN1620
1625 REM-PRINT INSTRUCTION
1630 GOSUB1900:CLS0
1635 REM TRANSLATE ROUTINE
1640 FORI=1TOA-1
1650 J=I+A
1660 A(I)=(3-A(J))*2
1670 NEXTI
1710 W=A(A)
1715 REM-COMPLETE CELL INFORMATION
1720 FORJ=1TOV
1730 K=(J-1)*H
1740 FORI=1TOH
1750 L=I+K
1760 IFJ<>1THEN1790
1770 IFI=WTHEN1820
1780 M=1:GOTO1810
1790 M=INT(A(L-H)/2)
1800 M=M-INT(M/2)*2
1810 A(L)=A(L)+M*8
1820 IFI=1THENM=1:GOTO1850
1830 M=INT(A(L-1)/4)
1840 M=INT(M-INT(M/2)*2)
1850 A(L)=A(L)+M
1860 NEXTI
1870 NEXTJ
1875 REM SET UP START PARAMS
1880 X=W:Y=0:Z=1
1890 GOTO3050
1895 REM INSTRUCTION PRINTOUT
1900 CLS
1910 PRINT "ENTER A TO TURN LEFT"
1920 PRINT "      S TO TURN RIGHT"
1930 PRINT "      W TO GO FORWARD"
1940 PRINT "      H FOR HELP"
1950 PRINT "      Q TO GIVE UP!"
1960 RETURN
1995 REM HELP ROUTINE
2000 PRINT@0,"YOU ARE AT "; 
2010 PRINT@32,X;"EAST";
2020 PRINT@64,Y;"NORTH";
2030 PRINT@128,"YOU ARE FACING ";
2040 IFZ=1THENPRINT"NORTH";
2050 IFZ=2THENPRINT"EAST";
2060 IFZ=3THENPRINT"SOUTH";
2070 IFZ=4THENPRINT"WEST";
2080 PRINT@0,;
2090 GOSUB2450
2100 GOTO2200
2150 SS=1:LX=X:LY=Y:FORCC=1TODP:IFLX=XXANDLY=YYTHENONCCGOSUB20,14,6,3,1:SS=2:TC=CC:CC=DP:NEXT:RETURN
2160 LX=LX+V(Z,0):LY=LY+V(Z,1):NEXT:RETURN
2195 REM KEYBOARD ROUTINE
2200 IFY>VTHEN5000
2210 GOSUB2150
2224 TZ=Z:ONRND(5)GOTO2228,2228,2228,2228:Z=ZZ:A=XX:B=YY:GOSUB2600:IFE<>0THENZZ=RND(4):GOTO2228
2225 IFABS(XX-X)<3ANDABS(YY-Y)<3THENSOUND1,1
2226 XX=XX+V(Z,0):YY=YY+V(Z,1):PRINT@0,XX;YY;ZZ;:Z=TZ:ONSSGOTO2227:ONTCGOSUB20,34,26,23,21:GOTO3050
2227 GOSUB2150:ONSSGOTO2228:GOTO3050
2228 Z=TZ:I$=INKEY$:IFI$=""THEN2224
2230 IFI$="A"THENCLS0:GOTO2300
2240 IFI$="S"THENCLS0:GOTO2350
2250 IFI$="W"THENCLS0:GOTO3000
2260 IFI$="H"THEN2000
2270 IFI$="Q"THENEND
2280 GOTO2224
2295 REM LEFT TURN
2300 Z=Z-1:IFZ<1THENZ=Z+4
2320 GOTO3050
2345 REM RIGHT TURN
2350 Z=Z+1:IFZ>4THENZ=Z-4
2370 GOTO3050
2445 REM-RESET/HOME CURSOR
2450 PRINT@0,;
2460 RETURN
2595 REM ROTATE AND LOOK ROUTINE
2600 IFB=0THEN2710
2610 IFB>VTHENE=2:RETURN
2620 F=A(A+(B-1)*H):IFZ=1THEN2670
2640 FORI=2TOZ:F=INT(F/2)+(F-INT(F/2)*2)*8:NEXT
2670 C=F-INT(F/2)*2:D=INT(F/4)-INT(F/8)*2:E=INT(F/2)-INT(F/4)*2:RETURN
2705 REM OUTSIDE MAZE
2710 C=0:D=0:E=-1:IFZ<>1THEN2760
2730 E=1:IFA=WTHENE=0
2750 RETURN
2755 REM NO MAN"S LAND
2760 IFZ=3THENE=2
2770 IFZ=2THENIFA=HTHENE=2
2780 IFZ=4THENIFA=1THENE=2
2790 RETURN
2795 REM INDEX TO THE NEXT CELL
2800 IFE>0THEN2930
2810 A=A+V(Z,0):B=B+V(Z,1):RETURN
2855 REM MESSAGE ROUTINE
2860 PRINT@0,;
2890 FORI=1TO23
2900 PRINT@0,;:SOUND1,1
2910 NEXT I
2920 RETURN
2930 GOSUB2860
2940 IFE=1THENPRINT "  DEAD END   ",
2950 IFE=2THENPRINT "NO MAN'S LAND",
2960 GOSUB2450
2970 S=1
2980 RETURN
2995 REM FORWARD ROUTINE
3000 A=X:B=Y
3010 GOSUB2600
3020 GOSUB2800
3030 X=A:Y=B
3040 IFE>0THEN2200
3045 REM 3D DISPLAY ROUTINE
3050 A=X:B=Y
3060 GOSUB2600
3065 REM 5 DEPTHS
3070 FORT=1TO5
3080 GOSUB3200
3085 REM CHECK FOR NEXT DEPTH
3090 IFE<>0THEN2200
3100 GOSUB2800
3110 GOSUB2600
3120 IFE=2THEN2200
3130 NEXTT
3140 GOTO2200
3195 REM JUMP TO DISPLAY DEPTH
3200 ONTGOTO3210,3610,4010,4410,4810
3205 REM DISPLAY DEPTH 1
3210 DP=1:IFE<0THENRETURN
3230 IFE>1THENRETURN
3240 FORI=33TO481STEP32
3250 PRINT@I,"Õ";
3260 PRINT@I+28,"Ú";
3270 NEXTI
3280 IFE=0THENRETURN
3290 PRINT@34 ,"ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ";:PRINT@482,"ÓÓÓÓÓÓÓÓÓÓÓÓÓÓÓÓÓÓÓÓÓÓÓÓÓÓÓ";:RETURN
3600 REM DISPLAY DEPTH 2
3610 IFC=0THEN3690
3620 PRINT@34,"Ù";:PRINT@68,"Ù";:PRINT@102,"Ù";:PRINT@482,"Ö";:PRINT@452,"Ö";:PRINT@422,"Ö";:GOTO3730
3690 PRINT@130,"ÜÜÜÜÜ";:PRINT@386,"ÓÓÓÓÓ";
3730 IFD=0THEN3810
3740 PRINT@60,"Ö";:PRINT@90,"Ö";:PRINT@120,"Ö";:PRINT@508,"Ù";:PRINT@474,"Ù";:PRINT@440,"Ù";:GOTO3850
3810 PRINT@152,"ÜÜÜÜÜ";:PRINT@408,"ÓÓÓÓÓ";
3850 FORI=135TO391STEP32:PRINT@I,"Õ";:PRINT@I+16,"Ú";:NEXTI
3890 DP=2:IFE=0THENRETURN
3900 PRINT@136,"ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ";:PRINT@392,"ÓÓÓÓÓÓÓÓÓÓÓÓÓÓÓ";:RETURN
4000 REM DISPLAY DEPTH 3
4010 IFC=0THEN4070
4020 PRINT@136,"Ù";:PRINT@170,"Ù";:PRINT@392,"Ö";:PRINT@362,"Ö";:GOTO4110
4070 PRINT@200,"ÜÜÜ";:PRINT@328,"ÓÓÓ";
4110 IFD=0THEN4170
4120 PRINT@150,"Ö";:PRINT@180,"Ö";:PRINT@406,"Ù";:PRINT@372,"Ù";:GOTO4210
4170 PRINT@212,"ÜÜÜ";:PRINT@340,"ÓÓÓ";
4210 FORI=203TO331STEP32:PRINT@I,"Õ";:PRINT@I+8,"Ú";:NEXTI:DP=3:IFE=0THENRETURN
4260 PRINT@204,"ÜÜÜÜÜÜÜ";:PRINT@332,"ÓÓÓÓÓÓÓ";:RETURN
4400 REM DISPLAY DEPTH 4
4410 IFC=0THEN4450
4420 PRINT@204,"Ù";:PRINT@332,"Ö";
4440 GOTO4470
4450 PRINT@236,"Ü";:PRINT@300,"Ó";
4470 IFD=0THEN4510
4480 PRINT@210,"Ö";:PRINT@338,"Ù";
4500 GOTO4530
4510 PRINT@242,"Ü";:PRINT@306,"Ó";
4530 FORI=237TO301STEP32:PRINT@I,"Õ";:PRINT@I+4,"Ú";:NEXTI
4570 DP=4:IFE=0THENRETURN
4580 PRINT@238,"ÜÜÜ";:PRINT@302,"ÓÓÓ";:RETURN
4800 REM DISPLAY DEPTH 5
4810 IFC=0THEN4850
4820 PRINT@238,"Ù";:PRINT@302,"Ö";:GOTO4870
4850 PRINT@238,"Ó";:PRINT@302,"Ü";
4870 IFD=0THEN4910
4880 PRINT@240,"Ö";:PRINT@304,"Ù";:GOTO4930
4910 PRINT@240,"Ó";:PRINT@304,"Ü";
4930 PRINT@270,"Õ";:PRINT@272,"Ú";
4950 DP=5:IFE=0THENRETURN
4960 PRINT@239,"Ó";:PRINT@303,"Ü";:RETURN
4995 REM WAY OUT FOUND
5000 CLS0
5010 GOSUB2860
5020 PRINT"YOU MADE IT OUT ALIVE!"
5030 END
