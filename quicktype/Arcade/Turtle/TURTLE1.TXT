10 CLEAR 1000:DIMKE
100 REM BASIC WEEK 3: UNDER THE SEA
110 REM HTTP://REDDIT.COM/R/RETROBATTLESTATIONS
120 REM WRITTEN BY FOZZTEXX
130 REM COCO3 VERSION BY PAULO GARCIA
140 REM BASED ON APPLE II VERSION
150 REM MC-10 VERN BY JIM GERRIE
200 REM CLR SCREEN AND SETUP VARS
210 CLS
220 COLS=32:ROWS=16:REM COL & ROWS OF SCREEN
230 GOSUB2010:TU$=SP$:TW=SC:TH=SR
240 TE$=SE$:GOSUB2010:TD$=SP$
250 FOREE=1TO3:GOSUB2010:EN$(EE)=SP$
260 ER$(EE)=SE$:EW(EE)=SC:NEXT EE
270 GOSUB2010:GV$=SP$:GW=SC:GH=SR
280 AM=10:WV$="--."
290 TY=ROWS-2:SF=4:GOSUB4100:PT=-10:GOSUB4510
300 REM PLAY GAME
310 X=1:Y=SF:GOSUB1010
320 FOR I=1 TO COLS:PRINT MID$(WV$,RND(3),1);:NEXT I
325 NE=3
330 GOSUB5140:GOSUB6560:GOSUB6140
340 LY=TY
350 FORTX=1TOCOLS-TW
360 IFLY<>TYTHENGOSUB3510
370 GOSUB5010:GOSUB6510:GOSUB6010
380 SP$=TU$:X=TX:Y=TY:GOSUB1510
390 LY=TY:GOSUB2510
400 IFKE$="A"THEND=-1:GOSUB3010
410 IFKE$="Z"THEND=1:GOSUB3010
420 IFKE$="Q"THENEND
430 IFTY=SF+1THEN450
440 IFTX=(COLS/4)ORTX=(COLS/4)*3THENGOSUB4010
450 IFTY=SF+1ANDAIR<AMTHENGOSUB4100
460 NEXTTX
470 Y=TY:X=TX:SP$=TE$:GOSUB1510
480 GOTO310
1000 REM POSITION CURSOR AT X,Y
1010 PP=((Y-1)*32)+(X-1)
1020 PRINT@PP,"";
1030 RETURN
1500 REM DRAW SPRITE IN SP$ AT X,Y
1510 SE=1:SB=1:IF Y<1 OR Y>ROWS OR X<1 OR X>COLS THEN RETURN
1515 GOSUB1010
1520 REM SET LEFT MARG TO X POS
1530 IFMID$(SP$,SE,1)=CHR$(13)THEN1540
1533 IFSE>LEN(SP$)THENSE=0:GOTO1540
1535 SE=SE+1:GOTO1530
1540 IF SE=0 THEN SE=LEN(SP$)+1
1550 GOSUB1010
1560 SL=SE-SB:IFSL+X-1>COLSTHENSL=COLS-X+1
1570 PRINT MID$(SP$,SB,SL);
1580 IFSE>=LEN(SP$)THENRETURN
1590 IFASC(MID$(SP$,SE))=13THENSE=SE+1
1600 SB=SE:Y=Y+1:GOTO1520
2000 REM LOAD SPRITE FROM DATA INTO SP$
2010 SP$="":SE$="":SR=0:SC=0
2020 READT$,S$:IFLEN(SP$)>0THENSP$=SP$+CHR$(13)
2030 SP$=SP$+S$:SR=SR+1:IFLEN(S$)>SCTHENSC=LEN(S$)
2040 BL$="":FORI=1TOLEN(S$):BL$=BL$+" ":NEXTI
2050 IFLEN(SE$)>0THEN SE$=SE$+CHR$(13)
2060 SE$=SE$+BL$
2070 IFT$<>"SE"THEN2020
2080 RETURN
2500 REM READ THE KEYBOARD
2510 KE$="":KE=PEEK(17023)ANDPEEK(2):IFKETHENKE$=CHR$(KE)
2520 RETURN
3000 REM PLAYER WANTS TO MOVE TURTLE UP OR DOWN
3010 TY=TY+D
3020 IFTY<=SFTHENTY=SF+1
3030 IFTY>ROWS-TH+1THENTY=ROWS-TH+1
3040 RETURN
3500 REM TURTLE MOVED U/D, ERASE PREV LINE
3510 Y=LY:REM IF LY>TY THEN Y=T+TH-1
3520 X=TX:SP$=TE$:GOSUB1510
3530 RETURN
4000 REM SHOW REMAINING AIR
4010 AIR=AIR-1:IFAIR<0THENAIR=0
4020 IFAIR>AMTHENAIR=AM
4030 X=COLS-7-AM:Y=1:GOSUB1010
4040 PRINT"AIR: I";
4050 FORI=1TOAIR:PRINT"#";:NEXTI
4060 IFAIR<AMTHENFORI=AIR+1TOAM:PRINT " ";:NEXTI
4070 PRINT"I";
4080 IFAIR=0THEN4110
4090 RETURN
4100 AIR=AM:GOTO4030
4110 FORI=TYTOSF+1STEP-1
4120 X=TX:Y=I:SP$=TD$:GOSUB1510
4130 Y=Y+1:SP$=TE$:GOSUB1510
4140 NEXT I
4150 X=(COLS-GW)/2:Y=(ROWS/GH)/2
4160 SP$=GV$:GOSUB1510
4165 SOUND10,10
4170 X=1:Y=ROWS-2:GOSUB1010
4180 END
4500 REM DISPLAY SCORE
4510 PT=PT+10:OX=X:OY=Y
4520 X=3:Y=1:GOSUB1010:PRINT"SCORE: ";PT;
4530 X=OX:Y=OY:RETURN
5000 REM TAKE CARE OF BUBBLES
5005 REM (NB=NUM BUBBLES)
5007 REM (BX/Y=BX/BY)
5010 FORBB=1TONB
5020 IFBY(BB)<=SFTHEN5120
5030 NY=BY(BB)-(ROWS/COLS)
5040 X=BX(BB):Y=BY(BB)
5050 GOSUB5510:IFHIT=0THEN5080
5060 AIR=AIR+2:IFBUBBLE$(BB)=CHR$(111)THENAIR=AIR+1:SOUND200,1
5070 GOSUB4010:NY=ROWS+COLS
5080 IFNY=YTHEN5110
5090 SP$=" ":GOSUB1510:SP$=BUBBLE$(BB):Y=NY
5100 IFY>SFTHENGOSUB1510
5110 BY(BB)=NY
5120 NEXT BB
5130 RETURN
5140 NB=RND(3):FORBB=1TONB
5150 BX(BB)=RND(COLS):BY(BB)=ROWS-RND(3)
5160 BUBBLE$(BB)=CHR$(111)
5170 NEXTBB:RETURN
5500 REM HIT DETECTION
5510 HIT=0
5520 IFX<TX+TW-2THENRETURN
5530 IFX>TX+TW+1THENRETURN
5540 IFY<TY-1THENRETURN
5550 IFY>TY+1THENRETURN
5560 HIT=1:RETURN
6000 REM TAKE CARE OF ENEMIES
6005 REM NE=NUMENEMIES
6007 REM ENEMYX/Y/HIT=EX/EY/EH
6010 FOR EE=1 TO NE
6030 IF EY(EE)<=SFTHEN6120
6035 NX=EX(EE)+XV(EE):NY=EY(EE)+YV(EE)
6040 X=EX(EE):Y=EY(EE)
6050 IF EH(EE)=1THEN6070
6060 GOSUB5510:IFHIT=1THENGOSUB4010:EH(EE)=1:SOUND 10,1
6070 IFINT(NX)=XANDINT(NY)=YTHEN6110
6080 SP$=ER$(EC(EE)):GOSUB1510
6090 SP$=EN$(EC(EE)):X=INT(NX):Y=INT(NY)
6100 IFY>SFTHENGOSUB1510
6110 EX(EE)=NX:EY(EE)=NY
6120 NEXT EE
6130 RETURN
6140 FOREE=1TONE:SP$=ER$(EC(EE))
6150 X=EX(EE):Y=EY(EE):GOSUB1510:NEXTEE
6160 NE=RND(3):FOR EE=1 TO NE
6170 EX(EE)=RND(COLS)-2:EY(EE)=ROWS-RND(ROWS-SF)
6180 XV(EE)=RND(2)-1:YV(EE)=RND((ROWS-SF)/COLS)
6190 EC(EE)=RND(3):EH(EE)=0
6200 IFEX(EE)>COLS/2THENXV(EE)=-XV(EE)
6210 IFEY(EE)>ROWS/2THENYV(EE)=-YV(EE)
6220 NEXT EE:RETURN
6500 REM TAKE CARE OF FOOD
6505 REM NF=NUMFOOD
6507 REM FOODX/Y = FX/FY
6510 FOR FF=1 TO NF
6520 X=FX(FF):Y=FY(FF):GOSUB5510
6530 IF HIT=1 THEN GOSUB4510:SP$=" ":GOSUB1510:FY(FF)=ROWS+COLS:SOUND120,1
6540 NEXT FF
6550 RETURN
6560 SP$=" ":FORFF=1TONF:X=FX(FF):Y=FY(FF):GOSUB1510:NEXTFF
6670 NF=RND(3):FORFF=1TONF
6680 FX(FF)=RND(COLS):FY(FF)=ROWS-RND((ROWS-SF))
6690 FD$(FF)=CHR$(102)
6700 X=FX(FF):Y=FY(FF):SP$=FD$(FF):GOSUB1510
6710 NEXT FF:RETURN
9000 DATA SP," -=%#%#]@"
9010 DATA SE,"       ]"
9020 DATA SE,"  -OOOO]X"
9030 DATA SE,"()=("
9040 DATA SE,"<><"
9050 DATA SE,"$"
9060 DATA SE,"G A M E  *  O V E R"

