0 GOSUB3500:CLEAR5500:GOSUB1000
1 FORML=1TO64000:GOSUB3000:FORG=1TO500STEP2:RESET(X,Y(G)):RESET(A,Y(G+1)):RESET(X,B(G)):RESET(A,B(G+1))
2 FORC=1TOQ:S$(C)=MID$(S$(C),2)+CHR$(PEEK(P(C))):NEXT
3 PRINT@S,S$(1)B$S$(2)B$S$(3)B$S$(4)B$S$(5)B$S$(6)B$S$(7)B$S$(8)B$S$(9)B$S$(N)B$S$(P)B$S$(T)B$S$(Q)B$;
4 ONK(PEEK(K)ANDPEEK(2))GOSUB20,30,40,50,60:R=R-1:IFZ=RTHENGOSUB75
5 ONK(PEEK(Z))GOSUB83,70:POKEZ,SH:ONRND(DL)GOSUB8,9
6 EC=EC+1:ONEC(EC)GOSUB90:ONRND(7)GOSUB63,64,66,65,65,67,69:ONK(PEEK(R+ZZ))GOSUB68,69,75,80:R=R+ZZ:POKER,E:NEXT:GOSUB97:NEXT
7 GOTO99
8 POKEG(G+(2-RND(2))),N1:RETURN
9 POKEG(G+(2-RND(2))),N2:RETURN
20 PRINT@Z-ST,SH$;:RETURN
30 Z=Z-1:RETURN
40 Z=Z+1:RETURN
50 Z=Z-S:RETURN
60 Z=Z+S:RETURN
63 ZZ=-1:EC=EC+1:ONEC(EC)GOSUB90:RETURN
65 ZZ=(SGN(Z-R)*S)*ABS(ABS(Z-R)>Q):RETURN
64 ZZ=-S:RETURN
66 ZZ=S:RETURN
67 ZZ=0:RETURN
68 ONRND(6)GOSUB63,64,66,65,65,67:ONK(PEEK(R+ZZ))GOTO68:RETURN
69 RETURN
70 SOUNDSH,1:SC=SC+SW:SL=INT((Z-MC)/S):L$=S$(SL):VP=VARPTR(L$):POKE(PEEK(VP+2)*T6)+PEEK(VP+3)+(S*(((Z-MC)/S)-SL)),F3:S$(SL)=L$
72 PRINT@448,"SCORE:";SC;"            ";:RETURN
75 PRINT@448,"TORPEDO HIT!   ";:POKEZ,191:SOUND1,9:POKEV-(3-CC),S:CC=CC-1:GOSUB72:GOSUB90:IFCC<1THENGOSUB83
77 RETURN
80 PRINT@448,"YOU GOT IT!    ";:POKER+ZZ,159:SOUND100,2:SC=SC+SW:GOSUB72:GOSUB90:RETURN
83 FORY=1TON:SOUND1,1:POKEZ,240+RND(15):NEXT:SOUND1,4:GOSUB72:IFSC>HSTHENHS=SC
86 PRINT@464,"HIGH:";HS;:G=G+2:RESET(X,Y(G)):RESET(A,Y(G+1)):RESET(X,B(G)):RESET(A,B(G+1)):ML=64000:G=500:RETURN
90 EC=0:R=G(G)-1:ZZ=0:RETURN
97 IF(((Z-MC)/S)-INT((Z-MC)/S))*S<6THENFORY=1TON:SOUND1,1:POKEZ,240+RND(15):NEXT:SOUND1,4:ML=64000:G=500
98 RETURN
99 PRINT@480,"GAME OVER.  PLAY AGAIN (Y/N)?";
100 I$=INKEY$:IFI$="C"THEN100
110 IFI$="N"THENEND
120 IFI$<>"Y"THEN100
140 CLS1:SC=0:DL=6+LV:CV=0:CC=3:GOTO1
150 FORG=1TO450STEP2:R=2-RND(3):IFY+R<2ORY+R>B-NTHENR=0
151 Y=Y+R:Y(G)=Y:Y=Y+R:Y(G+1)=Y:R=2-RND(3):IFB+R+R>LORB+R<Y+NTHENR=0
152 B=B+R:B(G)=B:B=B+R:B(G+1)=B:G(G)=V+(INT(Y(G)/2)*S)+GV:G(G+1)=V+(INT(B(G)/2)*S)-GV:NEXT
160 FORG=451TO500STEP2:Y(G)=Y:Y(G+1)=Y:B(G)=B:B(G+1)=B:G(G)=V+(INT(Y(G)/2)*S)+GV:G(G+1)=V+(INT(B(G)/2)*S)-GV:NEXT
200 RETURN
1000 DIMS$(13),SH$,B$,L$,Y(502),B(502),K(255),P(13),K,N,P,T,Q,G,X,Y,A,B,C,Z,R,ZZ,E,L,S,EC
1001 DIMG(502),EC(32),SH,U,V,ML,ST,HS,MC,T6,F3,S4,CV,DL,SC,CC,GV,VP,LO,SL,SW,N1,N2,LV,I$,LN$:R=RND(-(PEEK(9)*256+PEEK(10)))
1002 U=30:V=16415:S=32:L=27:B$=CHR$(143):FORC=27TO32:EC(C)=1:NEXT:SH$="----":ST=16383:CC=3
1005 N=10:P=11:T=12:Q=13:K=17023:SH=126:E=95:GV=96:SW=50
1010 FORC=1TOQ:S$(0)=S$(0)+B$:LN$=LN$+B$:NEXT:FORC=QTOU:S$(0)=S$(0)+B$:LN$=LN$+CHR$(131):NEXT
1020 FORC=1TOQ:P(C)=V+(S*C):NEXT:G(501)=ST+256:G(502)=ST+256
2000 FORC=128TO142:K(C)=1:NEXT:K(30)=1:K(95)=1:K(13)=1:K(126)=3:K(109)=4:K(106)=2
2020 K(32)=1:K(65)=2:K(83)=3:K(87)=4:K(90)=5
2030 HS=0:MC=16384:T6=256:F3=143:S4=64:DL=6+LV:N1=42:N2=109
2090 GOSUB6080:RETURN
3000 PRINT@0,"PREPARE FOR THE NEXT CAVERN...  ";:CV=CV+1:IFCV>1THENGOSUB5000
3002 X=62:Y=8:A=63:B=22:Z=16655:EC=0
3003 DL=DL-1:IFDL<(LV+3)THENDL=(LV+3)
3005 GOSUB150
3040 FORC=1TOQ:S$(C)=S$(0):NEXT:S$(4)=LN$:S$(P)=LN$:GOSUB4000:FORC=MCTOV:POKEC,PEEK(C)-S4:NEXT
3050 R=16670:RETURN
3500 GOSUB4000:GOSUB6000:RETURN
4000 CLS1:PRINT" CAVERN RAIDER BY JIM GERRIE ";LEFT$(CHR$(95)+CHR$(95)+CHR$(95),CC);"   ";
4010 RETURN
5000 IFCV=5THENGOSUB5050
5010 IFCV=4ORCV=8ORCV=12ORCV=16ORCV=20ORCV=32THENGOSUB5070
5015 IFCV=10THENPOKE49151,64:SW=SW*N
5020 PRINT@227,"CONGRATULATIONS ON MAKING";
5030 PRINT@259,"IT TO THE END OF CAVERN";STR$(CV-1);"!";
5040 RETURN
5050 PRINT@163,"DADDY LOVES HIS GOOD CHUM";
5060 PRINT@195,"AND HIS BOO AND HIS BABY!";:GOSUB7200:RETURN
5070 PRINT@163,"DAMAGE CONTROL REPORTS ALL";
5080 PRINT@195,"SYSTEMS HAVE BEEN REPAIRED.";:CC=3:RETURN
6000 PRINT 
6010 PRINT"YOU HAVE DISCOVERED A SERIES OF"
6015 PRINT"CAVERNS LEFT BY THE LOST CITY"
6020 PRINT"OF ATLANTIS. USING YOUR TRUSTY"
6025 PRINT"SUBMARINE > YOU SEARCH FOR THE":POKE16554,62
6030 PRINT"GOLDEN TREASURE * WHILE TRYING":POKE16592,42
6035 PRINT"TO AVOID THE EXPLOSIVE MINES m"
6040 PRINT"LEFT TO PROTECT IT. THEY HAVE"
6045 PRINT"ALSO LEFT AUTOMATIC TORPEDO":POKE16700,31
6050 PRINT"LAUNCHERS, SO WATCH OUT! YOU"
6055 PRINT"CAN ONLY TAKE 3 HITS BEFORE YOU"
6060 PRINT"DIE. HIT THE WALLS OR MINES AND"
6065 PRINT"YOUR SHIP WILL BE DESTROYED"
6070 PRINT"IMMEDIATELY.":RETURN
6080 PRINT"  CHOOSE LEVEL (1-5) TO BEGIN";
6085 I$=INKEY$:IFI$=""THEN6085
6090 R=VAL(I$):IFR<1ORR>5THEN6085
6095 LV=5-R:SH$=LEFT$(SH$,LV):SW=SW+(N*(4-LV))
6099 CLS1:RETURN
7100 DATA 170,8,170,8,176,8,185,8,185,8,176,8,170,8,159,8,147,8
7110 DATA 147,8,159,8,170,8,170,12,159,4,159,16
7120 DATA 170,8,170,8,176,8,185,8,185,8,176,8,170,8,159,8,147,8
7130 DATA 147,8,159,8,170,8,159,12,147,4,147,16
7140 DATA 159,8,159,8,170,8,147,8,159,8,170,4,176,4,170,8,147,8
7150 DATA 159,8,170,4,176,4,170,8,159,8,147,8,159,8,108,16
7160 DATA 170,8,170,8,176,8,185,8,185,8,176,8,170,8,159,8,147,8
7170 DATA 147,8,159,8,170,8,159,12,147,4,147,16
7180 DATA -1,-1
7200 READ A,B
7210 IFA<0ANDB<0THEN7250
7220 SOUNDA,B
7240 GOTO7200
7250 RETURN
