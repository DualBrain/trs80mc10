0 GOSUB3500:CLEAR5500:GOSUB1000
1 FORML=1TO99:GOSUB3000:FORG=1TO500STEP2:RESET(X,Y(G)):RESET(A,Y(G+1)):RESET(X,B(G)):RESET(A,B(G+1))
2 FORC=1TOQ:S$(C)=MID$(S$(C),2)+CHR$(PEEK(P(C))):NEXT
3 PRINT@S,S$(1)B$S$(2)B$S$(3)B$S$(4)B$S$(5)B$S$(6)B$S$(7)B$S$(8)B$S$(9)B$S$(N)B$S$(P)B$S$(T)B$S$(Q)B$;
4 ONK(PEEK(K)ANDPEEK(2))GOSUB20,30,40,50,60:R=R-1:ONRND(D)GOSUB8,9:ONK(PEEK(Z))GOSUB82,70:POKEZ,F:IFZ=RTHENGOSUB75
6 J=J+1:ONJ(J)GOSUB90:ONRND(7)GOSUB63,64,66,65,65,67,69:ONK(PEEK(R+I))GOSUB68,69,75,80:R=R+I:POKER,E:NEXT:GOSUB97:NEXT
7 GOTO99
8 POKEG(G+(2-RND(2))),42:RETURN
9 POKEG(G+(2-RND(2))),109:RETURN
20 PRINT@Z-ST,F$;:RETURN
30 Z=Z-1:RETURN
40 Z=Z+1:RETURN
50 Z=Z-S:RETURN
60 Z=Z+S:RETURN
63 I=-1:J=J+1:ONJ(J)GOSUB90:RETURN
65 I=(SGN(Z-R)*S)*ABS(ABS(Z-R)>Q):RETURN
64 I=-S:RETURN
66 I=S:RETURN
67 I=0:RETURN
68 ONRND(6)GOSUB63,64,66,65,65,67:ONK(PEEK(R+I))GOTO68:RETURN
69 RETURN
70 SOUNDF,1:SC=SC+SW:SL=INT((Z-MC)/S):L$=S$(SL):VP=VARPTR(L$):POKE(PEEK(VP+2)*T6)+PEEK(VP+3)+(S*(((Z-MC)/S)-SL)),F3:S$(SL)=L$
72 PRINT@448,"SCORE:";SC;"            ";:RETURN
75 PRINT@448,"TORPEDO HIT!   ";:FORY=1TO7:POKEZ,207:SOUND1,1:POKEZ,175:SOUND1,1:NEXT:POKEV-(3-CC),S:CC=CC-1:GOSUB72:GOSUB90
76 IFCC<1THENGOSUB83
77 RETURN
80 PRINT@448,"YOU GOT IT!    ";:POKER+I,159:SOUND100,2:SC=SC+SW:GOSUB72:GOSUB90:RETURN
82 POKEV-(3-CC),S:CC=CC-1
83 FORY=1TO9:SOUND1,1:POKEZ,159:POKEZ,RND(2)*3+195:POKEZ,RND(2)*3+163:NEXT:POKEZ,96:GOSUB72
84 IFCC>0THENX=62:Y=8:A=63:B=22:Z=16655:J=-1:GOSUB3040:ML=1:G=-1:SOUND200,10:RETURN
85 IFSC>HSTHENHS=SC
86 PRINT@464,"HIGH:";HS;:G=G+2:RESET(X,Y(G)):RESET(A,Y(G+1)):RESET(X,B(G)):RESET(A,B(G+1)):ML=99:G=500:RETURN
90 J=0:R=G(G)-1:I=0:RETURN
97 IF(((Z-MC)/S)-INT((Z-MC)/S))*S<6THENFORY=1TON:SOUND1,1:POKEZ,240+RND(15):NEXT:SOUND1,4:ML=99:G=500
98 RETURN
99 PRINT@480,"GAME OVER.  PLAY AGAIN (Y/N)?";
100 I$=INKEY$:IFI$="C"THEN100
110 IFI$="N"THENEND
120 IFI$<>"Y"THEN100
140 CLS1:SC=0:D=6+LV:CV=0:CC=3:GOTO1
150 FORG=1TO450STEP2:R=2-RND(3):IFY+R<2ORY+R>B-NTHENR=0
151 Y=Y+R:Y(G)=Y:Y=Y+R:Y(G+1)=Y:R=2-RND(3):IFB+R+R>LORB+R<Y+NTHENR=0
152 B=B+R:B(G)=B:B=B+R:B(G+1)=B:G(G)=V+(INT(Y(G)/2)*S)+GV:G(G+1)=V+(INT(B(G)/2)*S)-GV:NEXT
160 FORG=451TO500STEP2:Y(G)=Y:Y(G+1)=Y:B(G)=B:B(G+1)=B:G(G)=V+(INT(Y(G)/2)*S)+GV:G(G+1)=V+(INT(B(G)/2)*S)-GV:NEXT
200 RETURN
1000 DIMS$(13),B$,L$,Y(503),B(503),K(255),P(13),G,Z,R,C,S,J,K,N,P,T,Q,X,Y,A,B,I,E,L,F$
1001 DIMG(503),J(32),F,U,V,ML,ST,HS,MC,T6,F3,S4,CV,D,SC,CC,GV,VP,LO,SL,SW,LV,I$,LN$:R=RND(-(PEEK(9)*256+PEEK(10)))
1002 U=30:V=16415:S=32:L=27:B$=CHR$(143):FORC=27TO32:J(C)=1:NEXT:F$="----":ST=16383:CC=3
1005 N=10:P=11:T=12:Q=13:K=17023:F=126:E=95:GV=96:SW=50
1010 FORC=1TOQ:S$(0)=S$(0)+B$:LN$=LN$+B$:NEXT:FORC=QTOU:S$(0)=S$(0)+B$:LN$=LN$+CHR$(131):NEXT
1020 FORC=1TOQ:P(C)=V+(S*C):NEXT:G(501)=ST+256:G(502)=ST+256
2000 FORC=128TO142:K(C)=1:NEXT:K(30)=1:K(95)=1:K(13)=1:K(126)=3:K(109)=4:K(106)=2
2020 K(32)=1:K(65)=2:K(83)=3:K(87)=4:K(90)=5
2030 HS=0:MC=16384:T6=256:F3=143:S4=64:D=6+LV
2090 GOSUB6080:RETURN
3000 PRINT@0,"PREPARE FOR THE NEXT CAVERN...  ";:CV=CV+1:IFCV>1THENGOSUB5000
3002 X=62:Y=8:A=63:B=22:Z=16655:J=0
3003 D=D-1:IFD<(LV+3)THEND=(LV+3)
3005 GOSUB150
3040 GOSUB4500:GOSUB4010:FORC=MCTOV:POKEC,PEEK(C)-S4:NEXT:R=16670:RETURN
3500 GOSUB4000:GOSUB6000:RETURN
4000 CLS1:PRINT" CAVERN RAIDER BY JIM GERRIE    ";:RETURN
4010 GOSUB4000:PRINT@30,;:FORC=1TOCC-1:PRINT">";:NEXT:RETURN
4500 FORC=1TOQ:S$(C)=S$(0):NEXT:S$(4)=LN$:S$(P)=LN$:RETURN
5000 IFCV=5THENGOSUB5050
5010 IFCV/3-INT(CV/3)=0THENGOSUB5070
5015 IFCV=10THENPOKE49151,64:SW=SW*N
5020 PRINT@226," CONGRATULATIONS ON MAKING ";
5030 PRINT@258," IT TO THE END OF CAVERN";STR$(CV-1);"! ";
5040 RETURN
5050 PRINT@162," DADDY LOVES HIS GOOD CHUM ";
5060 PRINT@194," AND HIS BOO AND HIS BABY! ";:GOSUB7200:RETURN
5070 PRINT@162," DAMAGE CONTROL REPORTS ALL ";
5080 PRINT@194," SYSTEMS HAVE BEEN REPAIRED. ";:CC=CC+1:IFCC>3THENCC=3
5090 RETURN
6000 PRINT:PRINT"YOU HAVE DISCOVERED A SERIES OF":PRINT"CAVERNS LEFT BY THE LOST CITY"
6020 PRINT"OF ATLANTIS. USING YOUR TRUSTY":PRINT"SUBMARINE > YOU SEARCH FOR THE":POKE16554,62
6030 PRINT"GOLDEN TREASURE * WHILE TRYING":POKE16592,42:PRINT"TO AVOID THE EXPLOSIVE MINES m"
6040 PRINT"LEFT TO PROTECT IT. THEY HAVE":PRINT"ALSO LEFT AUTOMATIC TORPEDO":POKE16700,31
6050 PRINT"LAUNCHERS, SO WATCH OUT! YOU":PRINT"CAN ONLY TAKE 3 HITS BEFORE YOU"
6060 PRINT"DIE. HIT THE WALLS OR MINES AND":PRINT"YOUR SHIP WILL HAVE TO RESTART"
6070 PRINT"THE CAVERN FROM THE BEGINNING.":RETURN
6080 PRINT"  CHOOSE LEVEL (1-5) TO BEGIN";
6085 I$=INKEY$:IFI$=""THEN6085
6090 R=VAL(I$):IFR<1ORR>5THEN6085
6095 LV=5-R:F$=LEFT$(F$,LV):SW=SW+(N*(4-LV))
6099 CLS1:RETURN
7100 DATA170,8,170,8,176,8,185,8,185,8,176,8,170,8,159,8,147,8,147,8,159,8,170,8,170,12,159,4,159,16
7120 DATA170,8,170,8,176,8,185,8,185,8,176,8,170,8,159,8,147,8,147,8,159,8,170,8,159,12,147,4,147,16
7140 DATA159,8,159,8,170,8,147,8,159,8,170,4,176,4,170,8,147,8,159,8,170,4,176,4,170,8,159,8,147,8,159,8,108,16
7160 DATA170,8,170,8,176,8,185,8,185,8,176,8,170,8,159,8,147,8,147,8,159,8,170,8,159,12,147,4,147,16,-1,-1
7200 READA,B
7210 IFA<0ANDB<0THEN7250
7220 SOUNDA,B
7240 GOTO7200
7250 RETURN
