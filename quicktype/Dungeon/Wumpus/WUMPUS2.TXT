0 REM IM 1 IN A MILLION CLUB TAPE 9
1 REM WUMPUS
2 T=RND(-(PEEK(9)*256+PEEK(10)))
3 CLS
15 DIM I$(1),P(5),S(20,3),L(6),M(6)
30 PRINT "INSTRUCTIONS? (Y/N)"
40 INPUT I$
50 IF I$="N" THEN 70
60 GOSUB 680
70 PRINT "JUST A MOMENT WHILE I CREATE A  UNIVERSE IN MY RAM CHIPS.":PRINT:PRINT 
80 FOR J=1 TO 20
90 FOR K=1 TO 3
100 READ S(J,K)
110 NEXT K:NEXT J
130 DATA 2,5,8,1,3,10,2,4,12,3,5,14,1,4,6
140 DATA 5,7,15,6,8,17,1,7,9,8,10,18,2,9,11
150 DATA 10,12,19,3,11,13,12,14,20,4,13,15,6,14,16
160 DATA 15,17,20,7,16,18,9,17,19,11,18,20,13,16,19
240 FOR J=1 TO 6
250 L(J)=INT(20*RND(0)+1)
260 M(J)=L(J)
270 NEXT J
290 FOR J=1 TO 6
300 FOR K=J TO 6
310 IF J=K THEN 330
320 IF L(J)=L(K) THEN 240
330 NEXT K
340 NEXT J
360 A=5
370 Q=L(1)
390 PRINT "HUNT THE WUMPUS"
400 PRINT "==============="
420 GOSUB 1100
440 GOSUB 1290
450 IF O=1 THEN 470
460 IF O=2 THEN 510
470 GOSUB 1370
480 IF F=0 THEN 420
490 GOTO 530
510 GOSUB 1890
520 IF F=0 THEN 420
530 IF F>0 THEN 580
550 PRINT "DUMMY, YOU LOSE--WUMPII JUST    LOVE YOU!"
560 GOTO 600
580 PRINT "O.K. HOTSHOT, THE WUMPII WILL   GET THEIR REVENGE!"
590 PRINT "WUMPII SPIRITS WILL HAUNT YOU   UNTIL THEN..."
600 FOR J=1 TO 6
610 L(J)=M(J)
620 NEXT J
630 PRINT "SAME SET UP? (Y/N)"
640 INPUT I$
650 IF I$<>"Y" THEN 240
660 GOTO 360
680 PRINT "WELCOME TO 'hunt the wumpus'"
690 PRINT "THE WUMPUS LIVES IN A CAVE OF   TWENTY ROOMS.  EACH ROOM HAS 3  TUNNELS LEADING INTO OTHER"
700 PRINT "ROOMS.  (LOOK AT A DUODECA-     HEDRON TO SEE HOW THIS WORKS.   IF YOU DON'T KNOW WHAT A"
710 PRINT "DUODECAHEDON IS, ASK SOMEONE."
730 GOSUB 2200:PRINT
740 PRINT "hazards":PRINT
750 PRINT "bottomless pit--THERE ARE TWO   OF THESE.  FALL INTO ONE AND    YOU WILL LAND IN CHINA.":PRINT
770 PRINT "super bats--TWO OTHER ROOMS     HAVE SUPER BATS, IF YOU GO THEREA BAT GRABS YOU TO SOME OTHER   ROOM AT RANDOM."
780 PRINT "(WHICH MIGHT BE TROUBLESOME)"
800 GOSUB 2200:PRINT:PRINT 
810 PRINT "wumpus"
820 PRINT "THE WUMPUS IS NOT BOTHERED BY   THE HAZARDS (HE HAS SUCKER FEET AND IS TOO BIG FOR A BAT TO     LIFT.)"
840 PRINT "USUALLY HE IS ASLEEP.  TWO      THINGS WAKE HIM UP, YOUR        ENTERING HIS ROOM OR SHOOTING   AN ARROW."
850 PRINT "IF THE WUMPUS WAKES, HE MOVES   (P=.75) ONE ROOM, OR STAYS WHEREHE IS (P=.25). AFTER THAT, IF HEIS WHERE YOU ARE"
860 PRINT "HE EATS YOU UP (AND YOU LOSE!)."
890 GOSUB 2200
900 PRINT:PRINT "you"
910 PRINT "EACH TURN YOU MAY MOVE OR SHOOT A CROOKED ARROW. YOU CAN ONLY   GO ONE ROOM (THROUGH ONE        TUNNEL AT A TIME)."
930 PRINT:PRINT "arrows.  YOU HAVE FIVE ARROWS---WHEN YOU RUN OUT YOU LOSE.      EACH ARROW CAN GO FROM ONE TO   FIVE ROOMS."
935 GOSUB 2200
940 PRINT "YOU AIM BY TELLING THE COMPUTER THE ROOM/S YOU WANT THE ARROW   TO GO.  IF THE ARROW CAN'T GO"
950 PRINT "THAT WAY (I.E. NO TUNNEL) IT    MOVES AT RANDOM TO THE NEXT     ROOM.  IF THE ARROW HITS THE    WUMPUS, YOU WIN"
990 PRINT "IF THE ARROW HITS YOU, YOU LOSE"
1000 GOSUB 2200:PRINT:PRINT "warnings"
1020 PRINT "WHEN YOU ARE ONE ROOM AWAY FROM THE WUMPUS OR A HAZARD THE      COMPUTER SAYS:"
1030 PRINT "WUMPUS--'I SMELL A WUMPUS'"
1040 PRINT "BAT--'BATS NEARBY'"
1050 PRINT "PIT--'I FEEL A DRAFT'"
1070 PRINT:PRINT:GOSUB 2200
1080 RETURN 
1100 PRINT
1110 FOR J=2 TO 6
1120 FOR K=1 TO 3
1130 IF S(L(1),K)<>L(J) THEN 1220
1140 IF J=2 THEN 1170
1150 IF J=3 THEN 1190
1151 IF J=4 THEN 1190
1160 IF J=5 THEN 1210
1161 IF J=6 THEN 1210
1170 PRINT "I SMELL A WUMPUS"
1180 GOTO 1220
1190 PRINT "I FEEL A DRAFT!"
1200 GOTO 1220
1210 PRINT "BATS NEARBY!"
1220 NEXT K:NEXT J
1240 PRINT "YOU ARE IN ROOM ";L(1)
1250 PRINT "TUNNELS LEAD TO ";S(Q,1)","S(Q,2)"&"S(Q,3)
1260 PRINT 
1270 RETURN 
1290 INPUT "SHOOT OR MOVE (S/M)";I$
1310 IF I$<>"S" THEN 1340
1320 O=1
1330 RETURN 
1340 IF I$<>"M" THEN 1290
1350 O=2
1360 RETURN 
1370 REM    ARROW ROUTINE
1380 F=0
1400 FORZZ=1TO5:P(ZZ)=0:NEXT
1410 INPUT "NUMBER OF ROOMS (1-5)";J9
1430 IF J9<1 THEN 1410
1431 IF J9>5 THEN 1410
1440 FOR K=1 TO J9
1450 INPUT "ROOM #";P(K)
1470 IF K<=2 THEN 1510
1480 IF P(K)<>P(K-2) THEN 1510
1490 PRINT "ARROWS ARE NOT MAGIC--":PRINT "BE REALISTIC."
1500 GOTO 1450
1510 NEXT K
1530 Q=L(1)
1540 FOR K=1 TO J9
1550 FOR K1=1 TO 3
1560 IF S(Q,K1)=P(K) THEN 1720
1570 NEXT K1
1590 Q=S(Q,INT(3*RND(0)+1))
1600 GOTO 1730
1610 NEXT K
1620 PRINT "MISSED"
1630 Q=L(1)
1650 GOSUB 1810
1670 REM    A=A-1
1680 IF A>0 THEN 1700
1690 F=-1
1700 RETURN 
1720 Q=P(K)
1730 IF Q<>L(2) THEN 1770
1740 PRINT "AHA! YOU GOT THE WUMPUS!"
1750 F=1
1760 RETURN 
1770 IF Q<>L(1) THEN 1610
1780 PRINT "OUCH!!! ARROW GOT YOU."
1790 GOTO 1690
1810 K=INT (4*RND(0)+1)
1820 IF K=4 THEN 1840
1830 L(2)=S(L(2),K)
1840 IF L(2)<>Q THEN 1870
1850 PRINT "WUMPUS GOT YA!!!"
1860 F=-1
1870 RETURN 
1890 F=0
1900 PRINT "O.K., WHERE TO NOW?"
1910 INPUT Q
1920 IF Q<1 THEN 1900
1921 IF Q>20 THEN 1900
1930 FOR K=1 TO 3
1950 IF S(L(1),K)=Q THEN 2010
1960 NEXT K
1970 IF Q=L(1) THEN 2010
1980 PRINT "THAT'S NOT POSSIBLE!"
1990 GOTO 1900
2010 L(1)=Q
2030 IF Q<>L(2) THEN 2100
2040 PRINT "YOU BUMPED INTO A WUMPUS!!"
2060 GOSUB 1810
2070 IF F=0 THEN 2100
2080 RETURN 
2100 IF Q<>L(3) THEN IF Q<>(4) THEN 2150
2110 PRINT "A PIT!!! CHINA HERE I COME..."
2120 F=-1
2130 RETURN 
2150 IF Q<>L(5) THEN  IF Q<>L(6) THEN 2190
2160 PRINT "SUPER BATS!!! GOOD LUCK!!!"
2170 Q=INT(20*RND(0)+1)
2180 GOTO 2010
2190 RETURN 
2200 PRINT "press any key to continue";
2210 I$=INKEY$:IF I$=""THEN 2210
2220 FOR T=1 TO 25:PRINTCHR$(8);:NEXT
2230 RETURN 
2250 SOUND1,1:END 
