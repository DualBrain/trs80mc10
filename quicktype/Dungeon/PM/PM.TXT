6 P=0
8 GOSUB 1750
9 REM SET WORDS
10 DIM A$(135),B(18)
12 A$(1)="cave entr."
14 A$(2)=" door     "
16 A$(3)="blank wall"
18 A$(4)="passageway"
20 A$(5)="stairs up."
22 A$(6)="steps down"
24 A$(7)=" exit     "
26 A$(8)="          "
28 A$(9)=" left.    "
30 A$(10)=" forward. "
32 A$(11)=" right.   "
34 A$(12)="SALAMANDER"
36 A$(13)="RED ZOMBIE"
38 A$(14)="BEHOLDER I"
40 A$(15)="BEHOLDER V"
42 A$(16)="BEHOLDER X"
44 A$(17)="HEEL HOUND"
46 A$(18)="BLUE ETTIN"
48 A$(19)="GREY ETTIN"
50 A$(20)="PIT WRAITH"
52 A$(21)="GARGOYLE I"
54 A$(22)="GARGOYLE V"
56 A$(23)="GARGOYLE X"
58 A$(24)="FIRE ETTIN"
60 A$(25)="CLAY ETTIN"
62 A$(26)="IRON ETTIN"
64 A$(27)="HIIL ETTIN"
66 A$(28)="BASILISK I"
68 A$(29)="BASILISK V"
70 A$(30)="BASILISK X"
72 A$(31)="ZOMBIE RAT"
74 A$(32)="WRAITH ORC"
76 A$(33)="PINK GHOST"
78 A$(34)="ICE WRAITH"
80 A$(35)="MIND GHOST"
82 A$(36)="FIRE GHOST"
84 A$(37)="CLAY GHOST"
86 A$(38)="IRON GHOST"
88 A$(39)="MIND HYDRA"
90 A$(40)="PIT HYDRA"
92 A$(41)="CLAY HYDRA"
94 A$(42)="IRON HYDRA"
96 A$(43)="RED MEDUSA"
98 A$(44)="MEDUSA ORC"
100 A$(45)="ICE MEDUSA"
102 A$(46)="PIT MEDUSA"
104 A$(47)="RED FLAYER"
106 A$(48)="BLUE MUMMY"
108 A$(49)="CLAY MUMMY"
110 A$(50)="IRON MUMMY"
112 A$(51)="MINOTAURUS"
114 A$(52)="MINOTAURAK"
116 A$(53)="MINOTAURIC"
118 A$(54)="PIT FLAYER"
120 A$(55)="ICE FLAYER"
122 A$(56)="WERE-WOLF"
124 A$(57)="WERE-BOAR"
126 A$(58)="WERE-BEAR"
128 A$(59)="WERE-RAT"
130 A$(60)="WERE-WORM"
132 A$(61)="BLACK-WORM"
134 A$(62)="WHITE WORM"
136 A$(63)="STONE WORM"
138 A$(64)="BONE-WORM"
140 A$(65)="UMBER HULK"
142 A$(66)="STONE HULK"
144 A$(67)="GREEN HULK"
146 A$(68)="CLOUD HULK"
148 A$(69)="STORM HULK"
150 A$(70)="HOBGOBLIN"
152 A$(71)="HOBGOBELOW"
154 A$(72)="MANTACORE"
156 A$(73)="BAALZEBUL"
158 A$(74)="TELEMENTAL"
160 A$(75)="PIT FIEND"
162 A$(76)="HORNED ORC"
164 A$(77)="BARBED ORC"
166 A$(78)="RED GERYON"
168 A$(79)="JUBILEX I"
170 A$(80)="JUBILEX II"
172 A$(81)="DEMOGORGON"
174 A$(82)="RED ORCUS "
176 A$(83)="BLUE ORCUS"
178 A$(84)="GREY ORCUS"
180 A$(85)="FIRE ORCUS"
182 A$(86)="WILD TROLL"
184 A$(87)="HELL TROLL"
186 A$(88)="FIRE TROLL"
188 A$(89)="BOAR TROLL"
200 A$(90)="BLUE TROLL"
202 A$(91)="WILD DEMON"
204 A$(92)="BLUE DEMON"
206 A$(93)="RED DEMON"
208 A$(94)="PIT DEMON"
210 A$(95)="FIRE DEMON"
212 A$(96)="ICE DEMON"
214 A$(97)="BEAR DEMON"
216 A$(98)="CLAY DEMON"
218 A$(99)="GREY DEMON"
220 A$(100)="IRON DEMON"
222 A$(101)="WILD ETTIN"
224 A$(102)="PIT ETTIN"
226 A$(103)="ICE ETTIN"
228 A$(104)="MIND ETTIN"
230 A$(105)="HELL ETTIN"
232 A$(106)="WILD GHOST"
234 A$(107)="RED GHOST"
236 A$(108)="PIT GOLEM"
238 A$(109)="ICE GOLEM"
240 A$(110)="FLESH RAT"
242 A$(111)="MIND DEVIL"
244 A$(112)="FLESH HULK"
246 A$(113)="GOLD COINS"
248 A$(114)="+ A SILVER"
250 A$(115)=" SWORD"
252 A$(116)="+ A MAGIC "
254 A$(117)="LANCE"
256 A$(118)="+ A SUIT "
258 A$(119)="OF ARMOUR"
260 A$(120)="+ A SPECIA"
262 A$(121)="L ARTEFACT"
264 A$(122)="+ AN ANCIE"
266 A$(123)="NT SCROLL"
268 A$(124)="+ A SECRET"
270 A$(125)=" POTION"
272 A$(126)="+ SOME MOR"
274 A$(127)="E SPELLS"
276 A$(128)="+ A WISDOM"
278 A$(129)=" BOOK"
280 A$(130)="+ A GOLDEN"
282 A$(131)=" RING"
284 A$(132)="+ A SPELL"
286 A$(133)="LUCKSTONE"
299 REM SET UP
300 C=0
302 E=6
304 L=20
306 K=0
308 H=100
310 F=0
312 S=10
314 G=0
316 GOTO 403
400 REM MAIN PROGRAM
401 IF F=1 THEN 426
402 IF INT(RND(0)*200)<5 THEN GOSUB 1000
403 SOUND100,1::SOUND50,1:FORZZ=1TO18:B(ZZ)=0:NEXT:GOSUB524
404 IF L=1 THEN E=7
405 IF L<0 OR G>150 AND S>2 AND INT(RND(0)*100)<8 THEN GOSUB 750
406 FOR Z=1 TO 3
408 B(Z)=INT(RND(0)*E)+1
410 IF B(Z)<3 THEN GOSUB 1100
412 NEXT Z
414 IF B(1)=3 AND B(2)=3 AND B(3)=3 THEN 406
416 CLS
417 GOSUB 506
418 GOSUB 500
420 PRINT A$(B(1));"ß";A$(B(2));"ß";A$(B(3));
422 GOSUB 500
423 GOSUB 506
424 PRINT A$(9);"ß";A$(10);"ß";A$(11);
425 GOSUB 506
426 REM PRINT
427 F=0
428 PRINT "WHAT DIRECTION? ";
430 Z=0
432 Z$=INKEY$:IF Z$="" THEN 432
435 IF Z$="A" THEN 2000
436 IF Z$="L" THEN Z=1
438 IF Z$="F" THEN Z=2
440 IF Z$="R" THEN Z=3
441 IF Z$="S" THEN GOSUB 550
442 IF Z<1 THEN 432
444 PRINT Z$
446 IF B(Z)=6 THEN L=L+1
448 IF B(Z)=5 THEN L=L-1
450 IF B(Z)=3 THEN GOSUB 600
452 IF B(Z)=7 THEN GOSUB 700
454 IF B(Z)=2 THEN GOSUB 800
456 IF B(Z)=1 THEN GOSUB 900
457 IF INT(RND(0)*100)>90 THEN GOSUB 1300
458 GOTO 400
499 REM SET DISPLAY/RND(0) NO.S
500 PRINT A$(8);"ß";A$(8);"ß";A$(8);
502 PRINT A$(8);"ß";A$(8);"ß";A$(8);
504 RETURN
506 PRINT "ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß";
508 RETURN
510 R=INT(RND(0)*101)+12
512 RETURN
514 PRINT
516 PRINT "THE CAVE CONTAINS ";
518 PRINT "A";
520 IF MID$(A$(R),1,1)="I" THEN PRINT "N";
522 PRINT " ";A$(R);
524 FORZZ=1TO1500:NEXT:RETURN
526 FOR X=7 TO 14
528 PRINT@32*X,"                                ";
530 NEXT X:PRINT@32*15,"                               ";
532 PRINT@32*7,"";
534 RETURN
536 W=INT(RND(0)*101)+12
538 RETURN
540 W=(INT(RND(0)*16)+1)*2
542 RETURN
549 REM STATUS
550 PRINT "STATUS",
552 GOSUB 526
554 PRINT "YOU HAVE KILLED";K;"MONSTERS"
556 PRINT
558 PRINT "YOU ARE ON LEVEL";L,,,
560 PRINT "YOU HAVE";H;"HIT POINTS",,,
562 PRINT "YOU HAVE";S;"SPELLS",,,
564 PRINT "AND YOU HAVE";G;"GOLD PIECES ";
566 IF INKEY$="" THEN 566
570 GOSUB 526
571 PRINT@32*7,"                               ";:PRINT@32*7+16,"";
572 RETURN
599 REM WALL
600 GOSUB 526
602 PRINT "NO-ENTRY....WALL UNSTABLE",,,
604 F=1
606 GOSUB 536
608 IF W>50 THEN RETURN
610 PRINT "IT SHAKES..ROOF CRUMBLES..ROCKS",,,"FALL..KNOCK YOU TO THE GROUND",,,"TAKE ";W;" HIT POINTS DAMAGE."
612 H=H-W
614 RETURN
699 REM END SCORE
700 CLS
702 PRINT "WELL DONE,YOU GOT OUT ALIVE."
704 PRINT
706 PRINT "YOU KILLED ";K;" MONSTERS.",,,
708 PRINT "YOU HAVE ";G;" GOLD COINS",,,
710 PRINT "YOU SCORED ";G*K+S+H;" POINTS.",,,
711 REM
712 IF P THEN P=G*K+H+S+H
714 PRINT "BEST SCORE SO FAR ";P,,,,,
716 PRINT "ANOTHER GAME?"
718 IF INKEY$="" THEN 718
720 GOTO 8
749 REM SCREECH BAT
750 CLS
752 PRINT
754 PRINT " a screech bat descends on you  ",,"KNOCKS YOU TO THE FLOOR AND",,,"TAKES ALL YOUR GOLD,GIVING YOU",,,
755 PRINT INT(H/2);"HIT POINTS DAMAGE:BREAKING"
756 PRINT
758 PRINT INT(S/2);"OF YOUR SPELLS:LEAVING YOU",,,"ON LEVEL";L+1;"."
759 G=0
760 H=H-INT(H/2)
762 S=S-INT(S/2)
768 L=L+1
770 PRINT
772 PRINT " (HIT SPACE TO CONTINUE)"
774 IF INKEY$="" THEN 774
776 RETURN
799 REM DOOR ENTRY
800 GOSUB 526
801 PRINT "DOOR...O-OPEN,L-LISTEN ";
802 Z$=INKEY$:IF Z$="" THEN 802
806 IF Z$="L" THEN 811
808 IF Z$="O" THEN 898
810 GOTO 802
811 PRINT Z$,,,
812 PRINT "YOU HEAR ";
814 IF B(Z+3)>0 THEN PRINT A$(B(Z+3));"S MAKING",,,"NOISES";
816 IF B(Z+3)=0 THEN PRINT "NOTHING.";
818 PRINT "...NOW WHAT?",,,"G-GO IN,H-HELP,L-LEAVE ";
820 Z$=INKEY$:IF Z$="" THEN 820
824 IF Z$="G" THEN 850
826 IF Z$="H" THEN 950
828 IF Z$="L" THEN 832
830 GOTO 820
832 PRINT Z$;"-CHICKEN",,
838 F=1
840 IF INT(RND(0)*100)<10 THEN GOSUB 1000
842 RETURN
849 REM ENTRY CONTENTS
850 GOSUB 526
852 PRINT@32*7,"";
854 IF B(Z+3)>0 THEN R=B(Z+3)
856 IF B(Z+3)>0 THEN GOSUB 516
857 IF B(Z+3)>0 THEN PRINT
858 IF B(Z+3)=0 THEN PRINT "A MONSTER-LESS CAVERN"
860 PRINT
862 IF B(Z+6)>0 THEN PRINT "WITH";B(Z+6);A$(113),,,
864 IF B(Z+9)>0 THEN PRINT A$(B(Z+9)+100);A$(B(Z+9)+101),,,
866 IF B(Z+12)>0 THEN PRINT A$(B(Z+12)+100);A$(B(Z+12)+101),,,
868 IF B(Z+15)>0 THEN PRINT A$(B(Z+15)+100);A$(B(Z+15)+101),,,
870 IF B(Z+3)>0 THEN GOSUB 1314
871 IF B(Z+3)>0 AND C=1700 THEN 838
872 G=G+B(Z+6)
874 FOR X=9 TO 15 STEP 3
876 IF B(Z+X)>0 AND B(Z+X)<25 THEN H=H+30
878 IF B(Z+X)>23 THEN S=S+1
880 NEXT X
882 RETURN
897 REM CAVE CONTENTS
898 PRINT Z$
900 GOSUB 526
902 IF B(Z+3)>0 THEN R=B(Z+3)
904 IF B(Z+3)>0 THEN GOSUB 516
905 W=0
906 FOR X=6 TO 15 STEP 3
908 IF B(Z+3)>0 AND B(Z+X)>0 THEN W=2
910 IF B(Z+3)=0 AND B(Z+X)>0 THEN W=1
912 NEXT X
914 IF W=2 THEN PRINT ,,,"AND TREASURE";
916 IF W=1 THEN PRINT "THE CAVE CONTAINS TREASURE",,,,
918 IF B(Z+3)=0 AND W=0 THEN PRINT "THE CAVE IS EMPTY";
919 IF B(Z+3)>0 AND W=0 THEN PRINT ,,
920 GOTO 818
949 REM HELP
950 PRINT Z$
951 GOSUB 526
952 IF B(Z+3)>0 THEN PRINT "THE ";A$(B(Z+3));" HAS";B(Z+3)-12;"HIT POINTS";
954 IF B(Z+3)=0 THEN PRINT "NO MONSTERS?"
956 PRINT
957 PRINT ,
958 GOTO 818
999 REM FLOOR CAVE-IN
1000 GOSUB 526
1001 F=0
1002 GOSUB 536
1004 PRINT "THE FLOOR CAVES IN AND YOU FALL",,,"DOWN A";INT(W/2);"FOOT PIT TO LEVEL",,,
1006 L=L+INT(W/12)
1008 PRINT "'";L;"' ";
1010 GOSUB 536
1012 IF W>95 THEN PRINT "AND INTO A POOL OF ACID",,,"TAKE";H;"HIT POINTS DAMAGE.",,,
1014 IF W>95 THEN H=0
1016 IF W<40 THEN PRINT " AND ONTO SOME SPIKES",,,"TAKE";INT(H/2);"HIT POINTS DAMAGE",,,
1018 IF W<40 THEN H=H-INT(H/2)
1020 RETURN
1099 REM SET ROOM CONTENTS
1100 FOR X=3 TO 6 STEP 3
1102 GOSUB 536
1104 IF W>70 THEN B(Z+X)=W
1106 GOSUB 536
1108 IF B(Z+X)>1 THEN B(Z+X)=W
1109 NEXT X
1110 FOR X=9 TO 15 STEP 3
1112 GOSUB 536
1113 IF W>75 THEN B(Z+X)=W
1114 GOSUB 540
1116 IF B(Z+X)>0 AND W>12 THEN B(Z+X)=W
1117 IF B(Z+X)>0 AND W<13 THEN B(Z+X)=0
1118 NEXT X
1120 RETURN
1299 REM MONSTER APEARS
1300 GOSUB 526
1302 GOSUB 510
1304 GOSUB 518
1306 PRINT " ARRIVES"
1308 PRINT
1310 PRINT "WHAT NOW?"
1312 PRINT
1314 PRINT "C-COMBAT S-SPELL CAST R-RETREAT"
1316 C=0
1318 Z$=INKEY$:IF Z$="" THEN 1318
1322 IF Z$="C" THEN C=1
1324 IF Z$="S" THEN C=2
1326 IF Z$="R" THEN C=3
1328 IF C<1 THEN 1318
1329 PRINT Z$
1330 ON C GOSUB 1500,1600,1700
1332 IF C<1 THEN 1316
1334 RETURN
1499 REM BATTLE RESULTS
1500 GOSUB 526
1502 PRINT "YOU HAD A TERRIFIC BATTLE WITH"
1504 PRINT
1506 PRINT "THE ";A$(R);
1508 IF H<(R-12) THEN PRINT " BUT HE KILLED YOU",,"AND TOOK ALL YOUR TREASURE."
1510 IF H<(R-12) THEN 711
1512 PRINT " AND YOU KILLED",,,"HIM."
1514 K=K+1
1516 H=H+12-R
1518 RETURN
1520 PRINT "SORRY...YOU'RE OUT OF SPELLS."
1522 PRINT
1524 PRINT "NOW WHAT?"
1526 C=0
1528 RETURN
1599 REM SPELL RESULTS
1600 GOSUB 526
1602 GOSUB 536
1604 IF S<1 THEN 1520
1606 S=S-1
1608 IF W>R THEN PRINT "THE NOW SPELL-BLASTED",,,A$(R);" LIES DEAD ON THE",,,"FLOOR."
1610 IF W>R THEN K=K+1
1612 IF W>R THEN RETURN
1614 PRINT "THE ";A$(R);" IS ANGRY HE",,,"ADVANCES...";
1616 IF W<27 THEN PRINT ".HE ATTACKS.........."
1618 IF W<27 THEN 1502
1620 PRINT "WHAT NOW?"
1622 C=0
1624 RETURN
1700 GOSUB 536
1701 GOSUB 526
1702 IF W<40 THEN PRINT "OH..BAD LUCK HE ATTACKS",,,
1704 IF W<40 THEN 1502
1706 PRINT "..CHICKEN",,,
1708 F=0
1709 L=L+1
1710 RETURN
1749 REM INTRO.
1750 CLS
1751 PRINT " pit with monsters",,,,,,,,"BY JIM ENNES"
1752 IF INKEY$="" THEN 1752
1753 CLS
1754 PRINT "YOU FALL DOWN A 150 FOOT PIT","THAT CONTAINS TREASURE AND MANY","HAZARDS. THE OBJECT OF THE GAME",
1755 PRINT "IS TO MAKE YOUR WAY UP THE STEPS";"THROUGH THE MAZE OF CAVES TO THE";"EXIT. PRESS KEY 'S' WHEN ASKED",
1756 PRINT "WHAT DIRECTION? WILL GIVE YOUR","STATUS-THEN ANY KEY TO CONTINUE.";
1758 PRINT "IN COMBAT YOU USE HIT POINTS TO","KILL MONSTERS-SPELL CASTING IS","OFTEN SUCCESSFUL (NOT ALWAYS)."
1760 IF INKEY$="" THEN 1760
1761 CLS
1762 PRINT "OH..BY THE WAY, BEST OF LUCK....",,".........YOU'LL NEED IT."
1764 RETURN
2000 REM PM
2010 RUN
