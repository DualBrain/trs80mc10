10 REM TEMPLE OF APSHAI, INNKEEPER - TRS80, LEVEL ii, COPYRIGHT 1979, AUTOMATED SIMULATIONS, REV 1
20 CLEAR800:DEFINTD,I-Y
30 DIMNA$(5),W(5),P(5),NC$(6),CH(6),SD(2),SP(2),SW(2),NW$(6),WW(5),WP(5),WD(5),WR(5),DS(1,1),DW(5,1),R$(6),RR(6),NS$(2)
40 C%=15360:KA=-1*(65536-46864):Q=60:KB=KA+2251
50 J8=248:J0=59:J7=249:JQ=1
60 DATA"NONE",0,0,"LEATHER",150,30,"RING MAIL",350,100,"CHAIN MAIL",500,150,"PARTIAL PLATE",750,250,"FULL PLATE",1000,1000,"INTELLIGENCE","INTUITION","EGO","STRENGTH","CONSTITUTION","DEXTERITY"
70 FORI=0TO5:READNA$(I),W(I),P(I):W(I)=INT(W(I)/16+.5):NEXTI
80 W(0)=0:FORI=1TO6:READNC$(I):NEXTI
90 DATA"DAGGER",1,5,5,3,7,12,"SHORTSWORD",2,14,6,8,9,12,"BROADSWORD",3,18,7,10,9,13,"HAND-AND-A-HALF SWORD",6,35,8,16,9,14,"GREAT SWORD",9,70,10,15,7,15,"MAGIC SWORD"
100 FORI=1TO5:READNW$(I),WW(I),WP(I),WD(I),WR(I):FORJ=0TO1:READDW(I,J):NEXTJ:NEXTI:READNW$(6)
110 DATA6,3,10,8,15,12,5,15,6,17
120 FORI=1TO2:READSW(I),SD(I),SP(I):FORJ=0TO1:READDS(I-1,J):NEXTJ:NEXTI
130 LS=0:SM=0
140 DATA"I'D NOT PART WITH THESE FINE GOODS FOR THAT PITTANCE!    MAYHAP FOR",5,"NOT SO CHEAP MY FRIEND! BUT FOR THEE JUST",10,"BLACKHEART! THOU TAKEST THE FOOD FROM MY CHILDREN'S MOUTHS! I'LL NOT SETTLE FOR LESS THAN",5
150 DATA"WELL, LIFE IS SHORT AND THY ARSE LONG! WHAT SAY THEE TO",2,"HMM...BUT SUCH FINE WORKMANSHIP. i COULD NOT PART WITH THIS FOR LESS THAN",10,"A POX ON THEE! BUT I'D TAKE",3
160 RN=6:RT=0:FORI=1TORN:READR$(I),RR(I):RT=RT+RR(I):NEXTI
170 DATA"NONE","SMALL","LARGE":FORI=0TO2:READNS$(I):NEXTI
180 GOTO270
190 RANDOM:CLS:PRINTCHR$(23):PRINT@276,"DUNJONQUEST":PRINT@140,"THE TEMPLE OF APSHAI"
200 PRINT@594,"COPYRIGHT 1979":PRINT@714,"AUTOMATED  SIMULATIONS"
210 J=191:FORI=0TO63STEP2:POKEC%+I,J:NEXTI:FORI=960TO1023STEP2:POKEC%+I,J:NEXTI:FORI=64TO896STEP64:POKEC%+I,J:NEXTI:FORI=126TO958STEP64:POKEC%+I,J:NEXTI
220 K=140:FORI=834TO892STEP2:POKEC%+I,K:NEXTI
230 PRINT@904,"HIT ANY KEY TO CONTINUE";
240 I=RND(5):A$=INKEY$:IFLEN(A$)=0GOTO240
250 POKEKB,0
260 RETURN
270 IFPEEK(KB-12)<>123THENGOSUB190
280 CLS:WC=5:PRINT"THUS QUOTH THE INNKEEPER:"
290 IFPEEK(KB-12)=123THENINPUT"WOULDST THOU KNOW WHAT TREASURES THOU HAST";A$:IFLEFT$(A$,1)="Y"THENGOSUB2060
300 IFPEEK(KB-12)=123THENGOSUB1790
310 IFPEEK(KB-12)=123THENPRINT"I TRUST THINE WAS A PLEASANT SOJOURN WITHIN THE TEMPLE.":PRINT"ART BUT PASSING THROUGH THE INN AND WISH TO RETURN";:INPUTA$:IFLEFT$(A$,1)="Y"THENGOSUB2020:INPUT"HOW MANY SILVER PIECES HAST THOU";MO:OM=MO:GOSUB1330:GOTO350
320 POKEKB-12,0
330 PRINT"HAIL AND WELL MET! SHALL I FIND THEE A GOODLY CHARACTER, OR HASTTHOU BROUGHT ONE WITH THEE?  pRITHEE, SAY YEA IF i SHOULD.":INPUTC$:IFLEFT$(C$,1)="Y"THENGOSUB850:ELSEGOSUB940
340 OM=MO:INPUT"CHARACTER NAME";NM$:IFMO>0THENGOSUB1330
350 IFMO>0THENGOSUB1460
360 IFPS=0THENWP=WP+SP:SP=0
370 PB=PB-WP-INT(PEEK(KB+15)/2):IFWP>0THENPA=PA+WP-1ELSEIFWP<0THENPA=PA+WP+1
380 IFMO=0GOTO410ELSEGOSUB750
390 IFMO=0GOTO410ELSEGOSUB1550
400 IFMO=0GOTO410ELSEGOSUB1630
410 SE=100
420 INPUT"MONSTER SPEED (SLOW,MEDIUM, OR FAST)";C$:IFLEFT$(C$,1)="S"THENSE=250ELSEIFLEFT$(C$,1)="F"THENSE=60ELSESE=120
430 POKEKB+9,SE:POKEKB+11,HS:POKEKB+13,RS:POKEKB+14,RM:POKEKB+12,WC:POKEKB+7,WM
440 POKEKB+24,CA:POKEKB+6,HN:POKEKB+11,HS
450 POKEKB+23,AS:POKEKB+7,WM:POKEKB+18,PB:POKEKB+19,PA:POKEKB+21,IT:POKEKB+22,EG:POKEKB+4,SP:POKEKB+25,DX:J=INT(EX#/65536):POKEKB+3,J:JJ=INT(EX#/256-J*256):POKEKB+2,JJ:POKEKB+1,EX#-256*JJ-65536*J
460 WC=5+WW(PEEK(KB+17))+W(AA)+SW(INT(PEEK(KB)/2))+1+(RS+RM)/10
470 POKEKB+13,RS:POKEKB+14,RM:POKEKB+12,WC:POKEKB+20,CH(1)
480 POKEKB+10,SM
490 POKEKB,PS
500 POKEKB+8,AA:IFPEEK(KB-12)=123GOTO540
510 J=LEN(NM$):IFJ>11THENJ=11
520 FORI=1TOJ:POKEKB-12+I,ASC(MID$(NM$,I,1)):NEXTI
530 IFJ<11THENFORI=J+1TO11:POKEKB-12+I,ASC(" "):NEXTI
540 GOSUB1790
550 POKEKB-12,123
560 INPUT"ENTER DUNGEON LEVEL";L$:LV=VAL(L$)
570 IFPEEK(KA)=LVGOTO700
580 N=PEEK(KB-11)
590 AN$="LEVEL"+L$:OPEN"I",1,AN$
600 INPUT#1,A$:LQ=ASC(LEFT$(A$,1))-J0:PRINT"LEVEL:"LQ
610 K=0
620 IFLV=LQTHENGOSUB720
630 FORI=2TO9
640 IFLEN(A$)=249GOTO660
650 PRINT"*** POSSIBLE DATA READ ERROR - BLOCK LENGTH ="LEN(A$);" ***"
660 INPUT#1,A$:IFLV=LQTHENGOSUB720
670 NEXTI
680 CLOSE1
690 POKEKB-11,N
700 POKEKB-12,123:PRINT"LEVEL COMPLETE - LOADING DUNJONMASTER":RUN"DM
710 STOP
720 IA=VARPTR(A$):IA=-1*(65536-PEEK(IA+1)-256*PEEK(IA+2))
730 FORJ=0TOJ8:L=PEEK(IA+J)
740 POKEKA+K+J,L-J0:NEXTJ:K=K+J7:RETURN
750 INPUT"WILT THOU BUY NEW ARMOR";A$:IFLEFT$(A$,1)="Y"THENFORI=1TO900:NEXTI:CLS:PRINT"TYPE"TAB(25);"WEIGHT"TAB(35);"OFFERED PRICE":ELSERETURN
760 FORI=1TO5:PRINTNA$(I);TAB(25);W(I);TAB(35);P(I):NEXTI
770 INPUT"WHAT SORT OF ARMOR WOULDST THOU WEAR";A$:IFLEFT$(A$,1)="N"THENAM=0:AA=0:RETURN
780 FORI=1TO5:IFLEFT$(A$,2)=LEFT$(NA$(I),2)THENN=I:GOTO810
790 NEXTI:PRINT"I HAVE NOT "A$;" FOR SALE":GOTO770
800 IFPEEK(KB-12)=123THENWC=WC-W(AA)
810 LO=.3*P(N):AK=P(N):A1=AK:GOSUB1670:IFOO=0GOTO770
820 AA=N:MO=MO-OO:WC=WC+W(N):POKEKB+16,0
830 PRINT"THOU HAST"MO;"SILVER PIECES LEFT IN THY PURSE":RETURN
840 J=RND(6)+RND(6)+RND(6):RETURN
850 HB=0:SC=0:FORI=1TO6:GOSUB840:CH(I)=J:SC=SC+J:NEXTI:IFSC<60ORCH(4)<8ORCH(5)<7GOTO850
860 EX#=0:RS=0:RM=0:AA=0:PS=0:POKEKB+17,0:SP=0:WP=1:AM=0:SM=0:HS=0:HN=0:POKEKB+15,1
870 CLS:PRINT"THY QUALITIES:":FORI=1TO6:PRINTNC$(I);TAB(25);CH(I):NEXTI
880 FORI=1TO6:POKEKA-97+I,CH(I):NEXTI
890 PRINT" ":GOSUB840:MO=J*10:PRINT"THOU HAST"MO;" PIECES OF SILVER"
900 GOSUB930:L=1:POKEKB+16,0
910 EG=CH(3):IN=CH(1):AS=CH(4):CA=CH(5):IT=CH(2):DX=CH(6):PA=11:PB=11
920 RETURN
930 FORI=KA-90TOKA-81:POKEI,0:NEXTI:RETURN
940 POKEKB+15,0:FORI=1TO6:PRINT"ENTER "NC$(I);:INPUTCH(I):POKEKA-97+I,CH(I):IFCH(I)>18THENPRINTCH(I);"BE TOO HIGH. NO MORE THAN 18 CAN IT BE.":I=I-1
950 NEXTI
960 INPUT"THY CHARACTER'S EXPERIENCE IS";EX#:IFEX#>16000000THENPRINT"THEY CHARACTER IS TOO WORLDWISE; FIND ANOTHER MY FRIEND":GOTO940
970 E=EX#/1000:FORL=1TO20:IF2[L>ETHENGOTO980:ELSENEXTL
980 INPUT"HOW MUCH MONEY HAST THOU TO SPEND";MO
990 GOSUB1120 'PRESUMES A STRANGER TO THE QUEST SYSTEM
1000 INPUT"WHAT KIND OF SWORD HAST THOU";A$:FORI=1TO5:IFLEFT$(A$,1)=LEFT$(NW$(I),1)THENIFCH(4)>=WR(I)THENN=I:OO=0:GOSUB1420:GOTO1020
1010 NEXTI:PRINT"THOU CANNOT TAKE A "A$;" TO THE DUNJON. THOU MUST BUY ANOTHER"
1020 INPUT"WHAT SORT OF ARMOR DOST THOU WEAR";A$:FORI=0TO5:IFLEFT$(A$,2)=LEFT$(NA$(I),2)THENN=I:OO=0:GOSUB820:GOTO1040
1030 NEXTI:PRINT"THOU CANNOT WEAR "A$;" IN THE DUNJON":GOTO1020
1040 N=0:INPUT"HAST THOU A SHIELD";A$:IFLEFT$(A$,1)="Y"THENINPUT"BE IT LARGE OR SMALL";A$:N=1:IFLEFT$(A$,1)="L"THENN=2
1050 IFN>0THENGOSUB1500
1060 INPUT"HAST THOU A BOW";A$:IFLEFT$(A$,1)="Y"THENHB=1ELSEHB=0
1070 INPUT"HOW MANY ARROWS HAST THOU";RS:INPUT"HOW MANY MAGIC ARROWS HAST THOU";RM
1080 INPUT"HOW MANY HEALING POTIONS HAST THOU NOW";HN:INPUT"HOW MANY HEALING SALVES HAST THOU";HS:IFHS>10THENHS=10
1090 INPUT"IS THY SWORD MAGICAL";A$:IFLEFT$(A$,1)="Y"THENINPUT"WHAT BE THE PLUS";SM
1100 POKEKB+16,0:INPUT"IS THY ARMOR MAGICAL";A$:IFLEFT$(A$,1)="Y"THENINPUT"WHAT BE THE PLUS";AM:POKEKB+16,AM
1110 GOSUB930:GOTO910
1120 IFW2=1THENWP=L:SP=0:ELSEWP=INT((L+1)/2):SP=L-WP
1130 IFPEEK(KB+15)=LTHENRETURNELSEPOKEKB+15,L
1140 IFPEEK(KB-12)=123THENJ=L-1:L=2:ONJGOTO1150,1170,1190,1210,1230,1150,1170,1190,1210,1230
1150 L=L-1:IFL=0GOTO1260
1160 IFCH(5)<9THENCH(5)=CH(5)+1ELSECH(4)=CH(4)+1
1170 L=L-1:IFL=0GOTO1260
1180 IFCH(6)<9THENCH(6)=CH(6)+1ELSECH(5)=CH(5)+1
1190 L=L-1:IFL=0GOTO1260
1200 CH(5)=CH(5)+1
1210 L=L-1:IFL=0GOTO1260
1220 IFCH(6)<9THENCH(6)=CH(6)+1:ELSEIFCH(4)<CH(5)THENCH(4)=CH(4)+1:ELSECH(5)=CH(5)+1
1230 L=L-1:IFL=0GOTO1260
1240 IFCH(2)<CH(3)THENCH(2)=CH(2)+1ELSECH(3)=CH(3)+1
1250 GOTO1150
1260 FORI=1TO6:M=CH(I)-18:IFM<=0GOTO1320
1270 CH(I)=18:FORJ=1TOM:IFCH(4)<18THENCH(4)=CH(4)+1:GOTO1310
1280 IFCH(5)<18THENCH(5)=CH(5)+1:GOTO1310
1290 IFCH(6)<18THENCH(6)=CH(6)+1:GOTO1310
1300 R=RND(3):CH(R)=CH(R)+1
1310 NEXTJ
1320 NEXTI:CA=CH(5):AS=CH(4):DX=CH(6):EG=CH(3):IT=CH(2):RETURN
1330 PRINT"WILT THOU BUY ONE OF OUR FINE SWORDS":INPUTA$:IFLEFT$(A$,1)="N"THENN=PEEK(KB+17):GOSUB1430:RETURN:ELSEW2=0
1340 PRINT"WEAPON"TAB(30);"WEIGHT"TAB(40);"ASKING PRICE"
1350 FORI=1TO5:PRINTNW$(I);TAB(30);WW(I);TAB(40);WP(I):NEXTI
1360 INPUT"WHAT WEAPON WILT THOU PURCHASE";A$:FORI=1TO5:IFLEFT$(A$,1)=LEFT$(NW$(I),1)THENN=I:GOTO1380:ELSEIFLEFT$(A$,1)="N"THENN=PEEK(KB+17):GOSUB1430:RETURN
1370 NEXTI:PRINT"I HAVE NOT SUCH A WEAPON AS A "A$:GOTO1360
1380 IFWR(N)>ASTHENPRINT"THOU CANNOT WIELD SUCH A GREAT WEAPON":GOTO1360
1390 PRINT"FEAST THY EYES 'PON THIS FINE "NW$(N):IFRND(3)>2THENPRINT"'TIS SURE TO ALWAYS DRINK THY FOE'S BLOOD":ELSEPRINT"'TIS WELL FORGED IRON"
1400 LO=.3*WP(N):AK=WP(N):A1=AK:GOSUB1670:IFOO=0GOTO1360
1410 MO=MO-OO:PRINT"THOU HAST"MO;" SILVER PIECES LEFT"
1420 WM=INT(WD(N)*AS/10+.5):SM=0:POKEKB+10,0:POKEKB+17,N:WC=WC+WW(N):IFN=5THENW2=1
1430 IFDW(N,0)>CH(6)THENWP=WP+CH(6)-DW(N,0)ELSEIFDW(N,1)<CH(6)THENWP=WP+CH(6)-DW(N,1)
1440 IFWP>0THENWP=INT(1.3*LOG(WP)+1)
1450 RETURN
1460 IFW2=1THENPS=0:POKEKB,0:SP=0:RETURN
1470 INPUT"WILT THOU BUY A SHIELD";A$:IFLEFT$(A$,1)="N"THENGOSUB1520:RETURN:ELSEPRINT"SHIELD     WEIGHT     ASK":PRINT"SMALL"TAB(11);SW(1);TAB(22);SP(1):PRINT"LARGE"TAB(11);SW(2);TAB(22);SP(2):INPUT"WHAT SORT";C$:IFLEFT$(C$,1)="L"THENN=2ELSEN=1
1480 IFLEFT$(C$,1)="L"THENN=2ELSEIFLEFT$(C$,1)="S"THENN=1ELSEN=0:POKEKB,0:PS=0:RETURN
1490 LO=.3*SP(N):AK=SP(N):A1=AK:GOSUB1670:IFOO=0THENGOTO1470
1500 IFDS(N-1,0)>DXTHENSP=SP+DX-DS(N-1,0)ELSEIFDS(N-1,1)<DXTHENSP=SP+DX-DS(N-1,1)
1510 MO=MO-OO:PRINT"THOU HAST"MO;"SILVER PIECES LEFT":WC=WC+SW(N):PS=SD(N):POKEKB,PS:GOSUB1520:RETURN
1520 IFPS=5THENSP=INT(SP/2)
1530 IFSP>0THENSP=INT(1.3*LOG(SP)+1)
1540 SP=2*PS+SP:RETURN
1550 IFHB>0GOTO1590
1560 INPUT"WILT THOU BUY A BOW";C$:IFLEFT$(C$,1)="N"THENRETURN
1570 PRINT"I'VE A  FINE BOW, YEW AND NEARLY NEW, FOR 12 SILVER PIECES":LO=4:AK=12:A1=AK:GOSUB1670:IFOO=0THENRETURN
1580 MO=MO-OO:PRINT"THOU HAST"MO;"REMAINING"
1590 IFRS+RM>60THENRS=60-RM:GOTO1620
1600 N=0:INPUT"HOW MANY ARROWS WILT THOU BUY (AT 5 COPPERS EACH)";N:IFINT((N+1)/2)>MOTHENPRINT"NO CREDIT":GOTO1600:ELSEIFRS+RM+N>60THENPRINT"THOU CANST CARRY BUT 60 ARROWS. BUY FEWER.":GOTO1600
1610 RS=RS+N:MO=MO-INT((N+1)/2)
1620 RETURN
1630 REM
1640 INPUT"HOW MANY SALVES WILT THOU BUY? THEY COST THEE 10 SILVER PIECES  EACH";N:IF10*N>MOTHENPRINT"NO CREDIT":GOTO1640:ELSEMO=MO-10*N:HS=HS+N:IFHS>10THENN=HS-10:MO=MO+10*N:HS=10:PRINT"MORE THAN 10 WILL DO THEE NO GOOD"
1650 IFMO<.35*OMTHENPRINTNM$;"! THOU SPENDTHRIFT!":PRINT"THOU HAST BUT"MO;"SILVER PIECES LEFT TO THEE"ELSEPRINTNM$;", THOU ART FRUGAL. THOU HAST"MO;"SILVER PIECES LEFT."
1660 RETURN
1670 IFRND(3)>2THENPRINT"WHAT BE THY OFFER "NM$;"?":ELSEPRINT"WHAT OFFEREST THOU?"
1680 INPUTOO:IFOO>MOTHENPRINT"LIAR, THOU HAST BUT"MO:GOTO1670
1690 IFOO=0THENLS=0:RETURN
1700 IFOO<=LOTHENGOSUB1950:GOTO1670
1710 IFOO<=LSTHENGOSUB1910:GOTO1670
1720 IFOO>=AKTHENPRINT"DONE":LS=0:RETURN
1730 IFOO>AK-.3*RND(0)[2*AKTHENIFOO<.6*A1THENPRINT"THOU ART A HARD BARGAINER "NM$:LS=0:RETURN:ELSEPRINT"DONE":LS=0:RETURN
1740 IFRND(200)=200THENPRINT"i SEE THE GODS LOOK WITH FAVOR ON THEE, SO TAKE IT FOR THAT":LS=0:RETURN
1750 B=20/(CH(1)+CH(3)):IFCH(3)>12ANDCH(1)<10THENB=20/(CH(1)+20-CH(3))
1760 AK=OO+(AK-OO)*SQR(1-RND(0))*B
1770 LS=OO
1780 GOSUB1970:GOTO1670
1790 CH(1)=PEEK(KB+20):CH(2)=PEEK(KB+21):CH(3)=PEEK(KB+22):CH(4)=PEEK(KB+23):CH(5)=PEEK(KB+24):CH(6)=PEEK(KB+25):SM=PEEK(KB+10):RM=PEEK(KB+14):RS=PEEK(KB+13):AA=PEEK(KB+8):WM=PEEK(KB+7)-SM:WN=PEEK(KB+17)
1800 IFSM<>0THENWN=6
1810 GOSUB1980:CLS:PRINT"CHARACTER SUMMARY FOR "NM$
1820 PRINT"":FORI=1TO6:PRINTNC$(I);TAB(20);PEEK(KA-97+I):NEXTI
1830 J=0:IFPEEK(KB)=3THENJ=1ELSEIFPEEK(KB)=5THENJ=2
1840 PRINT"WEAPON: "NW$(WN);TAB(32);"ARMOR: "NA$(AA)
1850 IFWN=6THENPRINT"PLUS:"PEEK(KB+10);
1860 IFPEEK(KB+16)>0THENPRINTTAB(32);"PLUS:"PEEK(KB+16):ELSEPRINT" "
1870 PRINT"ARROWS:"RS;TAB(22);"MAGIC ARROWS:"RM;TAB(45);"SHIELD: "NS$(J)
1880 PRINT"SALVES:"PEEK(KB+11);TAB(22);"ELIXIRS:"PEEK(KB+6);TAB(45);"SILVER:"MO
1890 PRINT"EXPERIENCE:"PEEK(KB+1)+256*PEEK(KB+2)+65536*PEEK(KB+3);TAB(22);"WEIGHT CARRIED:"PEEK(KB+12)
1900 RETURN
1910 R=RND(3):R=RND(3):ONRND(3)GOTO1920,1930,1940
1920 PRINT"DOST THOU TAKE ME FOR A DOLT?":RETURN
1930 PRINT"FOOL OR KNAVE "NM$;"! MAKE AN OFFER HIGHER THAN THY LAST":RETURN
1940 PRINT"PERCHANCE THOU WOULDST NOT HAVE THIS AT ALL?":RETURN
1950 IFRND(100)>50THENPRINT"HA! 'TIS LESS THAN I PAID FOR IT"ELSEPRINT"I SPIT ON THY PALTRY OFFER"
1960 RETURN
1970 FORI1=1TO10:R=RND(RT):NEXTI1:RQ=0:R=RND(RT)-1:FORI1=1TORN:RQ=RQ+RR(I1):IFRQ<RTHENNEXTI1:ELSEPRINTR$(I1);INT(AK+.999):RETURN
1980 NM$="":FORI=1TO11:NM$=NM$+CHR$(PEEK(KB-12+I)):NEXTI
1990 FORI=1TO10:IFMID$(NM$,12-I,1)<>" "GOTO2010
2000 NEXTI
2010 NM$=LEFT$(NM$,12-I):RETURN
2020 SM=PEEK(KB+10):HS=PEEK(KB+11):HN=PEEK(KB+6):RS=PEEK(KB+13):RM=PEEK(KB+14):FORI=1TO6:CH(I)=PEEK(KB+19+I):NEXTI:AA=PEEK(KB+8):WM=PEEK(KB+7):PS=PEEK(KB):WC=PEEK(KB+12):IFAA=5THENW2=1ELSEW2=0
2030 EX#=PEEK(KB+1)+256*PEEK(KB+2)+65536*PEEK(KB+3):IFEX#>1999THENL=LOG(EX#/1000)/LOG(2)+1ELSEL=1
2040 HB=1:GOSUB1120:GOSUB910
2050 RETURN
2060 GOSUB1980:CLS:PRINT"TREASURES FOR "NM$:PRINT"TREASURE"TAB(12);"#"TAB(24);"TREASURE"TAB(36);"#"
2070 FORI=1TO20STEP2:PRINTI;TAB(12);PEEK(KA-81+I);TAB(24);I+1;TAB(36);PEEK(KA-80+I):NEXTI:INPUT"ART THOU READY FOR MORE";A$:RETURN





2200 REMCOPYRIGHT 1979,AUTOMATED SIMULATIONS,REV1
2210 CLEAR600:DEFINTD,H-Z
2220 Q=60:DIMP(4),S(4),ZA(3),ZD(5),TX(3,1),TY(3,1),TM(5),H(3),T$(9),RF(3),RN(3)
2230 Q1=12:DIMM$(Q1):C%=15360:KM=0:B1$="      "
2240 Q2=20:CLS:DATA0,2,-1,2,-2,6,2,1,4,1,12,-2,1,-2,2,18,-2,-1,-4,-1,10,13,2,1,1:FORI=0TO3:READH(I):FORJ=0TO1:READTX(I,J),TY(I,J):NEXTJ:NEXTI:FORI=1TO5:READTM(I):NEXTI
2250 KA=-1*(65536-46864):NO=KA:NT=4*Q+KA:D1=8*Q+KA:D2=12*Q+KA:MT=16*Q+KA:MN=MT+Q:NP=MN+Q:XP=NP+Q:YP=XP+Q:TR=YP+Q:XR=TR+Q:YR=XR+Q:KB=KA+2251:FORI=1TO60:POKEKA-I,0:NEXTI
2260 X1=YR+Q:X2=X1+2*Q:Y1=X2+2*Q:Y2=Y1+2*Q:TN=Y2+2*Q:TV=TN+10:TP=TV+10:TG=TP+10:TW=TG+10:TS=TW+Q2:UL=TS+Q2:UA=UL+Q1:UP=UA+Q1:US=UP+Q1:UD=US+Q1:UC=UD+Q1:UR=UC+Q1:UI=UR+Q1:UW=UI+Q1:UV=UW+Q1
2270 C%=15360:BL$=STRING$(15," "):ZD(0)=3:ZA(1)=0:ZD(1)=0:ZA(2)=3:ZD(2)=3:ZA(3)=-6:ZD(3)=-2:ZD(4)=5:ZD(5)=5:SE=100
2280 DATA"FLAME","DUST","MOLD","PIT","","SPEAR","NEEDLE","XBOW","CAVEIN","CEILING"
2290 FORI=0TO9:READT$(I):NEXTI:J9=191:J6=64:GOTO3260
2300 PC=PC-1:POKEKB+24,PEEK(KB+24)-1:POKEKA-92,PEEK(KA-92)-1:RETURN
2310 YY=W1-W2-5:IFYM>YYTHENYM=YYELSEIFYM<5THENYM=5
2320 XX=V2-V1-5:IFXM>XXTHENXM=XXELSEIFXM<5THENXM=5
2330 RETURN
2340 PRINT@945,J;"ARROWS";:RETURN
2350 PRINTCHR$(23);:FORI=1TO30:NEXTI:PRINTCHR$(28);:RETURN
2360 GOSUB2800:IFL=0GOTO2360ELSEJ=ASC(C$)-48:IFJ<0ORJ>9GOTO2360ELSERETURN
2370 PRINT@503,B1$;:PRINT@503,PEEK(KB+13);:RETURN
2380 PRINT@569,B1$;:PRINT@569,PEEK(KB+14);:RETURN
2390 FORI=1TO60:POKEKA-I,0:NEXTI:RETURN
2400 FORI=0TO3:RN(I)=0:IFRF(I)>0THENPOKENT+KR+I*Q,3:POKEKA-61+KR,0:RN(I)=1:RF(I)=0
2410 NEXTI:I=KF+1:IFI>3THENI=I-4
2420 IFRN(KF-1)>0THENRF(I)=1
2430 RETURN
2440 POKEN,PEEK(IA+NN):NN=NN+1:RETURN
2450 L1=YY:L2=YY+L-1:IFL1<0THENL1=0:IFL2<0THENRETURN
2460 IFL2>47THENL2=47:IFL1>47THENRETURN
2470 L3=INT((L1+3)/3):L4=INT((L2-3)/3):LM=3*L4+3:LX=3*L3-1:IFLM<L1THENLM=L1
2480 IFLX>L2THENLX=L2
2490 FORII=XXTOXX+1:IFII<0ORII>47THENRETURN
2500 I=2*II:FORIK=L1TOLX:GOSUB2740:NEXTIK:FORIK=LMTOL2:GOSUB2740:NEXTIK
2510 IFL4>=L3THENFORNY=L3TOL4:POKEC%+II+64*NY,J9:NEXTNY
2520 NEXTII:RETURN
2530 L1=YY:L2=YY+L-1:IFL1<0THENL1=0:IFL2<0THENRETURN
2540 IFL2>47THENL2=47:IFL1>47THENRETURN
2550 FORI=0TOI1:NX=2*XX+I:IFNX<0ORNX>96THENRETURN
2560 FORJ=L1TOL2:RESET(NX,J):NEXTJ:NEXTI:RETURN
2570 L1=2*XX:L2=L1+L-1:IFL1<0THENL1=0:IFL2<0THENRETURN
2580 IFL2>95THENL2=95:IFL1>95THENRETURN
2590 FORI=0TOI1:NY=YY+I:IFNY<0ORNY>47THENRETURN
2600 FORJ=L1TOL2:SET(J,NY):NEXTJ:NEXTI:RETURN
2610 L1=2*XX:L2=L1+L-1:IFL1<0THENL1=0:IFL2<0THENRETURN
2620 IFL2>95THENL2=95:IFL1>95THENRETURN
2630 FORI=0TOI1:NY=YY+I:IFNY<0ORNY>47THENRETURN
2640 FORJ=L1TOL2:RESET(J,NY):NEXTJ:NEXTI:RETURN
2650 I1=1
2660 XX=2*(XA-XB)-1:YY=YB-YA:IFKF=2ORKF=4GOTO2680
2670 FORI=XX-3TOXX+1STEP4:IK=YY-(KF-1)/2:ONI1GOSUB2740,2750:IK=IK+1:ONI1GOSUB2740,2750:NEXTI:I=XX-1:IK=YY-2+KF:ONI1GOSUB2740,2750:RETURN
2680 FORIK=YY-1TOYY+1STEP2:I=XX-5+KF:ONI1GOSUB2740,2750:I=I+2:ONI1GOSUB2740,2750:NEXTIK:IK=YY:I=XX+5-2*KF:ONI1GOSUB2740,2750:RETURN
2690 I1=2:GOTO2660
2700 I1=1
2710 XX=2*(XM-XB+V1)-1:YY=YB-YM-W2:I=XX:IK=YY-1:ONI1GOSUB2740,2750:IK=YY+1:ONI1GOSUB2740,2750:IK=YY:FORI=XX-2TOXX+2STEP4:ONI1GOSUB2740,2750:NEXTI:RETURN
2720 IFNB=0THENRETURN
2730 I1=2:GOTO2710
2740 IFI<0ORI>94ORIK<0ORIK>47THENRETURNELSEFORJ=0TO1:SET(I+J,IK):NEXTJ:RETURN
2750 IFI<0ORI>94ORIK<0ORIK>47THENRETURNELSEFORJ=0TO1:RESET(I+J,IK):NEXTJ:RETURN
2760 IFK=0THENSET(I,YY-J):SET(I,YY+J):ELSERESET(I,YY-J):RESET(I,YY+J)
2770 RETURN
2780 FORL=0TO2:FORK=0TO1:I=XX+1:J=1:GOSUB2760:I=XX-1:GOSUB2760:I=XX+2:J=0:GOSUB2760:I=XX-2:GOSUB2760
2790 I=XX+2:J=2:GOSUB2760:I=XX-2:GOSUB2760:I=XX+4:J=0:GOSUB2760:I=XX-4:GOSUB2760:NEXTK:NEXTL:RETURN
2800 LE=SE/(NB+1):FORIC=1TOLE
2810 C$=INKEY$:IFLEN(C$)>0THENL=1:RETURN
2820 NEXTIC:L=0:RETURN
2830 II=2*(XL-XB+V1-1):IJ=YB-YL-W2:FORL=0TO1:I=II+TX(KF-1,L):IK=IJ+TY(KF-1,L):IFK=0THENGOSUB2740:ELSEGOSUB2750
2840 NEXTL:RETURN
2850 PH=PEEK(KB+24):TA=TA-(ABS(M)/MM*(100/PH+5-5*PC/PH)*(1+WC/WT*3)/2)+11:IFTA>100THENTA=100
2860 PRINT@184,B1$;:PRINT@184,TA;"%";:RETURN
2870 Y=YB-PEEK(YP+KR)-W2:X=PEEK(XP+KR)+V1-XB:FORL=1TO15:I=2*X:IK=Y:GOSUB2740:GOSUB2750:NEXTL:RETURN
2880 IFPEEK(TR+KR)=0THENRETURN
2890 I=2*(V1+PEEK(XR+KR)-XB):YY=YB-W2-PEEK(YR+KR):FORIK=YY-1TOYY:IFPEEK(TR+KR)>0THENGOSUB2740ELSEGOSUB2750
2900 NEXTIK:RETURN
2910 N=PEEK(NP+KR):L=0:IFNB>0ORN=0ORABS(XL-PEEK(XP+KR))>3ORABS(YL-PEEK(YP+KR))>3ORRND(100)>PEEK(TP+N)THENRETURN
2920 PRINT@624,BL$;:PRINT@624,T$(PEEK(TG+N));" TRAP";:GOSUB2350:GOSUB2350:IFPEEK(TN+N)>0THENI=PEEK(TN+N):NB=1:GOSUB3240:XM=PEEK(XP+KR):YM=PEEK(YP+KR):PRINT@624,A$;:GOTO2940
2930 IFPEEK(TV+N)>0THENML=PEEK(TV+N):L=1:MD=2*ML
2940 POKENP+KR,0:RETURN
2950 LL=KR:GOTO2970
2960 LL=LR
2970 V3=PEEK(X1+LL)+128*PEEK(X1+Q+LL):V4=PEEK(X2+LL)+128*PEEK(X2+Q+LL):W3=PEEK(Y1+LL)+128*PEEK(Y1+Q+LL):W4=PEEK(Y2+LL)+128*PEEK(Y2+Q+LL):IFLL<>KRTHENRETURN
2980 V1=V3:V2=V4:W1=W3:W2=W4:RETURN
2990 K=0:MO=KA+2121:FORLL=1TOMQ:L=PEEK(K+MO):K=K+L+1:NEXTLL:K=K-L-1:A$="":FORLL=1TOL:A$=A$+CHR$(PEEK(K+MO+LL)):NEXTLL:RETURN
3000 XA=PEEK(KA+2236):YA=PEEK(KA+2237):KF=PEEK(KA+2238):PW=PEEK(KA+2235):EX#=PEEK(KB+3)*65536+PEEK(KB+2)*256+PEEK(KB+1):FORI=61TO80:POKEKA-I,0:NEXTI
3010 MM=4:PH=PEEK(KB+24):PC=PH:PB=PEEK(KB+18):PA=PEEK(KB+19):PS=PEEK(KB):AA=PEEK(KB+8):TA=100:AS=PEEK(KB+23)/10:WM=PEEK(KB+7):SE=PEEK(KB+9):WT=AS*AS*100:WC=PEEK(KB+12):SM=PEEK(KB+10):RETURN
3020 IFPEEK(KA+LR-61)=1THENRETURNELSEGOSUB2960:XX=V4-XB-2:IFV3-XB>-1ANDV4-XB<49ANDYB-W3>-1ANDYB-W4<49THENPOKEKA+LR-61,1
3030 FORK=1TO3STEP2:NN=LR+K*Q:YY=YB-W3:IFNS=KGOTO3050ELSEI1=3:L=W3-W4:IFPEEK(D2+NN)-PEEK(D1+NN)+4=LGOTO3050ELSEGOSUB2450
3040 I1=5-2*PEEK(NT+NN):IFI1>=0ANDI1<5THENYY=YB-W4-PEEK(D2+NN):L=PEEK(D2+NN)-PEEK(D1+NN):GOSUB2530
3050 XX=V3-XB:NEXTK
3060 YY=YB-W3:FORK=0TO2STEP2:NN=LR+K*Q:IFNS=KGOTO3070ELSEI1=1:XX=V3-XB:L=2*(V4-V3):IFPEEK(D2+NN)-PEEK(D1+NN)+4=L/2GOTO3070ELSEGOSUB2570:I1=2-PEEK(NT+NN):IFI1>=0ANDI1<2THENXX=V3-XB+PEEK(D1+NN):L=2*(PEEK(D2+NN)-PEEK(D1+NN)):GOSUB2610
3070 YY=YB-W4-2:NEXTK:RETURN
3080 LR=KR:GOSUB2950:IFPEEK(KA+KR-61)=1GOTO3120ELSECLS:GOSUB2390
3090 XB=V1:YB=W1:IFPEEK(NT+LR+3*Q)=1THENXB=XB-(48-V2+V1)/2:IFPEEK(NT+LR+Q)=1THENXB=XB+(V1-XB)/2
3100 IFPEEK(NT+LR)=1THENYB=YB+48-W1+W2:IFPEEK(NT+LR+2*Q)=1THENYB=YB-(YB-W1)/2
3110 PRINT@624,BL$;:XL=XA-V1:YL=YA-W2:IFPEEK(MT+KR)>0ANDPEEK(MN+KR)>0THENGOSUB3180:NB=1:ELSEGOSUB3210
3120 PRINT@48,"ROOM NO.:"KR;:PRINT@112,"WOUNDS:"INT(100*PC/PH+.5);"%";:PRINT@176,"FATIGUE:"TA;"%";:PRINT@240,"WGT:"WC;" LBS";:PRINT@624,A$;:PRINT@496,"ARROWS:"PEEK(KB+13);:PRINT@560,"MAGIC AR:"PEEK(KB+14);:PRINT@752,"TOTAL SLAIN:";:PRINT@816,KC;
3130 IFNB>0THENGOSUB2700
3140 NS=5:IFIB=3GOTO3170
3150 FORIR=0TO3:I=KR+IR*Q:IFPEEK(NT+I)=1ANDPEEK(NO+I)>0THENLR=PEEK(NO+I):GOSUB3020
3160 NEXTIR
3170 LR=KR:GOSUB3020:GOSUB2650:GOSUB2880:RETURN
3180 MQ=PEEK(MT+KR):MW=0:ML=PEEK(UL+MQ):MA=PEEK(UA+MQ):MP=PEEK(UP+MQ):MS=PEEK(UV+MQ):MD=PEEK(UD+MQ):MH=PEEK(UC+MQ):MR=0:GOSUB2990:IH=0
3190 XM=RND(V2-V1-8)+4:YM=RND(W1-W2-8)+4
3200 MF=RND(4):RETURN
3210 L=RND(100):IFL>PWTHENNB=0:A$="":RETURN
3220 L=RND(100):LS=0:NB=1:FORI=1TOQ1:LS=LS+PEEK(UW+I):IFL<=LSGOTO3240
3230 NEXTI
3240 POKEMT+KR,I:POKEMN+KR,1:MQ=I:ML=PEEK(UL+I):MA=PEEK(UA+I):MP=PEEK(UP+I):MS=PEEK(UV+I):MD=PEEK(UD+I):MH=PEEK(UC+I):GOSUB2990:IH=0:GOSUB3190:RETURN
3250 L=RND(100):IFL<PW/6GOTO3220ELSERETURN
3260 KR=1:O$="RLATPFMGEV!HQSYDO":GOSUB3000
3270 GOSUB3080:S(1)=W1-W2:S(2)=V2-V1:S(3)=0:S(4)=0'BUILD INITIAL ROOM
3280 P(1)=XA-V1:P(2)=YA-W2:P(3)=P(1):P(4)=P(2):P(0)=P(2)
3290 X0=XL:Y0=YL:IA=0:GOSUB2800:M=0:PRINT@304,BL$;:PRINT@368,BL$;:PRINT@432,BL$;:IFL=0GOTO4190
3300 IA=0:IFASC(C$)>47ANDASC(C$)<58GOTO3320ELSEFORI=1TO17:IFC$=MID$(O$,I,1)THENONIGOTO3550,3570,3610,3610,3610,3610,3610,3900,3880,3590,4070,4090,4140,4160,4120,4040,3780
3310 NEXTI:GOTO3290
3320 IFTA<1GOTO3610:ELSEM=ASC(C$)-48
3330 M1=M:N=PEEK(NP+KR):IB=0:ONKFGOTO3340,3380,3360,3400
3340 IFYA+M>W1-3THENM=W1-3-YA:IB=1
3350 GOTO3430
3360 M=-M:M1=M:IFYA+M<W2+4THENM=W2+4-YA:IB=1
3370 GOTO3430
3380 IFXA+M>V2-3THENM=V2-3-XA:IB=1
3390 GOTO3430
3400 M=-M:M1=M:IFXA+M<V1+4THENM=V1+4-XA:IB=1
3410 GOTO3430
3420 IFNB>0GOTO4190ELSEGOTO4250
3430 IFIB=0GOTO3510:ELSEXL=XA-V1:YL=YA-W2:IFPEEK(NT+KR+KF*Q-Q)<>1GOTO3510
3440 L=KR+KF*Q-Q:IFP(KF)>PEEK(D1+L)ANDP(KF)<PEEK(D2+L)THENGOSUB2720:GOSUB2400:NB=0:IN=0:KR=PEEK(NO+L):GOSUB2950:IB=2:M=ABS(M)+4:IFKR=0GOTO4500
3450 IFIB=1GOTO3510
3460 IFV1-XB>-1ANDV2-XB<49ANDYB-W1>-1ANDYB-W2<49THENIB=3:S(1)=W1-W2:S(2)=V2-V1:M=M1:GOTO3510
3470 IFKF=1THENYA=W2+4:GOTO3270
3480 IFKF=3THENYA=W1-4:GOTO3270
3490 IFKF=2THENXA=V1+4:ELSEXA=V2-4
3500 GOTO3270
3510 GOSUB2690:GOSUB2880:IFKF=1ORKF=3THENYA=YA+M:ELSEXA=XA+M
3520 IFPEEK(KA-86)>0THENM=M/2
3530 GOSUB2650:P(1)=XA-V1:P(3)=P(1):P(2)=YA-W2:P(4)=P(2):P(0)=P(2):IFIB=3THENLR=KR:GOSUB3110:IB=0:GOTO3290
3540 XL=XA-V1:YL=YA-W2:GOSUB2910:IFL>0THENGOSUB2850:GOTO4250ELSEGOTO4190
3550 GOSUB2690:KF=KF+1:IFKF>4THENKF=1
3560 GOSUB2650:GOTO3290
3570 GOSUB2690:KF=KF-1:IFKF<1THENKF=4
3580 GOSUB2650:GOTO3290
3590 GOSUB2690:KF=KF-2:IFKF<1THENKF=KF+4
3600 GOSUB2650:GOTO3290
3610 IFTA<1THENPRINT@368,BL$;:PRINT@368,"TOO TIRED";:GOTO4190:ELSEIA=I-2:IN=0:KM=0:ONIAGOTO3620,3620,3620,3690,3680
3620 HI=0:IFABS(XL-XM)>5ORABS(YL-YM)>5THENPRINT@368,"TOO FAR TO HIT";:GOTO4190
3630 M=TM(IA):K=0:GOSUB2830:P=PB-(PEEK(KB+22)-9)/3*EXP(-2*PC/PH)+ML/3-ZA(	IA):R=RND(20):IFR<PTHENPRINT@368,"SWISH!";:GOTO4180
3640 AK=AS*(R-P+1):PRINT@368,"CRUNCH!";:IFAK>WMTHENAK=WM
3650 IFAK<MHTHENAK=MH
3660 IFPEEK(US+MQ)<>2OR(PEEK(KB+10)>0ANDIA<3)ORIA=4THENMP=MP-AK+MH:IN=0
3670 GOTO4180
3680 I1=5:KM=5:IFPEEK(KB+14)<=0GOTO4190:ELSEPOKEKB+14,PEEK(KB+14)-1:GOSUB2380:GOTO3700
3690 I1=3:IFPEEK(KB+13)<=0GOTO4190:ELSEPOKEKB+13,PEEK(KB+13)-1:GOSUB2370
3700 ONKFGOTO3710,3740,3720,3750
3710 LY=YL+2:UY=YM-3:S=1:GOTO3730
3720 LY=YL-2:UY=YM+3:S=-1
3730 X=XL-1:FORY=LYTOUYSTEPS:I=2*(X+V1-XB):J=YB-Y-W2:SET(I,J):RESET(I,J):X=X*1:NEXTY:GOTO3770
3740 LX=XL+1:UX=XM-3:S=1:GOTO3760
3750 LX=XL-3:UX=XM+3:S=-1
3760 Y=YL:FORX=LXTOUXSTEPS:I=2*(X+V1-XB):J=YB-Y-W2:SET(I,J):RESET(I,J):Y=Y*1:NEXTX
3770 IFABS(X-XM)<I1ANDABS(Y-YM)<I1THENPRINT@368,"THWUNK!";:AK=RND(7)+KM:GOTO3650:ELSEPRINT@368,"SSWHT!";:GOTO4190
3780 K=KR+KF*Q-Q:IFPEEK(NT+K)=2THENIFP(KF)>PEEK(D1+K)ANDP(KF)<PEEK(D2+K)THENIFABS(P(KF-1)-S(KF))<6THENPOKENT+K,1:IJ=0:ONKFGOSUB3850,3820,3870,3840:GOTO3800
3790 GOTO4190
3800 LR=PEEK(NO+K):NS=KF+1:IFNS>3THENNS=NS-4
3810 POKENT+LR+NS*Q,1:NS=5:GOSUB3020:GOTO4190
3820 XX=V2-XB-2
3830 I1=3+IJ:YY=YB-W2-PEEK(D2+K):L=PEEK(D2+K)-PEEK(D1+K):GOSUB2530:RETURN
3840 XX=V1-XB:GOTO3830
3850 YY=YB-W1
3860 I1=1+IJ/2:XX=V1-XB+PEEK(D1+K):L=2*(PEEK(D2+K)-PEEK(D1+K)):GOSUB2610:RETURN
3870 YY=YB-W2-2:GOTO3860
3880 K=KR+(KF-1)*Q:IFPEEK(K+NT)=3ANDRND(40)<20+PEEK(KB+21)THENPOKEK+NT,2:RF(KF-1)=1:PRINT@304,BL$;:PRINT@304,"A SECRET DOOR!";:IJ=-2:LR=KR:ONKFGOSUB3850,3820,3870,3840:ELSEPRINT@304,"NOTHING";
3890 GOTO4190
3900 N=PEEK(TR+KR):PRINT@304,BL$;:IFN=0ORXL-PEEK(XR+KR)>3ORYL-PEEK(YR+KR)>3THENPRINT@304,"YOU CAN'T";:GOTO4190:ELSEPOKETR+KR,0:PRINT@304,"TREASURE #"N;:POKEKA+N-81,PEEK(KA+N-81)+1:WC=WC+PEEK(TW+N):PRINT@240,"WGT:"WC;:IN=0
3910 GOSUB2890:I=PEEK(TS+N):IFI>100THENJ=KB+I-81:POKEJ,PEEK(J)+1:J=KA+I-197:POKEJ,PEEK(J)+1:GOTO4190:ELSEONI+1GOTO4190,3930,3940,3950,3980,3990,4000,3920,4010,4020,4030,4630
3920 POKEKA-89,1:GOTO4190
3930 J=RND(6):PRINT@945,J;"ITEMS";:POKEKB+6,PEEK(KB+6)+J:GOTO4190
3940 PS=4:SP=SP+1:GOTO4190
3950 PRINT@944,"DOST USE SWORD?";:GOSUB2800:IFL=0GOTO3950ELSEPRINT@944,BL$;:IFC$="Y"THENPB=PB+SM:SM=RND(4)+RND(4)-4:PB=PB-SM:WM=AS*(7+SM)+.5:IFSM>0THENSM=SM-INT(2-PEEK(KA)/2):IFSM>1THENPRINT@944,"THE SWORD GLOWS";
3960 IFC$<>"Y"THENPOKEKA+N-81,PEEK(KA+N-81)-1:WC=WC-PEEK(TW+N):PRINT@944,BL$;
3970 GOTO4190
3980 J=RND(20):GOSUB2340:POKEKB+13,PEEK(KB+13)+J:GOSUB2370:GOTO4190
3990 J=RND(10):GOSUB2340:POKEKB+14,PEEK(KB+14)+J:GOSUB2380:GOTO4190
4000 POKEKA-90,1:PA=PA+1:GOTO4190
4010 POKEKA-88,1:GOTO4190
4020 POKEKA-87,1:GOTO4190
4030 PW=75:POKEKA+2235,PW:GOTO4190
4040 PRINT@944,"DROP SOME?";
4050 GOSUB2360:JJ=10*J:GOSUB2360:JJ=JJ+J:IFJJ<21THENI=PEEK(KA+JJ-81):IFI>0THENPOKEKA+JJ-81,I-1:WC=WC-PEEK(TW+JJ):PRINT@944,BL$;:PRINT@240,"WGT:"WC;:IFPEEK(TR+KR)=0THENPOKETR+KR,JJ:POKEXR+KR,XL:POKEYR+KR,YL:GOSUB2890
4060 PRINT@944,BL$;:GOTO4190
4070 IFRND(100)<.3*(PEEK(KB+20)+PEEK(KB+22))*PEEK(UI+MQ)THENIN=1ELSEGOTO4190
4080 PRINT@304,BL$;:PRINT@304,"PASS BY";:GOTO4190
4090 IFPEEK(KB+11)>0THENPOKEKB+11,PEEK(KB+11)-1:J=0ELSEGOTO4130
4100 PC=PC+1+J:IFPC>PHTHENPC=PH
4110 PRINT@945,BL$;:PRINT@119,"     ";:PRINT@119,INT(100*PC/PH+.5);"%";:GOTO4190
4120 I=KB+6:IFPEEK(I)>0THENPOKEI,PEEK(I)-1:J=RND(6)+1:GOTO4100
4130 PRINT@945,"NONE LEFT";:GOTO3290
4140 JJ=PEEK(KB+21):PRINT@304,BL$;:I=PEEK(NO+KR+Q*KF-Q):MQ=PEEK(MT+I):IFI=0ORPEEK(MN+I)=0ORRND(1000)>JJ*JJ+PEEK(KA-89)*700THENPRINT@304,"NOTHING";:GOTO4190
4150 GOSUB2990:PRINT@304,A$;:GOTO4190
4160 IFPEEK(NP+KR)>0ANDRND(20)<PEEK(KB+21)THENGOSUB2870:ELSEPRINT@304,BL$;:PRINT@304,"NOTHING";
4170 GOTO4190
4180 K=1:GOSUB2830
4190 GOSUB2850:IFNB>0GOTO4210ELSEGOSUB3250:IFNB=0GOTO3290
4200 PRINT@688,"APPEARS";:PRINT@624,BL$;:PRINT@624,A$;:GOSUB2700:GOTO3290
4210 PRINT@432,BL$;:IFIN>0ORHI>0GOTO4470
4220 IFABS(X0-XM)>5ORABS(Y0-YM)>5GOTO4350
4230 IM=MA
4240 IM=IM-1:IFIM<0GOTO4300
4250 P=PA-ZD(IA):R=RND(20)+ML:IFR<PTHENPRINT@432,BL$;:IM=IM*1:PRINT@432,"IT MISSED!";:GOTO4240
4260 GOSUB2350:IFRND(20)-1<PEEK(KB+4)THENPRINT@432,BL$;:PRINT@432,"SHIELD HIT!";:K=-PS:ELSEPRINT@432,BL$;:PRINT@432,"STRUCK THEE!";:K=0:IFPEEK(US+MQ)=2ANDRND(20)>PEEK(KB+21)/2+PEEK(KA-81)ANDPEEK(KA-88)=0THENGOSUB2300:PRINT@432,BL$;:PRINT@432,"A CHILL";
4270 K=K+(MD*(R-P))/10-AA-PEEK(KB+16):IFK<0THENK=0
4280 PC=PC-K:IFPC<1THENFORI=1TO1000:NEXTI:GOTO4580
4290 PRINT@119,B1$;:PRINT@119,INT(100*PC/PH+.5);"%";:IFNB>0GOTO4240ELSEGOTO3290
4300 IFNB=0GOTO3290ELSEGOSUB2730:L=2:ONRND(4)GOTO4310,4320,4330,4340
4310 YM=YM+L:GOTO4460
4320 XM=XM+L:GOTO4460
4330 YM=YM-L:GOTO4460
4340 XM=XM-L:GOTO4460
4350 GOSUB2730:GOSUB2880:XX=XL-XM:YY=YL-YM:IFABS(XX)<ABS(YY)GOTO4370:ELSEIFXX>0THENMF=2ELSEMF=4
4360 GOTO4380
4370 IFYY>0THENMF=1ELSEMF=3
4380 L=MS:ONMFGOTO4390,4410,4430,4450
4390 IFYM+L>YL-3THENYM=YL-3:ELSEYM=YM+L
4400 GOTO4460
4410 IFXM+L>XL-3THENXM=XL-3:ELSEXM=XM+L
4420 GOTO4460
4430 IFYM-L<YL+3THENYM=YL+3:ELSEYM=YM-L
4440 GOTO4460
4450 IFXM-L<XL+3THENXM=XL+3:ELSEXM=XM-L
4460 GOSUB2310:GOSUB2700
4470 IFMP<1ANDNB>0THENGOSUB2730:GOSUB2780:EX#=EX#+20*ML*ML+15:KC=KC+1:PRINT@816,KC;:PRINT@304,BL$;:PRINT@304,"MONSTER SLAIN!";:M=PEEK(MN+KR):IFM>0THENPOKEMN+KR,M-1:IFM>1THENGOSUB3180:PRINT@304,"ANOTHER APPEARS";:GOSUB2700
4480 IFMP<1THENNB=0:PRINT@368,BL$;:PRINT@432,BL$;:PRINT@624,BL$;:PRINT@688,BL$;
4490 GOTO3290
4500 CLS:PRINT"THOU LEAVEST THE DUNJON"
4510 PRINT"EXPERIENCE:"EX#:INPUT"DOST WISH TO REENTER";C$:IFLEFT$(C$,1)="Y"THENWC=PEEK(KB+12):KR=1:PC=PH:XA=PEEK(KA+2236):YA=PEEK(KA+2237):KF=PEEK(KA+2238):TA=100:GOSUB2390:GOTO3270
4520 INPUT"DOST WISH TO SAVE THE DUNJON";A$:IFLEFT$(A$,1)<>"Y"GOTO4560
4530 AN$="LEVEL"+RIGHT$(STRS$(PEEK(KA)),1):OPEN"O",1,AN$
4540 K=0:FORI=1TO9:A$="":FORJ=0TO248:A$=A$+CHR$(PEEK(KA+K+J)+59):NEXTJ:PRINT#1,A$:K=K+249:NEXTI:CLOSE1
4550 PRINT"LEVEL SAVED":STOP
4560 J=EX#/256:POKEKB+1,EX#-J*256:JJ=J/256:POKEKB+2,J-JJ*256:POKEKB+3,JJ:IFSM<0THENSM=0
4570 POKEKB+10,SM:PRINT"LOADING THE INNKEEPER":RUN"INN
4580 CLS:PRINT@401,CHR$(23);"THOU ART SLAIN!":FORI=1TO2500:NEXTI:CLS:I=SQR(RND(16))+.7:ONIGOTO4590,4610,4620,4600
4590 PRINT"THOU ART EATEN":POKEKB-12,0:RUN"INN
4600 PRINT"BENEDIC THE CLERIC FOUND THEE":GOTO4510
4610 PRINT"LOWENTHAL THE MAGE FOUND THEE":FORI=81TO90:POKEKA-I,0:NEXTI:GOTO4510
4620 PRINT"OLIAS THE DWARF FOUND THEE":FORI=61TO90:POKEKA-I,0:NEXTI:SM=0:POKEKB+10,0:POKEKB+16,0:POKEKB+14,0:POKEKB+6,0:GOTO4510
4630 STOP

