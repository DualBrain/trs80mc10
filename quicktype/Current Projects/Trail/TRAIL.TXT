0 ' "TRAIL SNAKE"
1 ' BY SWAMPYNESS
2 '
3 ' TYPE 'N RUN
4 ' RETROBATTLESTATIONS
5 ' REDDIT CONTEST ENTRY
6 ' APRIL 2015
7 '
8 '
9 ' BEGIN
10 GOSUB 100 ' INIT VARS
20 GOSUB 300 ' RESET
30 GOSUB 1400 ' INTRO
40 GOSUB 400 ' INIT GFX
50 GOSUB 900 ' ADD APPLE
60 GOSUB 500 ' LOOP
70 IF BITE = 1 THEN GOTO 1600 ' OUTRO
80 GOTO 60
90 '
100 ' INIT VARS
110 GREEN = 1
120 YELLOW = 2
130 BLUE = 3
140 RED = 4
150 AX = 0 ' APPLE X
160 AY = 0 ' APPLE Y
170 SKIN = BLUE ' SNAKE COLOR
180 PEEL = RED ' APPLE COLOR
190 BORDER = GREEN ' EDGE COLOR
195 BACK = YELLOW ' BACKGROUND COLOR
200 SW = 256
210 SH = 196 - 20
220 RETURN
230 '
300 ' RESET
310 DX = 1 ' SNAKE DIRECTION X
320 DY = 0 ' SNAKE DIRECTION Y
330 PX = SW / 2 ' SNAKE X
340 PY = SH / 2 ' SNAKE Y
350 IF SCORE > HISCORE THEN HISCORE = SCORE
360 SCORE = 0
370 BITE = 0 ' SELF COLLIDE
380 RETURN
390 '
400 ' INIT GFX
410 PMODE 3, 1
420 SCREEN 1, 0
430 COLOR BORDER, BACK
440 PCLS
450 LINE(0, SH)-(SW, SH + 16), PSET, BF ' SCOREBOARD
460 COLOR SKIN, BACK
470 RETURN
480 '
500 ' LOOP
510 GOSUB 600 ' INPUT
520 GOSUB 700 ' MOVE
530 GOSUB 800 ' CHECK SNAKE
540 LINE(PX, PY)-(PX + 2, PY + 3), PSET, BF ' TRAIL
550 RETURN
560 '
600 ' INPUT
610 IK$ = INKEY$
620 IF IK$ = "" THEN RETURN
630 IA = ASC(IK$)
640 IF IA = 94 THEN DY = -1:DX = 0:RETURN
650 IF IA = 8 THEN DY = 0:DX = -1:RETURN
660 IF IA = 9 THEN DY = 0:DX = 1:RETURN
670 IF IA = 10 THEN DY = 1:DX = 0:RETURN
680 RETURN
690 '
700 ' MOVE
710 PX = PX + (DX * 4)
720 PY = PY + (DY * 4)
730 IF PX < 0 THEN PX = SW - 4
740 IF PX > SW - 1 THEN PX = 0
750 IF PY < 0 THEN PY = SH - 4
760 IF PY > SH - 1 THEN PY = 0
770 RETURN
780 '
800 ' CHECK SNAKE
810 PP = PPOINT(PX, PY)
820 IF PP = SKIN THEN BITE = 1
830 IF PP = PEEL THEN GOSUB 1000 ' EAT APPLE
840 RETURN
850 '
900 ' ADD APPLE
910 GOSUB 1300 ' ERASE APPLE
930 AX = (RND((SW - 16) / 4) * 4) + 8
940 AY = (RND((SH - 16) / 4) * 4) + 8
950 GOSUB 1200 ' DRAW APPLE
960 RETURN
970 '
1000 ' EAT APPLE
1010 TX = AX
1020 TY = AY
1030 AX = 6 + (SCORE * 12)
1040 AY = SH + 8
1050 PLAY "L32;O5;C"
1060 GOSUB 1200 ' DRAW APPLE
1070 AX = TX
1080 AY = TY
1090 SCORE = SCORE + 1
1100 GOSUB 900 ' ADD APPLE
1110 RETURN
1120 '
1200 ' DRAW APPLE
1210 IF AX = 0 AND AY = 0 THEN RETURN
1220 FOR I =  2 TO 4 STEP 2
1230 CIRCLE(AX, AY), I, PEEL
1240 NEXT
1250 RETURN
1260 '
1300 ' ERASE APPLE
1310 IF AX = 0 AND AY = 0 THEN RETURN
1320 FOR I = 2 TO 4 STEP 2
1330 CIRCLE(AX, AY), I, BACK
1340 NEXT
1350 RETURN
1360 '
1400 ' INTRO
1410 CLS
1420 PRINT @10 + (1 * 32), "TRAIL SNAKE";
1430 PRINT @10 + (4 * 32), "HI-SCORE:";
1440 PRINT HISCORE;
1450 PRINT @6 + (10 * 32), "REDDIT BATTLESTATIONS";
1460 PRINT @10 + (11 * 32), "TYPE 'N RUN";
1470 PRINT @10 + (12 * 32), "APRIL  2015";
1480 PRINT @9 + (13 * 32), "BY SWAMPYNESS";
1490 IK$ = INKEY$
1500 IF IK$ = "" THEN 1490
1510 RETURN
1520 '
1600 ' OUTRO
1610 SOUND100,4
1620 CLS
1630 PRINT @10 + (1 * 32), "TRAIL SNAKE";
1640 PRINT @11 + (4 * 32), "SCORE:";
1650 PRINT SCORE;
1660 PRINT @11 + (11 * 32), "GAME OVER";
1670 IK$ = INKEY$
1680 IF IK$ = "" THEN 1670
1690 GOTO 20 ' RESET
1700 '
1710 ' END
