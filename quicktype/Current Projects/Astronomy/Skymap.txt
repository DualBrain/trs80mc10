05 REM  ON COMPUTERS OTHER THAN THE ATARI ST, ITEMS SUCH AS THE "LINEF" COMMANDS
06 REM  AND THE "COLOR" STATEMENTS WILL HAVE TO BE CHANGED TO MEET SYSTEM
07 REM  SPECIFIC REQUIREMENTS.  ALSO, LINES 1020, 1040, AND 1080 WILL HAVE TO
08 REM  BE CHANGED TO SUIT YOUR SCREEN RESOLUTION.
10 SCREEN 1
12 LOCATE 1,1:PRINT TAB(25)"PHYSICAL EPHEMERIS OF MARS":PRINT
14 PRINT TAB(10)" MODIFIED FOR THE ATARI ST BY ROBERT REEVES (CIS 72377,2576)"
16 PRINT" THIS PROGRAM IS A COMBINATION OF TWO .BAS LISTINGS APPEARING IN THE JANUARY"
18 PRINT" AND SEPTEMBER, 1990 ISSUES OF SKY AND TELESCOPE MAGAZINE.":PRINT
20 PRINT" THE PROGRAM ASKS FOR A DATE AND UT TIME, THEN PRESENTS OBSERVATIONAL DATA"
22 PRINT" ABOUT THE PLANET MARS, AS WELL AS A LINE DRAWING REPRESENTING THE TILT AND"
24 PRINT" SIZE OF MARS AS COMPARED TO THE MOST FAVORABLE OPPOSITION.":PRINT
26 PRINT" CENTRAL MERIDIAN = THE MARTIAN MERIDIAN OF LATITUDE FACING TOWARDS EARTH"
28 PRINT" P. A. OF AXIS    = THE POSITION ANGLE OF THE MARTIAN POLE"
30 PRINT" DECL. OF EARTH   = THE ANOUNT OF MARS AXIS TILT RELATIVE TO EARTH"
32 PRINT" ANGULAR DIAMETER = APPARENT MARS DIAMETER IN ARC SECONDS"
34 PRINT" PHASE            = PER CENT OF PLANET ILLUMINATED BY AS SEEN FROM EARTH"
36 PRINT" MAGNITUDE        = BRIGHTNESS IN STELLAR MAGNITUDES":PRINT
38 INPUT"                      DO YOU WANT TO CONTINUE (Y/N)";C$:IF C$="N" OR C$="n" THEN END
40 CLS:GOSUB 100
50 GOSUB 800
60 GOSUB 1000
70 GOSUB 1800
80 GOSUB 1900
90 GOSUB 2000
100 REM  PHYSICAL EPHEMERIS OF MARS
105 REM FROM SEPT, 1990 SKY & TEL, P. 297
106 DEFDBL A-Z
110 DT=57:REM VALUE OF ET-UT (SEC) IN 1990; CHANGE DT FOR OTHER YEARS AS NEEDED
115 P1=3.14159265
120 P2=2*P1: RD=180/P1: DR=1/RD
125 GOSUB 330:  REM  GET DATE AND UT
130 D=(J-2451545)+F
135 D=D+DT/86400: REM  UT TO ET
140 GOSUB 405:  PRINT
145 READ A1,A2,D1,D2
150 T=T-1: REM  CENT FROM 2000.0
155 A1=A1+A2*T
160 D1=D1+D2*T
165 Q=A1: GOSUB 710: A1=Q*DR
170 Q=D1: GOSUB 710: D1=Q*DR
175 D9=COS(D5)*SIN(A1-A5)
180 N9=SIN(D1)*COS(D5)*COS(A1-A5)
185 N9=N9-COS(D1)*SIN(D5)
190 K=RD*ATN(N9/D9)
195 IF D9<0 THEN K=K+180
205 READ W0,W1,W2: GOSUB 304
215 REM
220 REM  PLANETOCENTRIC DECL. EARTH
222 SD=-SIN(D1)*SIN(D5)
224 SD=SD-COS(D1)*COS(D5)*COS(A1-A5)
226 DE=RD*ATN(SD/SQR(1-SD*SD))
228 REM
230 REM  P.A. OF AXIS
232 SP=COS(D1)*SIN(A1-A5)
234 CP=SIN(D1)*COS(D5)
236 CP=CP-COS(D1)*SIN(D5)*COS(A1-A5)
238 PA=RD*ATN(SP/CP)
240 IF CP<0 THEN PA=PA+180
242 IF PA<0 THEN PA=PA+360
244 IF PA>=360 THEN PA=PA-360
248 REM
250 REM  PHASE AND MAGNITUDE
252 K4=((R4+RR)^2-R*R)/(4*R4*RR)
254 CI=(R4*R4+RR*RR-R*R)/(2*R4*RR)
256 I=RD*ATN(SQR(1-CI*CI)/CI)
258 M4=5*LOG(RR*R4)/LOG(10)
260 M4=-1.52+.016*I+M4
262 REM
270 A1$="Central meridian: ###.##"
272 A2$="P.A. of axis:     ###.##"
274 A3$="Decl. of Earth:   ###.##"
276 A4$="Angular diameter: ###.#"
278 A5$="Phase:            ###.###"
279 A6$="Magnitude:        ###.#"
280 LOCATE 1,1:PRINT TAB(26)"PHYSICAL EPHEMERIS OF MARS":PRINT
281 PRINT"Date (Y,M,D):";Y5;",";M5;",";D10
282 PRINT"Time (H,M,S UT):";H15;",";M15;",";S15 
283 PRINT USING A1$;C
284 PRINT USING A2$;PA
285 PRINT USING A3$;DE
286 PRINT USING A4$;9.36/RR
287 PRINT USING A5$;K4
288 PRINT USING A6$;M4
289 PRINT"  Angular diameter at"
290 PRINT"    best opposition   ----------->"
291 PRINT"     (24 arc sec)"
295 CM=C
300 GOTO 50 
304 REM   ROTATIONS FROM EPOCH
305 W=W0+W1*(D-.0057755*RR)+W2*T
306 Q=W-K: GOSUB 710: C=Q
308 RETURN
309 REM    NORTH POLE R.A.
310 DATA 317.681,0.678
311 REM    NORTH POLE DEC.
312 DATA 52.886,0.352
313 REM     ROTATION
314 DATA 176.655,350.8919830,0.62
315 REM
320 REM    CALENDAR --> JD
325 REM
330 LOCATE 1,1:INPUT "YR,MO,DAY  ";Y,M,D
331 Y5=Y: M5=M: D10=D
335 INPUT "UT (H,M,S) ";H1,M1,S1
336 H15=H1: M15=M1: S15=S1
337 CLS
340 D=D+H1/24+M1/1440+S1/86400
345 G=1:  IF Y<1582 THEN G=0
350 D1=INT(D): F=D-D1-.5
355 J=-INT(7*(INT((M+9)/12)+Y)/4)
360 IF G=0 THEN 380
365 S=SGN(M-9): A=ABS(M-9)
370 J3=INT(Y+S*INT(A/7))
375 J3=-INT((INT(J3/100)+1)*3/4)
380 J=J+INT(275*M/9)+D1+G*J3
385 J=J+1721027+2*G+367*Y
390 IF F>=0 THEN 400
395 F=F+1: J=J-1
400 RETURN
405 REM  FUNDEMENTAL ARGUMENTS
410 REM
415 T=D/36525+1
420 REM  T=CENTURIES FROM 1900.0
425 L0=.779072+.00273790931*D
430 G0=.993126+.0027377785*D
431 L4=.987353+.00145575328*D
432 G4=.053856+.00145561327*D
433 F4=.849694+.00145569465*D
435 L5=.089608+.00023080893*D
440 G5=.056531+.00023080893*D
445 G6=.882987+9.294371E-05*D
450 L0=(L0-INT(L0))*P2
455 G0=(G0-INT(G0))*P2
456 L4=(L4-INT(L4))*P2
457 G4=(G4-INT(G4))*P2
458 F4=(F4-INT(F4))*P2
460 L5=(L5-INT(L5))*P2
465 G5=(G5-INT(G5))*P2
470 G6=(G6-INT(G6))*P2
475 REM    SUN
480 L=6910*SIN(G0)
485 L=L+72*SIN(2*G0)
490 L=L-17*T*SIN(G0)
495 L=L-7*COS(G0-G5)
500 R=1.00014-.01675*COS(G0)
505 R=R-.00014*COS(2*G0)
510 L=L0+L/206265
515 REM   MARS
520 M=38451*SIN(G4)
525 M=M+2238*SIN(2*G4)
530 M=M+181*SIN(3*G4)
535 M=M-52*SIN(2*F4)
540 M=M+37*T*SIN(G4)
545 M=M-22*COS(G4-2*G5)
550 M=M-19*SIN(G4-G5)
555 M=M+17*COS(G4-G5)
556 M=M+17*SIN(4*G4)
557 M=M-16*COS(2*G4-2*G5)
558 M=M+13*COS(G0-2*G4)
559 M=M-10*SIN(G4-2*F4)
560 M=M+7*COS(G0-G4)
561 M=M-7*COS(2*G0-3*G4)
564 B=6603*SIN(F4)
565 B=B+622*SIN(G4-F4)
570 B=B+615*SIN(G4+F4)
575 B=B+64*SIN(2*G4+F4)
580 R4=1.53031-.1417*COS(G4)
585 R4=R4-.0066*COS(2*G4)
590 R4=R4-.00047*COS(3*G4)
615 L4=L4+M/206265: B4=B/206265
620 N9=R4*COS(B4)*SIN(L4-L)
625 D9=R4*COS(B4)*COS(L4-L)+R
630 L1=ATN(N9/D9)
635 IF D9<0 THEN L1=L1+P1
640 LL=L1+L
645 V=N9*N9+D9*D9
650 RR=SQR(V+((R4*SIN(B4))*(R4*SIN(B4))))
655 S=R4*SIN(B4)/RR
660 BB=ATN(S/SQR(1-S*S))
665 E=(84428-47*T)/206265
670 N9=SIN(LL)*COS(E)-TAN(BB)*SIN(E)
675 D9=COS(LL): A5=ATN(N9/D9)
680 IF D9<0 THEN A5=A5+P1
685 S=SIN(BB)*COS(E)
690 S=S+COS(BB)*SIN(E)*SIN(LL)
695 D5=ATN(S/SQR(1-S*S))
700 RETURN
705 REM
710 REM    NORMALIZE DEGREES
715 Q=Q/360: Q=Q-INT(Q): Q=Q*360
720 RETURN
800 REM  DRAW MAX OPPOSITION SIZE SCALE
806 LOCATE 8,38:PRINT"|":LOCATE 8,69:PRINT"|"
807 LOCATE 9,38:PRINT"|":LOCATE 9,69:PRINT"|"
808 LOCATE 10,38:PRINT"|":LOCATE 10,69:PRINT"|"
809 LOCATE 11,38:PRINT"|":LOCATE 11,69:PRINT"|"
810 LOCATE 12,38:PRINT"|":LOCATE 12,69:PRINT"|"
895 RETURN
1000 REM GLOBE-DRAWING PROGRAM
1011 REM FROM JAN 90 SKY&TEL P 88
1015 REM
1020 FF = .44: REM   GLOBE FLATTENING FACTOR
1040 RM = 5.1*(9.39/RR):REM RADIUS OF GLOBE
1050 T=DE
1060 DR=3.14159265#/180:TI=T*DR
1080 XC=424:YC=185:REM SCR CENTER
1090 CT=COS(TI):ST=SIN(TI)
1095 REM
1100 REM DRAW LATITUDE LINES
1105 COLOR 1,0,3:REM RED LINES
1110 FOR LA=-60 TO 60 STEP 30
1120 IF T>0 THEN 1150
1130 IF 90+T< LA THEN 1270
1140 GOTO 1160
1150 IF T-90>LA THEN 1270
1160 L=LA*DR:SL=SIN(L):R=RM*COS(L)
1170 GOSUB 1570
1190 FOR P1=PS+5 TO -PS STEP 2
1200 P=P1*DR:X=R*SIN(P)+XC
1210 Y1=RM*SL:Z1=R*COS(P)
1220 Z=Z1*CT+Y1*ST
1225 Y=-Z1*ST+Y1*CT+YC
1230 IF Z<0 THEN 1250
1240 LINEF XS,YS*FF,X,Y*FF
1250 XS=X:YS=Y
1260 NEXT P1
1270 NEXT LA
1280 REM DRAW LONGITUDE LINES
1285 COLOR 1,0,2:REM GREEN LINES
1290 FOR L=-180 TO 150 STEP 30
1300 LO=L*DR
1310 IF TI<0 THEN 1340
1320 TH=90*DR:DT=-5*DR:GOTO 1350
1340 TH=-90*DR:DT=5*DR
1350 GOSUB 1750
1360 Z=1:TH=TH+DT
1380 CH=COS(TH):X=RM*SIN(LO)*CH+XC
1390 Y1=RM*SIN(TH):Z1=RM*COS(LO)*CH
1400 Z=Y1*ST+Z1*CT:Y=Y1*CT-Z1*ST+YC
1410 IF Z<0 THEN 1430
1420 LINEF XS,YS*FF,X,Y*FF
1430 XS=X:YS=Y:TH=TH+DT
1440 IF Z>=0 THEN 1380
1450 NEXT L
1460 REM DRAW LIMB OF GLOBE
1470 XS=RM+XC:YS=YC
1480 FOR I=0 TO 360 STEP 2
1490 IA=I*DR:X=RM*COS(IA)+XC
1500 Y=RM*SIN(IA)+YC
1510 LINEF XS,YS*FF,X,Y*FF
1520 XS=X:YS=Y
1530 NEXT I
1550 RETURN
1560 REM STARTING LATITUDE LINE
1570 CP=-ST*SIN(L)/(CT*COS(L))
1580 IF CP>=1 OR CP<=-1 THEN 1640
1590 IF CP*CP>.001 THEN 1620
1600 P=-90*DR: GOTO 1650
1620 TP=SQR(1-CP*CP)/CP:P=ATN(TP)
1630 GOTO 1650
1640 P=0
1650 IF P<=0 THEN 1680
1660 P=-P:GOTO 1690
1680 P=-180*DR-P
1690 X=R*SIN(P)+XC
1700 Y1=RM*SIN(L):Z1=R*COS(P)
1710 Y=-Z1*SIN(TI)+Y1*COS(TI)+YC
1720 XS=X:YS=Y:PS=P/DR
1730 RETURN
1740 REM STARTING LONGITUDE LNE
1750 X=RM*SIN(LO)*COS(TH)+XC
1760 Y1=RM*SIN(TH)
1770 Z1=RM*COS(LO)*COS(TH)
1780 Y=-Z1*ST+Y1*CT+YC:XS=X:YS=Y
1790 RETURN
1800 REM  MERIDIAN FEATURES
1810 LOCATE 17,1:PRINT "      PROMINENT FEATURE AT MERIDIAN: "
1820 IF CM>=0 AND CM<20 THEN MR$="MERIDIANI SINUS"
1830 IF CM>=20 AND CM<50 THEN MR$="MARGARITIFER SINUS AND NILIACUS LACUS"
1840 IF CM>=50 AND CM<110 THEN MR$="SOLIS LACUS AND THARSIS"
1850 IF CM>=110 AND CM<160 THEN MR$="SERENUM MARE AND NIX OLYMPICA"
1860 IF CM>=160 AND CM<260 THEN MR$="CIMMERIUM MARE AND ELYSIUM"
1870 IF CM>=260 AND CM<320 THEN MR$="HELLAS AND SYRTIS MAJOR"
1880 IF CM>=320 AND CM<=360 THEN MR$="MERIDIANI SINUS"
1890 LOCATE 17,38:PRINT MR$
1899 RETURN
1900 REM  PRINT POLES
1910 LOCATE 3,47:PRINT"SOUTH \/ POLE":LOCATE 16,47:PRINT"NORTH /\ POLE"
1930 RETURN
2000 REM  REPEAT PROGRAM
2010 LOCATE 15,1:INPUT"** ANOTHER DATE (Y/N) **";IQ$
2020 IF IQ$="Y" OR IQ$="y" THEN RESTORE:CLS:GOTO 40
2030 END