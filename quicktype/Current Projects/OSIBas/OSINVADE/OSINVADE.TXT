 8 REM-INVADERS-
 9 REM-by John C. Ponte
 10 REM--2/20/80
 11 A=188:A1=166:A2=226:I=56900:I0=57089:U=64
 12 K=57088:Q=128:Q0=64:Q1=32:Q2=16:Q3=2:Q4=8:S=210:F=1727:F1=53440
 13 E=55168:E0=55231:E1=53376:E2=53439:C1=70:C2=150
 14 IFPEEK(K)<128THEN18
 15 E=54115:E0=54139:E1=53411:E2=53435:F=664:F1=53443:S=81
 16 C1=40:C2=70:Q=127:Q0=191:Q1=223:Q2=239:Q3=253:Q4=247:U=32
 18 FORX=1TO30:PRINT:NEXT
 19 PRINT"TRY DESTROYING THE ENEMY LIFE FORM BEFORE IT"
 20 PRINT"MULTIPLIES. TO DESTROY IT, RUN YOUR SHIP (";CHR$(A);") INTO"
 21 PRINT"THE ENEMY (";CHR$(A2);").":PRINT
 22 PRINT"<BE CAREFUL, THE BORDERS ARE DEFLECTION FIELDS."
 23 PRINT:PRINT"<DON'T HIT THE ASTEROIDS!":PRINT
 24 PRINT"   PRESS: 1-Left 2-Right 3-Up 4-Down 7-Quit":PRINT
 25 INPUT"ENTER 1 (BEGINNER), 2 (ADVANCED), OR 3 (PROFESSIONAL)";C
 26 INPUT"HOW MANY PLAYERS";V
 27 FORX=1TOV
 28 PRINT"WHAT'S THE NAME OF PLAYER #";X;
 29 INPUTN$(X):R$(X)="CAPTAIN ":NEXT
 30 IFC>1THENC1=C2
 31 INPUT"SEED";Z
 32 FORX=1TO32:PRINT:NEXT:POKE55104,32:GOSUB720
 33 D=0:W1=0:H=H+1:IFH>VTHENH=1
 35 FORX=1TOS:G=INT(F*RND(Z)+F1):POKEG,46:NEXT
 50 FORX=1TOC1:G=INT(F*RND(Z)+F1):POKEG,A1:NEXT
 53 IFU=32THENFORX=53468TO53474:FORT=XTOESTEPU:POKET,96:NEXTT:NEXTX
 56 X1=INT(F*RND(Z)+F1):IFU=32THENGOSUB740
 57 POKEX1,A
 58 X=RND(Z)
 60 G=INT(F*RND(Z)+F1):IFG=X1THEN60
 61 IFU=32THENGOSUB742
 62 POKEG,A2:W1=W1+1
 70 IFW1=5THEN390
 80 POKE530,1
 90 L=PEEK(X1):B=32:IFL=46THENB=46
 91 POKEI0,55:POKEI,3
 95 IFL=161THENGOSUB729
 100 POKEX1,A:POKEI,1:IFC<3THENFORX=1TO39:NEXT
 105 IFL=A2THEN398
 106 IFL=A1THEN380
 107 D=D+1
 108 IFD>16THEND=0:GOTO60
 110 POKEK,Q:P=PEEK(K)
 112 IFP=QTHEN150
 114 IFP=Q0THEN170
 116 IFP=Q1THEN190
 118 IFP=Q2THEN210
 119 IFP=Q3THEN383
 120 GOTO110
 150 POKEX1,B:X1=X1-1:GOTO90
 170 POKEX1,B:X1=X1+1:GOTO90
 190 POKEX1,B:X1=X1-U:GOTO90
 210 POKEX1,B:X1=X1+U:GOTO90
 380 POKEI0,40:POKEI,3:FORX=1TO80:NEXT
 381 FORX=1TO160:POKEX1,X:POKEI0,X:NEXT:POKEI,1:POKEX1,32
 382 PRINT"YOU HAVE COLLIDED WITH AN ASTEROID. TOO BAD!"
 383 FORX=E1TOE0:POKEX,32:NEXT
 388 GOTO540
 390 PRINT"YOU'VE BEEN OVER-POWERED BY THE LIFE FORM. YOU ARE DOOMED."
 391 GOTO383
 398 POKEI0,70:POKEI,3:FORX=1TO70:NEXT:POKEI,1
 400 W1=W1-1:D=0
 401 IFW1=0THEN404
 402 GOTO107
 404 GOSUB703
 406 PRINT"YOU HAVE DESTROYED THE INVADING LIFE FORM."
 408 PRINT"GOOD WORK, ";R$(H);N$(H);"."
 409 FORX=E1TOE0:POKEX,32:NEXT
 410 PRINT:GOTO520
 490 PRINT"  SCORE":PRINT"---------"
 491 FORX=1TOV
 492 PRINTR$(X);N$(X);": W=";W(X);", L=";L(X)
 493 NEXT
 494 J=H+1:IFJ>VTHENJ=1
 495 PRINT:IFV<>1THENPRINTR$(J);N$(J);" IS NEXT"
 501 PRINT:PRINT"PRESS 5 TO CONTINUE, 7 TO QUIT."
 502 POKEK,Q:P=PEEK(K)
 503 IFP=Q4THEN32
 504 IFP=Q3THEN590
 510 GOTO502
 520 W(H)=W(H)+1:IFH=VTHEN543
 530 GOTO490
 540 L(H)=L(H)+1:IFH=VTHEN543
 542 GOTO490
 543 FORX=1TOV
 544 IFW(X)=5ANDL(X)<2THENR=1:GOSUB600
 545 IFW(X)=10ANDL(X)<3THENR=2:GOSUB600
 546 IFW(X)=15ANDL(X)<4THENR=3:GOSUB600
 547 IFW(X)=20ANDL(X)<5THENR=4:GOSUB600
 548 IFW(X)=25ANDL(X)<6THENR=5:GOSUB600
 553 NEXTX:GOTO490
 590 FORX=1TO30:PRINT:NEXTX
 595 POKE530,0:END
 600 IFR=1THENR$(X)="COMMODORE "
 602 IFR=2THENR$(X)="REAR ADMIRAL "
 604 IFR=3THENR$(X)="VICE-ADMIRAL "
 606 IFR=4THENR$(X)="ADMIRAL "
 608 IFR=5THENR$(X)="FLEET ADMIRAL "
 700 PRINT"ATTENTION ";N$(X);", DUE TO YOUR EXCEPTIONAL RECORD, YOU"
 702 PRINT"HAVE BEEN PROMOTED TO THE RANK OF ";R$(X);".":PRINT
 703 POKEI,3:FORM=1TO10:POKEI0,INT(255*RND(Z))
 704 FORT=1TO50:NEXTT:NEXTM:POKEI,1
 715 RETURN
 720 FORX=ETOE0:POKEX,161:NEXT:FORX=E1TOE2:POKEX,161:NEXT
 722 RETURN
 729 POKEI0,20:POKEI,3
 730 X1=INT(F*RND(Z)+F1)
 731 IFPEEK(X1)=A1THEN380
 732 IFPEEK(X1)=A2THENPOKEX1,A:GOTO398
 733 POKEI,1:RETURN
 740 IFPEEK(X1)=NTHEN56
 741 RETURN
 742 IFPEEK(G)=96THEN60
 743 RETURN