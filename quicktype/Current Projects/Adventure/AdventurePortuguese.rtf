{\rtf1\ansi\ansicpg1252\deff0\deflang1033{\fonttbl{\f0\fswiss\fcharset0 Arial;}}
{\*\generator Msftedit 5.41.15.1515;}\viewkind4\uc1\pard\f0\fs20 10  HOME : PRINT "QUER INSTRUCOES (S/N)?"\par
15  POKE 284,0\par
20 A$ =  CHR$ ( PEEK (284)): IF A$ =  CHR$ (0) THEN 20\par
30  IF A$ = "S" THEN  GOSUB 6000\par
110  REM ** PREPARAR MATRIZES-RESPOSTAS **\par
120  DIM R$(19),R(19)\par
130  FOR K = 1 TO 19: READ R$(K),R(K): NEXT\par
140  DATA NADAR,5,ESVAZIAR,6,ACENDER,7,DESISTIR,8,LISTAR,9,MATAR,10,ATIRAR,10,AJUDAR,11\par
145  DATA PEGAR,2,APANHAR,2,CARREGAR,2,COLOCAR,3,DEIXAR,3,LARGAR,3,PUXAR,4\par
150  DATA NORTE,1,SUL,1,LESTE,1,OESTE,1\par
160  REM *** MATRIZES DOS OBJETOS ***\par
170  READ NB\par
180  DIM OB(NB),OB$(NB),SI$(NB)\par
190  FOR I = 1 TO NB: READ OB(I),OB$(I),SI$(I): NEXT\par
200  DATA 7,4,SACO,HA UM SACO DE BOLAS DE GUDE AQUI\par
210  DATA 14,TIJOLO,TEM UM TIJOLO NO CHAO\par
220  DATA 24,CORRENTE,HA UMA CORRENTE PENDURADA SOBRE O TRONO\par
230  DATA 0,REVOLVER,TEM UM REVOLVER NO CHAO\par
240  DATA 0,OLHO,UM OLHO CRAVEJADO DE BRILHANTES ESTA NO CHAO\par
250  DATA 22,LAMPADA,VOCE ESTA DIANTE DE UMA LAMPADA\par
260  DATA 0,COLETOR,DE REPENTE SURGE UM COLETOR DE  IMPOSTOS\par
270  REM *** POSICAO INICIAL ***\par
280 L = 15\par
290  GOTO 330\par
300  REM *** ACHAR LOCAL ***\par
310  HOME\par
320  IF  INT ( RND (1) * 15 + 1) = 1 AND TA = 0 THEN OB(7) = L:TA = 1\par
330  IF L < 11 THEN  ON L GOSUB 0,0,0,5020,0,0,5050,5080,0,5110: GOTO 370\par
340  IF L < 21 THEN  ON L - 10 GOSUB 5140,0,0,5180,5210,5240,5270,5300,0,0: GOTO 370\par
350  IF L < 26 THEN  ON L - 20 GOSUB 0,5330,0,5360\par
360  REM *** COLOCA OBJETOS NO SEU LUGAR ***\par
370  FOR I = 1 TO NB: IF OB(I) = L THEN  PRINT SI$(I)\par
380  NEXT\par
390  REM *** INDICAR DIRECOES ***\par
400  IF L <  > 11 OR (LA = 1 AND OB(6) =  - 1) THEN  PRINT "PODE SEGUIR";: GOTO 410\par
405  GOTO 460\par
410  IF N > 0 THEN  PRINT  TAB( 13);"NORTE"\par
420  IF E > 0 THEN  PRINT  TAB( 13);"LESTE"\par
430  IF S > 0 THEN  PRINT  TAB( 13);"SUL"\par
440  IF W > 0 THEN  PRINT  TAB( 13);"OESTE"\par
450  REM *** INSTRUCOES ***\par
460 I$ = "": PRINT : INPUT "PARA ONDE";I$\par
470  GOSUB 3010\par
480  IF OB(7) = L AND I <  > 10 THEN 1590\par
500  REM *** SELECIONA OPCAO ***\par
505  IF I = 0 THEN 520\par
510  ON I GOTO 1010,1150,1240,1310,1410,1460,1500,1360,1080,1550,3110\par
520  PRINT : PRINT "EU NAO SEI COMO ";V$: GOTO 330\par
1000  REM *** ROTINA DE MOVIMENTO ***\par
1010  IF I$ = "N" AND N > 0 THEN L = L - 6: GOTO 310\par
1020  IF I$ = "L" AND E > 0 THEN L = L + 1: GOTO 310\par
1030  IF I$ = "S" AND S > 0 THEN L = L + 6: GOTO 310\par
1040  IF I$ = "O" AND W > 0 THEN L = L - 1: GOTO 310\par
1050  REM *** SE NAO HOUVER LOCAL NESTA DIRECAO ***\par
1060  PRINT : PRINT "DESCULPE - VOCE NAO PODE IR POR ESTE CAMINHO.": GOTO 330\par
1070  REM *** LISTAR ***\par
1080  PRINT "VOCE TEM ";:IN = 0\par
1090  FOR G = 1 TO NB\par
1100  IF OB(G) =  - 1 THEN  PRINT  TAB( 10);OB$(G):IN = IN + 1\par
1110  NEXT\par
1120  IF IN = 0 THEN  PRINT "NADA"\par
1130  GOTO 330\par
1140  REM *** PEGAR ***\par
1150  FOR G = 1 TO NB\par
1160  IF  LEFT$ (OB$(G), LEN (N$)) = N$ THEN 1190\par
1170  NEXT\par
1180  PRINT N$;"???": GOTO 330\par
1190  IF OB(G) =  - 1 THEN  PRINT "VOCE PEGOU": GOTO 330\par
1200  IF OB(G) <  > L THEN  PRINT "NAO ESTA AQUI": GOTO 330\par
1210  PRINT "OK":OB(G) =  - 1\par
1220  GOTO 330\par
1230  REM *** DEIXAR ***\par
1240  FOR G = 1 TO NB\par
1250  IF  LEFT$ (OB$(G), LEN (N$)) = N$ THEN 1270\par
1260  NEXT : PRINT N$;"???": GOTO 330\par
1270  IF OB(G) <  >  - 1 THEN  PRINT "VOCE NAO PODE ";V$;" O QUE NAO TEM": GOTO 330\par
1280  PRINT "OK":OB(G) = L\par
1290  GOTO 330\par
1300  REM *** PUXAR ***\par
1310 IN = 1 AND ( LEFT$ ("CORRENTE", LEN (N$)) = N$): IF IN = 1 AND L <  > 24 THEN  PRINT "NADA ACONTECE": GOTO 330\par
1320  IF IN <  > 1 THEN  PRINT "VOCE NAO PODE PUXAR ISSO!": GOTO 330\par
1330  IF OB(5) <  >  - 1 THEN  PRINT "VOCE CAI DENTRO DO VASO E VAI   EMBORA COM A DESCARGA": GOTO 1360\par
1340  REM *** FIM DA AVENTURA ***\par
1350  PRINT "PARABENS! VOCE COMPLETOU A      TAREFA"\par
1360  PRINT : PRINT "QUER JOGAR NOVAMENTE (S/N)?"\par
1365  POKE 284,0\par
1370 A$ =  CHR$ ( PEEK (284)): IF A$ <  > "S" AND A$ <  > "N" THEN 1370\par
1380  IF A$ = "S" THEN  RUN\par
1390  END\par
1400  REM *** NADAR ***\par
1410  IF L <  > 7 THEN  PRINT "ONDE?!": GOTO 330\par
1420  IF OB(2) =  - 1 THEN  PRINT "QUE VERGONHA. VOCE SE AFOGOU!": GOTO 1360\par
1430  IF OB(4) >  - 1 THEN  PRINT "VOCE ACHOU UM REVOLVER":OB(4) =  - 1: GOTO 330\par
1440  PRINT "VOCE SE MOLHOU TODO": GOTO 330\par
1450  REM *** ESVAZIAR ***\par
1460  IF  LEFT$ ("SACO", LEN (N$)) <  > N$ THEN  PRINT "ISTO NAO PODE SER ESVAZIADO": GOTO 330\par
1470  IF OB(1) <  >  - 1 THEN G = 1: GOTO 1270\par
1480  PRINT "AS BOLINHAS SE ESPALHAM PELO    CHAO":OB(5) = L: GOTO 370\par
1490  REM *** ACENDER ***\par
1500  IF  LEFT$ ("LAMPADA", LEN (N$)) <  > N$ THEN  PRINT "NAO PODE SER FEITO": GOTO 330\par
1510  IF OB(6) <  >  - 1 THEN G = 6: GOTO 1270\par
1520  IF LA = 1 THEN  PRINT "JA ESTA ACESA": GOTO 330\par
1530 LA = 1: PRINT "OK": GOTO 330\par
1540  REM *** MATAR ***\par
1550  IF OB(4) <  >  - 1 THEN  PRINT "COM O QUE?": GOTO 320\par
1560  IF OB(7) <  > L THEN  PRINT V$;" QUEM?": GOTO 320\par
1570  PRINT "VOCE MATOU O ";OB$(7):OB(7) = 0: GOTO 330\par
1580  REM *** COLETOR DE IMPOSTOS ***\par
1590 IN = 0:OB(7) = 0\par
1600  FOR K = 1 TO NB\par
1610  IF OB(K) =  - 1 THEN IN = IN + 1\par
1620  NEXT\par
1630  IF IN = 0 THEN  PRINT "COMO VOCE NAO TEM NADA QUE POSSASER CONFISCADO, ELE O PRENDEU EMUMA MASMORRA IMUNDA.": GOTO 1360\par
1640 K =  INT ( RND (1) * NB + 1): IF OB(K) <  >  - 1 THEN 1640\par
1650  PRINT "ELE TOMA O ";OB$(K);" DE VOCE":OB(K) = 0: GOTO 330\par
3000  REM *** INSTRUCAO DE CHECAGEM ***\par
3010 N$ = ""\par
3011 I = 0\par
3012  FOR II = 1 TO  LEN (I$)\par
3013  IF  MID$ (I$,II,1) = " " THEN I = II:II = 256\par
3014  NEXT\par
3020  IF I = 0 THEN V$ = I$: GOTO 3050\par
3030 V$ =  LEFT$ (I$,I - 1)\par
3040 N$ =  MID$ (I$,I + 1)\par
3050 I = 0\par
3060  FOR K = 1 TO 19\par
3070  IF  LEFT$ (R$(K), LEN (V$)) = V$ THEN I = R(K):I$ =  LEFT$ (V$,1)\par
3080  NEXT\par
3090  RETURN\par
3100  REM *** AJUDAR ***\par
3110  IF L <  > 7 OR OB(2) <  >  - 1 THEN  PRINT "DESCULPE, NAO POSSO AJUDAR AGORA": GOTO 330\par
3120  PRINT "TIJOLOS SAO MUITO PESADOS. SEU  BRACO DEVE ESTAR DOENDO.": GOTO 330\par
5000  REM DESCRICAO DOS LOCAIS\par
5010  REM LOCAL 4\par
5020  PRINT "VOCE ESTA DO LADO DE FORA DE UMAGRANDE CONSTRUCAO"\par
5030 N = 0:E = 0:S = 1:W = 0: RETURN\par
5040  REM LOCAL 7\par
5050  PRINT "VOCE ESTA A BEIRA DE UM GRANDE  RIO"\par
5060 N = 0:E = 1:S = 0:W = 0: RETURN\par
5070  REM LOCAL 8\par
5080  PRINT "VOCE ESTA NUMA FLORESTA         PETRIFICADA"\par
5090 N = 0:E = 0:S = 1:W = 1: RETURN\par
5100  REM LOCAL 10\par
5110  PRINT "VOCE ESTA NUMA SALA MUITO SUJA"\par
5120 N = 1:E = 1:S = 1:W = 0: RETURN\par
5130  REM LOCAL 11\par
5140  PRINT "VOCE ESTA NUM QUARTO ESCURO"\par
5150  IF OB(6) <  >  - 1 OR LA <  > 1 THEN N = 0:S = 0:E = 0:W = 0: PRINT "ESTA MUITO ESCURO PARA VER AS   SAIDAS": RETURN\par
5160 N = 0:E = 0:S = 1:W = 1: RETURN\par
5170  REM LOCAL 14\par
5180  PRINT "VOCE ESTA NUM ATALHO ENLAMEADO"\par
5190 N = 1:E = 1:S = 0:W = 0: RETURN\par
5200  REM LOCAL 15\par
5210  PRINT "VOCE ESTA NA ENTRADA DA CIDADE  OCULTA"\par
5220 N = 0:E = 1:S = 0:W = 1: RETURN\par
5230  REM LOCAL 16\par
5240  PRINT "VOCE ESTA NO HALL DE ENTRADA"\par
5250 N = 1:E = 1:S = 1:W = 1: RETURN\par
5260  REM LOCAL 17\par
5270  PRINT "VOCE ESTA NO PATIO"\par
5280 N = 1:E = 1:S = 0:W = 1: RETURN\par
5290  REM LOCAL 18\par
5300  PRINT "VOCE ESTA NO JARDIM"\par
5310 N = 0:E = 0:S = 1:W = 1: RETURN\par
5320  REM LOCAL 22\par
5330  PRINT "VOCE ESTA NO GUARDA-LOUCAS"\par
5340 N = 1:E = 0:S = 0:W = 0: RETURN\par
5350  REM LOCAL 24\par
5360  PRINT "VOCE ESTA NA SALA DO TRONO"\par
5370 N = 1:E = 0:S = 0:W = 0: RETURN\par
6000  REM *** INSTRUCOES ***\par
6010  HOME : PRINT : PRINT "DEVIDO A UM COLAPSO FINANCEIRO  VOCE DEIXOU O PAIS."\par
6020  PRINT : PRINT "SEUS PROBLEMAS VAO TERMINAR     QUANDO VOCE ENCONTRAR O LEGENDA-RIO OLHO CRAVEJADO DE BRILHANTESDE UM TOTEM INCA. DEPOIS DE EN- CONTRA-LO VOCE PRECISA ACHAR A  SAIDA."\par
6030  PRINT : PRINT "CUIDADO COM O COLETOR DE IMPOS- TOS!"\par
6040  PRINT : PRINT "APERTE QUALQUER TECLA PARA CON- TINUAR"\par
6045  POKE 284,0\par
6050 A$ =  CHR$ ( PEEK (284)): IF A$ =  CHR$ (0) THEN 6050\par
6060  HOME : RETURN\par
}
 