0 CLS:GOSUB1500:CLEAR200:DIMZ,US,X,Y,X0,Y0,X1,Y1,R(5,5,5),S(200):Z=RND(-(PEEK(9)*256+PEEK(10)))
2 GOSUB2000:GOTO10
3 POKEVC,4:RETURN
4 POKEVC,0:RETURN
5 US=USR((X0*256+Y0)*65537):RETURN:REM FOR SINGLE POINT
6 US=USR((X0*256+Y0)*65536+X1*256+Y1):RETURN:REM TO DRAW LINE
10 PRINT"CONSTRUCTING A 3-D MAZE"
12 PRINT"PLEASE WAIT...";
14 D$="SRBLDULSRBDUBLSRDURBLSDU"
18 REM 
20 REM ARRAY DIMENSIONS
22 H=5:W=5:DP=5:FORZ=0TO200:S(Z)=0:NEXT:FORZ=0TOH:FORX=0TOW:FORY=0TODP:R(Z,X,Y)=0:NEXT:NEXT:NEXT
26 REM
28 REM FILL ROOMS WITH RANDOM DOORS
30 FORZ=1TOH:FORX=1TOW:FORY=1TODP
32 R(Z,X,Y)=RND(64)-1
34 IFY=1THENR(Z,X,Y)=R(Z,X,Y)AND61
36 IFY=DPTHENR(Z,X,Y)=R(Z,X,Y)AND55
38 IFX=1THENR(Z,X,Y)=R(Z,X,Y)AND59
40 IFX=WTHENR(Z,X,Y)=R(Z,X,Y)AND62
42 IFZ=1THENR(Z,X,Y)=R(Z,X,Y)AND31
44 IFZ=HTHENR(Z,X,Y)=R(Z,X,Y)AND47
46 NEXT:NEXT:NEXT
48 FORZ=1TOH:FORX=1TOW:FORY=1TODP
50 IFZ=1THEN54
52 IF(R(Z-1,X,Y)AND16)<>0THENR(Z,X,Y)=R(Z,X,Y)OR32
54 IFX=1THEN58
56 IF(R(Z,X-1,Y)AND1)<>0THENR(Z,X,Y)=R(Z,X,Y)OR4
58 IFY=1THEN62
60 IF(R(Z,X,Y-1)AND8)<>0THENR(Z,X,Y)=R(Z,X,Y)OR2
62 IFZ=HTHEN66
64 IF(R(Z+1,X,Y)AND32)<>0THENR(Z,X,Y)=R(Z,X,Y)OR16
66 IFX=WTHEN70
68 IF(R(Z,X+1,Y)AND4)<>0THENR(Z,X,Y)=R(Z,X,Y)OR1
70 IFY=DPTHEN74
72 IF(R(Z,X,Y+1)AND2)<>0THENR(Z,X,Y)=R(Z,X,Y)OR8
74 NEXT:NEXT:NEXT
76 REM
78 REM FIND CENTER ROOM
80 F=0
82 D=0
84 Y=INT(DP/2)
86 X=INT(W/2)
88 Z=INT(H/2)
90 S(1)=Z:S(2)=X:S(3)=Y:JE=3
94 S$=""
96 REM
98 REM MAIN LOOP
100 REM DO A RANDOM WALK
102 N=RND(6)-1
104 IFF=0THENF=1:GOTO130
106 IF(N=2ANDD=0)OR(N=0ANDD=2)OR(N=1ANDD=3)OR(N=3ANDD=1)THEN102
108 TZ=Z:TX=X:TY=Y
110 IFN=5THENTZ=TZ-1:GOTO122
112 IFN=4THENTZ=TZ+1:GOTO122
114 IFN=3THENTX=TX-1:GOTO122
116 IFN=2THENTY=TY+1:GOTO122
118 IFN=1THENTX=TX+1:GOTO122
120 TY=TY-1
122 FORJ=1TOJESTEP3
124 IF(TZ=S(J))AND(TX=S(J+1))AND(TY=S(J+2))THEN102
126 NEXTJ
128 IF(TX=0)OR(TX=W+1)OR(TY=0)OR(TY=DP+1)OR(TZ=0)OR(TZ=H+1)THENIFJE<(H*W*DP)/3THEN102
130 IFN=0THENM=2:GOTO140
132 IFN=1THENM=1:GOTO140
134 IFN=2THENM=8:GOTO140
136 IFN=3THENM=4:GOTO140
138 M=2^N
140 R(Z,X,Y)=R(Z,X,Y) OR M
142 IFM=32THENM=16:GOTO154
144 IFM=16THENM=32:GOTO154
146 IFM=8THENM=2:GOTO154
148 IFM=2THENM=8:GOTO154
150 IFM=4THENM=1:GOTO154
152 IFM=1THENM=4
154 S$=S$+MID$(D$,D*6+(N+1),1)
156 IFN=5THENZ=Z-1:GOTO168
158 IFN=4THENZ=Z+1:GOTO168
160 IFN=3THENX=X-1:GOTO168
162 IFN=2THENY=Y+1:GOTO168
164 IFN=1THENX=X+1:GOTO168
166 IFN=0THENY=Y-1
168 S(JE+1)=Z:S(JE+2)=X:S(JE+3)=Y:JE=JE+3
170 IFNOT((Z=H+1)OR(Y=DP+1)OR(X=W+1))THENR(Z,X,Y)=R(Z,X,Y)OR M
172 IFN<4THEND=N
176 IF(Z=0)OR(Z=H+1)OR(Y=0)OR(Y=DP+1)OR(X=0)OR(X=W+1)THEN230
178 GOTO102
180 REM
190 END
220 REM INITIALIZE
230 GOSUB3
260 CLS0
270 Y=INT(DP/2)
280 X=INT(W/2)
290 Z=INT(H/2)
300 D=0
310 C$="20154130540235431254"
320 REM 
330 REM MAIN LOOP - DO UNTIL OUT
340 CLS0
350 X0=0:Y0=0:X1=0:Y1=47:GOSUB6:X0=0:Y0=47:X1=63:Y1=47:GOSUB6:X0=63:Y0=47:X1=63:Y1=0:GOSUB6:X0=63:Y0=0:X1=0:Y1=0:GOSUB6
360 C=R(Z,X,Y)
370 GOSUB 710
380 M=INT(2^VAL(MID$(C$,(D*5)+1,1)))
390 IF(C AND M)<>0THENGOSUB790
400 M=INT(2^VAL(MID$(C$,(D*5)+2,1)))
410 IF(C AND M)<>0 THEN GOSUB 870
420 M=INT(2^VAL(MID$(C$,(D*5)+3,1)))
430 IF(C AND M)<>0 THEN GOSUB 950
440 M=INT(2^VAL(MID$(C$,(D*5)+4,1)))
450 IF(C AND M)<>0 THEN GOSUB 1110
460 M=INT(2^VAL(MID$(C$,(D*5)+5,1)))
470 IF(C AND M)<>0THEN GOSUB 1030
480 A=0:A$=INKEY$:IFA$=""THEN480
490 IFA$="U"THENA=1:GOTO580
491 IFA$="D"THENA=2:GOTO580
492 IFA$="B"THENA=3:GOTO580
493 IFA$="L"THENA=4:GOTO580
494 IFA$="S"THENA=5:GOTO580
495 IFA$="R"THENA=6:GOTO580
496 IFA$="H"THEN520
497 IFA$="Q"THENGOSUB4:CLS:GOTO680
500 GOTO480
520 GOSUB4:CLS:GOSUB1600:PRINT"Q RESTART OR QUIT":PRINT"H THIS MENU":PRINT
530 PRINT "THE SEQUENCE IS:";S$
540 PRINT:PRINT "PRESS ANY KEY TO CONTINUE"
550 A$=INKEY$:IFA$=""THEN550
555 IFA$="Q"THENEND
560 GOSUB3:GOTO340
580 A=INT(2^(VAL(MID$("543210542103541032540321",(D*6)+A,1))))
590 IF(C AND A)=0THEN480
600 IFA$="U"THENZ=Z-1
601 IFA$="D"THENZ=Z+1
610 IFA$="R"THEND=D+1:IFD=4THEND=0
620 IFA$="B"THEND=D+2:IFD>3THEND=D-4
630 IFA$="L"THEND=D-1:IFD=-1THEND=3
640 IF(A$="U")OR(A$="D")THEN660
650 IFD=0THENY=Y-1:GOTO660
651 IFD=1THENX=X+1:GOTO660
652 IFD=2THENY=Y+1:GOTO660
653 X=X-1
660 IFNOT((X=0)OR(X=W+1)OR(Y=0)OR(Y=DP+1)OR(Z=0)OR(Z=H+1))THEN340
670 GOSUB4:CLS:PRINT"YOU'RE OUT!":PRINT
680 PRINT"HIT ENTER TO TRY AGAIN,"
685 PRINT"N TO MAKE A NEW MAZE,":PRINT"OR Q TO QUIT.":PRINT
690 A$=INKEY$:IFA$=""THEN690
692 IFA$=CHR$(13)ORA$="Y"THEN230
694 IFA$="N"THEN10
696 IFA$="Q"THENEND
698 GOTO690
700 REM
710 REM DRAW ROOM OUTLINE
720 X0=20:Y0=15:X1=20:Y1=33:GOSUB6:X0=20:Y0=33:X1=44:Y1=33:GOSUB6:X0=44:Y0=33:X1=44:Y1=15:GOSUB6:X0=44:Y0=15:X1=20:Y1=15:GOSUB6
730 X0=0:Y0=0:X1=20:Y1=15:GOSUB6
740 X0=0:Y0=47:X1=20:Y1=33:GOSUB6
750 X0=63:Y0=0:X1=44:Y1=15:GOSUB6
760 X0=63:Y0=47:X1=44:Y1=33:GOSUB6
770 RETURN
780 REM 
790 REM DRAW LEFT DOOR
800 X0=6:Y0=43:X1=6:Y1=17:GOSUB6
810 X0=13:Y0=38:X1=13:Y1=19:GOSUB6
820 X0=6:Y0=17:X1=13:Y1=19:GOSUB6
830 X0=8:Y0=30:GOSUB5:RETURN
860 REM 
870 REM DRAW RIGHT DOOR
880 X0=50:Y0=38:X1=50:Y1=19:GOSUB6
890 X0=57:Y0=43:X1=57:Y1=17:GOSUB6
900 X0=50:Y0=19:X1=57:Y1=17:GOSUB6
910 X0=55:Y0=30:GOSUB5:RETURN
940 REM 
950 REM DRAW DOOR STRAIGHT AHEAD
960 X0=28:Y0=33:X1=28:Y1=21:GOSUB6
970 X0=36:Y0=33:X1=36:Y1=21:GOSUB6
980 X0=28:Y0=21:X1=36:Y1=21:GOSUB6
990 X0=34:Y0=27:GOSUB5:RETURN
1020 REM 
1030 REM DRAW LOWER OPENING
1040 X0=25:Y0=43:X1=39:Y1=43:GOSUB6
1050 X0=27:Y0=38:X1=36:Y1=38:GOSUB6
1060 X0=25:Y0=43:X1=27:Y1=38:GOSUB6
1070 X0=39:Y0=43:X1=36:Y1=38:GOSUB6:RETURN
1100 REM 
1110 REM DRAW UPPER OPENING
1120 X0=25:Y0=6:X1=39:Y1=6:GOSUB6
1130 X0=27:Y0=11:X1=36:Y1=11:GOSUB6
1140 X0=25:Y0=6:X1=27:Y1=11:GOSUB6
1150 X0=36:Y0=11:X1=39:Y1=6:GOSUB6:RETURN
1500 PRINT"    AN A'MAZE'ING ADVENTURE":PRINT"     BY WILLIAM BARDEN JR."
1510 PRINT"IT'S LIKE THIS: YOU HAVE BEEN"
1520 PRINT"KIDNAPPED. YOU AWAKE TO FIND"
1530 PRINT"YOURSELF IN THE MIDDLE OF A"
1540 PRINT"5X5X5 3-DIMENSIONAL MAZE. YOU"
1560 PRINT"MUST NAVIGATE THE VARIOUS ROOMS"
1570 PRINT"AND FLOORS TO ESCAPE..."
1600 PRINT"S DOOR STRAIGHT AHEAD":PRINT"B DOOR BEHIND YOU":PRINT"L DOOR ON YOUR LEFT":PRINT"R DOOR ON YOUR RIGHT"
1610 PRINT"U OPENING TO FLOOR ABOVE":PRINT"D OPENING TO FLOOR BELOW":RETURN
2000 VI=16384:VC=49151:W=2:E=3:J=0
2020 REM LINE DRAW SUBROUTINE BY GREG DIONNE
2030 REM US=USR((X0*256+Y0)*65537) FOR SINGLE POINT
2040 REM US=USR((X0*256+Y0)*65536+X1*256+Y1) TO DRAW LINE
3000 DATALNPDALMMABABNNLLNMMMJAMKCCAJCFADHPAALLHAAALLEANAMLCCAJCFADHPAALMHAAALMFANNNGBBCDHCEPJHMOJHNLJGNGJHNMINDCJG
3001 DATAMKJBMMCHCIJLLLJHMKJGMOJLNHJHMOBGJANLCKABEANANMCKABFABBCFOANGNMNHNLNLNGNHNMNGMLNLLMNHMLCANAFPHOOMOCNMMKNNNI
3002 DATAJGNJMGKLDNIGIAMAFFEEEEMLFFMBFFCEPIHEAANICEABEEIKIAJHNKIGCADNNLNIIJAAIKEAIEEBNNNINONINGNKOKAAOHAADJEPJHMOJH
3003 DATANLJGNHJHNMINMAJGMLJBMNCHLGJLLMJHMLJGMOJLNGJHMOBGJANLCKABEANANMCKABFABBCFOANGNMNHNLNLNHNHNMNGMKNLLLNHMKCANA
3004 Z=32768:FORX=0TO3:READA$:GOSUB3005:NEXTX:Z=16917:A$="HOIAAA"
3005 FORY=1TOLEN(A$)/2:POKEZ,(ASC(MID$(A$,2*Y-1,1))-65)*16+ASC(MID$(A$,2*Y,1))-65:Z=Z+1:NEXTY:RETURN
3010 END
