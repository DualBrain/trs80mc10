0 CLS:GOTO200
1 SET(X(T),Y(T),5):RETURN
2 S=POINT(X(T),Y(T)):IFS<>5ANDS<>C(T)THENTM=T:T=6:Z=1505:RETURN
3 SET(X(T),Y(T),C(T)):RETURN
5 SOUND100,1:K(S,3)=K(S,3)+1:IFK(S,3)>2THENK(S,3)=1
6 PRINT@K(S,2)*32+K(S,1),S$(K(S,0),K(S,3));:PRINT@K(S,2)*32+K(S,1)+32,W$(K(S,0),K(S,3));:RETURN
10 FORZ=1TO1500:FORT=1TON
11 FORS=1TOL:NEXT:I$=INKEY$:IFI$<>""THENS=ASC(I$)-64:IFS>0ANDS<13THENGOSUB5
19 IFB(T)THEN31
21 IFPOINT(X(T)+A(T),Y(T))>0THENGOSUB1:X(T)=X(T)+A(T):GOSUB2:NEXT:NEXT:GOTO50
22 IFPOINT(X(T)+A(T),Y(T)-1)>0THENGOSUB1:X(T)=X(T)+A(T):Y(T)=Y(T)-1:GOSUB2:NEXT:NEXT:GOTO50
23 IFPOINT(X(T)+A(T),Y(T)+1)>0THENGOSUB1:X(T)=X(T)+A(T):Y(T)=Y(T)+1:GOSUB2:NEXT:NEXT:GOTO50
24 IFPOINT(X(T),Y(T)-1)>0THENGOSUB1:Y(T)=Y(T)-1:A(T)=0:B(T)=-1:GOSUB2:NEXT:NEXT:GOTO50
25 IFPOINT(X(T),Y(T)+1)>0THENGOSUB1:Y(T)=Y(T)+1:A(T)=0:B(T)=1:GOSUB2:NEXT:NEXT:GOTO50
26 GOTO40
31 IFPOINT(X(T),Y(T)+B(T))>0THENGOSUB1:Y(T)=Y(T)+B(T):GOSUB2:NEXT:NEXT:GOTO50
32 IFPOINT(X(T)-1,Y(T)+B(T))>0THENGOSUB1:Y(T)=Y(T)+B(T):X(T)=X(T)-1:GOSUB2:NEXT:NEXT:GOTO50
33 IFPOINT(X(T)+1,Y(T)+B(T))>0THENGOSUB1:Y(T)=Y(T)+B(T):X(T)=X(T)+1:GOSUB2:NEXT:NEXT:GOTO50
34 IFPOINT(X(T)-1,Y(T))>0THENGOSUB1:X(T)=X(T)-1:B(T)=0:A(T)=-1:GOSUB2:NEXT:NEXT:GOTO50
35 IFPOINT(X(T)+1,Y(T))>0THENGOSUB1:X(T)=X(T)+1:B(T)=0:A(T)=1:GOSUB2:NEXT:NEXT:GOTO50
40 TM=T:T=6:NEXT:Z=1505:NEXT
50 IFZ=1501THENPRINT@480,"OUT OF GAS!";:P=0:NR=NR+1:T=1:GOTO60 
52 T=TM:IFS=2ANDT=1THENPRINT@480,"YOU MADE IT!";:SC=N:SR=SR+1:NR=NR+1:GOTO60
54 PRINT@480,"C R A S H ! !";:P=0:NR=NR+1
60 FORTM=1TO23:GOSUB1:FORS=1TO150:NEXT:GOSUB3:FORS=1TO150:NEXT:NEXT
70 PRINT@480,"WANT TO TRY AGAIN?";
72 I$=INKEY$:IFI$=""THEN72
74 IFI$="Y"THEN80
76 IFI$="N"THEN90
78 GOTO72
80 IFP=0THEN100
82 PRINT@480,"TRY THE NEXT # OF TRAINS?";
84 I$=INKEY$:IFI$=""THEN84
86 IFI$="Y"THENN=N+1:GOTO100
88 IFI$="N"THEN100
89 GOTO84
90 CLS:PRINT"YOU CAN CONTROL"SC"TRAINS!":PRINT"YOUR SUCCESS RATE WAS"LEFT$(STR$(SR*100/NR),5)" %":PRINT"AT LEVEL"L/10:END
100 IFN>6THENN=6
110 GOSUB1000:RESTORE:X(1)=48+RND(14):Y(1)=1:A(1)=-1:B(1)=0:C(1)=3:TM=3+RND(2):IFN>4THENX(1)=48
115 FORZ=2TON
120 S=8*RND(3)-TM:T=2+RND(12):IFT<>7THENP=32*T+S+16384:IFPEEK(P)=195ANDPEEK(P+1)=195ANDPEEK(P+2)=195ANDPEEK(P+3)=195THEN135
130 GOTO120
135 IFS=20THENS=22
136 X(Z)=S*2:Y(Z)=T*2+1:A(Z)=1:B(Z)=0:READC(Z):SET(X(Z),Y(Z),C(Z)):SOUND1,20:NEXT
140 IFN<6THENFORZ=N+1TO6:READT:NEXT
150 FORT=1TO12:READK(T,0),K(T,1),K(T,2),K(T,3):NEXT
199 PRINT@480,"";:GOTO10
200 DIMX(6),Y(6),A(6),B(6),C(6),K(12,3),T,S,P,Z,L,TM,SR,NR:T=RND(-(PEEK(9)*256+PEEK(10)))
210 GOSUB2000:GOSUB900:GOTO100
800 CLS:PRINTTAB(4)"CURSOR #19 RAIL (C) 1980"
810 PRINTTAB(2)"ORIGINAL BY CHRIS TORKILDSON"
815 PRINTTAB(2)"TANDY MC-10 VERSION J GERRIE"
820 PRINTTAB(2)"":RETURN
900 GOSUB800:PRINT"SWITCH THE RAILROAD"
910 PRINT:INPUT"ENTER LEVEL (1hard-5easy)";I$:L=INT(VAL(I$))*10:IFL<0ORL>50THEN900
920 IFL=0THENL=50
930 PRINT:INPUT"WANT DIRECTIONS";I$:IFLEFT$(I$,1)="Y"THEN950
940 GOTO994
950 GOSUB800:PRINT"YOU ARE THE MASTER OF A LARGE"
952 PRINT"SWITCHYARD. YOU CAN HAVE UP TO"
954 PRINT"6 LOCOMOTIVES RUNNING AT A TIME,";
956 PRINT"DEPENDING ON YOUR SKILL LEVEL."
958 PRINT"THE OBJECT IS TO DIRECT THE BLUE";
960 PRINT"TRAIN TO THE YELLOW SIDING."
962 PRINT"YOU DO THIS BY GIVING ORDERS"
964 PRINT"TO YOUR YARD SWITCHMEN. HOWEVER"
966 PRINT"YOU MUST BE QUICK, BECAUSE YOU"
968 PRINT"CANNOT STOP ANY OF THE TRAINS."
970 INPUT"PRESS enter TO CONTINUE";I$:
972 GOSUB800:PRINT"THERE ARE 12 SWITCHES LABELED"
974 PRINT"a - l. YOU THROW THE SWITCH BY"
976 PRINT"TYPING ITS LETTER. THROWING A"
978 PRINT"STRAIGHT SWITCH CURVES THE TRACK";
980 PRINT"AROUND THE LETTER."
982 PRINT"YOU MUST TRY TO MOVE THE BLUE"
984 PRINT"TRAIN FROM ITS STARTING POINT"
986 PRINT"IN THE UPPER RIGHT HAND CORNER"
988 PRINT"TO THE SIDING NEAR THE LOWER"
990 PRINT"LEFT HAND CORNER SWITCH (j)."
992 INPUT"PRESS enter TO CONTINUE";I$
994 CLS:INPUT"HOW MANY TRAINS ARE YOU ABLE TO HANDLE";N
996 IFN<2ORN>6THEN994
998 RETURN
1000 CLS0:PRINT"รรรรรรรรรรรรรรรรรรรรรรรรรรรรรย";
1001 PRINT"ฦaย";
1002 PRINT"สbส";
1003 PRINT"สรรรรรรรรรรรรรรรรรรรรรยรรรยส";
1004 PRINT"สcยdฤยeส";
1005 PRINT"สสสส";
1006 PRINT"สfสสส";
1007 PRINT"ฤรรรรรรรรรรรรฦสส";
1008 PRINT"ยสส";
1009 PRINT"สสส";
1010 PRINT"ฤรรรรรรรรรยรรรรรยรรรฦส";
1011 PRINT"gยhฤยiฤยส";
1012 PRINT"สสสส";
1013 PRINT"สjสklสส";
1014 PRINT"รรรรรรรรรรยฤรรรรรฦรรรรรรรรฦ";
1015 PRINT"";
1020 PRINT@480,"thetrainsarerolling";:POKE16887,33
1030 RETURN
2000 S$(1,1)="รร":W$(1,1)="ย":S$(1,2)="ย":W$(1,2)="ฤย"
2010 S$(2,1)="สร":W$(2,1)="ส":S$(2,2)="ฤร":W$(2,2)="ย"
2020 S$(3,1)="รร":W$(3,1)="":S$(3,2)="ฦ":W$(3,2)=""
2030 S$(4,1)="ยส":W$(4,1)="ส":S$(4,2)="ย":W$(4,2)="ฤย"
2040 S$(5,1)="รร":W$(5,1)="":S$(5,2)="ยฤ":W$(5,2)=""
2060 RETURN
2999 DATA 4,7,8,6,1
3000 DATA 1,28,0,1
3001 DATA 2,1,3,1
3002 DATA 1,14,3,1
3003 DATA 1,23,3,2
3004 DATA 4,28,3,1
3005 DATA 2,1,7,2
3006 DATA 1,3,10,1
3007 DATA 1,11,10,2
3008 DATA 1,18,10,2
3009 DATA 5,3,14,1
3010 DATA 5,11,14,2
3011 DATA 3,18,14,2
