0 DIMA(16*3),B(16*3),D(16*5),P(16*5),X,Y,T:M=16384:T=RND(-(PEEK(9)*256+PEEK(10)))
10 CLS
30 PRINT " DIGGER"
40 PRINT
50 PRINT"WRITTEN BY ANDREW (sparcie)"
80 SC=0:PRINT:INPUT "NAME";N$
100 CLS:LI=10:L=0:MX=RND(30):MY=1:X=15:Y=7:MS=227:TI=600
110 PRINT "INFORMATION","TOP SCORES"
120 PRINT CHR$(178);" = DIRT"
130 PRINT "X = YOU"
150 PRINT "# = ENEMYS"
160 PRINT "* = DIAMONDS"
170 PRINT "$ = MONEY BAG"
180 ST=0:FORI=1TO10:IFSC>TS(I)ANDST=0THENFORT=10TOI+1STEP-1:TS(T)=TS(T-1):NA$(T)=NA$(T-1):NEXTT:TS(I)=SC:NA$(I)=N$:ST=1
200 PRINT@I*32+15,RIGHT$(STR$(I)+") ",4);LEFT$(NA$(I),8);TS(I)
220 NEXTI:IFSC<>0THENSC=0:PRINT:INPUT "NAME";N$
230 Z$=IN$(1)
240 PRINT:PRINT "HIT A KEY TO BEGIN"
250 C$=INKEY$:IFC$=""THEN250
280 L=L+1:IFL>5THENL=5
290 SOUND1,1:SOUND50,1:SOUND100,1:SOUND150,1:SOUND200,1
300 FORI=16384TO16895:POKEI,178:NEXT
380 ND=5+L
390 NE=2+L
400 FORI=1TOND
410 P(I)=RND(15)*2
420 Q(I)=RND(7)*2
430 PRINT@Q(I)*32+P(I),"*";
460 NEXTI
470 FORI=1TONE
480 A(I)=RND(30)
490 B(I)=RND(15)
500 PRINT@B(I)*32+A(I),"#";
530 NEXTI
560 FORT=1TOTI:C$=INKEY$:IFC$=""THEN700
660 IFC$="W"THEND=1:GOTO700
670 IFC$="A"THEND=2:GOTO700
680 IFC$="Z"THEND=3:GOTO700
690 IFC$="S"THEND=4
700 POKEY*32+X+M,96:ONDGOTO701,703,705,707
701 Y=Y-1:IFY<0THENY=15
702 GOTO710
703 X=X-1:IFX<0THENX=31
704 GOTO710
705 Y=Y+1:IFY>15THENY=0
706 GOTO710
707 X=X+1:IFX>31THENX=0
710 POKEY*32+X+M,89:FORI=1TONE:ONRND(2)GOTO800:PRINT@B(I)*32+A(I)," ";
730 IFA(I)>XTHENA(I)=A(I)-1
740 IFA(I)<XTHENA(I)=A(I)+1
750 IFB(I)>YTHENB(I)=B(I)-1
760 IFB(I)<YTHENB(I)=B(I)+1
770 PRINT@B(I)*32+A(I),"#";:IFA(I)=XANDB(I)=YTHENLI=LI-1:SOUND100,2:GOSUB2100:IFLI=<0THENT=TI:GOTO1040
780 IFA(I)=MXANDB(I)=MYTHENSOUND1,1:SOUND50,1:SOUND23,1:SC=SC+50:GOSUB2100
800 NEXTI:IFND=0THENT=TI:GOTO1040
810 FORI=1TOND:PRINT@Q(I)*32+P(I),"*";:IFX=P(I)ANDY=Q(I)THENGOSUB2000
860 NEXTI:POKEY*32+X+M,88:IFMF=0THENMF=INT(RND(0)*2):NEXTT:GOTO1050
960 PRINT@MY*32+MX,CHR$(178);:IFMY=15THENGOSUB2010
970 MY=MY+1:PRINT@MY*32+MX,"$";:IFMY=YANDMX=XTHENSC=SC+100:SOUND200,3:GOSUB2010
1020 MS=MS-10:SOUNDMS,1:IFMS=<37THENMS=227
1040 NEXTT
1050 IFND=0THEN280
1060 GOTO100
2000 PRINT@Q(I)*32+P(I)," ";:P(I)=P(ND):Q(I)=Q(ND):ND=ND-1:SC=SC+10:SOUND15,1:RETURN
2010 MF=0:MX=RND(30):MY=0:RETURN
2100 PRINT@B(I)*32+A(I),CHR$(255);:SOUND200,1:PRINT@B(I)*32+A(I)," ";:IFNE>0THENB(I)=B(NE):A(I)=A(NE):NE=NE-1
2110 RETURN
