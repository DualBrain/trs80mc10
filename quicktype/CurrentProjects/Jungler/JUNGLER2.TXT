0 DIML1,L2,A(31),JU(30,15),PX,PY,CA(8,2),PL(8,2),SC,GG,AM,LV,RA,TR,BU,WA,RU,PL,CA,JE,AB,GU,GO,ID,UP$,DN$,RT$,LT$,MD:GOTO30
8 XM=0:RETURN
9 YM=0:RETURN
10 XT=0:RETURN
11 YT=0:RETURN
30 GOSUB210:GOSUB340
40 CLS:PRINT@6*32,;:M$="RAOUL BEGINS HIS QUEST":GOSUB138:PRINT:M$="DEEP IN THE JUNGLE...":GOSUB138
45 GOSUB405:CLS0:GOSUB180
49 REM MAIN LOOP
50 FORWI=1TO65000:GOSUB580:IFLV<>5THENGOSUB150
53 GOSUB460:NEXT
55 END
126 REM BLOOD
130 FORR=1TO200:X=RND(32)-1:IFA(X)<15THENPRINT@32*A(X)+X,"¿";:A(X)=A(X)+1
134 NEXT:RETURN
136 REM CENTER (M$)
138 PRINTTAB(INT((32-LEN(M$))/2));M$:RETURN
139 REM CHECKCONTACT
140 FORL1=1TO8:IFPX=CA(L1,1)ANDPY=CA(L1,2)THENMD=1:GOSUB700
147 IFPX=PL(L1,1)ANDPY=PL(L1,2)THENMD=2:GOSUB700
148 NEXT:RETURN
149 REM CHECKFORWIN
150 WI=1:FORL1=1TO8:IFCA(L1,1)<>0THENWI=0
157 IFPL(L1,1)<>0THENWI=0
158 NEXT:IFWI=1THENGOSUB520
159 RETURN
180 FORL1=1TO30:FORL2=1TO15:POKE16384+32*L2+L1,JU(L1,L2):NEXT:NEXT
190 POKE16384+32*PY+PX,RA
192 FORL1=1TO8:IFCA(L1,1)<>0THENPOKE16384+32*CA(L1,2)+CA(L1,1),CA
198 IFPL(L1,1)<>0THENPOKE16384+32*PL(L1,2)+PL(L1,1),PL
203 NEXTL1:PRINT@0,"SCORE"SC"   ";:PRINT@11,"AMMO"AM;
204 IFGG=1THENPRINT"YOU HAVE A GUN";:RETURN
206 PRINT"              ";:RETURN
209 REM INITIALIZE
210 RA=24:EM=32:TR=30:BU=35:WA=223:RU=219:PL=37:CA=0:JE=42:AB=15:GU=31:GO=36:ID=38
215 UP$="W":DN$="Z":RT$="S":LT$="A":LV=1:RETURN
225 L2=((PEEK(17024)AND1)*256+PEEK(17025))+16384:PRINT" ";:POKEL2,L1:RETURN
229 REM SUB INSTRUCTIONS
230 CLS
233 L1=RA:GOSUB225:PRINT": I AM JUNGLE RAOUL, THE"
238 PRINT"FEARLESS EXPLORER."
240 PRINT"YOU CAN MOVE ME AROUND USING"
245 PRINT"THE ARROW KEYS."
246 PRINT"PRESS THE space KEY AND I WILL"
251 PRINT"STAND STILL FOR A TURN, LETTING"
252 PRINT"THE ENEMIES MOVE."
255 PRINT"PRESS m, FOLLOWED BY AN ARROW"
257 PRINT"KEY, AND I WILL SWING MY TRUSTY"
258 PRINT"MACHETE, DESTROYING ANY BUSHES"
259 PRINT"OR CARNIVOROUS PLANTS IN MY WAY.";
261 PRINT"PRESS f, FOLLOWED BY AN ARROW"
264 PRINT"KEY, AND I WILL FIRE MY GUN (IF,";
265 PRINT"OF COURSE, I POSESS A GUN,)"
266 PRINT"WHICH WILL KILL ANY ENEMY."
268 GOSUB570
270 CLS
271 L1=TR:GOSUB225:PRINT": TREES (IMPASSABLE)"
275 L1=BU:GOSUB225:PRINT": BUSHES-- THESE CAN BE"
277 PRINT"HACKED AWAY WITH ONE SWIPE OF"
278 PRINT"OF MY MACHETE."
279 L1=WA:GOSUB225:PRINT": WALLS OR RUINS (IMPASSABLE)"
283 L1=GU:GOSUB225:PRINT",";:L1=AB:GOSUB225
287 PRINT": GUN & AMMO BOX (3 SHOTS)."
290 L1=GO:GOSUB225:PRINT",";:L1=JE:GOSUB225
294 PRINT": GOLD & JEWELS - BONUS PTS"
297 L1=PL:GOSUB225
299 PRINT": CARNIVOROUS PLANTS. THESE"
300 PRINT"CAN BE KILLED WITH EITHER A"
301 PRINT"MACHETE OR A GUNSHOT."
304 L1=CA:GOSUB225:PRINT": CANNIBALS. THEY CAN BE"
307 PRINT"KILLED ONLY BY A GUNSHOT."
310 L1=ID:GOSUB225:PRINT": THE SAPPHIRE AMPERSAND--"
313 PRINT "THE OBJECT OF YOUR QUEST..."
314 GOSUB570
316 CLS
318 M$="HINTS:":GOSUB138
321 PRINT"ONCE YOU HAVE KILLED ALL OF THE"
322 PRINT"ENEMIES ON A LEVEL, YOU WILL"
323 PRINT"MOVE ON TO THE NEXT LEVEL."
325 PRINT"YOU ONLY HAVE ONE LIFE"
326 PRINT"-- MAKE IT COUNT."
327 PRINT"AMMO NOT USED ON ONE LEVEL"
328 PRINT"CARRIES OVER INTO THE NEXT."
329 PRINT"DON'T WASTE IT, BECAUSE YOU'LL"
330 PRINT"NEED IT."
331 PRINT"DON'T WASTE BULLETS ON THE"
332 PRINT"PLANTS-- THEY ARE EASILY "
333 PRINT"DISPATCHED WITH YOUR MACHETE."
335 GOSUB570:RETURN
340 CLS:PRINT
350 M$="/////////"+CHR$(92)+CHR$(92)+CHR$(92)+CHR$(92)+CHR$(92)+CHR$(92)+CHR$(92)+CHR$(92)+CHR$(92):GOSUB138
352 M$="//////////"+CHR$(92)+CHR$(92)+CHR$(92)+CHR$(92)+CHR$(92)+CHR$(92)+CHR$(92)+CHR$(92)+CHR$(92)+CHR$(92):GOSUB138
354 M$=" ! J U N G L E    !":GOSUB138
356 M$=" !                !":GOSUB138
358 M$=" !      R A O U L !":GOSUB138
360 M$=" ------------------":GOSUB138:PRINT
363 M$="IN":GOSUB138
366 M$="THE QUEST":GOSUB138
368 M$="FOR THE":GOSUB138
370 M$="SAPPHIRE AMPERSAND (&)":GOSUB138:PRINT
372 M$="(P)LAY (I)NSTRUCTIONS (Q)UIT":GOSUB138
396 A$=INKEY$:IFA$=""THEN396
397 IFA$="P"THENCLS:RETURN
398 IFA$="I"THENGOSUB230:GOTO340
399 IFA$="Q"THENEND
400 GOTO396
402 REM MAKELEVEL
405 FORL1=1TO30:FORL2=1TO15:JU(L1,L2)=EM:NEXT:NEXT
415 FORL1=1TO8:PL(L1,1)=0:PL(L1,2)=0:CA(L1,1)=0:CA(L1,2)=0:NEXTL1
416 IFLV<>1THEN435
418 FORL1=1TO100:JU(RND(30),RND(15))=TR:JU(RND(30),RND(15))=BU:IFL1>96THENJU(RND(30),RND(15))=GO
422 NEXT
423 PX=4:PY=4
425 JU(4,4)=EM
426 FORL1=1TO5:PL(L1,1)=RND(20)+10:PL(L1,2)=RND(15):JU(PL(L1,1),PL(L1,2))=0:NEXT
433 RETURN
435 C1=1:P1=1:FORL1=1TO15:READL$:FORL2=1TO30:C$=MID$(L$,L2,1)
439 IFC$="W"THENJU(L2,L1)=WA
440 IFC$="R"THENJU(L2,L1)=RU
441 IFC$="T"THENJU(L2,L1)=TR
442 IFC$=" "THENJU(L2,L1)=EM
443 IFC$="B"THENJU(L2,L1)=BU
444 IFC$="G"THENJU(L2,L1)=GU
445 IFC$="$"THENJU(L2,L1)=GO
446 IFC$="A"THENJU(L2,L1)=AB
447 IFC$="J"THENJU(L2,L1)=JE
448 IFC$="I"THENJU(L2,L1)=ID
449 IFC$="*"THENPX=L2:PY=L1:JU(L2,L1)=EM
450 IFC$="C"THENCA(C1,1)=L2:CA(C1,2)=L1:C1=C1+1:JU(L2,L1)=EM
452 IFC$="P"THENPL(P1,1)=L2:PL(P1,2)=L1:P1=P1+1:JU(L2,L1)=EM
454 NEXTL2:NEXTL1:RETURN
459 REM MOVEENEMIES
460 FORL1=1TO8:IFPL(L1,1)=0THEN483
461 IFRND(0)>.4THENXM=RND(3)-2:YM=RND(3)-2:IFXM<>0ANDYM<>0THENONRND(2)GOSUB8,9
468 NX=PL(L1,1)+XM:NY=PL(L1,2)+YM:IFNX<1ORNX>30ORNY<1ORNY>15THEN483
470 IFJU(NX,NY)<>EMTHEN483
472 FORL2=1TO8:IF(L1<>L2)ANDNX=PL(L2,1)ANDNY=PL(L1,2)THEN483
474 NEXT:POKE16384+32*PL(L1,2)+PL(L1,1),EM:PL(L1,1)=NX:PL(L1,2)=NY:POKE16384+32*NY+NX,PL
483 NEXT
484 FORL1=1TO8:XT=0:YT=0:IFCA(L1,1)=0THEN509
487 IFPX=CA(L1,1)THENXT=0
488 IFPX<CA(L1,1)THENXT=-1
489 IFPX>CA(L1,1)THENXT=1
490 IFPY=CA(L1,2)THENYT=0
491 IFPY<CA(L1,2)THENYT=-1
492 IFPY>CA(L1,2)THENYT=1
493 IFXT<>0ANDYT<>0THENONRND(2)GOSUB10,11
496 NX=CA(L1,1)+XT:NY=CA(L1,2)+YT:IFJU(NX,NY)<>EMTHEN509
500 FORL2=1TO8:IF(L1<>L2)ANDNX=CA(L2,1)ANDNY=CA(L2,2)THEN509
501 NEXTL2
502 POKE16384+32*CA(L1,2)+CA(L1,1),EM:CA(L1,2)=NY:CA(L1,1)=NX:POKE16384+32*NY+NX,CA
509 NEXTL1:GOSUB140:RETURN
515 REM NEXTLEVEL
520 CLS:PRINT@6*32,;:LV=LV+1:ONLV-1GOTO525,535,545,555
525 M$="RAOUL HEADS EASTWARD,":GOSUB138
528 PRINT:M$="TOWARD THE FORBIDDING SOUND":GOSUB138:PRINT:M$="OF BEATING DRUMS...":GOSUB138:GOTO565
535 M$="RAOUL EMERGES INTO A CANNIBAL":GOSUB138:PRINT:M$="VILLAGE, CRUDE HUTS SURROUNDING":GOSUB138
539 PRINT:M$="A WEATHERED STONE ALTAR...":GOSUB138:GOTO565
545 M$="RAOUL EMERGES FROM THE VILLAGE":GOSUB138:PRINT:M$="TO FIND THE ENTRANCE TO WHAT MUST":GOSUB138
548 PRINT:M$="SURELY BE THE FINAL RESTING PLACE":GOSUB138:PRINT:M$="OF THE FABLED SAPPHIRE AMPERSAND...":GOSUB138:GOTO565
555 M$="RAOUL ENTERS THE ANCIENT TEMPLE,":GOSUB138:PRINT:M$="OBLIVIOUS OF THE DANGERS WITHIN.":GOSUB138
559 PRINT:M$="THE AMPERSAND IS CLOSE AT HAND...":GOSUB138
565 GOSUB405:CLS0:GOSUB180:RETURN
570 PRINT@488,"PRESS ANY KEY...";
572 I$=INKEY$:IFI$=""THEN572
573 RETURN
574 REM PLAYERMOVES
580 V=0:M=0
585 A$=INKEY$:IFA$=""THEN585
587 IFA$=UP$THENV=1:M=1:XM=0:YM=-1
588 IFA$=DN$THENV=1:M=1:XM=0:YM=1
589 IFA$=RT$THENV=1:M=1:XM=1:YM=0
590 IFA$=LT$THENV=1:M=1:XM=-1:YM=0
591 IFA$=" "THENV=1:M=0:XM=0:YM=0
592 IFA$="M"THENV=1:GOSUB630
593 IFA$="F"THENV=1:GOSUB655
596 IFV=0THEN585
597 IFM=0THENRETURN
600 NX=PX+XM:NY=PY+YM:NS=JU(NX,NY):IFNX<1ORNX>30ORNY<1ORNY>15ORNS=TRORNS=BUORNS=WAORNS=RUTHENV=0:RETURN
606 POKE16384+32*PY+PX,32:PX=NX:PY=NY:POKE16384+32*PY+PX,RA:GOSUB140
613 IFJU(PX,PY)=GOTHENSC=SC+10+RND(15):PRINT@5,SC;
615 IFJU(PX,PY)=JETHENSC=SC+20+RND(10):PRINT@5,SC;
617 IFJU(PX,PY)=GUTHENGG=1:PRINT19,"YOU HAVE A GUN";:JU(PX,PY)=AB
619 IFJU(PX,PY)=ABTHENAM=AM+3:PRINT@15,AM;
621 IFJU(PX,PY)=IDTHEN750
623 JU(PX,PY)=EM:RETURN
627 REM MACHETE
630 A$=INKEY$:IFA$=""THEN630
632 V=0
633 IFA$=UP$THENV=1:XC=0:YC=-1
634 IFA$=DN$THENV=1:XC=0:YC=1
635 IFA$=RT$THENV=1:XC=1:YC=0
636 IFA$=LT$THENV=1:XC=-1:YC=0
637 CX=PX+XC
638 CY=PY+YC
639 IFCX<1ORCX>30THENV=0
640 IFCY<1ORCY>15THENV=0
641 IFV=0THENV=1:RETURN
642 IFJU(PX+XC,PY+YC)=BUTHENJU(PX+XC,PY+YC)=0:POKE16384+32*(PY+YC)+PX+XC,32
647 FORL1=1TO8:IFCX=PL(L1,1)ANDCY=PL(L1,2)THENPOKE16384+32*PL(L1,2)+PL(L1,1),32:PL(L1,1)=0:SC=SC+30:PRINT@5,SC;
650 NEXT:V=1:RETURN
654 REM SHOOT
655 IFGG=0ORAM=0THENRETURN
656 AM=AM-1:PRINT@15,AM;
660 A$=INKEY$:IFA$=""THEN660
664 V=0
665 IFA$=UP$THENV=1:XS=0:YS=-1
666 IFA$=DN$THENV=1:XS=0:YS=1
667 IFA$=RT$THENV=1:XS=1:YS=0
668 IFA$=LT$THENV=1:XS=-1:YS=0
669 IFV=0THEN660
670 SX=PX:SY=PY:SA=0
675 SX=SX+XS:SY=SY+YS
676 IFSX=0ORSX=31ORSY=0ORSY=16THEN690
677 IFJU(SX,SY)<>EMTHEN690
678 POKE16384+32*SY+SX,43
679 FORL1=1TO8:IFSX=CA(L1,1)ANDSY=CA(L1,2)THENCA(L1,1)=0:POKE16384*SY+SX,32:SA=50:GOTO690
685 IFSX=PL(L1,1)ANDSY=PL(L1,2)THENPL(L1,1)=0:POKE16384*SY+SX,32:SA=20:GOTO690
687 NEXT:POKE16384+32*SY+SX,32:GOTO675
690 IFSA<>0THENSC=SC+SA:PRINT@5,SC;
698 RETURN
700 GOSUB130
710 PRINT@480,"PRESS ANY KEY...";
715 I$=INKEY$:IFI$=""THEN715
715 CLS:PRINT@32*5,;:ONMDGOTO717,720
717 M$="RAOUL IS SAVAGELY BEHEADED,":GOSUB138
718 M$="CHOPPED INTO PIECES, AND LATER":GOSUB138
719 M$="EATEN BY THE INDIGENT CANNIBALS.":GOSUB138:GOTO724
720 M$="RAOUL IS STRANGLED AND ABSORBED":GOSUB138
723 M$="BY A FIERCE CARNIVOROUS PLANT.":GOSUB138
724 M$="HIS BONES LITTER THE GROUND, A":GOSUB138
725 M$="SINISTER TESTAMENT TO HIS":GOSUB138
726 M$="FOOLISHNESS.":GOSUB138
730 M$="YOUR FINAL SCORE:"+STR$(SC)+" PTS.":GOSUB138
740 PRINT:PRINT "THANKS FOR PLAYING!"
741 END
743 REM SUB YOUWIN
750 CLS
756 PRINT"YOU'VE DONE IT!"
757 PRINT"YOU HAVE BRAVED THE TERRORS"
758 PRINT"OF THE JUNGLE, AND EMERGED"
759 PRINT"TRIUMPHANT, WITH THE "
760 PRINT"FABLED SAPPHIRE AMPERSAND"
761 PRINT"IN HAND."
763 PRINT"--------------------"
765 PRINT"- CONGRATULATIONS! -"
767 PRINT"--------------------"
770 PRINT"YOUR FINAL SCORE:";SC;"PTS."
794 PRINT"THANKS FOR PLAYING..."
795 END
1050 DATA "TTBTTTTTBTTT  C   TTBTBBTBBBBB"
1063 DATA "TTTT P TTBT     TTTTTTTTTBBBJB"
1064 DATA "TBTP G  TT     TTT     TTTBBBB"
1065 DATA "TTT    PTT    TTT $$  TTBTBBBB"
1066 DATA "TBTT P TT    TTTT   TTTT   BBB"
1067 DATA "TTBT  TT      TT   TTTC   BBBB"
1068 DATA "TTT  TT                  BBBBB"
1069 DATA "     T     TBBBBB    T     BBB"
1070 DATA " *       TTTBBBBTBC     T BBBB"
1071 DATA " TTTTT    TT        B BBB   BB"
1072 DATA " TTTTT   BBBBTT     TRWBWTB   "
1073 DATA " BTTT   BBBTTTT    TBR  WBTB  "
1074 DATA " B     BBTTTTT     BTW ARTB   "
1075 DATA "   $ TTTTTTTTTTTT   TRWRWB    "
1076 DATA "   TTTTTTTTTTTTTTTT  BBBB     "
1077 DATA "TTTBTBTTTBTTTTTTTTBTTTTTBTTTTT"
1078 DATA "TBBTTTTTTTTTT     TTTTBTTTTBTT"
1079 DATA "TTTTTT           BB    TTTTTBT"
1080 DATA "TT     BB       BAAB      PTTT"
1081 DATA "T  P  B$$B      BAAB        TT"
1082 DATA "T     BCC         B   BB     T"
1083 DATA "       BB     WW     BCAB     "
1084 DATA "*             WW      CCB    T"
1085 DATA "T        B            BB    TT"
1086 DATA "T       BA B    B           TT"
1087 DATA "BT      BJCB   BC B     P  TTT"
1088 DATA "TTT      BB    B$CB       TTTT"
1089 DATA "TBTTTT          BB      TTTBTT"
1090 DATA "TTTBBTTTTT   P     TTTTTTBTTBT"
1091 DATA "TTBTTBTTTTTTTTTTTTTBBTBTTTTBBT"
1092 DATA "TTTTTTTTTTTTTTTTBBBB  C   BT W"
1093 DATA "TTTTTTTTT  C  TTBBB      BT WW"
1094 DATA "TTTTTTT      TTTT   C B B TWW "
1095 DATA "TTTTTT      TTTT        B WW$T"
1096 DATA "TTT          TT     WWWWWWW BP"
1097 DATA "TT     TTT       C  WRWRWW PB "
1098 DATA "TT*   TTTT      BBBTWWWWW TP  "
1099 DATA "TTT     TTT    BBB BB  B  B  $"
1100 DATA "TTT             B BTWWWWW  T B"
1101 DATA "TTTT   TTT      C B WRWRWWTT  "
1102 DATA "TTTTT   TTTT        WWWWWWWB P"
1103 DATA "TTTTTTT    TTT       B   BWW$T"
1104 DATA "TTTTTTTTTT           B   TBWWB"
1105 DATA "TTTTTTTTTTTTTTBBBC     B  BTWW"
1106 DATA "TTTTTTTTTTTTTTTTBBBB     B  BW"
1107 DATA "BTBWWWWWWWWWWWWWWWWWWWWWWWWTBT"
1108 DATA "TBW     W$$$JW  C   W      WTB"
1109 DATA "TW   C   $$$JW      W       WT"
1110 DATA "WWW WWWWWWWWWWW W WWWWWWWWWWWW"
1111 DATA "W        P   W  W  WW   P    W"
1112 DATA "W  C  W      W  W WW     WWWWW"
1113 DATA "WWWWWWWWW  WWW WW W  C   W   W"
1114 DATA "*            W  W    C     I W"
1115 DATA "WWW WWWWW  WWW WWWW  C   W   W"
1116 DATA "W     W  AA  W    WW     WWWWW"
1117 DATA "W  C  W      W P   WW   P    W"
1118 DATA "WWWW WWWWWWWWWW  WWWWWWWWWWWWW"
1119 DATA "TW    P               C     WT"
1120 DATA "BTW     W    W      W      WBT"
1121 DATA "TBBWWWWWWWWWWWWWWWWWWWWWWWWTTB"
2000 REM 1996 ZACK JOHNSON
