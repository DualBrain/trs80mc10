5 REM (C) AUTOMATED SIMULATIONS 1981
10 CLEAR300:REM DEFINTD,H-Z
12 J=0:I=0:U=0:U1=1:U2=2:U3=3:MQ=0:XX=0:YY=0:XA=0:YA=0:DIMCX(11),CY(11),MQ(10),MP(10),XM(10),YM(10),MF(10),PA(20)
13 DIMOC(9,9),PW(15),IX(5),IY(5),MI(4),MN(10),JX(5),JY(5),XE(5),YE(5),F$(4),MK(12),B(3),A(4,13),H$(3),W$(3),NP(34)
14 DIMRS$(26),MN$(14),CN$(4),HH$(7),A$(9)
15 Q1=8
17 CLS:KA=-1*(65536-47285):UL=KA+1040:UA=UL+Q1:UP=UA+Q1
18 UD=UP+Q1:UC=UD+Q1:UR=UC+Q1:UI=UR+Q1:UW=UI+Q1:UV=UW+Q1:UB=UV+Q1:TS=KA+1192:TD=KA+2
19 C%=15359:BL$=CHR$(30):MF(0)=3
23 DATA10,1,3,3,4,1,2,0,1,.6,1,1,.6,1,0,1,-.6,.6,-1,0,-1,-.6,-1,-1,-.6,-1,0
24 DATA-1,.6,-.6,1,3,1,10,1,2,1,0,33,1,2,10,0,15,1,0,5,0,10,1,0
25 FORI=UTOU2:READB(I):NEXT:FORI=U1TO4:READMI(I):NEXT:FORI=UTO11:READCX(I),CY(I):NEXT:FORJ=0TO4
26 FORI=0TO3:READA(I,J):NEXT:NEXT
27 DATA2,1,1,4,1,6,2,8,8,4,8,6,NORTH,NORMAL,CRITICAL,EAST,HUNGRY,HURT,SOUTH,FAMISHED,SCRATCHED,WEST,RAVENOUS,HEALTHY
28 FORI=UTO5:READXE(I),YE(I):NEXT:FORI=1TO4:READF$(I),H$(I-1),W$(I-1):NEXT
29 DATA-1,0,7,0,0,1,0,-7,1,0,-7,0,0,-1,0,7,-1,0,7,0,0,1,0,-7:FORI=UTO5:READIX(I),IY(I),JX(I),JY(I):NEXT:GOSUB14000:GOTO900
30 I1=U1:GOSUB31:GOSUB100:RETURN
31 NS=MQ(MQ)+9:XX=XM:YY=YM:RETURN
32 FORM=U1TOR:SOUNDM,1:NEXT:RETURN
35 GOSUB31:I1=U1:IFXX=XAANDYY=YATHENGOSUB205:RETURN
36 NS=PEEK(BA+XX+YY*8-8)+U1:GOSUB100
37 RETURN
43 IFK=UTHENSET(I/2,(YY-L)*.33):SET(I/2,(YY+L)*.33):RETURN
44 RESET(I/2,(YY-L)*.33):RESET(I/2,(YY+L)*.33):RETURN
46 XD=XA-XT:YD=YA-YT:D(K)=XD*XD+YD*YD
47 RETURN
49 PRINT@816,BL$;:RETURN
50 PRINT@V,BL$;:RETURN
51 PRINT@432,BL$;:RETURN
52 GOSUB50:PRINT@V/2,"CAN'T";:RETURN
55 PRINT@880,"HEAD TILT:";:SET(110,45):SET(111,45):XX=110+U2*HT:YY=42+ABS(HT):IFK=U1THENSET(XX,YY):SET(XX+U1,YY):RETURN
56 RESET(XX,YY):RESET(XX+U1,YY):RETURN
57 PC!=PC!+MK(5)/10:IFPC!>MK(U2)THENPC!=MK(U2)
58 GOTO88
60 H=U:GOSUB390:P!=MK(U1)/(4+D2):IFD2>DDORMA=UORP!<.3*RND(U)THENRETURN
61 IFD2<=2THENH=U1:RETURN
62 I=4+4*PEEK(UI+M1):IFXD=UTHENA=U:B=XA:GOTO67ELSEIFYD=UTHENA=U:B=YA:GOTO64
63 IFXD>YDTHENA=YD/XD:B=YA-XA*AELSEA=XD/YD:B=XA-YA*A:GOTO67
64 IFXD>UTHENXL=XM+U1:XU=XA-U1ELSEXL=XA+U1:XU=XM-U1
65 FORX=XLTOXU:Y=A*X+B+.5:IFPEEK(BA+X+Y*8-8)>ITHENRETURN
66 NEXT:H=U1:RETURN
67 IFYD>UTHENYL=YM+U1:YU=YA-U1ELSEYL=YA+U1:YU=YM-U1
68 FORY=YLTOYU:X=A*Y+B+.5:IFPEEK(BA+X+Y*8-8)>ITHENRETURN
69 NEXT:H=U1:RETURN
70 GOSUB500:IFL=UGOTO70ELSEJ=ASC(C$)-48:IFJ<UORJ>9GOTO70ELSERETURN
72 IFGB>UTHENPRINT@560,"PAW FULL ";:RETURNELSEPRINT@560,"PAW EMPTY";:RETURN
74 PRINT@48,BL$;:PRINT@48,"WIND:"F$(WD);:RETURN
75 PRINT@496,BL$;:K=INT(HU/25):IFK>U3THENK=U3
76 PRINT@496,H$(K);:RETURN
77 GOSUB500:IFL=UTHEN77ELSERETURN
78 PRINTCHR$(23);:FORI=1TO30:NEXTI:PRINTCHR$(28);:RETURN
80 PRINT@240,BL$;:PRINT@240,"HDG: "F$(KF);:RETURN
81 K=K+U1:IFK>4THENK=U1:RETURN
82 RETURN
88 PRINT@112,BL$;:K=INT(3.99*PC!/MK(U2)):PRINT@112,W$(K);:RETURN
100 II=C%+128*(8-YY)+6*XX-5
110 POKEKA+1584,PEEK(VARPTR(II)):POKEKA+1588,I1:POKEKA+1585,PEEK(VARPTR(II)+1):J=USR2(TS+NS*12-11):RETURN
195 RETURN
200 IFIN>UTHENRETURNELSEXX=XA:YY=YA
205 NS=17+MT:IFFL<UTHENNS=26
210 I1=U1:GOSUB100:RETURN
250 IFOC(XA,YA)>UTHENNS=MQ(OC(XA,YA))+9ELSENS=PEEK(BA+XA+YA*8-8)+U1
260 XX=XA:YY=YA:I1=U1:GOSUB100:RETURN
330 I=(RND(U3)-U2)*U3+XX:J=(RND(U3)-U2)*U3+YY:RETURN
340 GOSUB50:PRINT@V,"GOT 'EM";
342 CD=CD+PEEK(UD+MQ(MQ)):ND=ND+U1
345 XM=XM(MQ):YM=YM(MQ):GOSUB35:OC(XM,YM)=U
350 IFMQ=NBTHEN360
355 FORI2=MQTONB-U1:MQ(I2)=MQ(I2+1):MP(I2)=MP(I2+1):XM(I2)=XM(I2+U1):YM(I2)=YM(I2+U1):MF(I2)=MF(I2+U1):OC(XM(I2),YM(I2))=I2:NEXT
360 NB=NB-1:RETURN
380 H=U:IFXT>8ORXT<U1THENGOSUB46:RETURN
382 IFYT>8ORYT<U1THENGOSUB46:RETURN
384 H=U1:IFXT=XAANDYT=YATHENXT=XM:YT=YM:IFMA>UTHENJV=MV
386 RETURN
390 XD=XA-XM:YD=YA-YM:D2=XD*XD+YD*YD:RETURN
450 XX=12*XX-6:YY=51-6*YY:FORK=UTOU1:I=XX+U1:L=U1:GOSUB43:I=XX-U1:GOSUB43:I=XX+U2:L=U:GOSUB43:I=XX-U2:GOSUB43
460 I=XX+U2:L=U2:GOSUB43:I=XX-U2:GOSUB43:I=XX+4:L=U:GOSUB43:I=XX-4:GOSUB43:NEXTK:RETURN
500 LE=SE:FORM=U1TOLE
510 C$=INKEY$:IFLEN(C$)>UANDC$>" "ANDC$<"["THENL=U1:RETURN
520 NEXT:L=U:RETURN
600 IFRND(U)>.2THENRETURN
605 IFRND(U)>.8THENWD=RND(4):GOSUB74
610 FORI=U1TO8:FORN=U1TO8:K=BA+I+8*N-8:IFPEEK(K)<8THEN650
620 IFRND(U)>.5THENXX=I+IX(WD):YY=N+IY(WD):IFXX>UANDXX<9ANDYY>UANDYY<9THENL=BA+XX+8*YY-8:IFPEEK(L)>4ANDPEEK(L)<8THENDB=DB+B(PEEK(L)-5):POKEL,8:NS=9:GOSUB100:GOTO650
630 IFRND(U)>.7THENPOKEK,4:NS=5:XX=I:YY=N:GOSUB100
650 NEXT:NEXT:GOSUB200:RETURN
900 RANDOM:CLS:PRINTCHR$(23):PRINT@136,"CRUSH, CRUMBLE & CHOMP":PRINT@264,"THE MOVIE MONSTER GAME"
910 PRINT@598,"(C)  1981":PRINT@714,"AUTOMATED SIMULATIONS"
920 PRINT@908,"HIT ANY KEY TO BEGIN";
930 FORJ=1TO20:I=RND(0):NEXT:A$=INKEY$:IFLEN(A$)=UTHENPRINT@908,STRING$(20," ");:FORI=1TO200:NEXT:GOTO920
995 CLS:BC$="NESWU":V=304
1000 INPUT"DO YOU HAVE SOUND";A$:IFLEFT$(A$,1)="Y"THENIS=1ELSEIS=U
1001 WD=2:T=22:SE=67:SP=1:DEFUSR1=KA+1506:DEFUSR2=KA+1579
1002 GB=U:HU=U:FL=1:PL=PA!:LV=U:KF=U1:INPUT"DO YOU WANT TO RESTORE A GAME IN PROGRESS";A$:IFLEFT$(A$,1)="Y"THENGOSUB13500:GOTO1605
1003 INPUT"SPEED (FAST,SLOW)";A$:IFLEFT$(A$,1)="S"THENSP=2:SE=3*SE
1004 INPUT"DO YOU WANT TO PLAY ONE OF THE FEATURE CREATURES (F), OR DO YOU WANT TO GROW YOUR OWN (G)";A$:IFLEFT$(A$,1)="G"THENGOSUB2000:GOTO1015
1005 CMD"T":FORI=1TO6:PRINTI;MN$(I):NEXT:INPUT"ENTER MONSTER NUMBER";MT:IFMT<1ORMT>6THEN1005
1010 GOSUB1980
1015 MM=MK(6):PA!=MK(7):PL=PA!:PC!=MK(U2)
1020 CLS:PRINT"1 BALANCED
2 KILLER
3 COMBAT
4 DESTRUCTION
5 SURVIVAL":INPUT"ENTER OBJECTIVE NUMBER";SO:SO=SO-1:IFSO<UORSO>4THEN1020
1030 OA=U:IFSO=U2THENOA=50
1136 REM SOUNDEX BY ROXTON BAKER
1140 FORI=1TO4:PRINTI;CN$(I):NEXT:INPUT"ENTER CITY NUMBER";SN:IFSN>4THEN1140
1150 OPEN"I",1,"TMPGEO":INPUT#1,LG,NS
1155 FORI=1TONS:FORJ=UTO15:INPUT#1,K:IFI=SNTHENPW(J)=K
1156 NEXT:INPUT#1,J,K:INPUT#1,L,M:IFMK(8)>UTHENJ=L:K=M
1157 INPUT#1,L:IFI=SNTHENXA=J:YA=K:KR=L
1158 NEXT
1160 POKEMC+4,64:FORL=1TOLG:A$="":INPUT#1,A$
1170 FORJ=UTO15:IFPW(J)=LTHENK=64*J+17:GOSUB1950:POKEKA+J,ASC(MID$(A$,65,U1))-59
1180 NEXT:NEXT
1190 CLOSE:FORI=UTO15:PW(I)=5:NEXT
1605 AN$="MONFILE"
1610 OPEN"I",1,AN$
1620 L=152:POKEMC+4,L:K=1041:POKEMC+10,59
1640 FORI=1TO3
1642 A$="":INPUT#1,A$:IFLEN(A$)=LGOTO1650
1644 PRINT"*** POSSIBLE DATA READ ERROR - BLOCK LENGTH ="LEN(A$);" ***"
1650 GOSUB1950
1655 IFI>1THENL=234
1660 POKEMC+4,L:POKEMC+10,0
1665 NEXTI:POKEMC+10,59
1666 CLOSE1
1670 ONERRORGOTO0
1690 FORI=1579TO1620:READJ:POKEKA+I,J:NEXT:POKEKA+1508,10
1900 IFF=UTHENGOSUB2600
1905 GOTO5000
1950 POKEMC+6,PEEK(VARPTR(A$)+1):POKEMC+7,PEEK(VARPTR(A$)+2)
1960 IFMC#=1THENMC!=USR0(KA+K)ELSEMC!=USR(KA+K)
1970 K=K+L:RETURN
1980 OPEN"I",1,"TMPMON":INPUT#1,N:FORJ=1TON:INPUT#1,O$:INPUT#1,OP$:FORI=1TO20:INPUT#1,PA(I):NEXT:FORI=0TO12:INPUT#1,MK(I):NEXT:FORI=0TO34:INPUT#1,NP(I):NEXT:IFJ=MTTHENJ=N
1990 NEXT:CLOSE:RETURN
2000 MG=6:PRINT"SELECT A CARCASS FOR YOUR MONSTER":FORI=1TO8:PRINTI;MN$(MG+I):NEXT:INPUTMT:IFMT<1ORMT>8THEN2000
2010 GOSUB1980:O$="":CC=NP(0):CLS:GOSUB2900
2020 FORI=1TO20:A$=MID$(OP$,I,1):N=ASC(A$)-64:IFNP(I)=0THENO$=O$+A$:GOTO2060
2030 IFNP(I)>CCTHENO$=O$+"R":GOTO2060
2040 PRINT"DO YOU WANT ";:IFI<13ORI>14ANDI<>18THENPRINT"TO ";
2045 PRINTRS$(N);"?  COST IS"NP(I);"CRUNCH CREDITS"
2050 INPUTB$:IFLEFT$(B$,1)="Y"THENO$=O$+A$:CC=CC-NP(I):GOSUB2900ELSEO$=O$+"R"
2060 NEXT:B$=""
2070 CLS:GOSUB2900:MK(5)=0:IFCC<NP(21)THEN2110
2080 PRINT"SELECT THE DEGREE TO WHICH YOUR MONSTER REGENERATES DAMAGE
0 NONE
1 A LITTLE ("NP(21);"CC)
2 A LITTLE MORE ("NP(22);"CC)
3 LOTS ("NP(23);"CC)
4 A WHOLE BUNCH ("NP(24);"CC)";:INPUTN:IFN<0ORN>4THEN2080
2090 IFN=0THEN2110ELSEIFNP(20+N)>CCTHEN2080
2100 RP(2)=2:RP(3)=5:RP(4)=10:RP(5)=20:MK(5)=RP(N):CC=CC-NP(20+N):GOSUB2900
2110 CLS:GOSUB2900:PRINT"SELECT YOUR STRENGTH
WEAK - YOU GET BACK"NP(25);"CRUNCH CREDITS
NORMAL, OR
STRONG - COSTS YOU"NP(25);"CRUNCH CREDITS"
2120 INPUTA$:IFLEFT$(A$,1)="W"THENCC=CC+NP(25):MK(3)=.9*MK(3):GOTO2160
2130 IFLEFT$(A$,1)<>"S"THEN2160
2140 IFCC<NP(25)THEN2160
2150 CC=CC-NP(25):MK(3)=1.1*MK(3)
2160 MK(4)=0:IFCC<NP(26)THEN2200
2170 CLS:GOSUB2900:PRINT"HOW THICK IS YOUR HIDE?
0 OH-SO-TENDER (0)
1 THIN ("NP(26);"CC)
2 SO SO ("NP(27);"CC)
3 TOUGH ("NP(28);"CC)
4 ROCK HARD ("NP(29);"CC)
5 LIKE IRON ("NP(30);"CC)":INPUTN
2180 IFN<1THEN2200ELSEIFN>5ORNP(N+25)>CCTHEN2170
2190 MK(4)=N:CC=CC-NP(N+25)
2200 MK(8)=0:IFCC<NP(31)THEN2230ELSEIFNP(31)=0THENMK(8)=1:GOTO2230
2210 CLS:GOSUB2900:PRINT"WOULD YOU LIKE TO SWIM? IT COSTS"NP(31);"CRUNCH CREDITS.":INPUTA$
2220 IFLEFT$(A$,1)="Y"THENMK(8)=1:CC=CC-NP(31)
2230 MK(10)=0:IFMID$(O$,6,1)="F"THENNP(32)=2*NP(32):NP(33)=2*NP(33)
2235 IFCC<NP(32)THEN2300
2240 CLS:GOSUB2900:PRINT"DO YOU WANT TO LEAVE RADIOACTIVE RUBBLE IN YOUR WAKE (COST:"NP(32);")";:INPUTA$
2250 IFLEFT$(A$,1)="Y"THENMK(10)=4:CC=CC-NP(32):GOTO2300
2260 IFCC<NP(33)THEN2300
2270 PRINT"HOW ABOUT A NICE TRAIL OF FIRE (COST:"NP(33);")";:INPUTA$
2280 IFLEFT$(A$,1)="Y"THENMK(10)=8:CC=CC-NP(32)
2300 MK(2)=NP(34)+CC:MK(1)=MK(2)/10+MK(3)/5
2310 MK(9)=0:IFMK(8)=0THENMK(9)=1
2400 INPUT"WHAT IS YOUR MONSTER'S NAME";MN$(MT+MG):IFLEN(MN$(MT+MG))>12THENMN$(MT+MG)=LEFT$(MN$(MT+MG),12)
2500 RETURN
2600 CLS:PRINT@896,"E P Y X   P R E S E N T S :":DU=311:PT=25:FORI=30TO130STEP2:GOSUB32:K=99-I:FORJ=1TOK:NEXT:NEXT:GOSUB2700:PRINT"

"TAB(INT((56-LEN(A$)-LEN(B$))/2))A$;"   VS   "B$:FORJ=1TO2500:NEXT:PRINT"
"
2610 XX=6:YY=1:NS=17+MT:PRINT"
         STARRING:

"TAB(5);"* *   Y O U   * *

AS   "A$:GOSUB2800:IFMID$(O$,6,1)="F"THENNS=26:XX=8:GOSUB2800
2612 GOSUB2800
2618 PRINT"


        CO-STARRING:";:XX=5:FORNS=10TO17:PRINT"

"HH$(NS-10):GOSUB2800
2619 NEXT
2620 XX=6:NS=1:PRINT"


         AND FEATURING:":PRINT"

WATER":GOSUB2800
2630 XX=4:NS=2:PRINT"
BRIDGES":GOSUB2800
2640 XX=6:NS=4:PRINT"
PARKS":GOSUB2800
2650 XX=4:NS=7:PRINT"
BUILDINGS":GOSUB2800
2660 YY=1:XX=6:NS=8:PRINT"
SKYSCRAPERS":GOSUB2800
2670 XX=4:NS=6:PRINT"
POWER PLANTS":GOSUB2800
2690 B$="":A$="":RETURN
2700 A$="":FORI=1TOLEN(MN$(MT+MG)):A$=A$+MID$(MN$(MT+MG),I,1)+" ":NEXT:B$="":FORI=1TOLEN(CN$(SN)):B$=B$+MID$(CN$(SN),I,1)+" ":NEXT:A$=LEFT$(A$,LEN(A$)-1):B$=LEFT$(B$,LEN(B$)-1):RETURN
2800 I1=1:GOSUB100:FORII=1TO900:NEXT:RETURN
2900 PRINT"YOU HAVE"CC;"CRUNCH CREDITS LEFT":RETURN
4000 TM=U:NR=4*LV+KR:IFF<UTHEN4005ELSEFORI=UTO9:FORJ=UTO9:OC(I,J)=U:K=BA+I+8*J-8:IFPEEK(K)=8THENPOKEK,4
4003 NEXT:NEXT
4005 CLS:GOSUB88:GOSUB80:K=U1:GOSUB55:GOSUB72:GOSUB74
4020 I1=U1:BA=KA+64*NR+16:FORI=U1TO8:IJ=BA+(8-I)*8:L=C%+128*I-133:FORM=U1TO8:NS=PEEK(IJ+M)+U1:II=6*M+L:GOSUB110
4040 NEXT:NEXT
4050 IFF=UORNB=UTHEN4500:RETURN
4060 F=U:FORMQ=1TONB:GOSUB30:NEXT:RETURN
4100 LM=L:K=KR+IX(KF):L=LV+IY(KF):IFK<UORK>3ORL<UORL>3THENXB=XA:YB=YA:GOSUB52:RETURN
4105 XB=XA+JX(KF):YB=YA+JY(KF):IFLM>U1THENXB=XB+IX(KF):YB=YB+IY(KF)
4110 IJ=PEEK(KA+16+(4*L+K)*64+XB+8*YB-8):IFFL<UTHEN4120ELSEIFMK(8)=U1ANDIJ<U2THEN4120ELSEIFMK(9)=U1ANDIN=U1ANDIJ>U1THEN4120ELSEIFIJ=UORIJ>MMORIN>UTHENXB=XA:YB=YA:GOSUB52:RETURN
4120 PW(NR)=U2*NB:KR=K:LV=L:IFPW(NR)>10THENPW(NR)=10
4130 GOTO4000
4500 M=PW(NR):NB=U
4505 NB=NB+1:GOSUB4880:MQ(NB)=I:GOSUB4510:IFNB<MTHEN4505ELSERETURN
4510 XM=RND(8):YM=RND(8):K=PEEK(BA+XM+YM*8-8):MF(NB)=U3:IFOC(XM,YM)>UOR(XM=XAANDYM=YA)THEN4510ELSEIFK>PEEK(UB+I)ORK=UTHENNB=NB-U1:M=M-1:RETURN
4520 MP(NB)=PEEK(UP+MQ(NB)):OC(XM,YM)=NB:XM(NB)=XM:YM(NB)=YM:J=NB:MQ=NB:GOSUB30:RETURN
4550 IFPEEK(UA+I)=UTHENGOSUB4510:NB=NB+U1:MQ(NB)=I:GOSUB4510:RETURN
4555 J=RND(6)-U1:IFOC(XE(J),YE(J))=UAND(XE(J)<>XAORYE(J)<>YA)THENXM=XE(J):YM=YE(J):GOTO4590
4560 MF(NB)=U:FORJ=5TOUSTEP-1:XM=XE(J):YM=YE(J):IFOC(XM,YM)>UOR(XM=XAANDYM=YA)THENNEXT
4590 IFPEEK(BA+XM+8*YM-8)=UTHENMF(NB)=U:RETURNELSEMF(NB)=(J+2)/U2:GOSUB4520:RETURN
4800 L=RND(100):IFL>PEEK(TD+NR)+TMORNB>8THENRETURN
4830 GOSUB4880:NB=NB+U1:MQ(NB)=I:GOSUB4550:IFMF(NB)=UTHENNB=NB-U1
4840 RETURN
4880 L=RND(100)+EC+OA+TM:LS=U:FORI=U1TOQ1:LS=LS+PEEK(UW+I):IFL<=LSTHENRETURN
4890 NEXT:I=7:RETURN
5000 IFMK(11)>UTHENAP=PA!/MK(11)ELSEAP=3
5030 GOSUB4000:GOSUB200
5044 GOSUB200:PRINT@1020,"   ";:IFLEN(B$)>UTHEN6050ELSEGOSUB51:GOSUB75:PRINT@368,BL$;:GOSUB74
5045 IFPL<U1THEN7000ELSEIFHU>80THEN5200
5046 IFIG>UTHENIA=IG:GOTO5049ELSEIFPL<BPTHENGOSUB5850:GOTO5044
5047 PRINT@1020,CHR$(149);"?"CHR$(170);:GOSUB500:PRINT@1020,C$;"  ";:GOSUB50:IFL=UTHENPL=PL-U1:GOTO5044
5048 FORIA=U1TOLEN(O$):IFC$<>MID$(O$,IA,U1)THEN5050
5049 IFPL<PA(IA)THENIG=IA:GOTO7000ELSEPRINT@1020,"   ";:IG=U:PL=PL-PA(IA):HU=HU+PA(IA)/MK(U):ONIAGOTO5300,5300,5090,5100,5400,5800,5500,5600,5700,5900,6000,6100,6200,6300,6400,6500,6600,6700,6800,6900
5050 NEXT:IFC$="Q"THENPRINT@V,"WANT TO QUIT?";:GOSUB500:IFC$="Y"THEN13000
5052 IFC$="#"THENPRINT@V,"SCORE:";:GOSUB10000:PRINTS;
5060 GOTO5044
5090 IFFL>UTHENPL=U:GOTO7000ELSEPL=BP-U1:GOTO5044
5100 IFFL<UTHEN7000
5102 L=U1:GOSUB5105:IFK=MMTHENPL=U:GOTO7000ELSE5044
5105 K=PEEK(BA+XA+YA*8-8):IFMK(10)=UTHEN5110
5107 IFK>UTHENPOKEBA+XA+YA*8-8,MK(10):IFK>4ANDK<8THENDB=DB+B(K-5)
5108 MQ=OC(XA,YA):IFMQ>UTHENIFPEEK(UI+MQ(MQ))=UTHENGOSUB340
5110 GOSUB250:XB=XA+L*IX(KF):YB=YA+L*IY(KF):IFXB<U1ORXB>8ORYB<U1ORYB>8THENGOSUB4100
5115 K=PEEK(BA+XB+YB*8-8):IFK=5THEN5150ELSEIFFL<UTHEN5120ELSEIFMK(8)=U1ANDK<U2THEN5120ELSEIFMK(9)=U1ANDIN=U1ANDK>U1THEN5120ELSEIFK=UORK>MMTHENGOSUB200:RETURNELSEIFIN>UANDK>U1THENRETURN
5120 XA=XB:YA=YB:GOSUB200:R=1:PT=95:DU=2000:T=1:GOSUB32:T=22:IFK=UANDFL>UTHENPL=PL+U2
5130 RETURN
5150 FORJ=1TO30:GOSUB78:NEXTJ:CLS:PRINT"DONE IN BY THE POWER PLANT...":GOTO11000
5200 PRINT@368,"BERSERK";:IFGB<>UTHENPL=PL-PA(20):GOTO6800
5210 H=U:FORI=1TO4:IFOC(XA+IX(I),YA+IY(I))>UTHENKF=I:GOSUB80:PL=PL-PA(U1):H=U1:I=4
5220 NEXT:IFH>UTHENPL=PL-PA(18):GOTO6700
5230 IFRND(U)<.7THEN5046ELSEIFFL<UTHENPL=BP-U1:GOTO5046ELSE5340
5300 K=U:GOSUB55:HT=U:KF=KF+3-U2*IA:IFKF>4THENKF=U1ELSEIFKF<U1THENKF=4
5310 K=U1:GOSUB55:GOSUB80:GOTO5044
5340 IFPEEK(BA+XA+IX(KF)+8*(YA+IY(KF)-U1))<=MMTHENPL=PL-PA(4):GOTO5100ELSEIA=RND(U2):GOTO5049
5400 MM=7:L=U2:GOSUB5105:MM=MK(6):PL=U:GOTO7000
5500 XM=XA+IX(KF):YM=YA+IY(KF):J=HT:IFABS(J)>U1THENJ=J/U2
5501 IFJ<>UTHENXM=XM+IX(KF+J):YM=YM+IY(KF+J)
5502 IFXM<U1ORXM>8ORYM<U1ORYM>8THEN5044
5505 K=BA+XM+8*YM-8:IFPEEK(K)>4ANDPEEK(K)<8THENDB=DB+B(PEEK(K)-5)
5507 NS=9:XX=XM:YY=YM:GOSUB100:IFPEEK(K)>U2THENPOKEK,8
5510 MQ=OC(XM,YM):IFMQ>UTHENIFRND(20)>PEEK(UC+MQ(MQ))THENGOSUB340
5515 HU=HU+6/MK(U):IFPEEK(K)<8THENNS=PEEK(K)+U1:GOSUB100
5520 GOTO5044
5600 PC!=PC!*.8:GOSUB88:NS=9:GOSUB260:FORI=1TO4:GOSUB6550:MQ=OC(XX,YY):IFMQ>UTHENXM=XX:YM=YY:GOSUB342
5605 IFXX<1ORXX>8ORYY<1ORYY>8THEN5630
5610 K=BA+XX+8*YY-8:IFPEEK(K)>4ANDPEEK(K)<8THENDB=DB+B(PEEK(K)-5):POKEK,8
5620 NS=9:GOSUB100
5630 NEXT:FORI=1TO4:GOSUB6550:IFXX<1ORXX>8ORYY<1ORYY>8THEN5640ELSEK=BA+XX+8*YY-8:NS=PEEK(K)+U1:GOSUB100
5640 NEXT:PL=U:GOTO7000
5700 IFFL<UTHEN7000ELSEXM=XA:YM=YA:H=U
5705 MQ=OC(XM,YM):IFMQ=UORPEEK(UI+MQ(MQ))>UTHEN5720
5710 IFRND(8)<PEEK(UL+MQ(MQ))THEN5720ELSEGOSUB340
5720 IFH>UTHEN5044ELSEK=BA+XM+8*YM-8:IFPEEK(K)<5OR(MK(U3)+RND(20)-4<PEEK(K)*10)THEN5044
5730 DB=DB+B(PEEK(K)-5):POKEK,4:PRINT@V,"CRUNCH!";:GOTO5044
5800 GOSUB250:FL=-FL:GOSUB200:GOTO7000
5850 BP=BP-AP:IFFL>UTHENRETURN
5860 L=U1:GOSUB5105:RETURN
5900 H=4
5905 HU=HU+3/MK(U):AX=XA:AY=YA:DD=5+RND(15):R=50:DU=35:PT=U1:GOSUB32:M=U3*KF-U3+HT:IFM<UTHENM=12+M
5910 AX=AX+CX(M):AY=AY+CY(M):X=AX+.5:Y=AY+.5:IFX>8ORX<U1ORY>8ORY<U1THEN5044
5915 FORI=1TO10:SET(12*X-6,48-6*Y+3):RESET(12*X-6,48-6*Y+3):NEXT:MQ=OC(X,Y)
5925 K=BA+X+Y*8-8:IFPEEK(K)<=HORPEEK(K)=8THEN5940ELSEIFDD<5+PEEK(K)THEN5044ELSEDB=DB+B(PEEK(K)-5):POKEK,4:NS=5:I1=U1:XX=X:YY=Y:GOSUB100:IFMQ=UTHEN5044
5940 IFMQ>UTHENIFRND(6)>=PEEK(UL+MQ(MQ))THENIFH=4ORFL<UORPEEK(UI+MQ(MQ))>UTHENXX=X:YY=Y:GOSUB450:IFDD>PEEK(UC+MQ(MQ))THENMP(MQ)=MP(MQ)+PEEK(UC+MQ(MQ))-DD:IFMP(MQ)<U1THENXM=X:YM=Y:GOSUB340
5950 GOTO5910
6000 IFFL<UTHEN5044ELSEK=BA+XA+8*YA-8:IFPEEK(K)=UANDMK(8)>UTHEN6005ELSEIFPEEK(K)>U1ANDMK(9)>UTHEN6005ELSE5044
6005 PRINT@V,"DESCENDING";:B$=""
6010 PRINT@432,"DIRECTION?";:GOSUB77:GOSUB51
6020 J=U:FORI=U1TO5:IFC$=MID$(BC$,I,U1)THENJ=I:I=5
6030 NEXT:IFJ=UTHEN6010ELSEB$=B$+CHR$(J):IFJ=5THEN7000ELSEIFLEN(B$)<5THEN6010
6040 B$=B$+CHR$(5):GOTO7000
6050 PL=U:K=ASC(LEFT$(B$,1)):IFK=5THENIN=U:B$="":GOSUB200:PL=PA!-U2:GOTO5044
6060 PRINT@V,"SUBSURFACE";:IN=U1:B$=RIGHT$(B$,LEN(B$)-U1):KF=K:GOSUB80:HU=HU+(MK(9)*6)/MK(U):L=U1:GOSUB5110:GOTO5044
6100 IFNB=UTHEN5044
6110 FORMQ=U1TONB:XM=XM(MQ):YM=YM(MQ):GOSUB390:IFRND(U)<4/(D2*(U1+PEEK(UA+MQ(MQ)))+.1)THENMN(MQ)=U1
6120 NEXT:GOTO5044
6200 GOSUB51:PRINT@432,"SCREAM!";:DU=30000:PT=U3:R=U1:GOSUB32:MQ=1:IFNB=UTHEN5044
6210 XM=XM(MQ):YM=YM(MQ):GOSUB390:IFRND(U)<4/(D2+4+PEEK(UC+MQ(MQ)))THENGOSUB342:MQ=MQ-1
6215 IFMQ<NBTHENMQ=MQ+1:GOTO6210
6220 GOTO5044
6300 I=MI(KF):H=U1
6305 GOSUB6550:XM=XX:YM=YY:IFXM<U1ORXM>8ORYM<U1ORYM>8THEN5044
6310 GOTO5705
6400 GOSUB50:PRINT@V,"R OR L?";:GOSUB500:GOSUB50:IFLEN(C$)=0THEN5044
6420 K=U:GOSUB55:IFC$="R"THENHT=HT+U1ELSEIFC$="L"THENHT=HT-U1
6440 IFHT<-2THENHT=-2ELSEIFHT>U2THENHT=U2
6450 K=U1:GOSUB55:GOTO5044
6500 I=KF:IFGB<>UTHENGB=U:GOSUB72
6505 GOSUB6550:IFXX<U1ORXX>8ORYY<U1ORYY>8THEN5044
6510 K=BA+XX+8*YY-8:IFPEEK(K)=U1AND(MK(U3)+RND(20)>=73)THENDB=DB+B(2):POKEK,U:NS=U1:GOSUB100:MQ=OC(XX,YY):IFMQ>UANDPEEK(UI+MQ(MQ))=UTHENGOSUB345:GOTO5044ELSEGOTO5044
6515 IFPEEK(K)<5ORPEEK(K)>7OR(MK(U3)+RND(20)<PEEK(K)*10)THEN5044
6520 DB=DB+B(PEEK(K)-5):POKEK,4:NS=5:GOSUB100:GOTO5044
6550 XX=XA+IX(I):YY=YA+IY(I):RETURN
6600 H=10:GOTO5905
6700 I=KF:GOSUB6550:XM=XX:YM=YY:MQ=OC(XM,YM):IFXM<U1ORXM>8ORYM<U1ORYM>8ORMQ=UTHEN5044
6705 IFGB<>UTHENGB=U:GOSUB72
6710 IFRND(6)>PEEK(UL+MQ(MQ))THENGOSUB35:GB=2.5*MP(MQ)-PEEK(UC+MQ(MQ)):GOSUB72:GOSUB340:GOTO5044
6720 PRINT@V,"ARR-MISSED";:GOTO5044
6800 IFGB=UTHENPRINT@V,"NO FOOD";:GOTO5044
6810 FORI=1TO3:GOSUB50:PRINT@V,"CHOMP";:FORJ=1TO400:NEXT:GOSUB50:FORJ=1TO300:NEXT:NEXT:HU=HU-GB:GB=U:GOSUB72:IFHU<UTHENHU=U:PRINT@V,"BURP";
6820 GOTO5044
6900 K=BA+XA+8*YA-8:IFPEEK(K)>UANDPEEK(K)<4THENPOKEK,4
6910 GOTO5044
7000 GOSUB57:GOSUB600:HU=HU+U3/MK(U):TM=TM+U1:EC=EC+U1:PA!=PA!-PD!:PRINT@752,"TIME:"INT(EC);:PL=PL+PA!:BP=PA!:IFIN>UTHEN5044ELSEGOSUB4800:MQ=U
7002 IFPA!<UTHEN11000
7004 MQ=MQ+U1:IFMQ>NBTHEN5044
7005 IFMN(MQ)>UTHENMN(MQ)=U:GOTO7520
7010 M1=MQ(MQ):MB=PEEK(UB+M1):MA=PEEK(UA+M1):XM=XM(MQ):YM=YM(MQ):DD=PEEK(UR+M1):GOSUB60:IFH=UTHEN7200
7100 XX=XA:YY=YA:R=1:PT=40:DU=200:FORI=1TO10:GOSUB32:NEXT:IFRND(U)<P!THENGOSUB7710ELSEGOSUB450
7110 IFMA=U1ORRND(U)+PEEK(UI+M1)<P!THEN7500
7200 MV=PEEK(UV+M1):FORJV=U1TOMV:J=MI(MF(MQ)):K=RND(4):FORI=U1TO4:D(K)=U:IFK=JTHEN7220
7210 XT=XM+IX(K):YT=YM+IY(K):L=PEEK(BA+XT+8*YT-8):GOSUB380:IFH>UTHENIF(MB>=LAND(MB>6ORL>U))ANDOC(XT,YT)=UTHENGOSUB46
7211 IFH=UTHENIFRND(U)>.4THEND(K)=U
7213 IFH>UORMA>UTHEN7215ELSEGOSUB35:OC(XM,YM)=U:GOSUB350::MQ=MQ-U1:IFMQ<NBTHEN7520
7214 GOTO5044
7215 IFD(K)=UTHEN7220ELSEIFMA>UANDD(K)<D2THEN7300ELSEIFMA=UANDD(K)>D2THEN7300
7220 GOSUB81:NEXTI:K=RND(4):FORI=1TO4:IFD(K)>UTHENXT=XM+IX(K):YT=YM+IY(K):GOTO7300
7230 GOSUB81:NEXTI:IFJV=U1THENK=J:XT=XM+IX(K):YT=YM+IY(K):IFOC(XT,YT)=UTHENGOSUB46:GOTO7300
7240 JV=MV:GOTO7310
7300 D2=D(K):MF(MQ)=K:OC(XM,YM)=U:GOSUB35:IFXT>UANDXT<9ANDYT>UANDYT<9THENXM=XT:YM=YT:GOSUB30:OC(XM,YM)=MQELSEGOSUB350:GOTO7004
7305 IFPEEK(BA+XM+8*YM-8)=MBTHENJV=MV
7310 NEXTJV:XM(MQ)=XM:YM(MQ)=YM
7500 IFMP(MQ)<U1THENOC(XM,YM)=U:GOSUB342
7520 GOTO7004
7710 GOSUB450:K=PEEK(UD+M1)+RND(4)-MK(4):GOSUB78:IFM1=5THENPD!=PD!+.2:GOSUB50:PRINT@V,"EEE-";:FORI=1TO400:NEXT:PRINT"ARGGH!";
7720 IFK<U1THENRETURNELSEPC!=PC!-K:IFPC!<UTHEN11000
7730 GOSUB88:RETURN
10000 S=A(0,SO)*DB+A(1,SO)*EC+A(2,SO)*CD+A(3,SO)*ND:S=INT(S/SP):RETURN
11000 FORI=1TO1000:NEXTI:CLS:PRINT@319,CHR$(23);MN$(MT+MG);" DIES!
HUMANITY TRIUMPHS!":FORI=1TO1500:NEXTI
11500 GOSUB10000:IFS<1THENK=0:I=0:GOTO12020
12000 I=INT(LOG(S/100)/LOG(3)):IFI>2THENI=2ELSEIFI<0THENI=0
12005 J=100*2[I:K=INT(S/J):IFK>6THENK=6
12020 PRINT" ":PRINT"YOUR SCORE IS:"S:PRINT"TIME:"EC:PRINT"
YOU WERE A";A$(I);" ";A$(K+3)
12022 OPEN"I",1,"SCORES":FORI=UTO13:FORJ=UTO4:INPUT#1,A(J,I):NEXT:NEXT:INPUT#1,K:CLOSE
12023 IFA(SO,MT+MG-1)>UTHENPRINT"
HIGHEST PREVIOUS SCORE FOR
"MN$(MT+MG);", OBJECTIVE #"SO+1;"
WAS"A(SO,MT+MG-1)
12024 IFS<=A(SO,MT+MG-1)THEN12030
12025 PRINT"A NEW RECORD IN MAYHEM":A(SO,MT+MG-1)=S:IFK>UTHENPRINT"HIGHEST SCORE EVER WAS"K
12026 IFK<STHENK=S
12027 OPEN"O",1,"SCORES":FORI=UTO13:FORJ=UTO4:PRINT#1,A(J,I):NEXT:NEXT:PRINT#1,K:CLOSE
12030 A$=INKEY$:PRINT"
HIT ANY KEY TO PLAY AGAIN"
12040 A$=INKEY$:IFLEN(A$)=UTHEN12040
12050 RUN
13000 CLS:INPUT"DO YOU WANT TO SAVE THE GAME";A$:IFLEFT$(A$,1)<>"Y"THEN11500
13010 INPUT"INSERT THE DISK ON WHICH YOU WANT TO SAVE THE GAME AND PRESS
ENTER";A$:PRINT"NOW SAVING GAME IN PROGRESS"
13020 OPEN"O",1,"INPROG":PRINT#1,O$:PRINT#1,MT;BA;DB;EC;FL;GB;HT;HU;KF;KR;LV;XA;YA;NR;NB;PA!;PC!;PD;PL;SO;SE;SP,OA,MG,ND:FORI=0TO12:PRINT#1,MK(I):NEXT
13030 FORI=1TONB:PRINT#1,MN(I);MP(I);MQ(I);XM(I);YM(I):NEXT
13040 FORI=1TO8:FORJ=1TO8:PRINT#1,OC(I,J):NEXT:NEXT
13050 FORI=KATOKA+1040:J=PEEK(I):PRINT#1,J:NEXT
13060 FORI=1TO20:PRINT#1,PA(I):NEXT
13070 FORI=0TO15:PRINT#1,PW(I):NEXT
13075 PRINT#1,MN$(MT+MG)
13080 CLOSE:PRINT"SAVE OF GAME COMPLETE.  tYPE RUN TO PLAY AGAIN":END
13500 F=-1:INPUT"INSERT DISK WITH SAVED GAME AND PRESS ENTER";A$:PRINT"NOW RESTORING PREVIOUSLY SAVED GAME"
13510 OPEN"I",1,"INPROG":INPUT#1,O$
13520 INPUT#1,MT,BA,DB,EC,FL,GB,HT,HU,KF,KR,LV,XA,YA,NR,NB,PA!,PC!,PD,PL,SO,SE,SP,OA,MG,ND
13530 FORI=0TO12:INPUT#1,MK(I):NEXT
13540 FORI=1TONB:INPUT#1,MN(I),MP(I),MQ(I),XM(I),YM(I):NEXT
13550 FORI=1TO8:FORJ=1TO8:INPUT#1,OC(I,J):NEXT:NEXT
13560 FORI=KATOKA+1040:INPUT#1,J:POKEI,J:NEXT
13570 FORI=1TO20:INPUT#1,PA(I):NEXT
13580 FORI=0TO15:INPUT#1,PW(I):NEXT:MM=MK(6)
13585 INPUT#1,MN$(MT+MG)
13590 CLOSE:RETURN
14000 DATAATOMIZE,"BREATHE FIRE","CRUSH & CRUMBLE",DESCEND,E,"FLY LIKE A BIRD","MECHANICAL GRABBERS",H,IMMOLATE,"LEAP TALL BUILDINGS AT A SINGLE BOUND"
14010 DATAK,L,M,N,OBLITERATE,PARALYZE,Q,R,STOMP,"A TAIL TO LASH","AN ULTRASONIC SCREAM",V,"WEAVE WEBS",X,Y,"ZAP HELICOPTERS"
14500 FORI=1TO26:READRS$(I):NEXT
14600 DATAGOSHILLA,KRAKEN,ARACHNIS,"THE GLOB",MECHISMO,MANTRA,BIPED,"SEA MONSTER",INSECT,AMORPHOUS,ROBOT,FLYER,BRONTOSAUR,"THE SERPENT"
14610 DATA"NEW YORK","THE GOLDEN GATE","WASHINGTON DC","TOKYO"
14620 DATAAUTOMOBILES,"PANIC-STRICKEN MOBS","POLICE CARS",HELICOPTERS,"THE MAD SCIENTIST","NATIONAL GUARD","M48 TANKS","155MM GUNS","N INSIGNIFICANT
"," MINOR"," MAJOR",PEST,ANNOYANCE,NUISANCE,SCOURGE,MENACE,CALAMITY,DISASTER
14630 FORI=1TO14:READMN$(I):NEXT:FORI=1TO4:READCN$(I):NEXT:FORI=0TO7:READHH$(I):NEXT:FORI=0TO9:READA$(I):NEXT
14800 DATA205,127,10,229,33,0,0,209,62,0,254,1,32,1,235,1,6,0,237,176,254,1,32,1,235,213,17,58,0,25,209,254,1,32,1,235,1,6,0,237,176,201
14900 RETURN

