10 CLEAR1000 
20 CLSRND(9)-1:DIMN$(20),G(20),W(20),L$(4):I=RND(-(PEEK(9)*256+PEEK(10)))
30 FORI=1TO13:READNT,LT:NEXT:PRINT@172,"DREIDEL";:FORI=1TO13:READNT,LT:SOUNDNT,LT+1:NEXT:PRINT@238,"BY";
40 FORI=1TO13:READNT,LT:SOUNDNT,LT+1:NEXT:PRINT@296,"HAROLD SCHNEIDER";:FORI=1TO13:READNT,LT:SOUNDNT,LT+1:NEXT
50 PRINT@480,"HIT ANY KEY TO START TO PLAY!"; 
60 A$=INKEY$:IFA$=""THEN60
70 CLSRND(9)-1 
80 IFA$="G"THEN140 
90 GOSUB630 
100 GOSUB800 
110 CLSRND(6)+1 
120 DATA147,3,89,3,89,3,108,3,108,3,125,3,89,6,125,3,147,3,147,3,133,3,125,3,108,10,108,3,108
121 DATA3,108,3,125,3,125,3,133,3,108,6,108,3,147,3,133,3,125,3,108,3,89,10
130 DATA125,3,147,3,125,3,147,3,125,3,147,3,125,6,125,3,147,3,147,3,133,3,125,3,108,10,108,3,133
131 DATA3,108,3,133,3,108,3,133,3,108,6,108,3,147,3,147,3,159,3,170,3,176,10
140 PRINT@64,"NOW WE ARE GOING TO PLAY":PRINT"THE GAME OF DREIDEL.":INPUT"HOW MANY PEOPLE WANT TO PLAY";N
150 FORI=1TON:PRINT"WHAT IS THE NAME OF PLAYER #"I;:INPUT N$(I) 
160 G(I)=10
170 NEXTI:GOSUB1390 
180 T=N:CLSRND(6)+1 
190 PRINT@64,"EACH PLAYER HAS 10 POINTS,":PRINT"AND THERE ARE"N"POINTS IN THE"
191 PRINT"POT. HOW MANY TIMES WOULD YOU":INPUT"LIKE EACH PLAYER TO PLAY";NN 
200 FORII=1TONN:FORPT=1TON:A$=INKEY$
210 IFT<=0THENT=0:GOSUB1330
220 IFNB>=N-1THEN280 
230 IFG(PT)<0THEN270 
240 CLSRND(6)+1:PRINT@96,"IT IS TIME FOR "N$(PT):PRINT"TO HAVE TURN NUMBER"STR$(II)"."
241 PRINT"THE POT HAS"T"POINTS.":PRINT"YOU HAVE"G(PT)"POINTS.":PRINT"HIT ANY KEY TO SPIN THE":PRINT"DREIDEL!"; 
250 GOSUB990
260 IF NB>=N-1THEN280
270 NEXTPT,II
280 REM GAME OVER. FIND WINNER. 
290 HS=-100 
300 FORI=1TON 
310 IFG(I)<=HSTHEN330 
320 HS=G(I)
330 NEXTI:IFHS<0THEN370 
340 FORI=1TON 
350 IFG(I)<HSTHENW(I)=0:GOTO360
351 W(I)=1 
360 NEXTI 
370 NW=0:FORI=1TON 
380 NW=NW+W(I)
390 NEXTI
400 CLSRND(6)+1
410 IFNW=0THENPRINT@160,"NO ONE HAS ANY POINTS LEFT!":PRINT"EVERYONE LOSES!!":GOTO480
420 IFNW=1THENPRINT@160,"THE WINNER IS ";:FORI=1TON:IFW(I)=1THENPRINTN$(I)"!"
430 IFNW=1THENNEXTI:GOTO480 
440 PRINT@160,"IT'S A TIE! THE WINNERS ARE:"
450 FORI=1TON
460 IFW(I)=1THENPRINTN$(I)
470 NEXTI
480 GOSUB970:CLSRND(6)+1
490 PRINT@32,"THE FINAL SCORES WERE:"
500 FORI=1TON:IFG(I)<0THENG(I)=0
510 NEXTI
520 FORI=1TON
530 PRINTN$(I);G(I)
540 NEXTI
550 PRINT@448,"WOULD YOU LIKE TO PLAY AGAIN";:INPUTA$
560 IFLEFT$(A$,1)="Y"THENRUN
561 END 
630 CLS 3:GOSUB640:GOSUB650:GOSUB660:GOSUB670:GOSUB680:GOSUB690:GOSUB700:GOSUB710:RETURN
640 PRINT@130,"I HAD A LITTLE DREIDEL,";:RETURN
650 PRINT@165,"I MADE IT OUT OF CLAY.";:RETURN
660 PRINT@194,"AND WHEN IT'S DRY AND READY,";:RETURN
670 PRINT@229,"THEN DREIDEL I SHALL PLAY!";:RETURN
680 PRINT@258,"O DREIDEL, DREIDEL, DREIDEL,";:RETURN 
690 PRINT@293,"I MADE IT OUT OF CLAY.";:RETURN
700 PRINT@322,"O DREIDEL, DREIDEL, DREIDEL,";:RETURN
710 PRINT@357,"NOW DREIDEL I SHALL PLAY!";:RETURN
720 DATA130,i,147,132,had,89,136,a,89,138,lit,108,141,tie,108,145,drei,125,149,del,89
730 DATA165,i,125,167,made,147,172,it,147,175,out,133,179,of,125,182,clay,108
740 DATA194,and,108,198,when,108,203,"it's",108,208,dry,125,212,and,125,216,rea,133,219,dy,108
750 DATA229,then,108,234,drei,147,238,del,13 3,24 2,i,125,244,shall,108,250,play,89
760 DATA258,o,125,260,drei,147,264,del,125,269,drei,147,273,del,125,278,drei,147,282,del,125
770 DATA293,i,125,295,made,147,300,it,147,303,out,133,307,of,125,310,clay,108
780 DATA322,o,108,324,drei,133,328,del,108,333,drei,133,337,del,108,342,drei,133,346,del,108
790 DATA357,now,108,361,drei,147,365,del,147,369,i,159,371,shall,170,377,play,176
800 FORI=1TO6:READLC,W$,NT:PRINT@LC,W$;:SOUNDNT,4:GOSUB640:NEXTI
810 READLC,W$,NT:PRINT@LC,W$;:SOUNDNT,8:GOSUB640
820 FORI=1TO5:READLC,W$,NT:PRINT@LC,W$;:SOUNDNT,4:GOSUB650:NEXTI
830 READLC,W$,NT:PRINT@LC,W$;:SOUNDNT,12:GOSUB650
840 FORI=1TO6:READLC,W$,NT:PRINT@LC,W$;:SOUNDNT,4:GOSUB660:NEXTI 
850 READLC,W$,NT:PRINT@LC,W$;:SOUNDNT,8:GOSUB660
860 FORI=1TO5:READLC,W$,NT:PRINT@LC,W$;:SOUNDNT,4:GOSUB670:NEXTI 
870 READLC,W$,NT:PRINT@LC,W$;:SOUNDNT,12:GOSUB670
880 FORI=1TO6:READLC,W$,NT:PRINT@LC,W$;:SOUNDNT,4:GOSUB680:NEXTI 
890 READLC,W$,NT:PRINT@LC,W$;:SOUNDNT,8:GOSUB680
900 FORI=1TO5:READLC,W$,NT:PRINT@LC,W$;:SOUNDNT,4:GOSUB690:NEXTI 
910 READLC,W$,NT:PRINT@LC,W$;:SOUNDNT,12:GOSUB690 
920 FORI=1TO6:READLC,W$,NT:PRINT@LC,W$;:SOUNDNT,4:GOSUB700:NEXTI 
930 READLC,W$,NT:PRINT@LC,W$;:SOUNDNT,8:GOSUB700 
940 FORI=1TO5:READLC,W$,NT:PRINT@LC,W$;:SOUNDNT,4:GOSUB710:NEXTI
950 READLC,W$,NT:PRINT@LC,W$;:SOUNDNT,12:GOSUB710 
960 RETURN 
970 A$=INKEY$:PRINT@480,"HIT ANY KEY TO CONTINUE.";
980 A$=INKEY$:IFA$=""THEN980
981 RETURN 
990 REM SPIN DREIDEL 
1000 K=RND(4):A$=INKEY$:IFA$=""THEN1000
1010 CLS0:PRINT@15,TP$;:PRINT@172,BM$;:RESTORE
1020 FORI=0TO16-K:L=I+1-INT(I/4)*4
1030 PRINT@76,L$(L);:TX=0
1040 READNT,LT:TX=TX+LT:SOUNDNT,LT:IFNT=176THENRESTORE
1050 IFTX<ITHEN1040
1060 NEXTI
1070 ONLGOTO1080,1130,1180,1230
1080 REM NUN 
1090 PRINT@359,"YOU GOT A 'NUN'.";
1100 PRINT@391,"YOU DO NOTHING.";
1110 PRINT@423,"YOU NOW HAVE"G(PT)"POINTS.";
1120 GOSUB970:RETURN
1130 REM GIMMEL 
1140 PRINT@358,"YOU GOT A GIMMEL!";
1150 PRINT@390,"YOU WIN THE WHOLE POT!";
1160 G(PT)=G(PT)+T:T=0:PRINT@422,"YOU NOW HAVE"G(PT)"POINTS.";
1170 GOSUB970:RETURN
1180 REM HEY 
1190 PRINT@357,"YOU GOT A HEY!";
1200 PRINT@389,"YOU WIN HALF OF THE POT!";
1210 H=INT((T+1)/2):G(PT)=G(PT)+H:T=T-H:PRINT@423,"YOU NOW HAVE"G(PT)"POINTS.";
1220 GOSUB970:RETURN
1230 REM SHIN 
1240 PRINT@356,"YOU GOT A SHIN.";
1250 PRINT@384,"YOU LOSE ONE POINT TO THE POT."; 
1260 G(PT)=G(PT)-1:IFG(PT)<0THENPB=PT:GOSUB1280:GOTO1270
1261 T=T+1:PRINT@420,"YOU NOW HAVE"G(PT)"POINTS.";
1270 GOSUB970:RETURN
1280 REM PLAYER #PB IS BROKE
1290 CLS4
1300 PRINT@160,N$(PB)" CAN NOT PAY!";
1310 PRINT@224,N$(PB)" HAS LOST, AND IS";:PRINT@256,"NO LONGER IN THE GAME.";
1320 NB=NB+1:GOTO970 
1330 REM PUT INTO POT 
1340 CLSRND(6)+1:PRINT@192,"THE POT IS EMPTY.  EACH PLAYER  MUST PUT ONE POINT IN THE POT.":GOSUB970
1350 FORI=1TON:IFG(I)<0THEN1370
1360 G(I)=G(I)-1:IFG(I)<0THENPB=I:GOSUB1280:GOTO1370
1361 T=T+1 
1370 NEXTI
1380 RETURN
1390 TP$=CHR$(174)+CHR$(173)+CHR$(128)+CHR$(128):SP$=CHR$(128)
1400 FORI=1TO23:SP$=SP$+CHR$(128):NEXTI:TP$=TP$+SP$+CHR$(128)+CHR$(174):A$=CHR$(172)+CHR$(172)+CHR$(168)+CHR$(164)
1410 A$=A$+CHR$(172)+CHR$(172)+CHR$(173):TP$=TP$+A$
1420 L$(1)=CHR$(170)+CHR$(128)+CHR$(128):L$(1)=L$(1)+CHR$(140)+CHR$(141)+CHR$(128)
1430 A$=CHR$(128)+CHR$(165)+SP$+CHR$(170)+CHR$(128):L$(1)=L$(1)+A$+CHR$(128)+CHR$(128)+CHR$(133):L$(1)=L$(1)+CHR$(128)+A$
1440 L$(1)=L$(1)+CHR$(128)+CHR$(131)+CHR$(135):L$(1)=L$(1)+CHR$(128)+CHR$(128)+CHR$(165)
1450 BM$=CHR$(169)+CHR$(162)+CHR$(128)+CHR$(128)+CHR$(128)
1451 BM$=BM$+CHR$(128)+CHR$(161)+CHR$(166)+SP$:BM$=BM$+CHR$(128)+CHR$(164)+CHR$(169)
1460 BM$=BM$+CHR$(162)+CHR$(161)+CHR$(166)+CHR$(168):BM$=BM$+CHR$(128)+SP$+CHR$(128)+CHR$(128)
1461 BM$=BM$+CHR$(128)+CHR$(164)+CHR$(168)
1470 L$(2)=CHR$(170)+CHR$(128)+CHR$(128):L$(2)=L$(2)+CHR$(252)+CHR$(253)+CHR$(128)
1471 L$(2)=L$(2)+A$+CHR$(128)+CHR$(128)+CHR$(245):L$(2)=L$(2)+CHR$(128)+A$
1480 L$(2)=L$(2)+CHR$(128)+CHR$(243)+CHR$(246):L$(2)=L$(2)+CHR$(242)+CHR$(128)+CHR$(165)
1490 L$(3)=CHR$(170)+CHR$(128)+CHR$(148):L$(3)=L$(3)+CHR$(156)+CHR$(156)+CHR$(154)
1491 L$(3)=L$(3)+A$+CHR$(149)+CHR$(128)+CHR$(128):L$(3)=L$(3)+CHR$(154)+A$
1500 L$(3)=L$(3)+CHR$(149)+CHR$(128)+CHR$(128):L$(3)=L$(3)+CHR$(154)+CHR$(128)+CHR$(165)
1510 L$(4)=CHR$(170)+CHR$(128)+CHR$(189):L$(4)=L$(4)+CHR$(180)+CHR$(186)+CHR$(189)
1511 L$(4)=L$(4)+A$+CHR$(181)+CHR$(128)+CHR$(186):L$(4)=L$(4)+CHR$(181)+A$
1520 L$(4)=L$(4)+CHR$(181)+CHR$(179)+CHR$(187):L$(4)=L$(4)+CHR$(183)+CHR$(128)+CHR$(165)
1530 RETURN
