
100 'CONSTRUCT A 3-D MAZE
110 '
120 D$="SRBLDULSRBDUBLSRDURBLSDU"
130 DIM S(200)
140 '
150 'INPUT DIMENSIONS, GET ARRAY
160 INPUT "HEIGHT:";H
170 IF H<2 THEN GOTO 160
180 INPUT "WIDTH:";W
190 IF W<3 THEN GOTO 180
200 INPUT "DEPTH:";DP
210 IF DP<3 THEN GOTO 200
220 DIM R(H,W,DP)
230 '
240 'FILL ROOMS WITH RANDOM DOORS
250 FOR Z=1 TO H:FOR X=1 TO W:FOR Y=1 TO DP
260 R(Z,X,Y)=RND(64)-1
270 IF Y=1 THEN R(Z,X,Y)=R(Z,X,Y) AND &H3D
280 IF Y=DP THEN R(Z,X,Y)=R(Z,X,Y) AND &H37
290 IF X=1 THEN R(Z,X,Y)=R(Z,X,Y) AND &H3B
300 IF X=W THEN R(Z,X,Y)=R(Z,X,Y) AND &H3E
310 IF Z=1 THEN R(Z,X,Y)=R(Z,X,Y) AND &H1F
320 IF Z=H THEN R(Z,X,Y)=R(Z,X,Y) AND &H2F
330 NEXT:NEXT:NEXT
340 FOR Z=1 TO H: FOR X=1 TO W: FOR Y=1 TO DP
350 IF Z=1 THEN GOTO 360 ELSE IF (R(Z-1,X,Y) AND 16) <> 0 THEN R(Z,X,Y) = R(Z,X,Y) OR 32
360 IF X=1 THEN GOTO 370 ELSE IF (R(Z,X-1,Y) AND 1) <> 0 THEN R(Z,X,Y) = R(Z,X,Y) OR 4
370 IF Y=1 THEN GOTO 380 ELSE IF (R(Z,X,Y-1) AND 8) <> 0 THEN R(Z,X,Y) = R(Z,X,Y) OR 2
380 IF Z=H THEN GOTO 390 ELSE IF (R(Z+1,X,Y) AND 32) <> 0 THEN R(Z,X,Y) = R(Z,X,Y) OR 16
390 IF X=W THEN GOTO 400 ELSE IF (R(Z,X+1,Y) AND 4) <> 0 THEN R(Z,X,Y) = R(Z,X,Y) OR 1
400 IF Y=DP THEN GOTO 410 ELSE IF (R(Z,X,Y+1) AND 2) <> 0 THEN R(Z,X,Y) = R(Z,X,Y) OR 8
410 NEXT:NEXT:NEXT
420 '
430 'FIND CENTER ROOM
440 F=0
450 D=0
460 Y=INT(DP/2)
470 X=INT(W/2)
480 Z=INT(H/2)
490 S(1)=Z: S(2)=X: S(3)=Y: JE=3
500 PRINT Z;X;Y
510 A$=""
520 '
530 'MAIN LOOP
540 'DO A RANDOM WALK
550 N=RND(6)-1
560 IF F=0 THEN F=1: GOTO 640
570 IF (N=2 AND D=0) OR (N=0 AND D=2) OR (N=1 AND D=3) OR (N=3 AND D=1) THEN GOTO 550
580 TZ=Z: TX=X: TY=Y
590 IF N=5 THEN TZ=TZ-1 ELSE IF N=4 THEN TZ=TZ+1 ELSE IF N=3 THEN TX=TX-1 ELSE IF N=2 THEN TY=TY+1 ELSE IF N=1 THEN TX=TX+1 ELSE TY=TY-1
600 FOR J=1 TO JE STEP 3
610 IF (TZ=S(J)) AND (TX=S(J+1)) AND (TY=S(J+2)) THEN GOTO 550
620 NEXT J
630 IF (TX=0) OR (TX=W+1) OR (TY=0) OR (TY=DP+1) OR (TZ=0) OR (TZ=H+1) THEN IF JE<(H*W*DP)/3 THEN GOTO 550
640 IF N=0 THEN M=2 ELSE IF N=1 THEN M=1 ELSE IF N=2 THEN M=8 ELSE IF N=3 THEN M=4 ELSE M=2^N
650 R(Z,X,Y)=R(Z,X,Y) OR M
660 IF M=32 THEN M=16 ELSE IF M=16 THEN M=32 ELSE IF M=8 THEN M=2 ELSE IF M=2 THEN M=8 ELSE IF M=4 THEN M=1 ELSE IF M=1 THEN M=4
670 A$=A$+MID$(D$,D*6+(N+1),1)
680 IF N=5 THEN Z=Z-1 ELSE IF N=4 THEN Z=Z+1
690 IF N=3 THEN X=X-1 ELSE IF N=2 THEN Y=Y+1 ELSE IF N=1 THEN X=X+1 ELSE IF N=0 THEN Y=Y-1
700 S(JE+1)=Z: S(JE+2)=X: S(JE+3)=Y: JE=JE+3
710 IF NOT((Z=H+1) OR (Y=DP+1) OR (X=W+1)) THEN R(Z,X,Y)=R(Z,X,Y) OR M
720 IF N<4 THEN D=N
730 PRINT Z;X;Y
740 IF (Z=0) OR (Z=H+1) OR (Y=0) OR (Y=DP+1) OR (X=0) OR (X=W+1) THEN GOTO 770 ELSE GOTO 550
750 '
760 'STORE MAZE IN MAZE FILE
770 INPUT"MAZE FILE NAME:";MF$
780 OPEN "O",#1,MF$
790 PRINT#1,H,W,DP
800 FOR Z=1 TO H: FOR X=1 TO W: FOR Y=1 TO DP
810 PRINT#1,R(Z,X,Y)
820 NEXT:NEXT:NEXT
830 PRINT#1,A$
840 CLOSE
