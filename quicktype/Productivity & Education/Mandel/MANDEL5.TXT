10 CLS
1000 PRINT"MANDELBROT SET PROJECT"
1010 PRINT"QUITE BASIC MATH PROJECT"
1020 PRINT"------------------------"
1040 PRINT"THIS PROGRAM PLOTS A GRAPHICAL"
1050 PRINT"REPRESENTATION OF THE FAMOUS"
1060 PRINT"MANDELBROT SET. IT TAKES A WHILE";
1070 PRINT"TO FINISH SO HAVE PATIENCE AND"
1080 PRINT"DON'T HAVE UNREASONABLY HIGH":PRINT"EXPECTATIONS."
1090 PRINT:INPUT"HIT <ENTER> TO CONTINUE";I$
1100 CLS0
2000 REM INITIALIZE THE COLOR PALETTE
2020 REM L IS THE MAXIMUM ITERATIONS TO TRY
2030 L=100
2035 REM MAP FROM PIXEL COORDINATES (I,J) TO MATH (U,V)
2040 FORI=0TO63:FORJ=0TO31:U=I/32-1.5:V=J/16-1:X=U:Y=V:N=0
2110 REM INNER ITERATION LOOP STARTS HERE 
2120 R=X*X:Q=Y*Y:ON1-(R+Q>4ORN>=L)GOTO2150,2200
2150 Y=2*X*Y+V:X=R-Q+U:N=N+1:GOTO2120
2190 REM COMPUTE THE COLOR TO PLOT
2200 ON1-(N<10)GOTO2210:RESET(I,J):NEXT:NEXT:GOTO2240
2210 C=INT(8*(N-10)/(L-10)):SET(I,J,C):NEXT:NEXT
2240 SOUND1,1
2250 I$=INKEY$:IFI$=""THEN2250
2260 END
