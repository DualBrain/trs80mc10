1000 REM Mandelbrot Set Project
1010 REM Quite BASIC Math Project
1020 REM ------------------------ 
1030 CLS
1040 PRINT "This program plots a graphical representation of the famous Mandelbrot set.  It takes a while to finish so have patience and don't have too high expectations;  the graphics resolution is not very high on our canvas."
2000 REM Initialize the color palette
2010 GOSUB 3000
2020 REM L is the maximum iterations to try
2030 L = 100
2040 FOR I = 0 TO 100
2050 FOR J = 0 TO 100
2060 REM Map from pixel coordinates (I,J) to math (U,V)
2060 U = I / 50 - 1.5
2070 V = J / 50 - 1
2080 X = U
2090 Y = V
2100 N = 0
2110 REM Inner iteration loop starts here 
2120 R = X * X
2130 Q = Y * Y
2140 IF R + Q > 4 OR N >= L THEN GOTO 2190
2150 Y = 2 * X * Y + V
2160 X = R - Q + U
2170 N = N + 1
2180 GOTO 2120
2190 REM Compute the color to plot
2200 IF N < 10 THEN C = "black" ELSE C = P[ROUND(8 * (N-10) / (L-10))]
2210 PLOT I, J, C 
2220 NEXT J
2230 NEXT I
2240 END
3000 REM Subroutine -- Set up Palette
3010 ARRAY P
3020 P[0] = "black"
3030 P[1] = "magenta"
3040 P[2] = "blue"
3050 P[3] = "green"
3060 P[4] = "cyan"
3070 P[5] = "red"
3080 P[6] = "orange"
3090 P[7] = "yellow"
3090 P[8] = "white"
3100 RETURN