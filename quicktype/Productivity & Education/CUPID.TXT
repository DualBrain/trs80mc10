10 CLEAR 250
20 GOSUB 100
25 GOSUB 200
30 GOSUB 300
35 GOSUB 400
40 GOSUB 450
45 GOSUB 500
50 GOSUB 600
55 GOSUB 700
60 GOSUB 900
65 END
100 REM INTIALIZE
105 DIM TN$(20),CC
110 DIM R(20,10)
115 DIM IM(10)
120 DIM CH$(10)
125 NQ=10
130 BL=453
135 X=0:Y=0:F=0:T$=""
140 L1=163:L2=178:L3=176:L4=L3+92
145 RETURN
200 REM COVER SCREEN
205 C=RND(8):IF C=4 THEN 205
210 GOSUB 710
215 T1$="COMPUTER":T2$="CUPID!":T3$="PRESS ANY KEY"
220 PRINT@140,T1$;
225 PRINT@205,T2$;
235 PRINT@297,T3$;:GOSUB 1070
240 RETURN
241 REM 
300 REM INPUT PERSONAL DATA
305 CLS
310 PRINT:PRINT"HI, WHAT IS YOUR NAME?"
315 INPUT UN$:IFI$=""THEN315
320 UN$=LEFT$(UN$,12):PRINT"OKAY, ";UN$;","
325 PRINT"ENTER <F> IF YOU'RE A FEMALE"
330 PRINT"ENTER <M> IF YOU'RE A MALE"
335 INPUT US$:IF LEFT$(US$,1)<>"F" AND LEFT$(US$,1)<>"M" THEN GOTO 325
340 GOSUB 980:GOSUB 1055
345 REM 
350 CLS:PRINT:PRINT"NOW IT'S TIME TO FIND OUT WHAT  YOU LOOK FOR IN A "TS$"."
360 PRINT"YOU WILL HAVE TO RATE EACH      QUALITY I SHOW YOU ON A SCALE"
365 PRINT"FROM 1 TO 10."
366 PRINT "A <1> MEANS YOU DON'T MUCH CARE IF A "TS$" HAS THAT QUALITY OR"
367 PRINT "NOT. A <10> MEANS IT IS REALLY  IMPORTANT TO YOU THAT A ";TS$
368 PRINT "HAS THAT QUALITY."
369 GOSUB 1055
370 T$="YOUR IDEAL "+TS$:F=1
375 GOSUB 925
380 GOSUB 1080:RETURN
385 IF F=1 THEN IM(Y)=N:RETURN
390 IF F=2 THEN R(X,Y)=N:RETURN
395 RETURN
400 REM INPUT NAMES
405 CLS:PRINT"NOW IT IS TIME TO ENTER THE     NAMES OF SOME ";T2$;"S"
406 PRINT"YOU ARE INTERESTED IN."
407 PRINT:PRINT"RATE EACH "TS$" ON A SCALE OF":PRINT"1 TO 10 FOR EACH QUALITY."
410 PRINT"A <1> MEANS THE "TS$" IS LOW ON":PRINT"THAT QUALITY, WHILE A <10>"
415 PRINT"MEANS THE "TS$" HAS A LOT":PRINT"OF THAT QUALITY."
420 PRINT"BE HONEST FAIR, AND OBJECTIVE."
425 GOSUB 1055
430 CLS:PRINT:PRINT"ENTER THE NAME OF EACH "TS$:PRINT"YOU ARE INTERESTED IN."
432 PRINT"PRESS <ENTER> AFTER EACH NAME."
433 PRINT"PRESS <ENTER> AGAIN WHEN YOU AREFINISHED.":X=1
435 PRINT TS$" NUMBER ";X
440 INPUT TN$(X)
445 IF TN$(X)="" OR TN$(X)=CHR$(13)THEN NT=X-1:RETURN
446 TN$(X)=LEFT$(TN$(X),12)
447 X=X+1:GOTO 435
450 CLS:F=2:FOR X=1 TO NT:T$=TN$(X)
455 GOSUB 925
460 NEXT X:F=0
465 RETURN
500 REM SORT, RANK, COMPARE
505 PRINT:PRINT"PLEASE WAIT.":PRINT:PRINT"I'M CHECKING THESE ";T2$;"S OUT..."
510 FOR X=1 TO NT
515 FOR Y=1 TO NQ
520 SC(X)=SC(X)+R(X,Y)*IM(Y)
525 FOR TIM=1 TO 70:NEXT TIM:SOUND 100+RND(150),RND(5)
530 NEXT Y:NEXT X
535 REM RANK
540 W=1
545 FOR C=2 TO NT
550 IF SC(C)>SC(W) THEN W=C
555 NEXT C
560 C1$=TN$(W)
565 GOSUB 1055
570 RETURN
600 REM REPORT CHOICE
605 S=3:GOSUB 1165:PRINT"ARE YOU READY FOR THIS?":GOSUB 1055
610 GOSUB 1165:PRINT"THE WINNER...":GOSUB 1055
615 GOSUB 1165:PRINT"THE ONE WHO COMES CLOSEST TO    MEETING YOUR REQUIREMENTS...":GOSUB 1055
625 GOSUB 1165:PRINT"IS...":GOSUB 1055
630 RETURN
700 GOSUB 705:GOSUB 740:RETURN
705 X$="":C=0
710 RESTORE:CLS0
715 READ X:READ Y:IF Y=255 THEN RETURN
720 SET(X,Y,4)
730 GOTO 715
740 PRINT@L4,"TRUE LOVE!";:PRINT@L3,"+";
750 GOSUB1200:GOTO 805
760 DATA 1,9,1,10,1,11,1,12,2,7,2,8,2,13,2,14,3,6,3,15,4,5,4,16
765 DATA 5,4,5,17,6,4,6,18,7,3,7,18,8,3,8,19
770 DATA 9,2,9,20,10,2,10,20,11,2,11,21,12,2,12,21,13,1,13,22
772 DATA 14,1,14,23,15,1,15,23,16,1,16,1,16,24,17,1,17,24,18,1,18,25
775 DATA 19,1,19,25,20,1,20,26,21,1,21,26,22,1,22,27,23,2,23,27,24,2
777 DATA 24,27,25,2,25,28,26,3,26,28
780 DATA 27,3,27,29,28,4,28,29,29,4,29,30,30,5,30,30,31,6,31,31
785 DATA 32,6,32,31,33,5,33,30,34,4,34,30,35,4,35,29,36,3,36,29,37,3
787 DATA 37,28,38,2,38,28,39,2,39,27,40,2,40,27,41,1,41,27
790 DATA 42,1,42,26,43,1,43,26,44,1,44,25,45,1,45,25,46,1,46,24,47,1
792 DATA 47,24,48,1,48,23,49,1,49,23,50,1,50,22
795 DATA 51,2,51,21,51,22,52,2,52,21,53,2,53,20,54,2,54,20,55,3,55,19
797 DATA 56,3,56,18,57,4,57,18,58,4,58,17,59,5,59,16,60,6,60,15,61,7
798 DATA 61,8,61,13,61,14,62,9,62,10,62,11,62,12
800 DATA 255,255
805 X$=INKEY$:IFX$<>""THENRETURN
810 PRINT@L1+((12-LEN(UN$))/2),UN$;:PRINT@L2+((12-LEN(C1$))/2),C1$;:GOSUB1225
815 PRINT@L1+((12-LEN(U2$))/2),U2$;:PRINT@L2+((12-LEN(C2$))/2),C2$;:GOSUB1225:GOTO805
900 REM DO IT AGAIN OR QUIT
905 PRINT@480,"WANT TO TRY AGAIN (Y/N)";
910 AN$=INKEY$:IFAN$=""THEN910
915 IF AN$="Y" OR AN$="y" THEN RUN
917 IF AN$="N" OR AN$="n" THEN RETURN
920 GOTO910
925 REM PRINT QUALITIES
930 CLS:PRINT"RATE "T$:PRINT"ON EACH QUALITY":PRINT
935 FOR Y=1 TO NQ
940 PRINT TAB(0)CH$(Y)TAB(27)"";
945 GOSUB 960:GOSUB 385
950 NEXT Y
955 GOSUB 1080:RETURN
960 REM 
965 INPUT N
970 IF N<1 OR N>10 OR N<>INT(N) THEN PRINT "ENTER A NUMBER FROM 1 TO 10":GOTO 965
975 RETURN
980 REM 
985 IF LEFT$(US$,1)="F" OR LEFT$(US$,1)="F" THEN GOSUB 1025:RETURN
990 SBJ$="SHE":OBJ$="HER":PS$="HER":TS$="GIRL":T2$="GIRL"
995 CH$(1)="PRETTY FACE"
996 CH$(2)="WELL-BUILT
997 CH$(3)="INTELLIGENT"
998 CH$(4)="PASSIONATE"
999 CH$(5)="THOUGHTFUL AND CONSIDERATE"
1005 CH$(6)="SENSE OF HOMOUR"
1006 CH$(7)="DELICATE AND FEMININE"
1007 CH$(8)="THE RIGHT HEIGHT"
1008 CH$(9)="RICH"
1009 CH$(10)="ATHLETIC/LIKES SPORTS"
1015 RETURN
1020 REM 
1025 SBJ$="HE":OBJ$="HIM":PS$="HIS":TS$="GUY":T2$="BOY"
1030 CH$(1)="HANDSOME":CH$(2)="MUSCULAR":CH$(3)="INTELLIGENT"
1035 CH$(4)="PASSIONATE":CH$(5)="THOUGHTFUL AND CONSIDERATE"
1037 CH$(6)="RICH"
1040 CH$(7)="TOUGH AND MACHO":CH$(8)="THE RIGHT HEIGHT"
1045 CH$(9)="SENSE OF HUMOUR":CH$(10)="WELL-GROOMED"
1050 RETURN
1055 REM WAIT FOR USER
1060 W$="PRESS ANY KEY TO GO ON"
1065 PRINT@480,W$;
1070 I$=INKEY$:IF I$="" THEN 1070
1075 RETURN
1080 REM 
1085 SM=0
1090 FOR P=1 TO NQ
1095 IF F=1 THEN SM=SM+IM(P)
1097 IF F=2 THEN SM=SM+R(X,P)
1100 NEXT P
1105 IF SM>(NQ-1)*10 THEN GOSUB 1110
1107 IF SM<15 THEN GOSUB 1130
1108 RETURN
1110 CLS:PRINT:PRINT"COME OFF IT!"
1115 PRINT:PRINT"NO "TS$" IS THAT PERFECT!"
1120 PRINT:PRINT"BE A LITTLE MORE REALISTIC":PRINT"THE NEXT TIME YOU PLAY!"
1122 GOSUB 1055:IF SM=1000 AND TS$="GIRL" THEN GOSUB 1150:GOSUB 1055
1125 RUN
1130 CLS
1135 PRINT:PRINT"YOU'RE SURE NOT FUSSY!"
1137 PRINT:PRINT"JUST SO LONG AS THE "TS$:PRINT"IS ALIVE AND MOVING, EH?"
1140 GOSUB 1055:RUN
1145 REM 
1150 FOR C=0 TO 8:CLS:FOR W=1 TO 50:NEXT W:NEXT C
1155 CLS:PRINT"IF THE GIRL REALLY IS **** A PERFECT 10 ****"
1157 PRINT:PRINT"THEN CALL TELL HER TO GIVE ME A CALL"
1160 REM 
1165 CLS:FOR X=1 TO S
1175 PRINT
1180 NEXT
1185 S=S+1
1190 RETURN
1195 REM CONVERT NAMES FOR FLASH
1200 C2$="":U2$=""
1205 FORT=1TOLEN(C1$):CC=ASC(MID$(C1$,T,1)):IFCC>64THENCC=CC+32
1207 C2$=C2$+CHR$(CC):NEXTT
1210 FORT=1TOLEN(UN$):CC=ASC(MID$(UN$,T,1)):IFCC>64THENCC=CC+32
1215 U2$=U2$+CHR$(CC):NEXTT
1220 RETURN
1225 FORT=1TO500:NEXTT:RETURN
