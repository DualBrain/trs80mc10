10 CLS0:CLEAR1500:GOSUB430:FORZZ=0 TO3:PRINT@ZZ*128,;
11 PRINTCHR$(128)CHR$(128)MID$(P1$,ZZ*28+1,28)CHR$(128)CHR$(128);
12 PRINTCHR$(128)CHR$(128)MID$(P2$,ZZ*28+1,28)CHR$(128)CHR$(128);
13 PRINTCHR$(128)CHR$(128)MID$(P3$,ZZ*28+1,28)CHR$(128)CHR$(128);
14 PRINTCHR$(128)CHR$(128)MID$(P4$,ZZ*28+1,28)CHR$(128);
15 NEXT:PRINT@5*32+10,"MOON EFFECTS";:PRINT@9*32+6,"BY BRADLEY E. SCHAEFER";
20 FORZZ=1 TO2500:NEXT:CLS0:PRINT"MOON'S PHASE, DISTANCE AND POS":PRINT
30 P2=2*3.14159:REM RADIANS IN A FULL CIRCLE
40 INPUT"INPUT year,mn,dy";Y,M,D
50 YY=Y-INT((12-M)/10)
60 MM=M+9:IF MM>=12 THEN MM=MM-12
70 K1=INT(365.25*(YY+4712))
80 K2=INT(30.6*MM+.5)
90 K3=INT(INT((YY/100)+49)*.75)-38
100 J=K1+K2+D+59:REM JD FOR DATES IN JULIAN CALENDAR
110 IF J>2299160 THEN J=J-K3:REM FOR GREGORIAN CALENDAR
120 REM J IS THE JULIAN DATE AT 12H UT ON DAY IN QUESTION
130 REM
140 REM CALCULATE ILLUMINATION (SYNODIC) PHASE
150 V=(J-2451550.1)/29.530588853:GOSUB 400:IP=V
160 AG=IP*29.53:REM MOON'S AGE IN DAYS
170 IP=IP*P2:REM    CONVERT PHASE TO RADIANS
180 REM
190 REM CALCULATE DISTANCE FROM ANOMALISTIC PHASE
200 V=(J-2451562.2)/27.55454988:GOSUB 400:DP=V
210 DP=DP*P2:REM CONVERT TO RADIANS
220 DI=60.4-3.3*COS(DP)-.6*COS(2*IP-DP)-.5*COS(2*IP)
230 REM
240 REM CALCULATE LATITUDE FROM NODAL (DRACONIC) PHASE
250 V=(J-2451565.2)/27.212220817:GOSUB 400:NP=V
260 NP=NP*P2:REM CONVERT TO RADIANS
270 LA=5.1*SIN(NP)
280 REM
290 REM CALCULATE LONGITUDE FROM SIDEREAL MOTION
300 V=(J-2451555.8)/27.321582241:GOSUB 400:RP=V
310 LO=360*RP+6.3*SIN(DP)+1.3*SIN(2*IP-DP)+.7*SIN(2*IP)
320 REM
330 PRINT "MOON'S AGE FROM NEW (DAYS):";LEFT$(STR$(AG)+"     ",5);
340 PRINT "DISTANCE (EARTH RADII):    ";LEFT$(STR$(DI)+"     ",5);
350 PRINT "ECLIPTIC LAT. (DEGREES):   ";LEFT$(STR$(LA)+"     ",5);
360 PRINT "ECLIPTIC LONG (DEGREES):   ";LEFT$(STR$(LO)+"     ",5);
365 PRINTSTRING$(32,128);:IFAG>28 THENAG=28
366 PRINTSTRING$(14,128)MID$(P1$,INT(AG)*4+1,4)STRING$(14,128);
367 PRINTSTRING$(14,128)MID$(P2$,INT(AG)*4+1,4)STRING$(14,128);
368 PRINTSTRING$(14,128)MID$(P3$,INT(AG)*4+1,4)STRING$(14,128);
369 PRINTSTRING$(14,128)MID$(P4$,INT(AG)*4+1,4)STRING$(14,128);
370 INPUT "CONTINUE (Y OR N)";Q$
380 IF Q$<>"N" THEN 40
390 END
400 REM NORMALIZE VALUES TO RANGE 0 TO 1
410 V=V-INT(V):IF V<0 THEN V=V+1
420 RETURN
430 P1$=CHR$(193)+CHR$(204)+CHR$(204)+CHR$(194)+CHR$(128)+CHR$(128)+CHR$(128)+CHR$(194)+CHR$(192)+CHR$(192)+CHR$(196)
431 P1$=P1$+CHR$(194)+CHR$(192)+CHR$(192)+CHR$(196)+CHR$(194)+CHR$(192)+CHR$(192)+CHR$(205)+CHR$(194)+CHR$(192)+CHR$(196)
432 P1$=P1$+CHR$(207)+CHR$(194)+CHR$(192)+CHR$(197)+CHR$(207)+CHR$(194)+CHR$(192)+CHR$(197)+CHR$(207)+CHR$(194)+CHR$(192)
433 P1$=P1$+CHR$(197)+CHR$(207)+CHR$(194)+CHR$(192)+CHR$(197)+CHR$(207)+CHR$(194)+CHR$(192)+CHR$(197)+CHR$(207)+CHR$(194)
434 P1$=P1$+CHR$(192)+CHR$(199)+CHR$(207)+CHR$(194)+CHR$(193)+CHR$(207)+CHR$(207)+CHR$(194)+CHR$(193)+CHR$(207)+CHR$(207)
435 P1$=P1$+CHR$(194)+CHR$(193)+CHR$(207)+CHR$(207)+CHR$(194)+CHR$(193)+CHR$(207)+CHR$(207)+CHR$(194)+CHR$(193)+CHR$(207)
436 P1$=P1$+CHR$(207)+CHR$(194)+CHR$(193)+CHR$(207)+CHR$(203)+CHR$(128)+CHR$(193)+CHR$(207)+CHR$(202)+CHR$(128)+CHR$(193)
437 P1$=P1$+CHR$(207)+CHR$(202)+CHR$(128)+CHR$(193)+CHR$(207)+CHR$(202)+CHR$(128)+CHR$(193)+CHR$(207)+CHR$(202)+CHR$(128)
438 P1$=P1$+CHR$(193)+CHR$(207)+CHR$(202)+CHR$(192)+CHR$(193)+CHR$(207)+CHR$(200)+CHR$(128)+CHR$(193)+CHR$(206)+CHR$(128)
439 P1$=P1$+CHR$(128)+CHR$(193)+CHR$(200)+CHR$(128)+CHR$(128)+CHR$(193)+CHR$(200)+CHR$(128)+CHR$(128)+CHR$(193)+CHR$(128)
440 P1$=P1$+CHR$(128)+CHR$(128)+CHR$(193)+CHR$(204)+CHR$(204)+CHR$(194)
441 P2$=CHR$(202)+CHR$(128)+CHR$(128)+CHR$(197)+CHR$(128)+CHR$(128)+CHR$(128)+CHR$(197)+CHR$(192)+CHR$(192)+CHR$(128)
442 P2$=P2$+CHR$(197)+CHR$(192)+CHR$(192)+CHR$(128)+CHR$(207)+CHR$(192)+CHR$(192)+CHR$(192)+CHR$(207)+CHR$(192)+CHR$(192)
443 P2$=P2$+CHR$(197)+CHR$(207)+CHR$(192)+CHR$(192)+CHR$(207)+CHR$(207)+CHR$(192)+CHR$(197)+CHR$(207)+CHR$(207)+CHR$(192)
444 P2$=P2$+CHR$(199)+CHR$(207)+CHR$(207)+CHR$(192)+CHR$(207)+CHR$(207)+CHR$(207)+CHR$(193)+CHR$(207)+CHR$(207)+CHR$(207)
445 P2$=P2$+CHR$(193)+CHR$(207)+CHR$(207)+CHR$(207)+CHR$(197)+CHR$(207)+CHR$(207)+CHR$(207)+CHR$(199)+CHR$(207)+CHR$(207)
446 P2$=P2$+CHR$(207)+CHR$(207)+CHR$(207)+CHR$(207)+CHR$(207)+CHR$(207)+CHR$(207)+CHR$(207)+CHR$(203)+CHR$(207)+CHR$(207)
447 P2$=P2$+CHR$(207)+CHR$(202)+CHR$(207)+CHR$(207)+CHR$(207)+CHR$(194)+CHR$(207)+CHR$(207)+CHR$(207)+CHR$(194)+CHR$(207)
448 P2$=P2$+CHR$(207)+CHR$(207)+CHR$(128)+CHR$(207)+CHR$(207)+CHR$(203)+CHR$(192)+CHR$(207)+CHR$(207)+CHR$(202)+CHR$(128)
449 P2$=P2$+CHR$(207)+CHR$(207)+CHR$(128)+CHR$(128)+CHR$(207)+CHR$(202)+CHR$(128)+CHR$(128)+CHR$(207)+CHR$(128)+CHR$(128)
450 P2$=P2$+CHR$(128)+CHR$(207)+CHR$(128)+CHR$(128)+CHR$(128)+CHR$(202)+CHR$(128)+CHR$(128)+CHR$(128)+CHR$(202)+CHR$(128)
451 P2$=P2$+CHR$(128)+CHR$(128)+CHR$(202)+CHR$(128)+CHR$(128)+CHR$(197)
452 P3$=CHR$(201)+CHR$(128)+CHR$(128)+CHR$(198)+CHR$(128)+CHR$(128)+CHR$(128)+CHR$(198)+CHR$(192)+CHR$(128)+CHR$(128)
453 P3$=P3$+CHR$(198)+CHR$(192)+CHR$(192)+CHR$(128)+CHR$(206)+CHR$(192)+CHR$(192)+CHR$(193)+CHR$(206)+CHR$(192)+CHR$(192)
454 P3$=P3$+CHR$(199)+CHR$(206)+CHR$(192)+CHR$(193)+CHR$(207)+CHR$(206)+CHR$(192)+CHR$(197)+CHR$(207)+CHR$(206)+CHR$(192)
455 P3$=P3$+CHR$(197)+CHR$(207)+CHR$(206)+CHR$(192)+CHR$(205)+CHR$(207)+CHR$(206)+CHR$(192)+CHR$(205)+CHR$(207)+CHR$(206)
456 P3$=P3$+CHR$(192)+CHR$(207)+CHR$(207)+CHR$(206)+CHR$(197)+CHR$(207)+CHR$(207)+CHR$(206)+CHR$(197)+CHR$(207)+CHR$(207)
457 P3$=P3$+CHR$(206)+CHR$(205)+CHR$(207)+CHR$(207)+CHR$(206)+CHR$(205)+CHR$(207)+CHR$(207)+CHR$(202)+CHR$(205)+CHR$(207)
458 P3$=P3$+CHR$(207)+CHR$(202)+CHR$(205)+CHR$(207)+CHR$(207)+CHR$(128)+CHR$(205)+CHR$(207)+CHR$(206)+CHR$(192)+CHR$(205)
459 P3$=P3$+CHR$(207)+CHR$(206)+CHR$(128)+CHR$(205)+CHR$(207)+CHR$(202)+CHR$(128)+CHR$(205)+CHR$(207)+CHR$(202)+CHR$(128)
460 P3$=P3$+CHR$(205)+CHR$(207)+CHR$(194)+CHR$(128)+CHR$(205)+CHR$(203)+CHR$(128)+CHR$(128)+CHR$(205)+CHR$(194)+CHR$(128)
461 P3$=P3$+CHR$(128)+CHR$(205)+CHR$(128)+CHR$(128)+CHR$(128)+CHR$(201)+CHR$(128)+CHR$(128)+CHR$(128)+CHR$(201)+CHR$(128)
462 P3$=P3$+CHR$(128)+CHR$(128)+CHR$(201)+CHR$(128)+CHR$(128)+CHR$(198)
463 P4$=CHR$(128)+CHR$(204)+CHR$(204)+CHR$(128)+CHR$(128)+CHR$(128)+CHR$(128)+CHR$(128)+CHR$(128)+CHR$(192)+CHR$(196)
464 P4$=P4$+CHR$(128)+CHR$(128)+CHR$(192)+CHR$(196)+CHR$(128)+CHR$(128)+CHR$(192)+CHR$(204)+CHR$(128)+CHR$(128)+CHR$(196)
465 P4$=P4$+CHR$(204)+CHR$(192)+CHR$(128)+CHR$(196)+CHR$(204)+CHR$(128)+CHR$(128)+CHR$(196)+CHR$(204)+CHR$(128)+CHR$(128)
466 P4$=P4$+CHR$(196)+CHR$(204)+CHR$(192)+CHR$(192)+CHR$(196)+CHR$(204)+CHR$(128)+CHR$(128)+CHR$(196)+CHR$(204)+CHR$(128)
467 P4$=P4$+CHR$(192)+CHR$(196)+CHR$(204)+CHR$(128)+CHR$(128)+CHR$(204)+CHR$(204)+CHR$(128)+CHR$(128)+CHR$(204)+CHR$(204)
468 P4$=P4$+CHR$(128)+CHR$(128)+CHR$(204)+CHR$(204)+CHR$(128)+CHR$(128)+CHR$(204)+CHR$(204)+CHR$(128)+CHR$(128)+CHR$(204)
469 P4$=P4$+CHR$(204)+CHR$(128)+CHR$(128)+CHR$(204)+CHR$(200)+CHR$(128)+CHR$(128)+CHR$(204)+CHR$(200)+CHR$(128)+CHR$(128)
470 P4$=P4$+CHR$(204)+CHR$(200)+CHR$(128)+CHR$(128)+CHR$(204)+CHR$(200)+CHR$(128)+CHR$(128)+CHR$(204)+CHR$(200)+CHR$(128)
471 P4$=P4$+CHR$(128)+CHR$(204)+CHR$(200)+CHR$(128)+CHR$(128)+CHR$(204)+CHR$(200)+CHR$(128)+CHR$(192)+CHR$(204)+CHR$(192)
472 P4$=P4$+CHR$(128)+CHR$(128)+CHR$(200)+CHR$(128)+CHR$(128)+CHR$(128)+CHR$(200)+CHR$(128)+CHR$(128)+CHR$(128)+CHR$(128)
473 P4$=P4$+CHR$(128)+CHR$(128)+CHR$(128)+CHR$(204)+CHR$(204)+CHR$(128)
474 RETURN
480 REM THIS PROGRAM HELPS ANYONE WHO NEEDS TO KNOW THE MOON'S 
490 REM PHASE (AGE), DISTANCE, AND POSITION ALONG THE ECLIPTIC ON
500 REM ANY DATE WITHIN SEVERAL THOUSAND YEARS IN THE PAST OR FUTURE.
510 REM TO ILLUSTRATE ITS APPLICATION, BRADLEY SCHAEFER APPLIED IT 
520 REM TO A NUMBER OF FAMOUS EVENTS INFLUENCED BY THE MOON IN 
530 REM WORLD WAR II.  HIS ARTICLE APPEARED IN SKY & TELESCOPE FOR
540 REM APRIL 1994, PAGE 86.
550 REM COCO EDITS BY JIM GERRIE
