10 REM MOON EFFECTS BY BRADLEY E. SCHAEFER
20 CLS:CLEAR1000:GOSUB430:PRINT"MOON'S PHASE, DISTANCE AND POS"
30 P2=2*3.14159:REM RADIANS IN A FULL CIRCLE
40 PRINT:PRINT "INPUT year, month, day":INPUT Y,M,D
50 YY=Y-INT((12-M)/10)
60 MM=M+9:IF MM>=12 THEN MM=MM-12
70 K1=INT(365.25*(YY+4712))
80 K2=INT(30.6*MM+.5)
90 K3=INT(INT((YY/100)+49)*.75)-38
100 J=K1+K2+D+59:REM JD FOR DATES IN JULIAN CALENDAR
110 IF J>2299160 THEN J=J-K3:REM FOR GREGORIAN CALENDAR
120 REM J IS THE JULIAN DATE AT 12H UT ON DAY IN QUESTION
130 REM
140 REM CALCULATE ILLUMINATION (SYNODIC) PHASE
150 V=(J-2451550.1)/29.530588853:GOSUB 400:IP=V
160 AG=IP*29.53:REM MOON'S AGE IN DAYS
170 IP=IP*P2:REM    CONVERT PHASE TO RADIANS
180 REM
190 REM CALCULATE DISTANCE FROM ANOMALISTIC PHASE
200 V=(J-2451562.2)/27.55454988:GOSUB 400:DP=V
210 DP=DP*P2:REM CONVERT TO RADIANS
220 DI=60.4-3.3*COS(DP)-.6*COS(2*IP-DP)-.5*COS(2*IP)
230 REM
240 REM CALCULATE LATITUDE FROM NODAL (DRACONIC) PHASE
250 V=(J-2451565.2)/27.212220817:GOSUB 400:NP=V
260 NP=NP*P2:REM CONVERT TO RADIANS
270 LA=5.1*SIN(NP)
280 REM
290 REM CALCULATE LONGITUDE FROM SIDEREAL MOTION
300 V=(J-2451555.8)/27.321582241:GOSUB 400:RP=V
310 LO=360*RP+6.3*SIN(DP)+1.3*SIN(2*IP-DP)+.7*SIN(2*IP)
320 REM
330 PRINT "MOON'S AGE FROM NEW (DAYS):";LEFT$(STR$(AG),3)
340 PRINT "DISTANCE (EARTH RADII):    ";LEFT$(STR$(DI),3)
350 PRINT "ECLIPTIC LAT. (DEGREES):   ";LEFT$(STR$(LA),3)
360 PRINT "ECLIPTIC LONG (DEGREES):   ";LEFT$(STR$(LO),3)
365 PRINT MID$(P1$,INT(AG)*4+1,4)
366 PRINT MID$(P2$,INT(AG)*4+1,4)
367 PRINT MID$(P3$,INT(AG)*4+1,4)
368 PRINT MID$(P4$,INT(AG)*4+1,4)
370 PRINT:INPUT "CONTINUE (Y OR N)";Q$
380 IF Q$<>"N" AND Q$<>"N" THEN 40
390 END
400 REM NORMALIZE VALUES TO RANGE 0 TO 1
410 V=V-INT(V): IF V<0 THEN V=V+1
420 RETURN
430 P1$="ÀÀ€€ÀÀÄÂÀÀÄÂÀÀÍÂÀÄÏÂÀÅÏÂÀÅÏÂÀÅÏÂÀÅÏÂÀÅÏÂÀÇÏÂÁÏÏÂÁÏÏÂÁÏÏÂÁÏÏÂÁÏÏÂÁÏË€ÁÏÊ€ÁÏÊ€ÁÏÊ€ÁÏÊ€ÁÏÊÀÁÏÈ€ÁÎ€€ÁÈ€€ÁÈ€€Á€€€ÁÌÌÂ"
431 P2$="À€€€ÀÀ€ÅÀÀ€ÏÀÀÀÏÀÀÅÏÀÀÏÏÀÅÏÏÀÇÏÏÀÏÏÏÁÏÏÏÁÏÏÏÅÏÏÏÇÏÏÏÏÏÏÏÏÏÏËÏÏÏÊÏÏÏÂÏÏÏÂÏÏÏ€ÏÏËÀÏÏÊ€ÏÏ€€ÏÊ€€Ï€€€Ï€€€Ê€€€Ê€€€Ê€€Å"
432 P3$="À€€€À€€ÆÀÀ€ÎÀÀÁÎÀÀÇÎÀÁÏÎÀÅÏÎÀÅÏÎÀÍÏÎÀÍÏÎÀÏÏÎÅÏÏÎÅÏÏÎÍÏÏÎÍÏÏÊÍÏÏÊÍÏÏ€ÍÏÎÀÍÏÎ€ÍÏÊ€ÍÏÊ€ÍÏÂ€ÍË€€ÍÂ€€Í€€€É€€€É€€€É€€Æ"
433 P4$="€ÀÀÀ€ÀÄ€€ÀÄ€€ÀÌ€€ÄÌÀ€ÄÌ€€ÄÌ€€ÄÌÀÀÄÌ€€ÄÌ€ÀÄÌ€€ÌÌ€€ÌÌ€€ÌÌ€€ÌÌ€€ÌÌ€€ÌÈ€€ÌÈ€€ÌÈ€€ÌÈ€€ÌÈ€€ÌÈ€€ÌÈ€ÀÌÀ€€È€€€È€€€€€€€ÌÌ€"
434 RETURN
440 REM THIS PROGRAM HELPS ANYONE WHO NEEDS TO KNOW THE MOON'S 
450 REM PHASE (AGE), DISTANCE, AND POSITION ALONG THE ECLIPTIC ON
460 REM ANY DATE WITHIN SEVERAL THOUSAND YEARS IN THE PAST OR FUTURE.
470 REM TO ILLUSTRATE ITS APPLICATION, BRADLEY SCHAEFER APPLIED IT 
480 REM TO A NUMBER OF FAMOUS EVENTS INFLUENCED BY THE MOON IN 
490 REM WORLD WAR II.  HIS ARTICLE APPEARED IN SKY & TELESCOPE FOR
500 REM APRIL 1994, PAGE 86.
