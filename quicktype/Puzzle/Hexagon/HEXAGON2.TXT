0 CLS:DIMC1,CC,M$,MC,C$(5,5),A$(5,5),B$(5,5):MC=16384:GOTO11
7 PRINTM$;:CC=((PEEK(17024)AND1)*256+PEEK(17025))-1+MC:FORC1=CC-LEN(M$)+1TOCC:POKEC1,PEEK(C1)-64:NEXT:RETURN
11 GOSUB9600
45 C=0:EG=0
50 CLS:PRINT@0*32+7,"H E X A G O N A L":PRINT@1*32+7,"ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒ"
55 PRINT@2*32+5,"BY J. WINCHESTER 1984"
60 K=0:G$="1":FORP=1TO5:C$(1,P)="0":C$(2,P)="0":C$(3,P)="0":C$(4,P)="0":C$(5,P)="0":NEXT
110 B$(1,1)="2":B$(2,1)="4":B$(3,1)="4":B$(4,1)="4":B$(5,1)="3"
115 B$(1,2)="4":B$(2,2)="6":B$(3,2)="6":B$(4,2)="6":B$(5,2)="4"
120 B$(1,3)="4":B$(2,3)="6":B$(3,3)="6":B$(4,3)="6":B$(5,3)="4"
125 B$(1,4)="4":B$(2,4)="6":B$(3,4)="6":B$(4,4)="6":B$(5,4)="4"
130 B$(1,5)="3":B$(2,5)="4":B$(3,5)="4":B$(4,5)="4":B$(5,5)="2"
135 FORP=1TO5:A$(1,P)="0":A$(2,P)="0":A$(3,P)="0":A$(4,P)="0":A$(5,P)="0":NEXT
140 P=10:O=5
148 KK=0
149 PRINT@P*32+O,"† Ž‡";:PRINT@-32+O+P*32,"Žƒ†";:PRINT@33+O+P*32,"ƒ‡";
150 P=P-1:O=O+2:KK=KK+1
153 IFKK<>5THEN149
154 K=K+1:P=P+6:O=O-8
160 IFK<>5THEN148
163 GOSUB9500
170 P=10:O=6:X=1:Y=1
182 IFC$(X,Y)="1"THENPRINT@P*32+O,A$(X,Y);
184 IFC$(X,Y)="0"THENPRINT@P*32+O," ";
186 IFC$(X,Y)="2"THENPRINT@P*32+O,;:M$=A$(X,Y):GOSUB7
190 GOTO8000
200 GOSUB9000:IFEG=1THEN9050
201 PRINT@148,"SCORES:";:PRINT@182,"P1 =";X5;:PRINT@214,"P2 =";Y5;
203 PRINT@13*32+22,"PLAYER ";G$;:PRINT@15*32+18,"EG. ENTER A1";
204 PRINT@14*32+22,"                      E ƒ‡ ";:PRINT@14*32+22,;:INPUTZ$
205 X=ASC(MID$(Z$+"0",1,1))-64:IFX<1ORX>6THENSOUND1,1:GOTO201
210 Y=VAL(MID$(Z$,2,1)):IFY<1ORY>6THENSOUND1,1:GOTO201
211 IFC$(X,Y)="2"ANDG$="1"THENSOUND1,1:GOTO201
212 IFC$(X,Y)="1"ANDG$="2"THENSOUND1,1:GOTO201
213 C=C+1
215 A$(X,Y)=RIGHT$(STR$(VAL(A$(X,Y))+1),1)
220 J=0
221 C$(X,Y)=G$
222 GOSUB700
225 IFG$="1"THEN235
230 G$="1"
231 GOTO170
235 G$="2"
240 GOTO170
700 J=J+1:IFJ>10THENGOSUB9000:IFEG=1THENRETURN
710 FORN=1TO5:FORM=1TO5:IFVAL(A$(N,M))>=VAL(B$(N,M))THEN800
720 NEXT:NEXT
730 RETURN
800 IFN<>5THENA$(N+1,M)=RIGHT$(STR$(VAL(A$(N+1,M))+1),1):C$(N+1,M)=G$
805 IFN<>5ANDM<>1THENA$(N+1,M-1)=RIGHT$(STR$(VAL(A$(N+1,M-1))+1),1):C$(N+1,M-1)=G$
810 IFM<>1THENA$(N,M-1)=RIGHT$(STR$(VAL(A$(N,M-1))+1),1):C$(N,M-1)=G$
815 IFM<>5THENA$(N,M+1)=RIGHT$(STR$(VAL(A$(N,M+1))+1),1):C$(N,M+1)=G$
820 IFN<>1ANDM<>5 THENA$(N-1,M+1)=RIGHT$(STR$(VAL(A$(N-1,M+1))+1),1):C$(N-1,M+1)=G$
825 IFN<>1THENA$(N-1,M)=RIGHT$(STR$(VAL(A$(N-1,M))+1),1):C$(N-1,M)=G$
830 A$(N,M)="0":C$(N,M)="0"
840 M=5:NEXT:N=5:NEXT:GOTO700
8000 Y=Y+1:P=P-1:O=O+2:IFY<>6THEN182
8025 Y=1:X=X+1:P=P+6:O=O-8:IFX<>6THEN182
8055 GOTO200
9000 X5=0
9005 Y5=0
9010 FORL=1TO5:FORO=1TO5
9020 IFC$(L,O)="1"THENX5=X5+VAL(A$(L,O))
9025 IFC$(L,O)="2"THENY5=Y5+VAL(A$(L,O))
9030 NEXT:NEXT
9040 IFC>1AND(X5=0ORY5=0)THENEG=1
9045 RETURN
9050 PRINT@148,"SCORES:";:PRINT@182,"P1 =";X5;:PRINT@214,"P2 =";Y5;
9051 IFX5=0THENPRINT@14*32+22,;:M$="WINS!   ":GOSUB7:PRINT@13*32+22,;:M$="PLAYER 2":GOSUB7
9052 IFY5=0THENPRINT@14*32+22,;:M$="WINS!   ":GOSUB7:PRINT@13*32+22,;:M$="PLAYER 1":GOSUB7
9053 FORC1=100TO150STEP5:SOUNDC1,1:NEXT
9056 PRINT@15*32+18,"ANOTHER Y/N?";
9057 Z$=INKEY$:IFZ$=""THEN9057
9058 IFZ$="N"THENEND
9059 IFZ$="Y"THEN45
9060 GOTO9057
9500 FORP=1TO5:PRINT@(10+P)*32+2+2*P,RIGHT$(CHR$(64+P),1);:PRINT@(9-P)*32+2+2*P,RIGHT$(STR$(P),1);:NEXT:RETURN
9600 PRINT@0*32+0,"hexagonal IS FOR 2 AND IS PLAYED";
9602 PRINT"ON A 5X5 GRID OF HEXAGONS."
9605 PRINT" EACH PLAYER ALTERNATELY PLACES"
9610 PRINT"A PIECE IN A HEXAGON."
9615 PRINT" YOU MAY ONLY MOVE TO AN EMPTY"
9620 PRINT"HEXAGON, OR ONE YOU ALREADY HAVE";
9625 PRINT"SOME PIECES IN."
9630 PRINT" EACH TIME YOU MOVE TO A HEXAGON";
9635 PRINT"THE NUMBER OF PIECES IN THAT"
9640 PRINT"HEXAGON INCREASES BY ONE."
9645 PRINT" WHEN THE NUMBER IN ANY HEXAGON"
9650 PRINT"REACHES THE NUMBER OF ADJACENT"
9655 PRINT"HEXAGONS (EG. '6' FOR A HEXAGON"
9660 PRINT"IN THE CENTRE) THEN THAT HEXAGON";
9664 PRINT"EXPLODES. ";:GOSUB9800:CLS
9665 PRINT" WHEN A HEXAGON EXPLODES ITS"
9666 PRINT"NUMBER GOES TO ZERO AND THE"
9670 PRINT"NUMBERS IN ALL THE ADJACENT"
9675 PRINT"HEXAGONS GOES UP BY ONE."
9680 PRINT" ONE OF THESE IN TURN MAY NOW"
9685 PRINT"EXPLODE AND A CHAIN REACTION"
9690 PRINT"CAN OCCUR."
9700 PRINT" HOWEVER IF YOU START A REACTION";
9705 PRINT"ALL THE SQUARES INTO WHICH THE"
9710 PRINT"EXPLOSION OCCURS BECOME YOURS."
9715 PRINT" YOU WIN WHEN YOUR OPPONENT HAS"
9716 PRINT"NO PIECES."
9718 PRINT" PLAYER ONE'S PIECES ARE SHOWN"
9719 PRINT"AS NORMAL NUMBERS, WHILE PLAYER"
9720 PRINT"TWO'S PIECES ARE IN INVERSE"
9721 PRINT"VIDEO. ";:GOSUB9800
9730 RETURN
9800 M$="HIT A KEY":GOSUB7
9810 Z$=INKEY$:IFZ$=""THEN9810
9820 FORC1=1TO9:PRINTCHR$(8);:NEXT:RETURN
9900 REM MC-10 EDITS JIM GERRIE
