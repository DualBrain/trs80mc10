10 CLEAR2000:DIMM(50):CLS(0):X=RND(-(PEEK(9)*256+PEEK(10))):GOSUB1000
20 B$=CHR$(128):G$=CHR$(143):Y$=CHR$(159):U$=CHR$(175):R$=CHR$(191)
30 FOR X=1 TO 5:G1$=G1$+CHR$(140):G2$=G2$+CHR$(131):Y1$=Y1$+CHR$(156):Y2$=Y2$+CHR$(147):B1$=B1$+CHR$(172)
35 B2$=B2$+CHR$(163):R1$=R1$+CHR$(188):R2$=R2$+CHR$(179):B5$=B5$+B$:NEXTX 
40 GT$=CHR$(142)+G1$+CHR$(141)+B$ 
50 GB$=CHR$(139)+G2$+CHR$(135)+B$
60 GM$=CHR$(138)+B5$+CHR$(133)+B$ 
70 YT$=CHR$(158)+Y1$+CHR$(157)+B$ 
80 YB$=CHR$(155)+Y2$+CHR$(151)+B$ 
90 YM$=CHR$(154)+B5$+CHR$(149)+B$ 
100 BM$=CHR$(170)+B5$+CHR$(165)+B$
110 BB$=CHR$(171)+B2$+CHR$(167)+B$ 
120 BT$=CHR$(174)+B1$+CHR$(173)+B$ 
130 RT$=CHR$(190)+R1$+CHR$(189)+B$ 
140 RM$=CHR$(186)+B5$+CHR$(181)+B$ 
150 RB$=CHR$(187)+R2$+CHR$(183)+B$ 
160 M$=GM$+YM$+BM$+RM$ 
170 FOR X=1TO7:GN$=GN$+G$:YN$=YN$+Y$:UN$=UN$+U$:RN$=RN$+R$:NEXTX 
180 GN$=GN$+B$:YN$=YN$+B$:UN$=UN$+B$:RN$=RN$+B$:MG$=GN$+YM$+BM$+RM$:MY$=GM$+YN$+BM$+RM$:MB$=GM$+YM$+UN$+RM$
181 MR$=GM$+YM$+BM$+RN$
190 CLS 2 
200 PRINT@71,"SAY HELLO TO ZYMON"; 
210 PRINT@134,"HIT ANY KEY TO BEGIN";
220 Z=1 
230 D=RND(Z) 
240 IF INKEY$<>"" THEN 250
241 GOTO240
250 CLS0 
260 T=0 
270 GOSUB 590:GOSUB 610 
280 FOR R=1 TO 50 
290 M(R)=RND(4) 
300 NEXT R 
310 X=1 
320 FOR I=1 TO X:ON M(I) GOSUB 630,670,710,750:NEXTI
330 I=0 
340 FOR J=1 TO X 
350 Z$=INKEY$ 
360 IF Z$<>""THEN 400
370 T=T+1 
380 IF T>=125 THEN 490 
390 GOTO 350 
400 IF VAL(Z$)<>M(J) THEN 460 
410 ON M(J) GOSUB 630,670,710,750
420 NEXT J 
430 FOR T=1 TO 450:NEXT T
440 X=X+1:T=0 
450 GOTO 320
460 CLS(7) 
470 PRINT@168,"WRONG COLOR MAN!";
480 SOUND 1,30:CLS(0):GOTO 520 
490 CLS(8)
500 PRINT@168,"HEY! GO FASTER!";
510 SOUND 7,30:CLS(0) 
520 CLS 1: PRINT@139,"SORRY BUD!";
530 PRINT@163,"DO YOU WISH TO TRY AGAIN?";
540 PRINT@327,"YOU GOT"X-1"COLORS"; 
550 Y$=INKEY$ 
560 IF Y$=""THEN 550 
570 IF Y$="Y"THEN 190 
580 IF Y$="N"THEN END
585 GOTO 550 
590 PRINT@290," 1 ";:PRINT@298," 2 ";:PRINT@306," 3 ";:PRINT@314," 4 ";
600 RETURN 
610 PRINT@32,GT$+YT$+BT$+RT$+M$+M$+M$+M$+GB$+YB$+BB$+RB$;
620 RETURN 
630 PRINT@32,GN$+YT$+BT$+RT$+MG$+MG$+MG$+MG$+GN$+YB$+BB$+RB$;
640 SOUND 40,5 
650 GOSUB 610 
660 RETURN 
670 PRINT@32,GT$+YN$+BT$+RT$+MY$+MY$+MY$+MY$+GB$+YN$+BB$+RB$;
680 SOUND 67,5
690 GOSUB 610 
700 RETURN 
710 PRINT@32,GT$+YT$+UN$+RT$+MB$+MB$+MB$+MB$+GB$+YB$+UN$+RB$;
720 SOUND 98,5
730 GOSUB 610 
740 RETURN 
750 PRINT@32,GT$+YT$+BT$+RN$+MR$+MR$+MR$+MR$+GB$+YB$+BB$+RN$;
760 SOUND 134,5
770 GOSUB 610 
780 RETURN
1000 CLS
1001 PRINT"             ZYMON              ";
1002 PRINT"    A STRATEGIC MEMORY GAME     ";
1003 PRINT"         BY ROBERT KERN         ";
1004 PRINT"                                ";
1005 PRINT"ZYMON IS PATTERNED AFTER A FAM- ";
1006 PRINT"ILIAR GAME OF A SIMILAR NAME.   ";
1007 PRINT"TO PLAY THE GAME, ALL YOU HAVE  ";
1008 PRINT"TO DO IS FOLLOW THE FLASHING    "; 
1009 PRINT"BOX BY MATCHING THE PATTERN IT  ";
1010 PRINT"CREATES. TO MATCH THE PATTERN,  ";
1011 PRINT"PRESS THE KEY BELOW EACH SQUARE ";
1012 PRINT"THAT LIGHTS UP (1-4), IN THE    ";
1013 PRINT"SAME ORDER THE COMPUTER DOES.   ";
1014 PRINT"SEE HOW LONG YOU CAN LAST AND   ";
1015 PRINT"HAVE A LOT OF FUN.              ";
1016 PRINT"            hit a key         ";
1020 A$=INKEY$:IFA$=""THEN1020
1030 RETURN
