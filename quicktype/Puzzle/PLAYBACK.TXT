
1 REM PLAYBACK BY JONATHAN SKEAN
2 REM FOR MC-10 'POCO-COCO'
3 REM ALSO FOR 4/16/32/64K COCO   EXT. BASIC NOT REQUIRED.
4 REM COPYRIGHT WAIVED FOR NON- COMMERCIAL USE AND REPRODUCTION.
5 CLS:PRINT"  POCO WILL GENERATE A PATTERN    OF SOUNDS AND COLORS. REPEAT    THE PATTERN USING THE W-A-S-Z   KEYS.
10 PRINT:PRINT"    PRESS ANY KEY TO START.
15 K$=INKEY$:IFK$=""THENR=RND(20):GOTO15
20 CLS0:X0=31:Y0=5:LS=0:C=4:GOSUB50
25 X0=19:Y0=15:C=1:GOSUB50
30 X0=43:Y0=15:C=3:GOSUB50
35 X0=31:Y0=25:C=2:GOSUB50
40 PRINT@0,"HIGH SCORE";:PRINT@32,"          ";:PRINT@64,"LAST SCORE";:PRINT@96,"          ";
45 PRINT@52," PLAY BACK! ";:GOTO90
50 FORX=-5TO6:FORY=-5TO-4
55 GOSUB85:NEXTY,X
60 FORY=-3TO2
65 FORX=-5TO-4:GOSUB85:NEXTX
70 FORX=5TO6:GOSUB85:NEXTX,Y
75 FORX=-5TO6:FORY=3TO4
80 GOSUB85:NEXTY,X:RETURN
85 SET(X0+X,Y0+Y,C):RETURN
90 I$=""
95 FORD=1TO400:NEXTD:R=RND(4):I$=I$+RIGHT$(STR$(R),1)
100 FORI=1TOLEN(I$):P=VAL(MID$(I$,I,1)):GOSUB215:NEXTI
105 CU=0
110 CT=0
115 K$=INKEY$:IFK$=""THENCT=CT+1:IFCT>500THEN180
120 IFK$=""THEN115
125 P=0:IFK$="W"THENP=1
130 IFK$="Z"THENP=2
135 IFK$="A"THENP=3
140 IFK$="S"THENP=4
145 IFP=0THEN115
150 CU=CU+1
155 IFP<>VAL(MID$(I$,CU,1))THEN180
160 CL=CU
165 GOSUB215:CT=0
170 IFCU=LEN(I$)THEN95
175 GOTO115
180 PRINT@99,LEN(I$)-1"   ";:CU=0:FORJ=1TO5:SOUND45,1:SOUND25,1:SOUND58,1:NEXT
185 PRINT@52,"ANOTHER ONE?";
190 IFLEN(I$)-1>LS THENLS=LEN(I$)-1:CL=0
195 PRINT@35,INT(LS)"   ";
200 K$=INKEY$:IFK$=""THEN200
205 IFK$<>"N"THEN45
210 PRINT@425,"GAME";:PRINT@435,"OVER";:FORD=1TO9:PRINTCHR$(128);:NEXTD::END
215 S=INT(16-LEN(I$))/3:IFS<1THENS=1
220 ONP GOSUB225,230,235,240:ONP GOSUB245,250,255,260:ONP GOSUB265,270,275,280:RETURN
225 SET(31,5,4):RETURN
230 SET(31,25,2):RETURN
235 SET(19,15,1):RETURN
240 SET(43,15,3):RETURN
245 SOUND185,S:RETURN
250 SOUND108,S:RETURN
255 SOUND140,S:RETURN
260 SOUND159,S:RETURN
265 RESET(31,5):RETURN
270 RESET(31,25):RETURN
275 RESET(19,15):RETURN
280 RESET(43,15):RETURN
