6 GOSUB10005:GOSUB10000
7 REM
10 GOTO7000
100 INPUT"LARGEST X-VALUE:";WX
101 IFWX<4 OR WX>30 THENPRINT"BETWEEN 4 AND 30 ":PRINT:GOTO100
102 PRINT:PRINT
103 FORK=1 TOWX:Q=RND(9):NEXT
104 INPUT"LARGEST Y-VALUE:";WY
105 IFWY<4 ORWY>15 THENPRINT"BETWEEN 4 AND 15 ":PRINT:GOTO104
106 IFINT(WX/2)<>WX/2 THENWX=WX+1
107 IFINT(WY/2)<>WY/2 THENWY=WY+1
120 PRINT:PRINT:INPUT"DIFFICULTY LEVEL 1-4";S:IFS<1 ORS>4 THEN120
200 DIMA(WX,WY),O,C,K,Q,A1,A3:O=.5
220 PRINT:PRINT"PLEASE WAIT...":X=INT(WX/2):Y=INT(WY/2):A(X,Y)=1
230 IFRND(2)>1.1 THENXS=SGN(RND(2)-1.1):YS=0:GOTO250
240 XS=0:YS=SGN(RND(2)-1.1)
250 FORI=0 TO1:X=X+XS:Y=Y+YS:A(X,Y)=1
255 IFF THENIFX=1 OR X=WX-1 OR Y=1 OR Y=1 OR Y=WY-1 THEN280
260 IFX=0 OR X=WX OR Y=0 OR Y=WY THEN280
270 NEXT:GOTO230
280 F=F+1:IFF<S THEN220
2000 CLS0
2010 X=INT(WX/2):Y=INT(WY/2):X1=X:Y1=Y:R=0
2020 X(0)=1:Y(0)=0:X(1)=0:Y(1)=-1:X(2)=-1:Y(2)=0:X(3)=0:Y(3)=1
2030 RR=R+1:IFRR>3 THENRR=0
2035 XR=X(RR):YR=Y(RR)
2040 SOUND3,1:X1=X:Y1=Y:I=1.5:CLS0:GOSUB3000
2045 FORC=4 TO59:SET(C,4,6):SET(C,27,6):NEXT
2046 FORC=4 TO27:SET(4,C,6):SET(59,C,6):NEXT
2050 I=I+.5:X1=X1+X(R):Y1=Y1+Y(R)
2051 IFX1<0 OR X1>WX OR Y1<0 OR Y1>WY THEN GOSUB3300:GOTO2065
2052 IFA(X1,Y1)=0 THEN2065
2055 GOSUB3000
2060 GOTO2050
2065 GOSUB4000
2070 A$=INKEY$
2075 IFA$="X" THEN GOSUB5000:GOTO2040
2080 IFA$="A" THENR=R-1:GOTO2200
2085 IFA$="S" THENR=R+1:GOTO2200
2090 IFA$="W" THEN2100
2095 GOTO2070
2100 X1=X+X(R):Y1=Y+Y(R):IFX1<0 OR X1>WX OR Y1<0 OR Y1>WY THEN6000
2110 IFA(X1,Y1)=0 THEN2070
2115 X=X1:Y=Y1:GOTO2030
2200 SOUND31,1:IFR<0 THENR=3:GOTO2030
2210 IFR>3 THENR=0
2220 GOTO2030
3000 A1=60+59/I:B1=25+24/I:B2=60-B1:A3=127-A1
3001 B3=B1:B4=B2
3010 C1=60+59/(I-.5):D1=25+24/(I-.5):D2=60-D1:C3=127-C1
3011 D3=D1:D4=D2
3015 XT=X1+XR:YT=Y1+YR:IFXT<0 OR XT>WX OR YT<0 OR YT>WY THEN3040
3020 IFA(XT,YT)=1 THEN3040
3029 BA=B1:BB=B2:FORK=A1 TOC1 STEP2
3030 FORQ=BB TOBA STEP2:SET(O*K,Q*O,6):NEXT:BB=BB-1:BA=BA+1:NEXT
3031 GOTO3050
3040 FORK=A1 TOC1:FORQ=B2 TOB1 STEP2:SET(O*K,Q*O,3):NEXT:NEXT
3050 FORQ=B2 TOB1:RESET(O*A1,Q*.5):NEXT
3090 XT=X1-XR:YT=Y1-YR:IFXT<0 OR XT>WX OR YT<0 OR YT>WY THEN3140
3100 IFA(XT,YT)=1 THEN3140
3129 BB=B4:BA=B3:FORK=A3 TOC3 STEP-2
3130 FORQ=BB TOBA STEP2:SET(O*K,Q*O,6):NEXT:BB=BB-1:BA=BA+1
3131 NEXT:GOTO3150
3140 FORK=A3 TOC3 STEP-2:FORQ=B4 TOB3:SET(O*K,Q*O,3):NEXT:NEXT
3150 FORQ=B4 TOB3:RESET(O*A3,Q*.5):NEXT
3200 RETURN
3300 REM
3310 FORK=B2 TOB1:SET(O*62,K*O,4):NEXTK:FORQ=A3 TOA1:SET(O*Q,30*O,4):NEXT
3320 RETURN
4000 REM
4010 FORK=A3 TOA1+.5 STEP4:FORQ=B2 TOB1 STEP2:SET(O*K,Q*O,6):NEXT:NEXT
4020 RETURN
5000 CLS:FORZ=0 TOWY:FORQ=WX TO0 STEP-1
5005 IFQ=X THENIFZ=Y THENPRINT"O";:GOTO5050
5010 IFA(Q,Z)=0 THEN5040
5015 IFA(Q,Z)=1 THENPRINT" ";:GOTO5050
5020 IFQ=X AND Z=Y THENPRINT"*";:GOTO5050
5040 PRINTCHR$(128);
5050 NEXT:ON1-(Z<16) GOTO5051:PRINT
5051 NEXT
5055 A$=INKEY$
5060 IFA$="X" THENRETURN
5070 A$=INKEY$:GOTO5060
6000 PRINT:PRINT:PRINT:PRINT:PRINT:PRINT:PRINT
6002 PRINT"    "CHR$(128)STRING$(5,140)STRING$(5,140)CHR$(140)CHR$(140)CHR$(140)CHR$(140)CHR$(128)
6004 PRINT"    "CHR$(128)CHR$(159)CHR$(159)CHR$(159)CHR$(159)CHR$(159)CHR$(159)CHR$(159)CHR$(159)CHR$(159)CHR$(159)CHR$(159)CHR$(159)CHR$(159)CHR$(159)CHR$(128)
6006 PRINT"     "CHR$(128)CHR$(159)CHR$(159)CHR$(159)CHR$(159)"the"CHR$(159)CHR$(159)CHR$(159)CHR$(159)CHR$(159)CHR$(128)
6008 PRINT"      "CHR$(128)CHR$(159)CHR$(159)CHR$(159)CHR$(159)CHR$(159)CHR$(159)CHR$(159)CHR$(159)CHR$(159)CHR$(159)CHR$(128)
6010 PRINT"       "CHR$(128)CHR$(159)"winner"CHR$(159)CHR$(128)
6012 PRINT"        "CHR$(128)CHR$(159)CHR$(159)CHR$(159)CHR$(159)CHR$(159)CHR$(159)CHR$(128)
6014 PRINT"         "CHR$(148)CHR$(156)CHR$(156)CHR$(156)CHR$(156)CHR$(152)"          "CHR$(128)CHR$(128)CHR$(128)
6016 PRINT"           "CHR$(149)CHR$(154)"            "CHR$(128)"1"CHR$(128)
6018 PRINT"           "CHR$(149)CHR$(154)"         "CHR$(128)CHR$(128)CHR$(128)CHR$(128)CHR$(128)CHR$(128)CHR$(128)CHR$(128)CHR$(128)
6020 PRINT"          "CHR$(145)CHR$(151)CHR$(155)CHR$(146)"        "CHR$(128)"2"CHR$(128)CHR$(128)CHR$(128)CHR$(128)CHR$(128)"3"CHR$(128)
6021 POKE1024+32*12+26,ASC("1"):POKE1024+32*14+23,ASC("2"):POKE1024+32*14+29,ASC("3")
6022 PRINT"         "CHR$(128)CHR$(156)CHR$(156)CHR$(156)CHR$(156)CHR$(128)
6029 G=250
6030 PRINT@G,CHR$(226);:PRINT@G+31,CHR$(137)CHR$(128)CHR$(134);:PRINT@G+64,CHR$(128);
6032 PRINT@G+95,CHR$(137)" "CHR$(134);
6033 SOUND5,2:SOUND10,2:SOUND15,2:SOUND20,2:SOUND25,2:SOUND30,5:SOUND20,3:SOUND30,3:SOUND20,3:SOUND30,3:SOUND20,1
6040 FORX=1 TO20:PRINT@G+31,CHR$(131)CHR$(128)CHR$(131);:FORZ=1 TO100:NEXTZ
6041 PRINT@G+31,CHR$(137)CHR$(128)CHR$(134);:FORZ=1 TO100:NEXTZ:NEXTX
6050 PRINT@480,;:INPUT"PLAY AGAIN (Y/N)";A$:IFA$="Y" THENRUN
6055 CLS:PRINT@480,"B Y E ! !":GOSUB10010:GOSUB10003:END
7000 REM
7005 PRINT
7010 PRINT" "CHR$(129)CHR$(131)CHR$(131)CHR$(131)CHR$(131)CHR$(131)STRING$(5,131)+STRING$(5,131)STRING$(5,131)STRING$(5,131)CHR$(131)CHR$(131)CHR$(131)CHR$(130)
7020 GOSUB7500
7030 PRINT " CHR$(133)"        3D LABYRINTH        "CHR$(138):GOSUB7500
7040 PRINT " CHR$(132)CHR$(140)STRING$(5,140)CHR$(140)"by"CHR$(140)"dieter"CHR$(140)"fryer"STRING$(5,140)CHR$(140)CHR$(136)
7100 RESTORE:FORX=1 TO9:READA:READB:SOUNDA,B:NEXT
7120 CLS:PRINT "THE GOAL OF THE GAME IS TO"
7130 PRINT "ESCAPE FROM THE LABYRINTH IN"
7140 PRINT "WHICH YOU NOW FIND YOURSELF."
7150 PRINT "USE THE KEYS <A>, <S>, AND <W>"
7160 PRINT "TO MOVE LEFT, RIGHT OR STRAIGHT.";
7170 PRINT "THE <X> KEY SHOWS THE LABYRINTH"
7190 PRINT "FROM ABOVE. PRESS <X> AGAIN"
7200 PRINT "TO RETURN TO THE LABYRINTH."
7210 PRINT "A CROSS MARKS THE WAY OUT.":PRINT
7220 PRINT "have fun playing!"
7250 PRINT@480,"press enter to begin";:INPUTA
7300 CLS:GOTO100
7500 FORX=1 TO3:PRINT" "CHR$(133);TAB(30)CHR$(138):NEXT:RETURN
8000 DATA23,4,20,4,23,4,20,4,21,4,20,4,18,4,16,2,1,6
10000 IF PEEK(65535)=27 THEN POKE65497,0:GOTO10002 ELSE CLS:INPUT"CAN YOUR COMPUTER HANDLE DOUBLE SPEED (Y/N)";A$
10001 IF A$="Y" THEN POKE65495,0 ELSE IF A$<>"N" THEN10000
10002 CLS:RETURN
10003 IF PEEK(65535)=27 THEN POKE65496,0 ELSE POKE65494,0
10004 RETURN
10005 ' ENABLE DRAGON SPEEDKEY
10006 IF PEEK(65535)<>180 THEN 10008
10007 IF PEEK(269)+PEEK(270)<>1 THEN POKE65283,52:POKE256,116:POKE257,1:POKE258,81:POKE259,126:POKE260,PEEK(269):POKE 261,PEEK(270):POKE269,1:POKE270,0:POKE65283,53
10008 RETURN
10009 ' DISABLE DRAGON SPEEDKEY
10010 IF PEEK(65535)<>180 THEN 10012
10011 IF PEEK(269)+PEEK(270)=1 THEN POKE65283,52:POKE269,PEEK(260):POKE270,PEEK(261):POKE65283,53
10012 RETURN
