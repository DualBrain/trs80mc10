100 CLS:PRINT"CONSTRUCT A 3-D MAZE"
110 PRINT"PLEASE WAIT..."
120 D$="SRBLDULSRBDUBLSRDURBLSDU"
130 DIM S(200)
140 REM 
150 REM INPUT DIMENSIONS, GET ARRAY
160 H=5:W=5:DP=5
220 DIM R(H,W,DP)
230 REM
240 REM FILL ROOMS WITH RANDOM DOORS
250 FOR Z=1 TO H:FOR X=1 TO W:FOR Y=1 TO DP
260 R(Z,X,Y)=RND(64)-1
270 IF Y=1 THEN R(Z,X,Y)=R(Z,X,Y) AND 61
280 IF Y=DP THEN R(Z,X,Y)=R(Z,X,Y) AND 55
290 IF X=1 THEN R(Z,X,Y)=R(Z,X,Y) AND 59
300 IF X=W THEN R(Z,X,Y)=R(Z,X,Y) AND 62
310 IF Z=1 THEN R(Z,X,Y)=R(Z,X,Y) AND 31
320 IF Z=H THEN R(Z,X,Y)=R(Z,X,Y) AND 47
330 NEXT:NEXT:NEXT
340 FOR Z=1 TO H: FOR X=1 TO W: FOR Y=1 TO DP
350 IF Z=1THEN360
351 IF (R(Z-1,X,Y) AND 16)<>0 THEN R(Z,X,Y)=R(Z,X,Y) OR 32
360 IF X=1THEN370
361 IF (R(Z,X-1,Y) AND 1)<>0 THEN R(Z,X,Y)=R(Z,X,Y) OR 4
370 IF Y=1THEN380
371 IF (R(Z,X,Y-1) AND 8)<>0 THEN R(Z,X,Y)=R(Z,X,Y) OR 2
380 IF Z=HTHEN390
381 IF (R(Z+1,X,Y) AND 32)<>0 THEN R(Z,X,Y)=R(Z,X,Y) OR 16
390 IF X=WTHEN400
391 IF (R(Z,X+1,Y) AND 4)<>0 THEN R(Z,X,Y)=R(Z,X,Y) OR 1
400 IF Y=DPTHEN410
401 IF (R(Z,X,Y+1) AND 2)<>0 THEN R(Z,X,Y)=R(Z,X,Y) OR 8
410 NEXT:NEXT:NEXT
420 REM
430 REM FIND CENTER ROOM
440 F=0
450 D=0
460 Y=INT(DP/2)
470 X=INT(W/2)
480 Z=INT(H/2)
490 S(1)=Z:S(2)=X:S(3)=Y:JE=3
500 PRINT Z;X;Y
510 A$=""
520 REM
530 REM MAIN LOOP
540 REM DO A RANDOM WALK
550 N=RND(6)-1
560 IF F=0 THEN F=1:GOTO 640
570 IF (N=2 AND D=0) OR (N=0 AND D=2) OR (N=1 AND D=3) OR (N=3 AND D=1)THEN550
580 TZ=Z:TX=X:TY=Y
590 IF N=5 THEN TZ=TZ-1:GOTO600
591 IF N=4 THEN TZ=TZ+1:GOTO600
592 IF N=3 THEN TX=TX-1:GOTO600
593 IF N=2 THEN TY=TY+1:GOTO600
594 IF N=1 THEN TX=TX+1:GOTO600
595 TY=TY-1
600 FOR J=1 TO JE STEP 3
610 IF (TZ=S(J)) AND (TX=S(J+1)) AND (TY=S(J+2))THEN550
620 NEXT J
630 IF (TX=0) OR (TX=W+1) OR (TY=0) OR (TY=DP+1) OR (TZ=0) OR (TZ=H+1) THEN IF JE<(H*W*DP)/3THEN550
640 IF N=0 THEN M=2:GOTO650
641 IF N=1 THEN M=1:GOTO650
642 IF N=2 THEN M=8:GOTO650
643 IF N=3 THEN M=4:GOTO650
644 M=2^N
650 R(Z,X,Y)=R(Z,X,Y) OR M
660 IF M=32 THEN M=16:GOTO670
661 IF M=16 THEN M=32:GOTO670
662 IF M=8 THEN M=2:GOTO670
663 IF M=2 THEN M=8:GOTO670
664 IF M=4 THEN M=1:GOTO670
665 IF M=1 THEN M=4
670 A$=A$+MID$(D$,D*6+(N+1),1)
680 IF N=5 THEN Z=Z-1:GOTO690
681 IF N=4 THEN Z=Z+1
690 IF N=3 THEN X=X-1:GOTO700
691 IF N=2 THEN Y=Y+1:GOTO700
692 IF N=1 THEN X=X+1:GOTO700
693 IF N=0 THEN Y=Y-1
700 S(JE+1)=Z:S(JE+2)=X:S(JE+3)=Y:JE=JE+3
710 IF NOT((Z=H+1) OR (Y=DP+1) OR (X=W+1)) THEN R(Z,X,Y)=R(Z,X,Y) OR M
720 IF N<4 THEN D=N
730 PRINT Z;X;Y
740 IF (Z=0) OR (Z=H+1) OR (Y=0) OR (Y=DP+1) OR (X=0) OR (X=W+1)THEN770
741 GOTO550
750 REM
760 REM STORE MAZE IN MAZE FILE
770 INPUT"MAZE FILE NAME:";MF$:CSAVE*R,MF$
780 INPUT"SHOW SOLUTION (Y/N)";MF$
790 IFLEFT$(MF$,1)="Y"THENPRINTA$
800 END
