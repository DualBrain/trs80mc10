1 CLEAR544:CLS:PRINT "JOHN CONWAY'S LIFE SIMULATION"
10 DIM A(32,64),B$(18),X,Y,X1,X2,X3,X4,Y1,Y2,Y3,Y4,I9,J,I,C,P
20 C=1:X1=1:Y1=1:X2=32:Y2=64
30 PRINT "ENTER YOUR PATTERN (.=off, *=on Q=finished):"
40 INPUTB$(C)
45 IFC=1ANDB$(C)="Q"THENGOSUB700:GOTO80
50 IFB$(C)="Q"ORC=18THENB$(X)="":GOTO80
60 C=C+1
70 GOTO40
80 L=0
90 FORX=1TOC-1
100 IFLEN(B$(X))>LTHENL=LEN(B$(X))
110 NEXT X
120 X1=15-C/2
130 Y1=32-L/2
140 FORX=1TOC-1
150 FORY=1TOLEN(B$(X))
160 IFMID$(B$(X),Y,1)="*"THENA(INT(X1+X),INT(Y1+Y))=1:P=P+1
170 NEXTY
180 NEXTX
210 CLS0:PRINT "GENERATION:";G,"POPULATION:";P;:PRINT@0,"";:IFI9=-1THENPRINT "INVALID!    ";
215 X3=32:Y3=64:X4=1:Y4=1:P=0
220 G=G+1
230 FORX=X1TOX2
250 FORY=Y1TOY2
253 IFA(X,Y)=2THENA(X,Y)=0:GOTO 270
256 IFA(X,Y)=3THENA(X,Y)=1:GOTO 261
260 IFA(X,Y)<>1THEN270
261 SET(Y,X,1)
262 IFX<X3THENX3=X
264 IFX>X4THENX4=X
266 IFY<Y3THENY3=Y
268 IFY>Y4THENY4=Y
270 NEXTY
290 NEXTX
300 X1=X3:X2=X4:Y1=Y3:Y2=Y4
301 IFX1<3THENX1=3:I9=-1
303 IFX2>30THENX2=30:I9=-1
305 IFY1<3THENY1=3:I9=-1
307 IFY2>61THENY2=61:I9=-1
309 P=0
500 FORX=X1-1TOX2+1
510 FORY=Y1-1TOY2+1
520 C=0
530 FORI=X-1TOX+1
540 FORJ=Y-1TOY+1
550 IFA(I,J)=1ORA(I,J)=2THENC=C+1
560 NEXTJ
570 NEXTI
580 IFA(X,Y)=0THEN610
590 IFC<3ORC>4THENA(X,Y)=2:GOTO600
595 P=P+1
600 GOTO620
610 IFC=3THENA(X,Y)=3:P=P+1
620 NEXTY
630 NEXTX
635 X1=X1-1:Y1=Y1-1:X2=X2+1:Y2=Y2+1
640 GOTO 210
700 B$(1)=".*."
710 B$(2)="..*"
720 B$(3)="***"
730 C=4:RETURN
