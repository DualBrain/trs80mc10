0 CLEAR145:DIMC1,CC,M$,D$(29),WP(29,6),TL(7),T$(7),TC$(14),TH(7):GOTO100
1 C1=1:CC=32
2 IFMID$(M$,CC,1)<>""ANDMID$(M$,CC,1)<>" "THENCC=CC-1:GOTO2
3 PRINTMID$(M$,C1,CC-C1):C1=CC+1:CC=C1+31:IFC1<=LEN(M$)THEN2
4 RETURN
5 GOTO176
6 FORJ=1TO6:FORK=1TO29:READWP(K,J):NEXTK,J:RETURN
10 PRINT"ALL TREASURES FOUND!":RETURN
15  PRINT"AN UNFRIENDLY SPIRIT GOT YOU!":GOTO500
100 GOSUB6:FORJ=1TO29:READD$(J):NEXT
130 FORJ=1TO14:READTC$(J):NEXT:FORJ=1TO7:READT$(J):NEXT:FORJ=1TO7:READTL(J):NEXT:FORJ=1TO7:TH(J)=0:NEXT:L=1
142 CLS
143 M$="":M$="LOCATION: "+D$(L):GOSUB1:IFL=1ANDX=7THENM$="IT TOOK"+STR$(T)" TURNS TO SCORE"+STR$(S)+" OUT OF 175.":GOSUB1:END
144 PRINT"EXITS: ";:FORJ=1TO6:IFWP(L,J)<>0THENPRINTTC$(J+5)",";
145 NEXT:PRINTCHR$(8)
152 IFX=7ANDZ=0THENGOSUB10:Z=1
154 IFL=1ANDX>1THENM$="YOU'RE MISSING TREASURES":GOSUB1
156 IFL=1ORL=2ORL=3ORL=4ORL=5ORL=6ORL=26ORL=27ORL=28ORL=29THEN160
158 IFTH(7)=0ANDX=6THENGOSUB10
160 IFL=15THENM$="A GHOST GRABS YOU AND CARRIES YOU AWAY!":GOSUB1:L=RND(29):GOTO143
162 IFL=13ANDX>=1THEN15
164 TN=0:FORJ=1TO7:IFL=TL(J)THENTN=J
166 NEXT:IFTH(4)=1THENZ4=1
170 IFL=10ORL=18ORL=26ORL=20ORL=23ORL=24THEN5
172 IFTN<>0THENM$="THERE IS "+T$(TN)+" HERE.":GOSUB1
174 IFT=75THEN15
176 INPUTC$:IFC$=""THEN176
178 T=T+1:IFLEFT$(C$,4)="EXAM"THENC$="EXAM"
185 IFZ4<>1THEN194
186 IFL=7ANDC$="S"THENGOSUB380:L=6:GOTO143
188 IFL=6ANDC$="N"THENGOSUB384:L=7:GOTO143
190 IFL=4ANDC$="S"THENGOSUB384:L=16:GOTO143
192 IFL=16ANDC$="N"THENGOSUB380:L=4:GOTO143
194 C=0:FORJ=1TO14:IFC$=TC$(J)THENC=J
196 NEXT
198 IFC=1THEN410
200 IFC=2ANDL=6THENM$="IT'S LABLED: A. HITCHCOCK":GOSUB1:GOTO5
202 IFC=2THENPRINT"I DON'T KNOW THAT WORD":GOTO5
203 IFC<>14THEN212
204 M$="NOTHING!":IFL=18ANDTH(3)<>1THENM$="IT'S A BRASS LAMP"
206 IFL=20ANDTH(4)<>1THENM$="THERE'S A KEY"
208 IFL=24ANDTH(6)<>1THENM$="YOU SEE SOME GOLD JEWELRY"
210 IFL=26ANDTH(7)<>1THENM$="YOU FOUND THE COMBINATION, OPEN THE SAFE AND SEE A BAG OF $"
211 GOSUB1:GOTO5
212 IFC=3ANDTH(3)=1THENM$="A GENIE APPEARS & KILLS YOU":GOSUB1:GOTO500
214 IFC<>4THEN224
218 PRINT"YOU'RE HOLDING:":FORJ=1TO7:IFTH(J)=1THENPRINTT$(J)
222 NEXT:GOTO5
224 IFC<>5THEN236
226 FORJ=1TO7:IFL=TL(J)THENTL(J)=0:TH(J)=1:J=7:NEXT:GOTO234
230 NEXT
232 M$="NO TREASURES HERE":GOSUB1:GOTO5
234 PRINT"TAKEN":X=X+1:S=S+25:GOTO152
236 IFC=12THEN142
238 IFC=13THENM$="YOU'VE USED"+STR$(T):GOSUB1:GOTO242
240 GOTO244
242 PRINT"TURNS TO SCORE ";S:GOTO5
244 IFC<6ORC>11THEN250
246 C=C-5:IFWP(L,C)=0THENM$="CAN'T":GOSUB1:S=S-1:GOTO5
248 IFWP(L,C)<>0THENL=WP(L,C):GOTO142
250 PRINT"WHAT?":GOTO5
251 DATA6,3,29,0,28,0,11,10,0,0,16,0,0,15,0,0,0,17,0,0,0,23,0,25,0,0,5,0,0,2,0,0,29,0,0,8,9,3,0,10,11,12,7,0,17,0,0,0,0,0
252 DATA24,25,0,0,0,1,4,0,0,0,2,0,27,1,0,0,0,8,7,0,0,0,14,11,18,0,0,0,0,0,22,0,24,0,0,5,3,27,1,0,28,0,0,14,7,8,11,12,13,0
253 DATA0,0,0,16,0,0,0,0,0,0,22,23,5,0,0,4,0,0,9,0,26,0,21,0,0,0,0,0,0,0,0,0,0,0,12,19,22,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0
254 DATA0,0,0,19,0,0,0,0,0,0,20,0,7,21,0,0,0,0,0,0,0
258 DATA"SIDEWALK","FRONT YARD","EAST YARD NEAR AN OPEN WINDOW","BACKYARD","WEST YARD NEAR A OPEN WINDOW"
259 DATA"FRONT PORCH ON A DOORMAT","FRONT HALLWAY","RECEPTION ROOM NEAR A TABLE WITH WEDDING GIFTS"
260 DATA"MUSIC ROOM NEAR AN OPEN WINDOW","DINING ROOM NEXT TO A TABLE SET FOR DINNER","REAR HALLWAY","THE KITCHEN","PANTRY"
276 DATA"DRAWING ROOM","STUDY","REAR PORCH","NORTH CONSERVATORY","EAST CONSERVATORY NEAR A TRUNK"
292 DATA"BASEMENT STAIRS","BASEMENT NEAR SOME SHELVES","STAIRS TO 2ND FLOOR","2ND FLOOR LANDING"
300 DATA"MASTER BEDROOM LOOKING AT A PAINTING","GUEST BEDROOM NEAR A DRESSER & BED","BATHROOM"
308 DATA"A SMALL ROOM WITH A SAFE","WEST OF THE HOUSE","BACK YARD","EAST OF THE HOUSE"
328 DATA"QUIT","READ","RUB","INV","GET","N","E","S","W","U","D","LOOK","SCORE","EXAM","A CRYSTAL BOWL"
330 DATA"SILVERWARE","A BRASS LAMP","A BRASS KEY","A REMBRANDT","SOME JEWELRY","A BAG OF $"
334 DATA8,10,18,20,23,24,26
380 M$="THE DOOR OPENS. YOU EXIT THE DOOR SLAMS SHUT BEHIND YOU":GOSUB1:RETURN
384 M$="YOU UNLOCK THE DOOR AND ENTER. THE DOOR BLOWS SHUT AND LOCKS BEHIND YOU":GOSUB1:RETURN
500 PRINT"YOU'RE DEAD."
