1 CLS:PRINT "JOHN CONWAYS'S LIFE SIMULATION"
2 PRINT "ENTER YOUR PATTERN (.=off, *=on
3 PRINT "Q=finished):"
5 X1=1:Y1=1:X2=32:Y2=64
10 DIM A(32,64),B$(16)
20 C=1
30 REM ENTER STARTING PATTERN
40 INPUT B$(C)
45 IF C=1 AND B$(C)="Q" THEN GOSUB 700:GOTO80
50 IFB$(C)="Q"ORC=14THENB$(X)="":GOTO80
60 C=C+1
70 GOTO40
80 L=0
90 FOR X=1 TO C-1
100 IF LEN(B$(X))>L THEN L=LEN(B$(X))
110 NEXT X
120 X1=11-C/2
130 Y1=33-L/2
140 FOR X=1 TO C-1
150 FOR Y=1 TO LEN(B$(X))
160 IF MID$(B$(X),Y,1)="*" THEN A(INT(X1+X),INT(Y1+Y))=1:P=P+1
170 NEXT Y
180 NEXT X
200 REM
210 CLS0:PRINT "GENERATION:";G,"POPULATION:";P;:PRINT@0,"";:IF I9=-1 THEN PRINT "INVALID!    ";
215 X3=32:Y3=64:X4=1:Y4=1:P=0
220 G=G+1
230 FOR X=X1 TO X2
250 FOR Y=Y1 TO Y2
253 IF A(X,Y)=2 THEN A(X,Y)=0:GOTO 270
256 IF A(X,Y)=3 THEN A(X,Y)=1:GOTO 261
260 IF A(X,Y)<>1 THEN 270
261 SET(Y,X,1)
262 IF X<X3 THEN X3=X
264 IF X>X4 THEN X4=X
266 IF Y<Y3 THEN Y3=Y
268 IF Y>Y4 THEN Y4=Y
270 NEXT Y
290 NEXT X
300 X1=X3:X2=X4:Y1=Y3:Y2=Y4
301 IF X1<3 THEN X1=3:I9=-1
303 IF X2>29 THEN X2=29:I9=-1
305 IF Y1<3 THEN Y1=3:I9=-1
307 IF Y2>61 THEN Y2=61:I9=-1
309 P=0
500 FOR X=X1-1 TO X2+1
510 FOR Y=Y1-1 TO Y2+1
520 C=0
530 FOR I=X-1 TO X+1
540 FOR J=Y-1 TO Y+1
550 IF A(I,J)=1 OR A(I,J)=2 THEN C=C+1
560 NEXT J
570 NEXT I
580 IF A(X,Y)=0 THEN 610
590 IF C<3 OR C>4 THEN A(X,Y)=2:GOTO620
595 P=P+1
600 GOTO 620
610 IF C=3 THEN A(X,Y)=3:P=P+1
620 NEXT Y
630 NEXT X
635 X1=X1-1:Y1=Y1-1:X2=X2+1:Y2=Y2+1
640 GOTO 210
700 B$(1)=".*."
710 B$(2)="..*"
720 B$(3)="***"
730 C=4:RETURN
