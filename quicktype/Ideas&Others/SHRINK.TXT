10 REM *** WRITTEN BY DAVE AHL, DIGITAL
20 &"HELLO, MY NAME IS PETEY P. ELEVEN.":INPUT "WHAT IS YOUR NAME";A$:&
30 INPUT "ARE YOU ENJOYING YOURSELF HERE IN FUN CITY";B$:&:N=1
40 IF B$="YES" THEN 60 ELSE IF B$="NO" THEN 80
50 &"SORRY, "A$", BUT I DON'T UNDERSTAND YOUR ANSWER OF '"B$"'."
	:&"COULD YOU TRY THAT WITH A 'YES' OR 'NO', PLEASE.":GOTO 30
60 &"HEY, THAT'S GREAT. SO AM I.":GOTO 90
80 &"OH, THAT'S TOO BAD "A$", BUT MAYBE I CAN CHEER YOU UP A BIT."
90 &"I CAN SOLVE ALL KINDS OF PROBLEMS "A$".  WHAT KINDS OF PROBLEMS"
100 INPUT "DO YOU HAVE (SEX, MONEY, HEALTH, OR NONE)";C$:&
110 IF C$="SEX" THEN 130 ELSE IF C$="MONEY" THEN 200 ELSE
	IF C$="HEALTH" THEN 250 ELSE IF C$="NONE" THEN 300
120 &"SORRY, "A$", BUT '"C$"' IS GREEK TO ME.  HOW ABOUT TRYING"
125 INPUT "AGAIN WITH EITHER SEX, HEALTH, MONEY, OR NONE -- WHICH";C$:&
	:GOTO 110
130 INPUT "IS YOUR PROBLEM TOO MUCH OR TOO LITTLE";D$:&
140 IF D$="TOO MUCH" OR D$="MUCH" THEN 160
145 IF D$="TOO LITTLE" OR D$="LITTLE" THEN 180
150 &"NOW DON'T GET ALL NERVOUS AND EXCITED "A$". JUST ANSWER THE"
155 &"WITH A SIMPLE 'TOO MUCH' OR 'TOO LITTLE'.  NOW THEN,":GOTO 130
160 &"YOU CALL THAT A PROBLEM "A$"!!!  I SHOULD HAVE SUCH PROBLEMS!"
165 &"IF IT REALLY BOTHERS YOU, TAKE A COLD SHOWER.":GOTO 400
180 &"WELL ,THAT'S A PITY "A$".  I COULD UNDERSTAND YOUR ANSWER IF"
185 &"WE WERE IN MAYNARD, MA OR BIBLE, KY, BUT HERE IN FUN CITY IT"
190 &"SEEMS A BIT STRANGE.  WHY DON'T YOU GO STAND ON THE CORNER OF"
195 &"7TH AND 53RD FOR A WHILE.  YOUR PROSPECTS MIGHT CHANGE!":GOTO 400
200 &"SORRY, "A$", BUT I'M BROKE TOO.  WHY DON'T YOU GET A JOB SELLING"
210 &"COMPUTERS?  I HEAR THE COMMISSIONS ARE VERY HIGH IN THAT INDUSTRY!":GOTO 400
250 &"MY ADVICE TO YOU "A$" IS:":&"     1. TAKE 2 ASPRIN"
260 &"     2. DRINK PLENTY OF FLUIDS (ORANGE JUICE, NOT BEER)"
270 &"     3. GO TO BED (ALONE!!)":GOTO 400
300 &"NONE !?!  NONE?!?  DID I HEAR YOU RIGHT, "A$"?"
310 &"THEN, WHAT ARE YOU COMING TO ME FOR?"
320 &:&"SINCE YOU ARE SO WELL OFF "A$", MY FEE WILL BE ONLY":N=10
	:GOTO 510
400 &
405 &"DO YOU HAVE ANY OTHER PROBLEMS YOU WANT SOLVED "A$;:INPUT E$:&
410 IF E$="YES" THEN 430 ELSE IF E$="NO" THEN 500
420 &"PLEASE, "A$".  A SIMPLE 'YES' OR 'NO' WILL DO NICELY.  NOW THEN,"
	:GOTO 405
430 &"O.K. "A$".  WHAT OTHER KINDS OF PROBLEMS":N=N+1:GOTO 100
500 &"I SOLVED"N"PROBLEM(S) FOR YOU "A$".  MY FEE FOR THIS ADVICE IS"
510&"$"5*N".  PLEASE LEAVE THE MONEY ON THE TERMINAL.":SLEEP 7%
520 &:&"DID YOU LEAVE THE MONEY "A$;:INPUT F$:&
530 IF F$="YES" THEN 550 ELSE IF F$="NO" THEN 570
540 &"COME ON, "A$", 'YES' OR 'NO', NOT '"F$"'.  O.K?":GOTO 520
550 &"HA, HA, HA, "A$".  YOU ARE TRYING TO FOOL ME.  BUT I AM"
560 &"NOT FOOLED.  YOU LEFT NO MONEY AT ALL "A$".":&:GOTO 600
570 &"WELL AT LEAST YOU'RE HONEST "A$".  BUT"
600 &"HOW DO YOU EXPECT ME TO CONTINUE WITH MY PSYCHIATRIC STUDIES"
610 &"IF MY PATIENTS DON'T PAY THEIR BILLS?":&
620 &"WELL ANYWAY, IT WAS NICE TALKING WITH YOU "A$"."
630 &"HAVE A NICE DAY!!":& FOR I=1 TO 10:GOTO 20
999 END
