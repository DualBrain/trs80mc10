10 REM MASTERMIND
20 REM FOR TRS-80 MC-10
30 REM HTTP://RUDIH.INFO
35 REM
40 REM THE PLAYER HAS A NUMBER OF ATTEMPTS TO GUESS A 4 DIGIT COMBINATION
50 REM WITH INDICATION OF WHITE FOR CORRECT DIGIT AND BLACK FOR
60 REM CORRECT PLACE AND DIGIT. THE DIGITS ARE 1-6.
70 REM
80 DIM DI(4)
90 DIM US(4)
100 REM
110 REM GENERATE COMBINATION
130 FOR X = 1 TO 4
140 DI(X) = RND (6)
150 NEXT X
170 REM PRINT MESSAGES ON SCREEN
180 CLS
190 PRINT " ---MASTERMIND---"
191 PRINT "<E>ASY <M>EDIUM <H>ARD"
192 X$=INKEY$
193 IF X$="" THEN GOTO 192
194 IF X$="E" THEN GL=36
195 IF X$="M" THEN GL=18
196 IF X$="H" THEN GL=9
200 PRINT "KEY IN 4 DIGITS FROM 1 TO 6"
202 PRINT "TO CRACK A SECRET CODE."
210 PRINT "WHITE=CORRECT DIGIT." 
212 PRINT "BLACK=CORRECT DIGIT AND PLACE."
220 PRINT "YOU HAVE ";GL;" ATTEMPTS. <E>XIT." 
240 REM PROGRAM MAIN LOOP
250 REM
260 AT = 1
270 REM LOOP
280  GOSUB 520
290  GOSUB 670
300  AT = AT + 1
310  IF AT = GL AND BL < 4 THEN PRINT "YOU LOSE. IT WAS:";:GOSUB 430:END
380 IF BL<4 THEN GOTO 270
390 PRINT "YOU GOT IT!"
395 END
420 REM 
430 REM DISP COMBO
440 REM
450 FOR X = 1 TO 4
460 PRINT DIGIT(X);
470 NEXT X
480 PRINT ""
490 RETURN
500 REM
510 REM GET GUESS INPUT FROM USER
520 REM
530 PRINT "ATTEMPT#";
531 PRINT AT;
532 PRINT "> ";
560 FOR X = 1 TO 4
580 REM LOOP
585 Y$=""
590 Y$ = INKEY$
592 IF Y$="E" THEN END
600 IF Y$="" THEN GOTO 580
620 PRINT Y$; " ";
630 USER(X) = VAL(Y$)
640 NEXT X
650 RETURN
660 REM
670 REM PROVIDE FEEDBACK ABOUT GUESS
672 X=0
673 Y=0
682 WH=0
683 BL=0
690 FOR X = 1 TO 4
702  IF US(X)=DI(X)THEN BL=BL+1
703   FOR Y = 1 TO 4
704    IF US(X)=DI(Y) AND US(X)<>US(Y)THEN WH=WH+1
705   NEXT
725 NEXT X
781 PRINT ""
790 IF WH<BL THEN WH=BL
792 IF WH>4 THEN WH=4
805 PRINT "BLACK ="; BL;
810 PRINT " WHITE ="; WH
825 Y=0
830 RETURN
