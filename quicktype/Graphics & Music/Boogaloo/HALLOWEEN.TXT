100 REM BASIC Week 2: Halloween Boogaloo
110 REM http://reddit.com/r/RetroBattlestations
120 REM written by FozzTexx
130 REM TRS-80 Color Computer port by FozzTexx
140 REM ----
200 REM Clear screen and setup variables
210 CLEAR 1500:REM CoCo needs room for strings
220 CLS:L$=CHR$(92)
230 CL=32:RW=16:REM Columns and Rows of screen
300 REM Load background and draw on screen
310 GOSUB1510
320 NR=SR:WD=SC:WX=-2
330 X=2:KF=X+WD:IFRND(2)>1THENX=CL-WD-2:KF=0
340 Y=RW-NR:KW=CL-WD-4
350 GOSUB2010
500 REM Load sprites into variables
510 GOSUB1510:BT$(0)=SP$:BE$=SE$:BC=SC
520 GOSUB1510:BT$(1)=SP$
530 GOSUB1510:WT$=SP$:WE$=SE$:WW=SC:WH=SR
600 REM Animation
610 NB=3
620 FORI=1TONB
630 BX(I)=RND(KW-3)-1:BY(I)=(RND((RW-4-NR/2)/NB)-1)*NB+I+1
640 BW(I)=RND(2)-1:BF(I)=RND(9)-1
650 NEXT
660 FORI=1TONB
670 Y=INT(BF(I)/3):X=INT((BF(I)/3-Y)*3+.05)
680 X=X+BX(I)+KF:Y=Y+BY(I):SP$=BE$:GOSUB2010
710 BW(I)=BW(I)+1:IFBW(I)>1THENBW(I)=0
720 BF(I)=RND(9)
730 Y=INT(BF(I)/3):X=INT((BF(I)/3-Y)*3+.05)
740 X=X+BX(I)+KF :Y=Y+BY(I):SP$=BT$(BW(I)):GOSUB2010
750 IFWX=-1THENSP$=WE$:X=0:Y=0:GOSUB2010:WX=-2
760 IFWX>=0THENSP$=WT$:X=WX:Y=0:GOSUB2010:WX=WX-1
770 IFWX=-2ANDRND(50)<3THENWX=CL-WW-1
780 NEXTI
790 GOTO660
1000 REM Position cursor at X,Y
1010 PRINT@Y*CL+X,;
1020 RETURN
1500 REM Load a sprite from DATA into a string
1510 SP$="":SE$="":SR=0:SC=0
1520 CC=CC+1:ONCCGOTO5000,5010,5020,5030,5040,5050,5060,5070,5080,5090,5100,5110,5120,5130,5140,5150,5160,5170,5180,5190
1521 ONCC-20GOTO5200,5210,5220,5230,5240
1522 IFLEN(SP$)>0THENSP$=SP$+CHR$(13)
1530 SP$=SP$+S$:SR=SR+1:IFLEN(S$)>SCTHENSC=LEN(S$)
1540 BL$="":FORI=1TOLEN(S$):BL$=BL$+" ":NEXT
1550 IFLEN(SE$)>0THENSE$=SE$+CHR$(13)
1560 SE$=SE$+BL$
1570 IFT$<>"SE"THEN1520
1580 RETURN
2000 REM Draw sprite in SP$ at X,Y
2010 SE=1:SB=1
2020 M$=SP$:I$=CHR$(13):BB=SB:GOSUB9000:SE=FF
2030 IF SE=0 THEN SE=LEN(SP$)+1
2040 GOSUB1010
2050 PRINTMID$(SP$,SB,SE-SB);
2060 IFSE>=LEN(SP$)THENRETURN
2070 IFASC(MID$(SP$, SE))=13THENSE=SE+1
2080 SB=SE:Y=Y+1:GOTO2020
5000 T$="SP":S$="        ^":GOTO1522
5010 T$="SP":S$="       / "+L$:GOTO1522
5020 T$="SP":S$="      //!"+L$+L$:GOTO1522
5030 T$="SP":S$="     / -+- "+L$:GOTO1522
5040 T$="SP":S$="    /  "+L$+"!/  "+L$:GOTO1522
5050 T$="SP":S$="   /---------"+L$:GOTO1522
5060 T$="SP":S$="     ! --- !":GOTO1522
5070 T$="SP":S$="     !! ! !!::::":GOTO1522
5080 T$="SP":S$="   /"+L$+" !-+-!!IIII":GOTO1522
5090 T$="SP":S$="  /  "+L$+"!-!-!!    "+L$:GOTO1522
5100 T$="SP":S$=" / "+CHR$(91)+CHR$(93)+" "+L$+"    !-----":GOTO1522
5110 T$="SP":S$="/-----     ! +-+!":GOTO1522
5120 T$="SP":S$="  !   +--+ ! !+!!":GOTO1522
5130 T$="SP":S$="  !   !  ! ! +-+!":GOTO1522
5140 T$="SP":S$="  !   !O ! !----!":GOTO1522
5150 T$="SE":S$="  !---!--!-!":GOTO1522
5160 T$="SE":S$="/V"+L$:GOTO1522
5170 T$="SE":S$="-V-":GOTO1522
5180 T$="SP":S$="     .":GOTO1522
5190 T$="SP":S$="   -/(- ":GOTO1522
5200 T$="SP":S$="   <"+L$+L$+" ":GOTO1522
5210 T$="SP":S$="   /;-) ":GOTO1522
5220 T$="SP":S$="=='/ <===<<< ":GOTO1522
5230 T$="SP":S$="   "+L$+"--"+L$+" ":GOTO1522
5240 T$="SE":S$="     '' ":GOTO1522
8997 REM INSTR SUBROUTINE
8998 REM INPUT VARIABLE: M$=STRING TO BE SEARCHED,I$=SEARCH TARGET,BB=BEGINING POSITION OF SEARCH
8999 REM OUTPUT VARIABLE:FF=POSITION OF SEARCH TARGET OR ZERO IF NOT FOUND
9000 FF=0:LL=LEN(I$):FORZZ=BBTOLEN(M$):ON1-(MID$(M$,ZZ,LL)=I$)GOTO9010:FF=ZZ:ZZ=255:NEXT:RETURN
9010 NEXT:RETURN
