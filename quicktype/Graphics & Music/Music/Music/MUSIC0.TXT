110 DIM T$(12),T(12,2),S(99,3)
120 DATA g,38,452,a,58,422,b,78,392,c,89,362,d,108,332,e,125,302,f,133,272,g,147,242
125 DATA a,159,212,b,170,182,c,176,152,d,185,122
130 FOR I=1 TO 12
140 READ T$(I),T(I,1),T(I,2):NEXT:GOSUB150:GOTO290
150 CLS:PRINT@9,"MUSIC PROGRAM"
160 FOR I=34 TO 290 STEP 64
170 FOR J=1 TO 25
180 PRINT@I+J,"-";
190 NEXT J,I
200 PRINT@361,"-  -";
210 PRINT@421,"-  -";
220 J=92
230 FOR I=1 TO 12
240 J=J+30
250 PRINT@J,T$(13-I);
260 NEXT
270 RETURN
290 J=4:C=1:S(C,1)=1:S(C,2)=1
300 A$=INKEY$
310 IF A$="" THEN 300
320 A=ASC(A$)
360 IF (A>48) AND (A<58) THEN I=A-48:T=T(I,1):GOTO410
361 IF (A>32) AND (A<42) THEN I=A-32:T=T(I,1)+((T(I,1)-T(I+1,1))/2):GOTO410
370 IF A=45 THEN I=12:T=T(I,1):GOTO410
371 IF A=64 THEN I=12:T=T(I,1)-5:GOTO410
380 IF A=48 THEN I=10:T=T(I,1):GOTO410
381 IF A=79 THEN I=10:T=T(I,1)+((T(I,1)-T(I+1,1))/2):GOTO410
390 IF A=58 THEN I=11:T=T(I,1):GOTO410
391 IF A=80 THEN I=11:T=T(I,1)+((T(I,1)-T(I+1,1))/2):GOTO410
392 IF A=70 THEN J=8:REM-FULL NOTE
393 IF A=84 THEN J=6:REM-3/4 NOTE
394 IF A=72 THEN J=4:REM-HALF NOTE
395 IF A=81 THEN J=2:REM-QTR NOTE
396 IF A=62 THEN CLS:FORT=1 TO E:LPRINT "SOUND ";S(T,1);",";S(T,2):PRINT S(T,1);S(T,2):NEXT:GOSUB600:GOSUB150:GOTO300
397 IF A=13 THEN E=C-1:C=1
398 IF A=32 THEN FORT=1 TO E:SOUNDS(T,1),S(T,2):N=S(T,0):I=S(T,3):GOSUB500:NEXT
399 IF A=65 THEN C=C-1:E=C-1
400 GOTO300
410 SOUND T,J:S(C,1)=T:S(C,2)=J:S(C,3)=I:N=T(I,2):S(C,0)=N:E=C:C=C+1:GOSUB500:GOTO300
500 PRINT@N,CHR$(191);
510 FOR Z=1 TO 30:NEXT
520 PRINT@N,T$(I);
530 RETURN
600 PRINT"PRESS ANY KEY TO CONTINUE";
610 A$=INKEY$:IFA$=""THEN610
620 RETURN
