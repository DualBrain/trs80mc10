110 DIM T$(12),T(12,2),S(500,3),A,C,I,J,A$,N$
120 DATA g,64,452,a,87,422,b,103,392,c,113,362,d,129,332,e,142,302,f,149,272,g,161,242
125 DATA a,171,212,b,181,182,c,185,152,d,193,122
130 GOSUB800:FOR I=1 TO 12
140 READ T$(I),T(I,1),T(I,2):NEXT:GOSUB600:GOSUB150:GOTO290
150 CLS:GOSUB790
160 FORI=34TO290STEP64
170 FORJ=1TO25
180 PRINT@I+J,"-";
190 NEXTJ,I
200 PRINT@361,"- -";
210 PRINT@421,"- -";
220 J=92
230 FORI=1TO12
240 J=J+30
250 PRINT@J,T$(13-I);
260 NEXT
270 J=4:RETURN
290 C=1:E=1:S(C,1)=1:S(C,2)=1:S(C,3)=1:S(1,0)=T(1,2)
300 A$=INKEY$:IFA$=""THEN300
320 A=ASC(A$):IF(A>48)AND(A<58)THENI=A-48:T=T(I,1):GOTO410
330 IF(A>32)AND(A<42)THENI=A-32:T=T(I,1)+((T(I,1)-T(I+1,1))/2):GOTO410
335 IFA=45THENI=12:T=T(I,1):GOTO410
340 IFA=64THENI=12:T=T(I,1)-5:GOTO410
345 IFA=48THENI=10:T=T(I,1):GOTO410
350 IFA=79THENI=10:T=T(I,1)+INT((T(I,1)-T(I+1,1))/2):GOTO410
355 IFA=58THENI=11:T=T(I,1):GOTO410
360 IFA=80THENI=11:T=T(I,1)+INT((T(I,1)-T(I+1,1))/2):GOTO410
361 IFA=32THENFORT=1TOE:SOUNDS(T,1),S(T,2):N=S(T,0):I=S(T,3):GOSUB500:NEXT:GOTO300
365 IFA=83THEN1000
370 IFA=76THEN2000
392 IFA=70THENJ=8:GOTO300
393 IFA=84THENJ=6:GOTO300
394 IFA=72THENJ=4:GOTO300
395 IFA=81THENJ=2:GOTO300
396 IFA=62THENCLS:FORT=1TOE:A$="SOUND"+STR$(S(T,1))+","+STR$(S(T,2)):LPRINTA$:PRINTA$:NEXT:GOSUB600:GOSUB150:GOTO300
397 IFA=13THEN3000
399 IFA=65THENIFC>1THEN:C=C-1:E=C-1:GOTO300
400 IFA=77THENGOSUB800:GOSUB600:GOSUB150:GOTO300
401 GOTO300
410 IFC<499THENSOUNDT,J:S(C,1)=T:S(C,2)=J:S(C,3)=I:N=T(I,2):S(C,0)=N:E=C:C=C+1:GOSUB500:GOTO300
420 SOUND1,1:GOTO300
500 PRINT@N,CHR$(191);
510 FORZ=1TO30:NEXT
520 PRINT@N,T$(I);
530 RETURN
600 PRINT"   PRESS ANY KEY TO CONTINUE";
610 A$=INKEY$:IFA$=""THEN610
620 CLS:RETURN
700 FORT=1TO499:IFS(T,1)=0THENC=T:E=C-1:T=499
710 NEXT
720 RETURN
790 CLS:PRINT@9,"MUSIC PROGRAM":RETURN
800 GOSUB790
810 PRINT"1- =PLAY NOTES GABCDEFGABCD":POKE16384+(32*1+0),49:POKE16384+(32*1+2),45
815 PRINT"shift 1-9 TO GET FLATS":POKE16384+(32*2+6),49:POKE16384+(32*2+8),57
816 PRINT"o,p & @=FLATS FOR 0,:, & -":POKE16384+(32*3+6),0:POKE16384+(32*3+18),48:POKE16384+(32*3+20),58
817 POKE16384+(32*3+25),45
820 PRINT"f=CHANGE TO FULL NOTES"
830 PRINT"t=3/4 NOTES"
840 PRINT"h=HALF NOTES"
850 PRINT"q=QUARTER NOTES"
860 PRINT"space=PLAY NOTES TYPED SO FAR"
870 PRINT"enter=RESET MEM TO BEGINNING"
880 PRINT"a=ERASE PREVIOUS NOTE TYPED"
890 PRINT"s=SAVE MUSICAL NOTES"
900 PRINT"l=LOAD MUSICAL NOTES"
910 PRINT">=OUTPUT AS SOUND STATEMENTS":POKE16384+(32*13),62
915 PRINT"m=THIS MENU"
920 RETURN
1000 CLS
1010 PRINT"TYPE FILENAME & ENTER TO SAVE":PRINT"OR JUST ENTER TO ABORT SAVE"
1015 INPUT A$
1020 IFA$=""THEN1030
1025 A$=LEFT$(A$,8):PRINT"SAVING ";A$:S(C,1)=0:CSAVE*S,A$
1030 GOSUB600:GOSUB150:GOTO300
2000 CLS
2010 PRINT"TYPE FILENAME & ENTER TO LOAD":PRINT"OR JUST ENTER TO ABORT LOAD"
2015 INPUT A$
2020 IFA$=""THEN2030
2025 A$=LEFT$(A$,8):PRINT"LOADING ";A$:CLOAD*S,A$
2030 GOSUB700:GOSUB150:GOTO300
3000 CLS
3010 PRINT"TO CLEAR MEMORY HIT ENTER ";:GOSUB610:PRINTA$:IFA$=CHR$(13)THENE=C-1:C=1
3030 GOSUB150:GOTO300
