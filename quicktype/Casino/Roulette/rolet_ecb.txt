   10 REM ROLET.ECB
   20 REM ----- SOL 20  ROULETTE --- EXTENDED BASIC --- 5/1/78
   25 REM
   30 DIM B(37),C(37),J(28),K(28),O(37),R(36),P(11),X(43),Y(43)
   40 FOR X=1 TO 37: LET B(X)=0: NEXT X
   50 PRINT "&K": CURSOR 3,23
   60 DATA 1,2,1,2,1,2,1,2,1,2,2,1,2,1,2,1,2,1,1,2
   70 DATA 1,2,1,2,1,2,1,2,2,1,2,1,2,1,2,1,3
   80 FOR X=1 TO 37: READ C(X): NEXT X
   90 PRINT ".-=**'^'**=-."
  100 PRINT TAB(20);".***'         '***."
  110 PRINT TAB(18);"/***/             \***\"
  120 PRINT TAB(17);"(***(    ROULETTE   )***)"
  130 PRINT TAB(18);"\***\             /***/"
  140 PRINT TAB(20);"'***.         .***'"
  150 PRINT TAB(23);"'-=*:.,.:*=-'"
  160 DATA 1,20,14,31,9,22,18,29,7,28,12,35,3,26,37,32,15,19
  170 DATA 4,21,2,25,17,34,6,27,13,36,11,30,8,23,10,5,24,16,33
  180 FOR X=1 TO 37: READ O(X): NEXT X
  190 CURSOR 13,10
  200 INPUT (1,100)"DO YOU NEED THE RULES ? (Y OR N)---=>",Y$
  220 IF Y$="N" THEN 1000
  230 PRINT "&K": CURSOR 2,5
  240 PRINT "WELCOME TO 'COMPUTOR ROULETTE'.&J"
  250 PRINT "     YOU HAVE JUST PURCHASED $350.00 WORTH OF CHIPS"
  260 PRINT " ON YOUR CHARGE CARD FOR THE ROULETTE TABLE."
  270 PRINT " 4% SALES TAX ON THE $350.00 YOU SPENT FOR THE"
  280 PRINT " CHIPS IS $14.00 FOR A TOTAL CHARGE OF $364.00"
  290 PRINT "     COATROOM CHARGES WERE $4.00 + .16 TAX AND"
  300 PRINT " PROTECTED PARKING WAS $2.00&J"
  310 PRINT "   THEREFORE, YOU'RE EFFECTIVELY NOW OUT $20.16&J"
  320 INPUT (1,300)" TYPE IN ANY NUMBER TO SEE MORE ---=>",Y$
  330 PRINT "&K": CURSOR 2,10
  340 PRINT "*** THE WHEEL IS STANDARD EUROPEAN ***&J"
  350 PRINT "   THERE ARE 37 NUMBERS TO BET ON.  ONE NUMBER"
  360 PRINT " IS GREEN ( 0 ), 18 ARE RED, AND 18 ARE BLACK."
  370 PRINT "     THE WHEEL IS SPUN IN ONE DIRECTION AND THEN"
  380 PRINT " THE BALL IS THROWN INTO IT IN THE OPPOSITE"
  390 PRINT " DIRECTION.&J"
  400 PRINT TAB(10);"***  (#) = THE BALL  ***&J"
  410 PRINT "    BEFORE THE SPIN, YOU WILL PLACE YOUR BET ON"
  420 PRINT " THE TABLE.&J"
  430 INPUT (1,300)" DO YOU NEED TO SEE THE TABLE ? (Y OR N)---=>",Y$
  440 IF Y$="N" THEN 470 ELSE  GOSUB 800
  450 CURSOR 15,1
  460 INPUT (1,300)" TYPE IN ANY NUMBER TO SEE MORE ---=>",Y$
  470 PRINT "&K": CURSOR 2,1
  480 PRINT TAB(8);"*SINGLE NUMBERS PAY 35 TO 1*&J"
  490 PRINT " RED/BLACK , HI/LOW , ODD/EVEN ALL PAY EVEN MONEY.&J"
  500 PRINT " BETTING ON:"
  510 PRINT TAB(7);"ANY 2 NUMBERS  =  17 TO 1"
  520 PRINT TAB(7);"ANY 3 NUMBERS  =  11 TO 1"
  530 PRINT TAB(7);"ANY 4 NUMBERS  =   8 TO 1"
  540 PRINT TAB(7);"ANY 5 NUMBERS  =   8 TO 1"
  550 PRINT TAB(7);"ANY 6 NUMBERS  =   5 TO 1"
  560 PRINT TAB(7);"ANY  DOZEN     =   2 TO 1"
  570 PRINT TAB(7);"ANY  COLUMN    =   2 TO 1"
  580 INPUT (1,150)" TYPE IN ANY NUMBER TO SEE MORE ---=>",Y$
  590 PRINT "&K&J&J    SINCE IT IS IMPOSSIBLE TO SHOW A FULL"
  600 PRINT " 37 POSITION WHEEL, A SMALLER 28 POSITION WHEEL WILL"
  610 PRINT " BE SHOWN INSTEAD.   A 'GAP' IN THE WHEEL OF 10"
  615 PRINT " NUMBERED COMPARTMENTS WILL BE SHOWN BY A"
  620 PRINT "   '======' SYMBOL OR 'BAR'.&J"
  625 PRINT "   WHEN THE BALL STOPS ON THE '0', ANY *EVEN MONEY*"
  630 PRINT " YOU'VE BET IS NEITHER WON NOR LOST, BUT IS"
  640 PRINT " PLACED IN 'PRISON' UNTIL A NON-ZERO NUMBER"
  650 PRINT " APPEARS.   IF THE BALL LANDS ON AN EVEN NUMBER"
  660 PRINT " DURING THE NEXT SPIN, YOU WIN!&J"
  670 INPUT (1,300)" TYPE IN ANY NUMBER TO SEE MORE ---=>",Y$
  680 PRINT "&K&J&J     TO SIMPLIFY THE GAME, BETTING ON TWO"
  690 PRINT " NUMBERS CAN BE ON *ANY* TWO NUMBERS ON THE BOARD."
  700 PRINT "    REGULAR 'ROULETTE' REQUIRES PLACING YOUR CHIPS"
  710 PRINT " ON THE LINE BETWEEN TWO NUMBERS TO MAKE THIS KIND"
  720 PRINT " OF BET.     THE SAME WILL HOLD FOR BETS ON 3"
  730 PRINT " NUMBERS, 4, 5, AND 6 NUMBER BETS.&J"
  740 PRINT " EXIT THE GAME WITH A NEGATIVE NUMBER BET."
  750 PRINT " YOU MAY 'PASS' A TURN BY A BET OF '0' DOLLARS.&J"
  760 PRINT " WE ARE NOW ABOUT READY TO BEGIN......."
  770 PRINT "================================================"
  780 INPUT (1,200)" TYPE IN ANY NUMBER TO SEE MORE ---=>",Y$
  790 GOTO 1000
  800 PRINT "&K": CURSOR 2,1
  810 PRINT TAB(20);"*** ROULETTE TABLE ***&J"
  820 PRINT "I";: FOR X=1 TO 59: PRINT "-";: NEXT X: PRINT "I"
  830 PRINT "I  3*:  6#:  9*: 12*: 15#: 18*: 21*: 24#: 27*: 30*: 33#: 36*I"
  840 PRINT "I";: FOR X=1 TO 59: PRINT "-";: NEXT X: PRINT "I"
  850 PRINT "I  2#:  5*:  8#: 11#: 14*: 17#: 20#: 23*: 26#: 29#: 32*: 35#I"
  860 PRINT "I";: FOR X=1 TO 59: PRINT "-";: NEXT X: PRINT "I"
  870 PRINT "I  1*:  4#:  7*: 10#: 13#: 16*: 19*: 22#: 25*: 28#: 31#: 34*I"
  880 PRINT "I";: FOR X=1 TO 59: PRINT "-";: NEXT X: PRINT "I"
  890 IF O=11 THEN 950
  900 PRINT "I     1ST DOZEN     I     2ND DOZEN     I     3RD DOZEN     I"
  910 PRINT "I";: FOR X=1 TO 59: PRINT "-";: NEXT X: PRINT "I"
  920 PRINT "I   1-18  I  EVEN   I  *RED*  I #BLACK# I   ODD   I  19-36  I"
  930 PRINT "I";: FOR X=1 TO 59: PRINT "-";: NEXT X: PRINT "I"
  940 REM
  950 RETURN
  960 REM
  970 REM
  980 REM
  990 REM
 1000 REM ************** S T A R T ****************
 1005 REM ...B = THE BET YOU INPUT
 1010 REM ...B( ) = NO. BET ON  : IF = 0 THEN NO BET PAYOFF
 1020 REM ......................: IF > 0 THEN IT'S = PAYOFF ODDS
 1030 REM ...C( ) = COLOR OF THE NUMBER
 1035 REM ..........1 = RED    2 = BLACK   3 = GREEN (ZERO)
 1037 REM ...E, E1= END OF NUMBER GAP IN THE WHEEL
 1040 REM ...J( ),K( ) = BALL (#) CURSOR POSITIONS ON WHEEL
 1045 REM ...M = YOUR MONEY SUPPLY.   $350.00 AT START
 1050 REM ...N = INPUT FOR CHOOSING NUMBERS TO BET ON
 1055 REM .......N1, N2 = NUMBER RANGE YOU'VE PICK TO BET ON
 1060 REM ...O = WHICH BETTING OPTION YOU TOOK
 1070 REM ...O( ) = ORDER OF NUMBERS ON THE WHEEL
 1080 REM ...P( ) = PAYOFF ON EACH OPTION (1 TO 12 OPTIONS)
 1090 REM ...R( ) = ORDER OF COLUMN NUMBERS FOR OPTION # 11
 1095 REM ...S,S1 = STARTING POINT FOR BALL ON THE WHEEL
 1100 REM ...Y( ),X( ) =  CURSOR POSITIONS ON THE WHEEL
 1110 REM ................FOR THE NUMBERED COMPARTMENTS
 1120 REM ...Z, Z1, Z2 = SCRATCH #'S
 1200 DATA 21,29,33,36,38,39,40,41,40,39,38,36,33,29,21
 1210 DATA 13,9,6,4,3,2,1,2,3,4,6,9,13,22
 1215 DATA 23,21,19,18,18,19,21,23,25,27,28,28,27,25
 1220 DATA 1,2,3,4,5,6,7,8,9,10,11,12,13,14,15
 1230 DATA 14,13,12,11,10,9,8,7,6,5,4,3,2,8
 1235 DATA 5,5,6,7,9,10,11,11,11,10,9,7,6,5
 1240 FOR X=1 TO 43: READ X(X): NEXT X
 1250 FOR X=1 TO 43: READ Y(X): NEXT X
 1260 LET M=350
 1270 DATA 23,26,30,33,35,36,37,38,37,36,35,33,30,26,23,19,15
 1275 DATA 12,10,9,8,7,8,9,10,12,15,19
 1280 DATA 2,2,3,4,5,6,7,8,9,10,11,12,13,14,14,14,13,12,11,10
 1285 DATA 9,8,7,6,5,4,3,2
 1290 FOR X=1 TO 28: READ K(X): NEXT X
 1295 FOR X=1 TO 28: READ J(X): NEXT X
 1300 DATA 35,17,11,8,8,5,1,1,1,2,2
 1310 FOR X=1 TO 11: READ P(X): NEXT X
 1400 DATA 3,6,9,12,15,18,21,24,27,30,33,36
 1410 DATA 2,5,8,11,14,17,20,23,26,29,32,35
 1420 DATA 1,4,7,10,13,16,19,22,25,28,31,34
 1430 FOR X=1 TO 36: READ R(X): NEXT X
 1900 PRINT "&K": REM ==== INTIAL WHEEL DISPLAY ====
 1910 GOSUB 6000: REM ......DISPLAY WHEEL
 2000 GOSUB 6150: REM ......MSG:  "PLACE YOUR BETS"
 2010 CURSOR Y(29),X(29)+1: PAUSE 15: PRINT "&K"
 2020 CURSOR 2,15: PRINT "*#*  BET OPTIONS  *#*&J"
 2030 PRINT " 0...PASS BETTING           6...ANY 6 NO. (5-1)"
 2040 PRINT " 1...SINGLE NUMBER (35-1)   7...RED/BLACK (1-1)"
 2050 PRINT " 2...ANY 2 NO'S    (17-1)   8...ODD/EVEN  (1-1)"
 2060 PRINT " 3...ANY 3 NO'S    (11-1)   9...HIGH/LOW  (1-1)"
 2070 PRINT " 4...ANY 4 NO'S    ( 8-1)   10..ANY DOZEN (2-1)"
 2080 PRINT " 5...ANY 5 NO'S    ( 8-1)   11..ANY COLUMN(2-1)"
 2085 PRINT TAB(15);"12...LEAVE THE ROULETTE TABLE"
 2090 GOSUB 6100: CURSOR 15,1
 2100 FOR Z=1 TO 37: LET B(Z)=0: NEXT Z
 2110 LET B=0
 2120 INPUT (2,150)" YOUR BET OPTION ? ( 0 TO 12 ) ---=>",O$
 2125 IF O$="" THEN  LET O=0 ELSE  LET O=ASC(O$(1,1))-48
 2130 IF LEN(O$)>1 THEN  LET O=(O*10)+(ASC(O$(2,2))-48)
 2140 IF O=12 THEN 9000
 2145 IF O>=0 AND O<12 THEN 2200
 2150 PRINT "&K": CURSOR 8,15
 2160 PRINT "**** INPUT ERROR ****";: CURSOR 10,5
 2170 PRINT O$;" WAS ENTERED. THE LIMITS ARE 0 TO 11"
 2180 PAUSE 20
 2190 GOTO 2000
 2200 PRINT "&K": IF O=0 THEN  GOSUB 6100: GOTO 3500
 2210 IF O>1 THEN 2300 ELSE  CURSOR 6,20
 2220 PRINT "### OPTION 1 ###";: CURSOR 8,15
 2230 PRINT "SINGLE NUMBER PAYING 35 TO 1";: CURSOR 15,1
 2240 INPUT (2,0)"<**  1 **>   BET ON WHICH NUMBER ? ---=>",N: PRINT " ";
 2250 IF N=0 THEN  LET N=37 ELSE  LET N=INT(N)
 2260 IF N<0 THEN  GOSUB 6400: GOSUB 800: GOTO 2240
 2270 IF N>37 THEN  GOSUB 6400: GOSUB 800: GOTO 2240
 2280 LET B(N)=35
 2300 IF O>6 THEN 2400
 2310 IF O<2 THEN 2400 ELSE  PRINT "&K": CURSOR 5,20
 2320 PRINT "### OPTION";O;" ###": CURSOR 7,15
 2330 PRINT "ANY";O;" NUMBERS PAYING";P(O);" TO 1&J&J"
 2340 FOR Z=1 TO O
 2350   PRINT "<** ";O;" **> ";Z;"  BET ON WHICH NUMBER ? ---=>";
 2355   INPUT (2,0)N: LET N=INT(N): IF N=0 THEN  LET N=37
 2360   IF N<0 THEN  PRINT "### WRONG ###...TRY AGAIN": GOTO 2350
 2365   IF N>37 THEN  PRINT "### WRONG ###...TRY AGAIN": GOTO 2350
 2370   IF B(N)>0 THEN  PRINT "@@@ YOU ALREADY BET ON ";N: GOTO 2350
 2380   LET B(N)=P(O)
 2390 NEXT Z
 2400 IF O=7 THEN  CURSOR 6,20 ELSE 2500
 2410 PRINT "### OPTION 7 ###";: CURSOR 8,15
 2420 PRINT "RED/BLACK PAYING 1 TO 1";: CURSOR 12,10
 2430 PRINT "(1) *RED*";: CURSOR 13,10
 2440 PRINT "(2) #BLACK#";: CURSOR 15,1
 2450 INPUT (1,0)"<** 7 **>  PICK ONE: (INPUT 1 OR 2) ---=>",N
 2460 LET N=INT(N)
 2465 IF N>2 OR N<1 THEN  GOSUB 6900: GOTO 2450
 2470 FOR Z=1 TO 36
 2480   IF N=C(Z) THEN  LET B(Z)=P(O)
 2490 NEXT Z
 2500 IF O=8 THEN  CURSOR 6,20 ELSE 2600
 2510 PRINT "### OPTION 8 ###";: CURSOR 8,15
 2520 PRINT "ODD/EVEN PAYING 1 TO 1";: CURSOR 12,10
 2530 PRINT "(1) .EVEN.";: CURSOR 13,10
 2540 PRINT "(2) .ODD.";: CURSOR 15,1
 2550 INPUT (1,0)"<** 8 **>  PICK ONE: (INPUT 1 OR 2) ---=>",N
 2560 LET N=INT(N)
 2565 IF N>2 OR N<1 THEN  GOSUB 6900: GOTO 2550
 2570 FOR Z=1 TO 36
 2580   IF N+Z=INT(N+Z) THEN  LET B(Z)=P(O)
 2590 NEXT Z
 2600 IF O=9 THEN  CURSOR 6,20 ELSE 2700
 2610 PRINT "### OPTION 9 ###";: CURSOR 8,15
 2620 PRINT "HIGH/LOW PAYING 1 TO 1";: CURSOR 12,10
 2630 PRINT "(1) .LOW.   (NUMBERS  1-18)";: CURSOR 13,10
 2640 PRINT "(2) .HIGH.  (NUMBERS 19-36)";: CURSOR 15,1
 2650 INPUT (1,0)"<** 9 **>  PICK ONE: (INPUT 1 OR 2) ---=>",N
 2660 LET N=INT(N)
 2665 IF N>2 OR N<1 THEN  GOSUB 6900: GOTO 2650
 2670 IF N=1 THEN  LET N1=1: LET N2=18 ELSE  LET N1=19: LET N2=36
 2680 FOR Z=N1 TO N2: LET B(Z)=P(O): NEXT Z
 2700 IF O=10 THEN  CURSOR 6,20 ELSE 2800
 2710 PRINT "### OPTION 10 ###";: CURSOR 8,15
 2720 PRINT "ANY DOZEN PAYING 2 TO 1";: CURSOR 11,10
 2730 PRINT "(1) 1ST DOZEN (NUMBERS  1-12)";: CURSOR 12,10
 2740 PRINT "(2) 2ND DOZEN (NUMBERS 13-24)";: CURSOR 13,10
 2750 PRINT "(3) 3RD DOZEN (NUMBERS 25-36)";: CURSOR 15,1
 2760 INPUT (1,0)"<** 10 **>  PICK ONE: (INPUT 1, 2, OR 3) ---=>",N
 2765 LET N=INT(N): LET N1=25: LET N2=36
 2770 IF N<1 OR N>3 THEN  GOSUB 6900: GOTO 2760
 2780 IF N=1 THEN  LET N1=1: LET N2=12
 2785 IF N=2 THEN  LET N1=13: LET N2=24
 2790 FOR X=N1 TO N2: LET B(X)=P(O): NEXT X
 2800 IF O=11 THEN  CURSOR 6,20 ELSE 3000
 2810 PRINT "### OPTION 11 ###";: CURSOR 8,15
 2820 PRINT "ANY COLUMN PAYING 2 TO 1"
 2825 PAUSE 10: GOSUB 800
 2830 PRINT "(1) 1ST OR TOP COLUMN"
 2840 PRINT "(2) 2ND OR MIDDLE COLUMN"
 2850 PRINT "(3) 3RD OR BOTTOM COLUMN&J"
 2860 INPUT (1,0)"<** 11 **>  PICK ONE: (INPUT 1, 2, OR 3) ---=>",N
 2865 LET N=INT(N): LET N1=25: LET N2=36
 2870 IF N<1 OR N>3 THEN  GOSUB 6900: GOTO 2860
 2880 IF N=1 THEN  LET N1=1: LET N2=12
 2885 IF N=2 THEN  LET N1=13: LET N2=24
 2890 FOR X=N1 TO N2: LET B(R(X))=P(O): NEXT X
 3000 PRINT "&K"
 3010 GOSUB 6100: REM ......DISPLAY MONEY
 3020 GOSUB 6150: REM ......MSG:  "PLACE YOUR BETS"
 3030 IF O=0 THEN 3500
 3040 IF B>M THEN  GOSUB 6200: REM ......MSG:  "NOT ENOUGH $ !"
 3050 CURSOR 14,1
 3060 INPUT "<$$$$$>  HOW MUCH FOR YOUR BET? ---=>",B
 3070 IF B<0 THEN 9000
 3080 IF B=0 THEN  PRINT "&K": GOSUB 6100: GOTO 3500
 3090 IF B>M THEN 3000
 3095 LET M=M-B: GOSUB 6100: REM ...=== DISPLAY $ LEFT ===
 3200 CURSOR 2,10: PRINT "$ ";B;: GOSUB 6800
 3205 PRINT " BET ON";: CURSOR 4,1
 3210 IF O>6 THEN 3300
 3220 FOR X=1 TO 37
 3230   IF B(X)=0 THEN 3290
 3240   IF C(X)=1 THEN  PRINT "**";X;"**&J";
 3250   IF C(X)=2 THEN  PRINT "##";X;"##&J";
 3260   IF C(X)=3 THEN  PRINT "[ ZERO ]&J";
 3290 NEXT X
 3300 IF O=7 THEN  IF N=1 THEN  PRINT "*RED*"; ELSE  PRINT "#BLACK#";
 3310 IF O=8 THEN  IF N=1 THEN  PRINT ".EVEN."; ELSE  PRINT ".ODD.";
 3320 IF O=9 THEN  IF N=1 THEN  PRINT ".LOW."; ELSE  PRINT ".HIGH.";
 3330 IF O=10 THEN  PRINT "=== DOZEN #";N;" ===";
 3340 IF O=11 THEN  PRINT "@@@ COLUMN #";N;" @@@";
 3350 PAUSE 15
 3360 PRINT "&K"
 3370 GOSUB 6100
 3380 CURSOR Y(29),X(29): PRINT "(#)";
 3390 PAUSE 10
 3500 CURSOR 1,38: PRINT "*** WE ARE READY  ***  ";
 3510 CURSOR 2,38: PRINT "*** TO SPIN IT !  ***";
 3520 LET X=INT(RND(0)*28)+1: REM ...=== WHEEL STARTING POINT ===
 3530 LET S=INT(RND(0)*37)+1: REM ...=== BALL STARTING POINT ===
 3540 LET X1=X: LET S1=S
 3550 FOR Z=1 TO 28: REM ...=== NUMBERED WHEEL LAYOUT ===
 3560   LET X=X+1: LET S=S+1
 3570   IF X>28 THEN  LET X=1
 3580   IF S>37 THEN  LET S=1
 3590   CURSOR Y(X),X(X)
 3600   IF C(O(S))=1 THEN  PRINT "*    *";
 3610   IF C(O(S))=2 THEN  PRINT "##  ##";
 3620   IF C(O(S))=3 THEN  PRINT "[  0 ]";: GOTO 3670
 3650   IF O(S)>9 THEN  CURSOR Y(X),X(X)+1 ELSE  CURSOR Y(X),X(X)+2
 3660   PRINT O(S);
 3670 NEXT Z
 3680 CURSOR Y(X),X(X): PRINT "======";
 3690 LET Y2=Y(X): LET X2=X(X)
 3700 GOSUB 6040: REM ...=== LAYOUT WHEEL SPOKES ===
 3710 CURSOR 1,38: PRINT "                     ";
 3720 CURSOR 2,38: PRINT "                     ";
 3730 PAUSE 5: LET T=1
 3740 GOSUB 6500: GOSUB 6700: REM ...=== SPIN SPOKES ===
 4000 LET X=S1: LET Z=X1: LET E=S1-9
 4010 IF E<=0 THEN  LET E=E+37
 4020 LET E1=E: IF E1<=0 THEN  LET E1=E1+37
 4100 LET X=X+1: LET Z=Z+1: LET E=E+1
 4110 IF E>37 THEN  LET E=1
 4120 IF X>37 THEN  LET X=1
 4130 IF Z>28 THEN  LET Z=1
 4150 CURSOR J(Z),K(Z): PRINT "(#)";
 4160 IF RND(0)<((.97)-(T*.09)) THEN  PAUSE T: GOTO 4200
 4170 LET T=T+1: GOSUB 6510
 4190 IF T>10 THEN 4500
 4200 CURSOR Y(Z),X(Z): PRINT "======";
 4210 CURSOR J(Z),K(Z): PRINT "   ";
 4220 CURSOR Y2,X2
 4230 IF C(O(E1))=1 THEN  PRINT "*    *";
 4240 IF C(O(E1))=2 THEN  PRINT "##  ##";
 4250 IF C(O(E1))=3 THEN  PRINT "[  0 ]";: GOTO 4280
 4260 IF O(E1)>9 THEN  CURSOR Y2,X2+1 ELSE  CURSOR Y2,X2+2
 4270 PRINT O(E1);
 4280 LET E1=E: LET Y2=Y(Z): LET X2=X(Z)
 4290 GOTO 4100
 4500 CURSOR 1,40: PRINT "### THE WINNER IS ###";
 4510 CURSOR 2,46: IF O(X)<37 THEN  PRINT O(X);
 4520 IF C(O(X))=1 THEN  PRINT "  *RED*";: GOTO 4700
 4530 IF C(O(X))=2 THEN  PRINT "  #BLACK#";: GOTO 4700
 4540 IF C(O(X))=3 THEN  PRINT "0  GREEN";
 4600 IF O=8 THEN 4610 ELSE 4700
 4610 IF N=2 THEN 4700
 4620 CURSOR 12,45: PRINT "*#* WE MUST";
 4630 CURSOR 13,45: PRINT "SPIN AGAIN *#*";
 4640 CURSOR 14,40: PRINT "*** 'EVEN' WAS BET ***";
 4650 PAUSE 30
 4660 CURSOR 12,45: PRINT "           ";
 4670 CURSOR 13,45: PRINT "              ";
 4680 CURSOR 14,40: PRINT "                      ";
 4690 PAUSE 10: GOTO 3500
 4700 IF B(O(X))>0 THEN  GOSUB 6600: REM ...=== YOU WON !!! ===
 4710 CURSOR 8,23: PAUSE 25
 4720 IF M<=0 THEN 5000: PAUSE 15: GOTO 9000
 4790 GOTO 2000
 5000 CURSOR 1,40: PRINT "### YOU ARE OUT ###";
 5010 CURSOR 2,40: PRINT "$$$ OF MONEY !! $$$";
 5020 PAUSE 15
 5090 GOTO 9000
 6000 PRINT "&K"
 6005 FOR X=1 TO 28
 6010   CURSOR Y(X),X(X)
 6020   PRINT "******";
 6030 NEXT X
 6040 CURSOR Y(29),X(29)-5: PRINT "O====(#)====O";
 6050 CURSOR Y(29)-1,X(29)+1: PRINT "I";
 6060 CURSOR Y(29)+1,X(29)+1: PRINT "I";
 6070 CURSOR Y(29)-2,X(29)+1: PRINT "I";
 6080 CURSOR Y(29)+2,X(29)+1: PRINT "I";
 6090 RETURN
 6100 REM ============== MONEY DISPLAY =============
 6110 CURSOR 7,50: PRINT "$";M;: GOSUB 6800
 6120 CURSOR 9,50: PRINT "[ MONEY ]";
 6130 CURSOR 8,50: PRINT "=========";
 6140 RETURN
 6150 CURSOR 1,40: PRINT "*** PLACE YOUR  ***  ";
 6160 CURSOR 2,40: PRINT "*** BETS PLEASE ***";
 6190 RETURN
 6200 CURSOR 1,40: PRINT "*** YOU HAVE'NT ***";
 6210 CURSOR 2,40: PRINT "***$ THE MONEY $***";
 6220 RETURN
 6300 FOR Z=1 TO O
 6310   CURSOR 3,1
 6320   PRINT "<*";O;" *>  NUMBER TO BET ON ---=>";
 6330   INPUT (2,0)N
 6340   IF N=0 THEN  LET N=37 ELSE  LET N=INT(N)
 6350   IF N<0 THEN  GOSUB 6400: GOTO 6310
 6360   IF N>37 THEN  GOSUB 6400: GOTO 6310
 6370 NEXT Z
 6390 RETURN
 6400 CURSOR 12,40: PRINT "### TRY AGAIN ###";: PAUSE 15
 6410 CURSOR 12,40: PRINT "                 ";
 6420 RETURN
 6500 CURSOR 8,17: PRINT "=----( )----=";: REM .....SPIN DISPLAY
 6510 FOR Z1=1 TO 11-T
 6515   CURSOR 7,26: PRINT "/";
 6520   CURSOR 6,28: PRINT "/";
 6530   CURSOR 7,20: PRINT "\";
 6540   CURSOR 6,18: PRINT "\";
 6550   CURSOR 9,20: PRINT "/";
 6560   CURSOR 10,18: PRINT "/";
 6570   CURSOR 9,26: PRINT "\";
 6580   CURSOR 10,28: PRINT "\";
 6585   IF Z1>7 THEN  GOSUB 6700: REM === 2ND PART OF SPIN DISPLAY ===
 6590 NEXT Z1
 6595 RETURN
 6600 CURSOR 12,45: PRINT "<YOU WIN>";
 6610 CURSOR 13,45: PRINT "$";B*B(O(X));: GOSUB 6800
 6620 LET M=M+(B*B(O(X))): PAUSE 10
 6640 GOSUB 6100: REM ...=== DISPLAY $ WON ===
 6650 PAUSE 10
 6660 CURSOR 12,45: PRINT "         ";
 6670 CURSOR 13,45: PRINT "         ";
 6680 PAUSE 10
 6690 RETURN
 6700 FOR Z2=30 TO 43
 6720   CURSOR Y(Z2),X(Z2)
 6750   CURSOR Y(Z2),X(Z2)+1
 6770 NEXT Z2
 6790 RETURN
 6800 IF M=INT(M) THEN  PRINT ".00";
 6810 RETURN
 6900 PRINT "### BAD INPUT ###": RETURN
 9000 PRINT "&K": CURSOR 4,20
 9010 PRINT "*** END OF GAME ***";: CURSOR 7,5
 9020 IF M=370 THEN  PRINT "O====  YOU BROKE EVEN  ====O"; ELSE 9100
 9030 CURSOR 10,5: PRINT "YOU LEAVE WITH $";M;: GOSUB 6800
 9090 GOTO 9500
 9100 IF M>370 THEN  CURSOR 9,20 ELSE 9200
 9110 PRINT "### OH WOW !!!! ###";
 9120 CURSOR 10,15: PRINT "### YOUR A WINNER !!! ###";
 9130 CURSOR 12,5: PRINT "YOU LEAVE WITH $";M;: GOSUB 6800
 9190 GOTO 9500
 9200 IF M<350 THEN  CURSOR 9,12 ELSE 9300
 9210 PRINT "*** BETTER LUCK NEXT TIME ***";
 9230 CURSOR 12,5: PRINT "YOU LEAVE WITH $";M;: GOSUB 6800
 9290 GOTO 9500
 9300 IF M<350 THEN 9500
 9310 IF M>370 THEN 9500
 9320 CURSOR 6,10: PRINT "*  YOU DID O.K.  *";
 9330 CURSOR 12,5: PRINT "YOU LEAVE WITH $";M;: GOSUB 6800
 9500 PRINT "&J&J"
 9510 END
