   10 REM BOULE.ECB --- SOL 20  BOULE --- EXTENDED BASIC --- 9/1/78
   20 REM ... by Ralph Hopkins...Eagan, Minn.
   30 DIM B(18),C(18),D(28),E(28),F(28),J(18),K(18),O(18)
   35 DIM X(18),Y(18),A$(31),C$(31),N$(54),B1(18),L(18),M(18)
   40 REM ...B = THE BET YOU INPUT
   50 REM ...B( ) = NO. BET ON  : IF = 0 THEN NO BET PAYOFF
   55 REM ......................: IF > 0 THEN IT'S = PAYOFF ODDS
   60 REM ...C( ) = COLOR OF THE NUMBER
   65 REM ......... 1 = RED    2 = BLACK   3 = THE NUMBER $5$
   70 REM ...D( ) = TIME DELAYS DURING BALL DROP
   80 REM ...E( ),F( ) = CURSOR POSITIONS FOR BALL DROP
   90 REM ...J( ),K( ) = BALL (#) CURSOR POSITIONS ON WHEEL
  100 REM ...M = YOUR MONEY SUPPLY.   $475.00 AT START
  110 REM ...N = INPUT FOR CHOOSING NUMBERS TO BET ON
  120 REM ...O = WHICH BETTING OPTION YOU TOOK
  130 REM ...O( ) = ORDER OF NUMBERS ON THE WHEEL
  135 REM ...P = WHICH OF THE OTHER 18 PEOPLE IS PLAYING NOW
  140 REM ...S = STARTING POINT FOR BALL ON THE WHEEL
  145 REM ...T = PAUSE TIME
  150 REM ...T1 = COUNT OF PEOPLE STILL WITH MONEY
  155 REM ...Y( ),X( ) =  CURSOR POSITIONS ON THE WHEEL
  160 REM ...X,Z, Z1, Z2 = SCRATCH #'S
  170 LET A$="===============================": LET B$="(+)"
  180 LET C$="-------------------------------": LET D$="   "
  190 LET N$="#1#*2*#3#*4*$5$#6#*7*#8#*9*": LET N$=N$+N$
  200 DATA 1,2,3,4,5,6,7,8,9,10,9,8,7,6,5,4,3,2
  205 DATA 15,23,26,28,29,29,28,26,23,15,7,4,2,1,1,2,4,7
  210 FOR X=1 TO 18: READ Y(X): NEXT X
  215 FOR X=1 TO 18: READ X(X): NEXT X
  220 PRINT "&K": GOSUB 3600: REM ...DISPLAY THE WHEEL
  230 FOR X=1 TO 9: LET O(X)=X: LET O(X+9)=X: NEXT X
  240 CURSOR 5,14: PRINT "L A";
  250 CURSOR 6,12: PRINT "B O U L E";
  260 CURSOR 14,1
  270 PRINT "<***>  WOULD YOU LIKE A DESCRIPTION OF THE GAME ?"
  280 INPUT ,(1,100)"  (Y OR N)---=>",Y$
  290 IF Y$="N" THEN 500
  290 IF Y$="n" THEN 500 ELSE  PRINT "&K&J"
  300 PRINT "   LA BOULE (the bowl) is an extremely popular"
  310 PRINT " game in European casinos and Swiss resort hotels."
  320 PRINT "   LA BOULE is played with a stationary wheel that is"
  330 PRINT " divided into 18 compartments numbered from 1 to 9 twice.&J"
  340 PRINT "    A 'croupier' spins a small ball around the rim of the bowl."
  350 PRINT " The ball will eventually stop in a number compartment"
  360 PRINT " which is considered the winning number.&J&J"
  370 PRINT " The House WILL ALWAYS win on the number $5$ !"
  380 CURSOR 15,1
  390 INPUT (1,300)" TYPE IN ANY NUMBER TO SEE MORE ---=>",Y$
  400 PRINT "&K": CURSOR 3,1
  410 PRINT "     You have just bought $500.00 worth of chips on your"
  420 PRINT " CHARGE card.     4% sales taxes and $5.00 parking "
  430 PRINT " have been already deducted.  So your starting with $475."
  435 CURSOR 8,5
  440 PRINT "You will be playing with several other people who"
  445 PRINT " will also place bets on the table.&J"
  450 PRINT " If YOU win and THEY lose, you get 1/2 of the"
  455 PRINT "smallest bet.    HOUSE gets the rest.&J"
  460 INPUT ,(1,200)"<***> DO YOU WANT TO SEE THE TABLE? (Y OR N) -=>",Y$
  470 IF Y$="N" THEN 500
  475 IF Y$="n" THEN 500 ELSE  PRINT "&K"
  480 PRINT TAB(10);"<*#*>  LA BOULE TABLE  <*#*>";
  490 GOSUB 2700: CURSOR 15,1: REM ...TABLE DISPLAY
  495 INPUT (1,300)" TYPE IN ANY NUMBER TO SEE MORE ---=>",Y$
  500 REM ******************  S T A R T  ******************
  510 DATA 2,1,2,1,3,2,1,2,1,2,1,2,1,3,2,1,2,1
  520 FOR X=1 TO 18: READ C(X): NEXT X
  530 DATA 2,2,3,4,5,6,7,8,9,9,9,8,7,6,5,4,3,2
  540 DATA 15,20,23,25,26,26,25,23,20,15,10,7,5,4,4,5,7,10
  550 LET M=475: LET S=INT(RND(0)*18)+1: LET R1=.25
  560 FOR X=1 TO 18: READ J(X): NEXT X
  570 FOR X=1 TO 18: READ K(X): NEXT X
  580 FOR X=1 TO 18: LET M(X)=0: NEXT X
  600 DATA 3,4,5,6,7,8,9,10,11,13,15,13,11,9,7,5,4,5
  610 DATA 38,38,38,38,38,38,38,38,38,38,38
  620 DATA 37,36,34,31,26,23,19
  630 DATA 3,1,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,1
  640 FOR X=1 TO 18: READ E(X): NEXT X
  650 FOR X=1 TO 18: READ F(X): NEXT X
  660 FOR X=1 TO 18: READ D(X): NEXT X
  670 DATA 1500,2500,500,1000,500,1500,50,100,40
  680 DATA 200,40,200,5,70,100,200,500,1000,500,300
  690 FOR X=1 TO 18: READ L(X): NEXT X
  700 PRINT "&K": GOSUB 3600: REM ... DISPLAY THE WHEEL
  710 CURSOR 5,14: PRINT "L A";: LET N9=N9+1
  720 CURSOR 6,12: PRINT "B O U L E";
  730 GOSUB 3100: REM ... MSG:  "PLACE YOUR BETS"
  740 CURSOR 14,1: PAUSE 15: LET W=0: LET B=0: LET W1=0
  750 GOSUB 3700: REM ... DISPLAY BETTING OPTIONS
  760 FOR Z=1 TO 18: LET B(Z)=0: NEXT Z
  770 FOR Z=1 TO 18: LET B1(Z)=0: NEXT Z
  780 IF RND(0)>.55 THEN  LET R=1 ELSE  LET R=0
  785 IF RND(0)<.3 THEN  LET R=2
  790 LET T=0: LET S1=INT(RND(0)*18)+1: LET P=S1
  800 IF R=1 THEN  GOSUB 4000: LET R3=R3+1: REM ... 2nd player bets 1st
  810 CURSOR 15,1
  820 INPUT (1,150)" YOUR BET OPTION ? ( 0 TO 9 ) ---=>",O$
  830 IF O$="" THEN  LET O=0 ELSE  LET O=ASC(O$(1,1))-48
  840 IF O=8 THEN 9000
  850 IF O>=0 AND O<10 THEN 900 ELSE  PRINT "&K"
  860 CURSOR 8,15: PRINT "**** INPUT ERROR ****";
  870 PRINT O$;" WAS ENTERED. THE LIMITS ARE 0 TO 11"
  880 PAUSE 20
  890 GOTO 750
  900 REM ...DISPLAY OTHER PLAYERS STATUS
  910 IF O<>9 THEN 1000 ELSE  PRINT "&K"
  920 FOR P=1 TO 9
  925   CURSOR P,1: GOSUB 4400: PRINT TAB(17);
  930   IF M(P)< 0 THEN  PRINT " behind ";M(P);
  931   IF M(P)>.1 THEN  PRINT " ahead +";M(P);
  932   IF M(P)=.1 THEN  PRINT "#@# BROKE #@#";
  935 NEXT P
  940 FOR P=10 TO 18
  950   CURSOR P-9,35: GOSUB 4400: PRINT TAB(50);
  960   IF M(P)<0 THEN  PRINT "behind ";M(P);
  961   IF M(P)>.1 THEN  PRINT "ahead +";M(P);
  962   IF M(P)=.1 THEN  PRINT "#@# BROKE #@#";
  970 NEXT P
  980 CURSOR 11,10: PRINT "You've seen the wheel spin ";N9-1;" times."
  985 CURSOR 13,1: INPUT (1,200)"*** TYPE IN SOMETHING ---=>",Y$
  990 LET N9=N9-1: LET O=0
 1000 PRINT "&K"
 1010 IF O=0 THEN  GOSUB 3000: GOTO 1500
 1020 IF O=1 THEN  CURSOR 3,10 ELSE 1200
 1030 PRINT "<*#*> OPTION 1 - ANY NUMBER <*#*>";
 1040 CURSOR 15,1
 1050 INPUT (1,0)"<***>  BET ON WHICH NUMBER ? (1-9) ---=>",N
 1060 IF N=5 THEN  CURSOR 13,1 ELSE 1120
 1100 PRINT "###   $5$   IS RESERVED FOR THE HOUSE   ###"
 1110 PRINT ,"***  TRY AGAIN  ***": GOTO 1000
 1120 IF N=0 THEN 700: REM ... RESTART
 1130 LET B(N)=7: LET B(N+9)=7
 1200 IF O=2 THEN  LET B(1)=1 ELSE 1230
 1210 LET B(3)=2: LET B(7)=2: LET B(9)=2: LET B(10)=2
 1220 LET B(12)=2: LET B(16)=2: LET B(18)=2
 1230 IF O=3 THEN  LET B(2)=1 ELSE 1260
 1240 LET B(4)=2: LET B(6)=2: LET B(8)=2: LET B(11)=2
 1250 LET B(13)=2: LET B(15)=2: LET B(17)=2
 1260 IF O<>4 THEN 1280
 1270 FOR X=1 TO 4: LET B(X)=2: LET B(X+9)=2: NEXT X
 1280 IF O<>5 THEN 1300
 1290 FOR X=6 TO 9: LET B(X)=2: LET B(X+9)=2: NEXT X
 1300 IF O<6 THEN 1500
 1310 FOR X=1 TO 18
 1320   IF (O-5)=C(X) THEN  LET B(X)=2
 1330 NEXT X
 1500 PRINT "&K": REM === INTIAL WHEEL DISPLAY ====
 1510 GOSUB 3000: REM ... DISPLAY MONEY
 1520 GOSUB 3100: REM ... MSG:  "PLACE YOUR BETS"
 1530 GOSUB 3800: REM ... OPTIONS PICKED
 1540 IF R=2 THEN  GOSUB 4000: LET R4=R4+1: REM ... 2nd player bets 1st
 1550 IF O=0 THEN  PAUSE 10: GOTO 2000
 1560 IF B>M THEN  GOSUB 3400: REM ... MSG:  "NOT ENOUGH $ !"
 1570 CURSOR 14,1
 1580 INPUT "<$$$$$>  HOW MUCH FOR YOUR BET? ---=>",B
 1600 IF B<0 THEN 9000
 1610 IF B=0 THEN  PRINT "&K": LET O=0: GOSUB 3000: GOTO 2000
 1620 IF B>M THEN 1500
 1630 LET M=M-B: GOSUB 3000: REM === DISPLAY $ LEFT ===
 1700 CURSOR 2,10: PRINT "$ ";B;: GOSUB 3200
 1710 PRINT " BET ON";: CURSOR 4,1
 1720 FOR X=1 TO 9
 1730   IF B(X)>0 THEN  LET Z=X*3: PRINT N$(Z-2,Z);" ";
 1740 NEXT X
 1750 PAUSE 15
 1760 PRINT "&K"
 1770 GOSUB 3000: REM ... DISPLAY MONEY
 1780 GOSUB 3800: REM ... OPTIONS PICKED
 1790 CURSOR E(1),F(1): PRINT B$;
 2000 CURSOR 1,38: PRINT "*** WE ARE READY  ***  ";
 2010 CURSOR 2,38: PRINT "***   TO PLAY !   ***";
 2020 LET X=INT(RND(0)*17)+2: LET X1=X-1
 2025 CURSOR 5,15: PRINT "/^\";
 2030 CURSOR 6,15: PRINT "\v/";
 2035 GOSUB 3600: REM ... DISPLAY WHEEL
 2040 GOSUB 3000: REM ... DISPLAY MONEY
 2050 GOSUB 3800: REM ... OPTIONS PICKED
 2060 GOSUB 2900: REM ... DROP BALL
 2070 GOSUB 3150: REM ... ERASE MSG
 2080 LET Z=INT((RND(0)*10)+5): LET X=1
 2090 CURSOR 1,38: PRINT "**  AROUND IT GOES **  ";
 2100 FOR Z1=1 TO Z
 2110   GOSUB 3500: REM ... SPIN CURSOR AROUND WHEEL
 2120 NEXT Z1
 2130 PAUSE 3: GOSUB 3150
 2140 LET S=INT(RND(0)*18)+1: LET X=S
 2200 LET X=X+1: IF X>18 THEN  LET X=1
 2210 CURSOR J(X),K(X): PRINT B$;
 2220 IF RND(0)<((.97)-(T*.09)) THEN  PAUSE T: GOTO 2250
 2230 LET T=T+1: GOSUB 3500: REM ....SPIN CURSOR
 2240 IF T>10 THEN 2500
 2250 CURSOR J(X),K(X): PRINT D$;
 2260 GOTO 2200
 2500 CURSOR 1,40: PRINT "*#* THE WINNER IS *#*";
 2510 CURSOR 2,46: IF O(X)<37 THEN  PRINT O(X);
 2520 IF C(O(X))=1 THEN  PRINT "  **RED**";
 2530 IF C(O(X))=2 THEN  PRINT "  #BLACK#";
 2540 IF C(O(X))=3 THEN  PRINT "$ HOUSE WINS $";
 2545 PAUSE 15: GOSUB 3150
 2550 IF B(O(X))>0 THEN  GOSUB 3300: REM === YOU WON !!! ===
 2560 CURSOR 8,23: PAUSE 25
 2570 IF R=3 THEN  GOSUB 4500: REM ... 2nd player status
 2580 IF M>0 THEN 700
 2600 CURSOR 1,40: PRINT "### YOU ARE OUT ###";
 2610 CURSOR 2,40: PRINT "$$$ OF MONEY !! $$$";
 2620 PAUSE 15
 2630 GOTO 9000
 2700 CURSOR 3,1: PRINT "   /'I";A$;"I'\"
 2710 PRINT "  /   I";C$;"I   \"
 2720 PRINT "/     I  (2)  I  (4)  I  (7)  I  (9)  I     \"
 2730 PRINT ": 1 m I";C$;"I p 6 :"
 2740 PRINT ": 2 a I   pairs    I     I   impairs  I a 7 :"
 2750 PRINT ":   n I  2 4 6 8   I $5$ I 1  3  7  9 I s :"
 2760 PRINT ": 3 q I            I     I            I s :"
 2770 PRINT ": 4 u I";C$;"I e 9 :"
 2780 PRINT "\   e I  (1)  I  (3)  I  (6)  I  (8)  I     /"
 2790 PRINT "  \   I";C$;"I   /"
 2800 PRINT "   \'I";A$;"I'/"
 2810 CURSOR 12,19: PRINT "#BLACK ";
 2820 CURSOR 4,19: PRINT "**RED**";
 2830 RETURN
 2900 FOR X=1 TO 18: REM  ... Drop the Ball
 2910   CURSOR E(X), F(X): PRINT B$;
 2920   PAUSE D(X)
 2930   CURSOR E(X), F(X): PRINT D$;
 2940 NEXT X
 2950 CURSOR 6,16: PRINT "+";: PAUSE 1
 2960 CURSOR 6,16: PRINT ".";
 2970 RETURN
 3000 REM ======= MONEY DISPLAY ===========
 3010 CURSOR  8,50: PRINT "$";M;: GOSUB 3200
 3020 CURSOR 10,50: PRINT "[ MONEY ]";
 3030 CURSOR  9,50: PRINT "=========";
 3040 RETURN
 3100 CURSOR 1,40: PRINT "*** PLACE  YOUR ***";
 3110 CURSOR 2,40: PRINT "*** BETS PLEASE ***";
 3130 RETURN
 3150 CURSOR 1,38: PRINT "                       ";
 3160 CURSOR 2,38: PRINT "                           ";
 3170 CURSOR 3,38: PRINT "                     ";
 3180 RETURN
 3200 IF M=INT(M) THEN  PRINT ".00";
 3210 RETURN
 3300 CURSOR 1,45: PRINT "<YOU WIN>";
 3310 CURSOR 2,45: PRINT "$";B*B(O(X));: GOSUB 3200
 3320 LET M=M+(B*B(O(X))): PAUSE 10
 3330 GOSUB 3000: REM === DISPLAY $ WON ===
 3340 PAUSE 10: LET W=1
 3350 CURSOR 1,45: PRINT "         ";
 3360 CURSOR 2,45: PRINT "         ";
 3370 PAUSE 10
 3380 RETURN
 3400 CURSOR 1,40: PRINT "*** YOU HAVE'NT ***";
 3410 CURSOR 2,40: PRINT "***$ THE MONEY $***";
 3420 RETURN
 3500 FOR Z2=X TO 18: REM  ... SPIN THE CURSOR
 3510   CURSOR J(Z2),K(Z2)
 3520 NEXT Z2
 3530 RETURN
 3600 FOR X=1 TO 18: REM ===== DISPLAY THE WHEEL =======
 3610   LET Z=X*3
 3620   CURSOR Y(X),X(X)
 3630   PRINT N$(Z-2,Z);
 3640 NEXT X
 3650 RETURN
 3700 PRINT "&K": CURSOR 1,5
 3705 PRINT "***  BETTING OPTIONS  ***&J"
 3710 PRINT "0.......PASS BETTING"
 3720 PRINT "1.......ANY  NUMBER    (1-4,6-9)  <PAYS 7 TO 1>"
 3730 PRINT "2.......ODD  NUMBER impairs (1,3,7,9) PAYS EVEN"
 3740 PRINT "3.......EVEN NUMBER  pairs  (2,4,6,8) PAYS EVEN"
 3750 PRINT "4.......1ST  FOUR   manque  (1,2,3,4) PAYS EVEN"
 3760 PRINT "5.......LAST FOUR   passe   (6,7,8,9) PAYS EVEN"
 3770 PRINT "6.......**RED**  NUMBERS    (2,4,7,9) PAYS EVEN"
 3780 PRINT "7.......#BLACK#  NUMBERS    (1,3,6,8) PAYS EVEN"
 3790 PRINT "8.......LEAVE THE 'BOULE' TABLE."
 3792 PRINT "9.......CHECK OTHER PLAYERS STATUS"
 3795 RETURN
 3800 CURSOR 4,40: PRINT "  <  OPTION ";O;"  >";
 3810 CURSOR 5,43: IF O=0 THEN  PRINT "*#*  PASS  *#*";
 3820 IF O=1 THEN  PRINT "@ ANY NUMBER @";
 3830 IF O=2 THEN  PRINT "ODD (impair)";
 3840 IF O=3 THEN  PRINT " EVEN  (pair)";
 3850 IF O=4 THEN  PRINT "1ST 4  (manque)";
 3860 IF O=5 THEN  PRINT "LAST 4  (passe)";
 3870 IF O=6 THEN  PRINT "  ** RED **";
 3880 IF O=7 THEN  PRINT "  # BLACK #";
 3890 IF O=0 THEN  RETURN  ELSE  CURSOR 6,42
 3900 FOR Z1=1 TO 9
 3910   IF B(Z1)=0 THEN 3930 ELSE  LET I=Z1*3
 3920   PRINT N$(I-2,I);" ";
 3930 NEXT Z1
 3940 RETURN
 4000 REM ====== OTHER PERSON PLAYING WITH YOU ========
 4005 IF R=2 THEN  PAUSE 5
 4010 LET N1=INT(RND(0)*9)+1: LET R=3
 4020 GOSUB 3150: IF N1=5 THEN  LET N1=N1+INT(RND(0)*4)+1
 4030 CURSOR 1,38: PRINT "<+++> ";
 4040 GOSUB 4400: REM ......DISPLAY NAME
 4050 LET B1=INT(RND(0)*L(P))+1: IF B>B1 THEN 4070
 4060 IF RND(0)>.15 THEN  LET B1=B1*(INT(RND(0)*3)+1)
 4070 IF M(P)=.1 THEN  CURSOR 2,38: PRINT "# IS BROKE #": RETURN
 4080 CURSOR 2,38: PRINT "BETS $";B1;
 4090 LET M(P)=M(P)-B1
 4100 LET O1=INT(RND(0)*7)+1
 4110 IF O=0 AND RND(0)>.6 THEN  LET O1=1
 4120 IF RND(0)<.4 THEN 4200
 4130 IF O=2 THEN  LET O1=3
 4140 IF O=3 THEN  LET O1=2
 4150 IF O=4 THEN  LET O1=5
 4160 IF O=5 THEN  LET O1=4
 4170 IF O=6 THEN  LET O1=7
 4180 IF O=7 THEN  LET O1=6
 4200 PRINT ": OPTION ";O1;: PAUSE 15
 4205 IF O1=1 THEN  CURSOR 3,45: PRINT "Number ";N1
 4210 IF O1=1 THEN  LET B1(N1)=7: LET B1(N1+9)=7
 4220 IF O1=2 THEN  LET B1(1)=2 ELSE 4250
 4230 LET B1(3)=2: LET B1(7)=2: LET B1(9)=2: LET B1(10)=2
 4240 LET B1(12)=2: LET B1(16)=2: LET B1(18)=2
 4250 IF O1=3 THEN  LET B1(2)=2 ELSE 4280
 4260 LET B1(4)=2: LET B1(6)=2: LET B1(8)=2: LET B1(11)=2
 4270 LET B1(13)=2: LET B1(15)=2: LET B1(17)=2
 4280 IF O1<>4 THEN 4300
 4290 FOR X=1 TO 4: LET B1(X)=2: LET B1(X+9)=2: NEXT X
 4300 IF O1<>5 THEN 4320
 4310 FOR X=6 TO 9: LET B1(X)=2: LET B1(X+9)=2: NEXT X
 4320 IF O1<6 THEN  RETURN
 4330 FOR X=1 TO 18
 4340   IF (O1-5)=C(X) THEN  LET B1(X)=2
 4350 NEXT X
 4360 RETURN
 4400 IF P< 7 THEN  PRINT "RICH ARAB #";P;: RETURN
 4410 IF P>15 THEN  PRINT "SHIEK #";P-15;: RETURN
 4420 IF P= 7 THEN  PRINT "MOTOR MOUTH MAMA";: RETURN
 4430 IF P= 8 THEN  PRINT "BIG TYCOON";: RETURN
 4440 IF P= 9 THEN  PRINT "SLEAZY SALESMAN";: RETURN
 4450 IF P=10 THEN  PRINT "WEATHY LAWYER";: RETURN
 4460 IF P=11 THEN  PRINT "POLISH JOKER";: RETURN
 4470 IF P=12 THEN  PRINT "FOXEY COUNTESS";: RETURN
 4480 IF P=13 THEN  PRINT "SICK DOCTOR";: RETURN
 4490 IF P=14 THEN  PRINT "FAT LANDLORD";: RETURN
 4495 IF P=15 THEN  PRINT "VERY RICH COP";: RETURN
 4500 REM === DISPLAY OTHER PLAYERS WIN OR LOSS ===
 4505 IF B1=0 THEN  RETURN : REM ...THEY didn't bet.
 4510 GOSUB 3150: LET W1=B1*B1(O(X))
 4520 CURSOR 1,38: PRINT "<+++> ";: GOSUB 4400: REM ... Get name
 4530 CURSOR 2,38
 4540 IF W1>0 THEN  PRINT "*WINS* ";W1: LET M(P)=M(P)+W1
 4550 IF W1=0 THEN  PRINT "LOSES ";B1: LET M(P)=M(P)-B1
 4560 PAUSE 10: GOSUB 3150
 4600 REM ....THEY get YOUR money - - - MAYBE
 4610 IF W1=0 OR B=0 OR W>0 THEN 4700
 4620 IF B1>B THEN  LET W2=INT(B/2) ELSE  LET W2=INT(B1/2)
 4630 IF L(P)*(-4)>M(P) THEN  LET M(P)=.1: REM === HE'S BROKE!
 4640 CURSOR 2,38: PRINT "$";W2;" of YOUR money bet."
 4650 LET M(P)=M(P)+W2
 4660 PAUSE 15: GOSUB 3150: PAUSE 7
 4700 CURSOR 2,38: REM ... SHOW status of other player
 4710 IF M(P)<0 THEN  PRINT "BEHIND "; ELSE  PRINT "AHEAD +";
 4720 PRINT M(P): PAUSE 15: GOSUB 3150: PAUSE 10
 4800 REM ....YOU get THEIR money - - - MAYBE
 4810 IF W1>0 OR B1=0 OR W=0 THEN  RETURN
 4820 IF B>B1 THEN  LET B2=INT(B1/2) ELSE  LET B2=INT(B/2)
 4830 CURSOR 1,40: PRINT "### YOU also win ...";
 4840 CURSOR 2,40: PRINT "$";B2;" of THEIR money!"
 4850 PAUSE 7: LET M=M+B2
 4860 GOSUB 3000: PAUSE 10: REM === DISPLAY $ WON ===
 4890 RETURN
 9000 PRINT "&K": CURSOR 4,20
 9010 PRINT "*** END OF GAME ***";: CURSOR 7,5
 9020 IF M=500 THEN  PRINT "O====  YOU BROKE EVEN  ====O"; ELSE 9100
 9090 GOTO 9500
 9100 IF M>600 THEN  CURSOR 9,20 ELSE 9200
 9110 PRINT "### OH...!   FAR OUT MAN !!!! ###";
 9120 CURSOR 10,15: PRINT "### YOUR A WINNER !!! ###";
 9190 GOTO 9500
 9200 IF M<400 THEN  CURSOR 9,12 ELSE 9300
 9210 PRINT "*** BETTER LUCK NEXT TIME ***";
 9290 GOTO 9500
 9300 IF M<400 OR M>600 THEN 9500
 9310 CURSOR 6,10: PRINT "*  YOU DID O.K.  *";
 9500 CURSOR 12,5
 9510 PRINT "AFTER ";N9-1;" times at the wheel, YOU LEAVE WITH $";M;
 9520 GOSUB 3200
 9530 CURSOR 14,1
 9540 END
