10 CLS:X=RND(-(PEEK(9)*256+PEEK(10)))
20 DIM PLAY(13),COMPU(13),DECK(13),GUESS(13),POSS(13),ASKED(13)
30 CARD$="A234567890JQK"
40 REM CA=4*13
50 FOR I=1 TO 13:DECK(I)=4:NEXT
60 FOR I=1 TO 9
70 GOSUB 1250
80 DECK(K)=DECK(K)-1
90 COMPU(K)=COMPU(K)+1
100 GOSUB 1250
110 DECK(K)=DECK(K)-1
120 PLAY(K)=PLAY(K)+1
130 NEXT I
140 PRINT "GO FISH"
150 PRINT "======="
160 PRINT
170 INPUT "WHAT IS YOUR NAME";NAME$
180 REM PRINT_HAND
190 PRINT "SCORE: "NAME$;" ";SCORE(0);" COMPUTER ";SCORE(1)
200 PRINT ,CA"CARDS REMAINING"
210 PRINT "YOUR HAND: ";
220 FOR I=1 TO 13
230 IF PLAY(I)=0 THEN 280
240 FOR J=1 TO PLAY(I)
250 PRINT MID$(CARD$,I,1);" ";
260 NEXT J
270 REM NEXT_CARD
280 NEXT I
290 PRINT
300 REM ASK_CARD
310 GOSUB 1100
320 S$=NAME$
330 INPUT "WHICH CARD DO YOU ASK FOR";C$
340 IF C$="" THEN 310
350 CN=0:FORZ=1TOLEN(CARD$):IFMID$(CARD$,Z,1)=C$THENCN=Z:Z=LEN(CARD$)
351 NEXTZ
360 IF CN=0 THEN PRINT "SORRY, THAT IS NOT A VALID CHOICE.":GOTO 310
370 IF PLAY(CN)=0 THEN PRINT "YOU DO NOT HAVE THAT CARD!":GOTO 310
380 GUESS(CN)=1
390 IF COMPU(CN)=0 THEN PRINT S$", GO FISH!":PRINT S$" DRAWS A ";:GOSUB800:GOSUB 920:GOTO 470
400 V=COMPU(CN)
410 COMPU(CN)=0
420 PLAY(CN)=PLAY(CN)+V
430 PRINT S$" GETS"V"MORE CARDS."
440 GOSUB 920
450 GOTO 190
460 REM COMPUTER_TURN_1
470 S$="COMPUTER"
480 FOR I=1 TO 13
490 ASKED(I)=0
500 NEXT
510 REM COMPUTER_TURN_2
520 GOSUB 1100
530 PO=0
540 FOR I=1 TO 13
550 IF (COMPU(I)>0) AND (GUESS(I)>0) THEN POSS(I)=1:PO=PO+1
560 NEXT
570 IF PO=0 THEN 650
580 REM DRAW_GUESS
590 K=RND(12)
600 IF POSS(K)=0 THEN 590
610 GUESS(K)=0
620 ASKED(K)=1
630 GOTO 680
640 REM DRAW_RAND
650 K=RND(12)
660 IF COMPU(K)=0 OR ASKED(K) THEN 650
670 REM MAKE_TURN
680 PRINT S$" WANTS YOUR "MID$(CARD$,K,1)"'S."
690 ASKED(K)=1
700 IF PLAY(K)=0 THEN PRINT S$", GO FISH!":PRINT S$" DRAWS A ";:GOSUB 860:GOSUB 1010:GOTO 190
710 V=PLAY(K)
720 PLAY(K)=0
730 COMPU(K)=COMPU(K)+V
740 PRINT S$" GETS"V"MORE CARDS."
750 GOSUB 1010
760 GOTO 520
770 GOTO 190
780 END
790 REM DRAW_CARD_P
800 GOSUB 1250
810 PRINT MID$(CARD$,K,1)"."
820 DECK(K)=DECK(K)-1
830 PLAY(K)=PLAY(K)+1
840 RETURN
850 REM DRAW_CARD_C
860 GOSUB 1250
870 PRINT "CARD."
880 DECK(K)=DECK(K)-1
890 COMPU(K)=COMPU(K)+1
900 RETURN
910 REM CHECK_BOOK_P
920 FOR I=1 TO 13
930 IF PLAY(I)<>4 THEN 980
940 PRINT S$" COMPLETES BOOK OF "MID$(CARD$,I,1)"'S."
950 PLAY(I)=0
960 SCORE(0)=SCORE(0)+1
970 REM NEXT_LOOP_P
980 NEXT I
990 RETURN
1000 REM CHECK_BOOK_C
1010 FOR I=1 TO 13
1020 IF COMPU(I)<>4 THEN 1070
1030 PRINT S$" COMPLETES BOOK OF "MID$(CARD$,I,1)"'S."
1040 COMPU(I)=0
1050 SCORE(1)=SCORE(1)+1
1060 REM NEXT_LOOP_C
1070 NEXT I
1080 RETURN
1090 REM CHECK_END
1100 NP=0:NC=0
1110 FOR I=1 TO 13
1120 NP=NP+PLAY(I)
1130 NC=NC+COMPU(I)
1140 NEXT I
1150 IF CA=0 OR NP=0 OR NC=0 THEN 1180
1160 RETURN
1170 REM END_GAME
1180 PRINT
1190 PRINT "*** GAME OVER! ***"
1200 PRINT
1210 IF SCORE(0)>SCORE(1) THEN PRINT NAME$" HAS WON.":END
1220 IF SCORE(0)<SCORE(1) THEN PRINT "THE COMPUTER HAS WON.":END
1230 PRINT "IT'S A TIE!":END
1240 REM DEAL_CARD
1250 CA=CA-1
1260 SC=CA*RND(0)+1
1270 FOR K=1 TO 13
1280 SC=SC-DECK(K)
1290 IF SC<=0 THEN RETURN
1300 NEXT
1310 END
