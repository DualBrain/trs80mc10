0 CLS:CLEAR 500:DIM DC(8):DC(0)=127:DC(1)=143:DC(2)=159:DC(3)=175:DC(4)=191:DC(5)=207:DC(6)=223:DC(7)=239:MC=15360
10 REM PIG BY DAVID MITCHELL
30 REM (c)2011 DRAGON32.COM
35 REM -----------------------
40 HS = 0:REM high score
41 GW = 0:REM games won
42 GD = 0:REM games drawn
43 GL = 0:REM games lost
44 HR = 0:REM highest roll total in single turn
45 GC = 0:REM number of century rolls
48 BL$="                                ":FORZZ=1TO32:BS$=BS$+CHR$(153):NEXT
49 PRINT"HIT A KEY"
50 I$=INKEY$:DX=RND(1000):IFI$=""THEN50
51 CLS:MA=12: REM num of turns in a game
60 CLS
70 GOSUB 120: REM SHOW TITLE.
75 GOSUB 220: REM HIGHSCORE
80 IF A$="I" THEN GOSUB 1000:A$="":GOTO75: REM instructions.
85 IF A$="G" THEN GOSUB 9500:A$="":GOTO75: REM achievments.
90 GOSUB 2000: REM START GAME
93 IF PS>HS THEN HS=PS: PRINT@480,"   NEW HIGH SCORE!        ";:GOTO75
95 A$=INKEY$:IFA$=""THEN 95
96 GOTO 60
110 END
120 REM -------------------
121 REM title screen
122 REM -------------------
123 TX=1:TY=1
130 PRINT@36,"welcome to pig BETA"
133 PRINT@104,"WWW.DRAGON32.COM"
140 DX=TX*3:DY=2+(TY*3):GOSUB4000
145 TX=TX+1:IF TX>9 THEN TX = 1:TY=TY+1
147 IF TY>3 THEN TY = 1
150 A$=INKEY$:IFA$=""THEN 140
160 RETURN
220 REM -------------------
221 REM highscore screen
222 REM -------------------
225 CLS:A$="":GOSUB9000
233 PRINT@423,"WWW.DRAGON32.COM";
236 PRINT@327,"i FOR INSTRUCTIONS"
237 PRINT@295,"p TO START THE GAME"
238 PRINT@359,"g FOR GAME STATS"
239 IF HS>0 THEN PRINT@56,"high";: PRINT@88, "score";:PRINT@119, STR$(HS)+"";
240 REM DX=RND(9)*3:DY=3+(RND(3)*3):GOSUB4000
250 A$=INKEY$:IFA$="I"ORA$="P"ORA$="G"THENRETURN
260 GOTO250
1000 REM-------------
1004 REM instructions
1008 REM-------------
1010 CLS:PRINT "PIG IS A DICE GAME FOR 2"
1020 PRINT "PLAYERS. THE OBJECT IS TO GET"
1030 PRINT "THE HIGHEST SCORE. YOU SCORE"
1040 PRINT "POINTS BY ROLLING 2 DICE. THE"
1050 PRINT "RESULT IS ADDED TO A RUNNING"
1060 PRINT "TOTAL FOR THIS TURN.":PRINT
1070 PRINT "YOU CAN THEN CHOOSE TO ROLL"
1080 PRINT "AGAIN, OR TO PASS AND TAKE THE"
1090 PRINT "POINTS."
1100 PRINT "IF CHOSE TO ROLL AGAIN, IF THE"
1110 PRINT "SAME VALUE AS YOUR FIRST ROLL"
1120 PRINT "THAT TURN THEN YOU GAIN NO" 
1130 PRINT "POINTS THAT TURN AND THE OTHER"
1140 PRINT "PLAYER STARTS THEIR TURN."
1145 A$=INKEY$:IFA$=""THEN 1145
1150 CLS:PRINT "FOR EXAMPLE,":PRINT "PLAYER 1 ROLLS"
1160 PRINT "[4][3] 7 <- HAVE TO AVOID 7"
1170 PRINT "[5][1] 13"
1180 PRINT "PLAYER 1 DECIDES TO PASS"
1190 PRINT "PLAYER 1 BANKS 13 POINTS":PRINT
1200 PRINT "PLAYER 2 PLAYS"
1210 PRINT "[5][5] 10 <- HAVE TO AVOID 10"
1220 PRINT "[6][1] 17"
1230 PRINT "[6][4] 0  <- OH NO 10 AGAIN!"
1240 PRINT "PLAYER 2 BANKS 0 POINTS":PRINT
1250 PRINT "THIS CONTINUES OVER " + STR$(MA) + " TURNS"
1255 A$=INKEY$:IFA$=""THEN 1255
1260 RETURN
2000 REM ------------------------------------
2010 REM start the game
2020 REM ------------------------------------
2030 PS=0:REM player score
2040 CS=0:REM dragon score
2050 RO=1:REM round
2055 RS=0: REM score from current round.
2056 WH=RND(2): REM random who plays first.
2057 RT=1: REM 1 IF PLAYER ROLLS
2065 AV = 0:REM first roll value
2066 CR = 0:REM current roll
2070 CLS
2080 PRINT@1, "turn"
2090 PRINT@12, "you"
2100 PRINT@22, "dragon"
2101 PRINT@5, STR$(RO)+"";
2102 PRINT@15, STR$(PS)+"";
2103 PRINT@28, STR$(CS)+"";
2104 PRINT@384, BS$+"";
2105 IFWH=1THENPOKEMC+1035,107:GOTO2107
2106 POKEMC+1035,143
2107 IFWH=2THENPOKEMC+1045,107:GOTO2110
2108 POKEMC+1045,143
2110 IFWH=1THENGOSUB5000:GOTO2180
2111 GOSUB6000
2180 IFRT=1THENGOSUB3000:REM ROLL DICE
2280 IFRT=1THEN2101
2290 REM ROUND OVER
2300 IFWH=1THENPS=PS+RS:WH=2:GOTO2302
2301 CS=CS+RS:WH=1
2302 AV=0
2305 GOSUB7000
2310 FORD=0TO200:NEXTD
2990 RO = RO + 1: RS = 0: AV=0: IF RO<=MA THEN 2080
2994 GOSUB8000
2999 RETURN
3000 REM ------------------------------------
3010 REM draw 2 dice. random movement. random roll length.
3020 SOUND 1,1:SOUND100,1
3026 PRINT@98, BL$+BL$+BL$+BL$+BL$
3027 PRINT@289, BL$+BL$
3030 DX = 5 + RND(3):DY = RND(5)+3:GOSUB 4000:RS=RS+DD:CR=DD
3040 DX = DX+3+RND(26-(DX)):DY = RND(5)+3:GOSUB 4000:RS=RS+DD:CR=CR+DD
3045 IF AV=0 THEN AV = CR:GOTO3050
3046 IF AV=CR THEN RT=2:RS=0
3050 REM IF AV<>RSC THEN PRINT@417, "avoid "+STR$(AV)
3060 PRINT@431, "dice total"+STR$(RSC)
3999 RETURN
4000 REM ------------------------------------
4003 REM DRAW A DICE AT DX DY WITH COLOUR CC.
4004 REM DICE VALUE SET AT DD.
4005 REM ------------------------------------
4006 DD = RND(6): CC=DC(DD):QQ=(DY*32)+DX+1024
4010 POKEMC+QQ,CC+2:POKEMC+QQ+1,CC+4:POKEMC+QQ+2,CC+3
4020 QQ=QQ+32
4030 POKEMC+QQ,CC+6:POKEMC+QQ+1,48+DD:POKEMC+QQ+2,CC+11
4040 QQ=QQ+32
4050 POKEMC+QQ,CC+5:POKEMC+QQ+1,CC+13:POKEMC+QQ+2,CC+9
4500 RETURN
5000 REM ------------------------------------
5003 REM USER TURN.
5005 REM ------------------------------------
5008 REM IF AV=0 THEN RT=1:RETURN
5010 IF AV=0 THEN PRINT@451, "r TO ROLL":RT=3
5015 IF AV>0 THEN PRINT@463, "b TO BANK":PRINT@417, "avoid "+STR$(AV)+"";
5020 A$=INKEY$:IFA$=""THEN 5020
5030 IF A$="R" THEN RT=1
5035 IF AV>0 AND A$="B" THEN RT=2:SOUND100,1
5040 IF RT=3 THEN 5020
5999 RETURN
6000 REM ------------------------------------
6003 REM  DRAGON TURN.
6005 REM ------------------------------------
6010 REM PRINT@480, "   DRAGON IS THINKING...";
6012 PRINT@451, "              ";
6013 IF AV>0 THEN PRINT@417, "avoid "+STR$(AV)+"";
6015 FORD=0TO200:NEXTD:PRINT@33,LEFT$(BL$,28);
6020 IF AV=0 THEN RT=1:RETURN
6023 IF CS=0 AND RS>15 THEN RT=2:RETURN
6025 IF RO=MA AND (CS+RS)>PS THEN RT=2:RETURN
6026 IF RO=MA AND CS<=PS THEN RT=1:RETURN
6028 IF RS>(40+RND(30)) THEN RT=2:RETURN
6030 IF AV<10 THEN RT=1:RETURN
6040 IF AV>4 AND AV<10 THEN RT=RND(2):RETURN
6050 RT=1:RETURN
7000 REM ------------------------------------
7010 REM  END OF TURN SCORE UPDATE
7012 REM ------------------------------------
7014 SOUND200,2:SOUND100,2
7015 PRINT@417, BL$
7018 IF WH=2 AND HR<RS THEN HR=RS
7019 IF WH=2 AND RS>100 THEN GC=GC+1
7020 IF WH=1 THEN PRINT@480, "   DRAGON SCORED:" +STR$(RS)+"     ";
7030 IF WH=2 THEN PRINT@480, "   YOU SCORED:"+STR$(RS)+"    ";
7040 FORD=0TO600:NEXTD:PRINT@480,LEFT$(BL$,28);
7100 FORZZ=1TO5:SOUND50,1:SOUND100,1:NEXT
7999 RETURN
8000 REM ------------------------------------
8010 REM  END OF GAME STATUS
8012 REM ------------------------------------
8014 SOUND 100,2:SOUND200,2
8015 PRINT@417, BL$
8020 IF PS>CS THEN RE$="YOU ARE THE WINNER":GW=GW+1
8030 IF CS>PS THEN RE$="THE DRAGON IS THE WINNER":GL=GL+1
8040 IF CS=PS THEN RE$="YOU AND THE DRAGON TIE!":GD=GD+1
8050 PRINT@15, STR$(PS)+"";:PRINT@28, STR$(CS)+"";
8150 FORD=0TO200:NEXTD:PRINT@480, RE$+"";
8160 A$=INKEY$:IFA$=""THEN 8160
8999 RETURN
9000 REM ------------------------------------
9010 REM DRAW A PIG
9015 REM ------------------------------------
9020 POKEMC+1095,239: POKEMC+1097,239: POKEMC+1127,239: POKEMC+1128,239: POKEMC+1129,239: POKEMC+1131,239: POKEMC+1132,239
9021 POKEMC+1133,239: POKEMC+1134,239: POKEMC+1135,239: POKEMC+1136,239: POKEMC+1158,239: POKEMC+1159,239: POKEMC+1160,239
9030 POKEMC+1161,239: POKEMC+1162,239: POKEMC+1163,239: POKEMC+1164,239: POKEMC+1165,239: POKEMC+1166,239: POKEMC+1167,239
9031 POKEMC+1168,239: POKEMC+1169,239: POKEMC+1190,239: POKEMC+1191,239: POKEMC+1192,239: POKEMC+1193,239: POKEMC+1194,239
9040 POKEMC+1195,239: POKEMC+1196,239: POKEMC+1197,239: POKEMC+1198,239: POKEMC+1199,239: POKEMC+1200,239: POKEMC+1201,239
9041 POKEMC+1226,239: POKEMC+1227,239: POKEMC+1228,239: POKEMC+1229,239: POKEMC+1230,239: POKEMC+1231,239: POKEMC+1232,239
9050 POKEMC+1258,239: POKEMC+1259,239: POKEMC+1263,239: POKEMC+1264,239
9060 RETURN
9500 REM ------------------------------------
9510 REM ACHIEVEMENTS
9520 REM ------------------------------------
9530 CLS
9535 PRINT TAB(2)+"pig":PRINT
9540 PRINT TAB(2)+"GAMES WON   : " + STR$(GW):PRINT
9550 PRINT TAB(2)+"GAMES LOST  : " + STR$(GL):PRINT
9560 PRINT TAB(2)+"GAMES DRAWN : " + STR$(GD):PRINT
9570 PRINT TAB(2)+"HIGHEST ROLL : " + STR$(HR):PRINT
9580 PRINT TAB(2)+"CENTURY ROLLS : " + STR$(GC)
9900 A$=INKEY$:IFA$=""THEN 9900
9930 RETURN
